************************************************************************
* [プログラム名]
*   ZMO012600        受発注伝票テキストコピー
*
* [処理概要]
*   選択画面で入力された受注テキストIDの内容を取得し、その内容で
*   発注テキストIDの内容を登録する。
*
* [改定履歴]
*　YYYY/MM/DD   Programar         Description
*  2006/06/01   DMC S.SHIGEMITU   新規開発　
*  2006/06/22   DMC R.HATA             得意先注番付加
************************************************************************
REPORT ZMO012600_SHIGEMITU NO STANDARD PAGE HEADING
LINE-SIZE  170
MESSAGE-ID Z1.

TABLES: VBAK,    "販売伝票：ヘッダ
VBAP,    "販売伝票：明細
EKKN,    "購買発注：勘定設定
KNA1,    "得意先マスタ
TTXIT,   "テキストテーブル
*Add 2006.06.22 --->
VBKD,   "販売伝票：ビジネスデータ
*Add 2006.06.22 <---
THEAD.

TYPES:
* 受注伝票番号明細取得用
BEGIN OF TYP_VBAKP,
VBELN  TYPE VBAP-VBELN,
POSNR  TYPE VBAP-POSNR,
BSTKD  TYPE VBKD-BSTKD,
END OF TYP_VBAKP,

* 購買伝票情報取得用
BEGIN OF TYP_EKKN,
EBELN  TYPE EKKN-EBELN,
EBELP  TYPE EKKN-EBELP,
VBELN  TYPE EKKN-VBELN,
VBELP  TYPE EKKN-VBELP,
END OF TYP_EKKN,

* 得意先名称取得用
BEGIN OF TYP_KNA1,
KUNNR  TYPE KNA1-KUNNR,
NAME1  TYPE KNA1-NAME1,
END OF TYP_KNA1,

* テキスト内容説明取得用
BEGIN OF TYP_TTXIT,
TDTEXT TYPE TTXIT-TDTEXT,
END OF TYP_TTXIT,

* 一覧出力用
BEGIN OF TYP_WRITE,
VBELN       TYPE VBAP-VBELN,
VBELP       TYPE VBAP-POSNR,
EBELN       TYPE EKKN-EBELN,
EBELP       TYPE EKKN-EBELP,
TEXT(50)    TYPE C,
MESSAGE(62) TYPE C,
END OF TYP_WRITE.

*----------------------------------------------------------------------*
*   内部テーブル等定義
*----------------------------------------------------------------------*
DATA:
* 受注番号明細取得用内部テーブル
H_VBAKP TYPE TYP_VBAKP,
T_VBAKP TYPE TABLE OF TYP_VBAKP,

* 購買伝票番号取得用内部テーブル
H_EKKN  TYPE TYP_EKKN,
T_EKKN  TYPE TABLE OF TYP_EKKN,

* 得意先名称取得用内部テーブル
H_KNA1  TYPE TYP_KNA1,
T_KNA1  TYPE TABLE OF KNA1,

* テキスト内容説明取得用内部テーブル
H_TTXIT TYPE TYP_TTXIT,
T_TTXIT TYPE TABLE OF TYP_TTXIT,

* 一覧出力用内部テーブル
H_WRITE TYPE TYP_WRITE,
T_WRITE TYPE TABLE OF TYP_WRITE.

*----------------------------------------------------------------------*
*  選択画面
*----------------------------------------------------------------------*
SELECT-OPTIONS:
* 受注登録日付
S_ERDAT FOR SY-DATUM   OBLIGATORY
NO-EXTENSION
NO INTERVALS,
* 得意先コード
S_KUNNR FOR KNA1-KUNNR OBLIGATORY
NO-EXTENSION
NO INTERVALS,
* 受注テキストID
S_VAID  FOR THEAD-TDID OBLIGATORY
NO-EXTENSION
NO INTERVALS,
* 発注テキストID
S_EKID  FOR THEAD-TDID OBLIGATORY
NO-EXTENSION
NO INTERVALS.
PARAMETERS:
* テスト実行
P_TEST AS CHECKBOX DEFAULT 'X'.
* 2006.06.22 Add --->
PARAMETERS:
* 得意先注番付加
P_ADDCHU AS CHECKBOX DEFAULT 'X'.
* 2006.06.22 Add <---


*----------------------------------------------------------------------*
*                      INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.
CLEAR:   H_VBAKP, H_EKKN, H_KNA1, H_WRITE.
REFRESH: T_VBAKP, T_EKKN, T_KNA1, T_WRITE.
*----------------------------------------------------------------------*
*                      AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.
SELECT SINGLE
*
FROM KNA1
WHERE KUNNR IN S_KUNNR.

IF SY-SUBRC <> 0.
MESSAGE E400 WITH '得意先コードが存在しません'.
ENDIF.

IF NOT S_VAID-LOW IS INITIAL.
SELECT SINGLE
*
FROM TTXIT
WHERE TDOBJECT = 'VBBP'
AND TDID     = S_VAID-LOW.

IF SY-SUBRC <> 0.
MESSAGE E400 WITH '受注テキストIDが存在しません'.
ENDIF.
ENDIF.

IF NOT S_EKID-LOW IS INITIAL.
SELECT SINGLE
*
FROM TTXIT
WHERE TDOBJECT = 'EKPO'
AND TDID     = S_EKID-LOW.

IF SY-SUBRC <> 0.
MESSAGE E400 WITH '発注テキストIDが存在しません'.
ENDIF.
ENDIF.
*----------------------------------------------------------------------*
*                      START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.
* テキスト名称取得
PERFORM SEL_TTXIT.

* 受注伝票明細取得処理
PERFORM SEL_VBAKP.

* 購買発注情報取得処理
PERFORM SEL_EKKN.

* 得意先名称取得処理
PERFORM SEL_KNA1.

* テキスト更新処理
PERFORM READ_TEXT.

* 一覧出力処理
PERFORM WRITE_DATE.

*---------------------------------------------------------------------*
*       FORM SEL_TTXIT                                                *
*---------------------------------------------------------------------*
*       テキスト内容説明取得処理                                      *
*---------------------------------------------------------------------*
FORM SEL_TTXIT.

SELECT SINGLE
TDTEXT
INTO H_TTXIT
FROM TTXIT
WHERE TDSPRAS = 'J'
AND TDOBJECT = 'EKPO'
AND TDID     = S_EKID-LOW.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM SEL_VBAKP                                                *
*---------------------------------------------------------------------*
*       受注伝票明細取得処理                                          *
*---------------------------------------------------------------------*
FORM SEL_VBAKP.

* 受注伝票番号・明細番号取得
SELECT K~VBELN
P~POSNR
V~BSTKD
INTO TABLE T_VBAKP
FROM VBAK AS K INNER JOIN
VBAP AS P
ON K~VBELN = P~VBELN
Inner Join
VBKD As V
On V~VBELN = k~vbeln
And V~POSNR = '000000'
WHERE K~ERDAT IN S_ERDAT
AND K~KUNNR IN S_KUNNR.

* 対象データが存在しない場合
IF SY-SUBRC <> 0.
MESSAGE S012 WITH '受注伝票'. "受注伝票に該当データは存在しません
STOP.
ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM SEL_EKKN                                                 *
*---------------------------------------------------------------------*
*       購買発注情報取得処理                                          *
*---------------------------------------------------------------------*
FORM SEL_EKKN.

* 購買情報取得
SELECT EBELN
EBELP
VBELN
VBELP
INTO TABLE T_EKKN
FROM EKKN
FOR ALL ENTRIES IN T_VBAKP
WHERE VBELN = T_VBAKP-VBELN
AND VBELP = T_VBAKP-POSNR.

* 対象データが存在しない場合
IF SY-SUBRC <> 0.
MESSAGE S012 WITH '購買発注'. "購買発注に該当データは存在しません
STOP.
ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM SEL_KNA1                                                 *
*---------------------------------------------------------------------*
*       得意先名称取得処理                                            *
*---------------------------------------------------------------------*
FORM SEL_KNA1.

* 得意先名称取得
SELECT SINGLE
KUNNR
NAME1
INTO H_KNA1
FROM KNA1
WHERE KUNNR IN S_KUNNR.

* 対象データが存在しない場合
IF SY-SUBRC <> 0.
MESSAGE S309 WITH '得意先名称が取得できません'.
STOP.
ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM READ_TEXT                                                *
*---------------------------------------------------------------------*
*       テキスト更新処理                                              *
*---------------------------------------------------------------------*
FORM READ_TEXT.

DATA: WK_VBELNP      TYPE THEAD-TDNAME,
WK_EBELNP      TYPE THEAD-TDNAME,
WK_MESSAGE(62) TYPE C,
WH_TLINE       TYPE TLINE,
WT_TLINE       TYPE TABLE OF TLINE.

LOOP AT T_EKKN INTO H_EKKN.

CLEAR:   WH_TLINE, WK_VBELNP, WK_EBELNP, H_WRITE.
REFRESH: WT_TLINE.

CONCATENATE H_EKKN-VBELN
H_EKKN-VBELP
INTO WK_VBELNP.

* 受注テキスト読み込み
CALL FUNCTION 'READ_TEXT'
EXPORTING
ID              = S_VAID-LOW
LANGUAGE        = 'J'
NAME            = WK_VBELNP
OBJECT          = 'VBBP'
TABLES
LINES           = WT_TLINE
EXCEPTIONS
NOT_FOUND       = 4
REFERENCE_CHECK = 5
OTHERS          = 8.

CASE SY-SUBRC.
* 受注テキストの内容が登録されていない場合
WHEN '4'.
H_WRITE-MESSAGE = '受注テキストの内容がありません'.
WHEN '0'.
* Add 2006.06.22 --->
If P_ADDCHU EQ 'X' .
Read Table WT_TLINE Into WH_TLINE Index 1 .
Read Table T_VBAKP Into H_VBAKP With Key VBELN = H_EKKN-VBELN
POSNR = H_EKKN-VBELP.
Concatenate WH_TLINE H_VBAKP-BSTKD Into WH_TLINE
Separated By ' '.
Modify WT_TLINE From WH_TLINE Index 1.

EndIf .
* Add 2006.06.22 <---


* 受注テキストIDが存在しない場合
WHEN OTHERS.
H_WRITE-MESSAGE = '受注テキスト読込に失敗しました'.

ENDCASE.

* 処理したデータを出力用内部テーブルに取得する。
MOVE-CORRESPONDING H_EKKN TO H_WRITE.

* 本実行の場合、購買発注テキストを登録する
IF P_TEST IS INITIAL AND SY-SUBRC = 0.

CONCATENATE H_EKKN-EBELN
H_EKKN-EBELP
INTO WK_EBELNP.

* 購買発注テキスト登録処理
PERFORM CREATE_TEXT TABLES   WT_TLINE
USING    WK_EBELNP
CHANGING WK_MESSAGE.
H_WRITE-MESSAGE = WK_MESSAGE.
ENDIF.

* 受注テキスト内容が取得できた場合、出力用に取得する
IF NOT WT_TLINE IS INITIAL.
LOOP AT WT_TLINE INTO WH_TLINE WHERE TDLINE <> ''.
H_WRITE-TEXT = WH_TLINE-TDLINE.
APPEND H_WRITE TO T_WRITE.
CLEAR: H_WRITE-MESSAGE.
ENDLOOP.

* 受注テキストが取得できない場合、そのまま取得する
ELSE.
APPEND H_WRITE TO T_WRITE.
ENDIF.

ENDLOOP.
ENDFORM.
*---------------------------------------------------------------------*
*       FORM CREATE_TEXT                                              *
*---------------------------------------------------------------------*
*       購買発注テキストに登録する                                    *
*---------------------------------------------------------------------*
FORM CREATE_TEXT TABLES   P_TLINE
USING    P_EBELNP
CHANGING P_MESSAGE.

* 購買発注テキスト登録
CALL FUNCTION 'CREATE_TEXT'
EXPORTING
FID       = S_EKID-LOW
FLANGUAGE = 'J'
FNAME     = P_EBELNP
FOBJECT   = 'EKPO'
TABLES
FLINES    = P_TLINE
EXCEPTIONS
NO_INIT   = 4
NO_SAVE   = 5.

* 正常登録された場合
IF SY-SUBRC = 0.
CONCATENATE '発注テキスト『'
H_TTXIT-TDTEXT
'』は正常登録されました'
INTO P_MESSAGE.

* 正常登録されなかった場合
ELSE.
P_MESSAGE = '登録できませんでした'.
ENDIF.

ENDFORM.
*---------------------------------------------------------------------*
*       FORM WRITE_DATE                                               *
*---------------------------------------------------------------------*
*       一覧出力処理                                                  *
*---------------------------------------------------------------------*
FORM WRITE_DATE.
DATA: WK_VBELN TYPE VBAK-VBELN.

* 出力用内部テーブルを順次読込む
LOOP AT T_WRITE INTO H_WRITE.

* 最初に1回だけ見出し処理を行う
AT FIRST.
WRITE: 45 '更新結果'.
SKIP 1.
WRITE:/01 '受注登録日付：',
15 S_ERDAT-LOW,
/01 '得意先コード：',
15 H_KNA1-KUNNR,
/01 '得意先名称  ：',
15 H_KNA1-NAME1.
SKIP 1.
WRITE:/01 '受注伝票番号',
15 '受注明細番号',
29 '発注伝票番号',
43 '発注明細番号',
57 'テキスト内容',
108 'メッセージ'.
ULINE.
ENDAT.

IF WK_VBELN <> H_WRITE-VBELN.
* 明細を出力する
WRITE:/01 H_WRITE-VBELN,
15 H_WRITE-VBELP,
29 H_WRITE-EBELN,
43 H_WRITE-EBELP,
57 H_WRITE-TEXT,
108 H_WRITE-MESSAGE.
WK_VBELN = H_WRITE-VBELN.
ELSE.
* 受注番号が変わるまでテキストとメッセージを出力する
WRITE:/57 H_WRITE-TEXT,
108 H_WRITE-MESSAGE.
ENDIF.
ENDLOOP.

ENDFORM.
