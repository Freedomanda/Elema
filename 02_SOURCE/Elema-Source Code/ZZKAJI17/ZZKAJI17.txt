*&---------------------------------------------------------------------*
*&  REPORT ZN040400                                                    *
*&         売掛金残高【確認】                                          *
*&---------------------------------------------------------------------*
*&  機能     : 得意先の売掛金を未消込明細単位でダウンロードする。
*&             ダウンロードしたデータは別途ACCESSで集計をして確認する
*&　　　　　　 ものとする
*&  作成日   : 2012/03/29
*&  作成者   : S.TAKIKAWA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 :
*&  変更内容 :
*&---------------------------------------------------------------------*
REPORT ZN040400
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.

*----------------------------------------------------------------------*
*   テーブル
*----------------------------------------------------------------------*
TABLES:KNA1.

*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
*-- ダウンロード項目
TYPES: BEGIN OF T_DOWNLOAD,
BUKRS TYPE BSID-BUKRS,   "会社コード
KUNNR TYPE BSID-KUNNR,   "得意先コード
NAME1 TYPE KNA1-NAME1,   "得意先名称
UMSKS TYPE BSID-UMSKS,   "特殊仕訳取引タイプ
UMSKZ TYPE BSID-UMSKZ,   "特殊仕訳コード
AUGDT TYPE BSID-AUGDT,   "決済日付
AUGBL TYPE BSID-AUGBL,   "決済伝票番号
ZUONR TYPE BSID-ZUONR,   "ソートキー
GJAHR TYPE BSID-GJAHR,   "会計年度
BELNR TYPE BSID-BELNR,   "会計伝票番号
BUZEI TYPE BSID-BUZEI,   "会計伝票内の明細番号
BUDAT TYPE BSID-BUDAT,   "伝票の転記日付
BLDAT TYPE BSID-BLDAT,   "伝票日付
CPUDT TYPE BSID-CPUDT,   "会計伝票登録日
WAERS TYPE BSID-WAERS,   "通貨コード
XBLNR TYPE BSID-XBLNR,   "参照伝票番号
BLART TYPE BSID-BLART,   "伝票タイプ
MONAT TYPE BSID-MONAT,   "会計期間
BSCHL TYPE BSID-BSCHL,   "転記キー
SHKZG TYPE BSID-SHKZG,   "借方/貸方フラグ
MWSKZ TYPE BSID-MWSKZ,   "税コード
DMBTR TYPE BSID-DMBTR,   "国内通貨額（借方）
WRBTR TYPE BSID-WRBTR,   "伝票通貨額（借方）
DMB_H TYPE BSID-DMBTR,   "国内通貨額（貸方）
WRB_H TYPE BSID-WRBTR,   "伝票通貨額（貸方）
MWSTS TYPE BSID-MWSTS,   "国内通貨税額
WMWST TYPE BSID-WMWST,   "伝票通貨税額
SAKNR TYPE BSID-SAKNR,   "G/L 勘定コード
HKONT TYPE BSID-HKONT,   "総勘定元帳勘定
ZFBDT TYPE BSID-ZFBDT,   "期日計算の支払基準日
ZTERM TYPE BSID-ZTERM,   "支払条件キー
SGTXT TYPE BSID-SGTXT,   "明細テキスト
END   OF T_DOWNLOAD.

*-- マスタ取得用
TYPES: BEGIN OF T_MASTER,
KUNNR TYPE KNA1-KUNNR,   "得意先コード
NAME1 TYPE ADRC-NAME1,   "名称1
END   OF T_MASTER.

*----------------------------------------------------------------------*
*   構造定義
*----------------------------------------------------------------------*
*-- ダウンロード用
DATA: W_DOWNLOAD TYPE T_DOWNLOAD.
*-- マスタ取得用
DATA: W_MASTER   TYPE T_MASTER.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*
*-- ダウンロード用
DATA: I_DOWNLOAD TYPE TABLE OF T_DOWNLOAD.
*-- マスタ取得用
DATA: I_MASTER   TYPE TABLE OF T_MASTER.

*----------------------------------------------------------------------*
*   変数定義
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
*-- 得意先コード
SELECT-OPTIONS: S_KUNNR FOR KNA1-KUNNR.
*-- 実行日
PARAMETERS: P_PASS(255) TYPE C LOWER CASE.

************************************************************************
INITIALIZATION.
************************************************************************
************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_PASS.
************************************************************************
*-- ファイルダウンロード検索ヘルプ
PERFORM FRM_CHK_FILE.

************************************************************************
AT SELECTION-SCREEN.
************************************************************************
************************************************************************
START-OF-SELECTION.
************************************************************************
*-- 初期処理
PERFORM INT_SEC.
*-- メイン処理
PERFORM MAIN_SEC.
*-- 終了処理
PERFORM END_SEC.
*&---------------------------------------------------------------------*
*&      Form  INT_SEC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INT_SEC.
CLEAR:W_DOWNLOAD,W_MASTER,I_DOWNLOAD,I_MASTER.

ENDFORM.                    " INT_SEC
*&---------------------------------------------------------------------*
*&      Form  MAIN_SEC
*&---------------------------------------------------------------------*
*       メイン処理
*----------------------------------------------------------------------*
FORM MAIN_SEC.
DATA:LW_LINE   TYPE CHAR35.
FIELD-SYMBOLS:<LF_DOWNLOAD> TYPE T_DOWNLOAD.

*-- 特殊仕訳以外の債権残高を全取得
SELECT
BUKRS   "会社コード
KUNNR   "得意先コード
*        NAME1   "仕入先名称
UMSKS   "特殊仕訳取引タイプ
UMSKZ   "特殊仕訳コード
AUGDT   "決済日付
AUGBL   "決済伝票番号
*        ZUONR   "ソートキー
GJAHR   "会計年度
BELNR   "会計伝票番号
BUZEI   "会計伝票内の明細番号
BUDAT   "伝票の転記日付
BLDAT   "伝票日付
CPUDT   "会計伝票登録日
WAERS   "通貨コード
XBLNR   "参照伝票番号
BLART   "伝票タイプ
MONAT   "会計期間
BSCHL   "転記キー
SHKZG   "借方/貸方フラグ
MWSKZ   "税コード
DMBTR   "国内通貨額（借方）
WRBTR   "伝票通貨額（借方）
*        DMB_H   "国内通貨額（貸方）
*        WRB_H   "伝票通貨額（貸方）
MWSTS   "国内通貨税額
WMWST   "伝票通貨税額
SAKNR   "G/L 勘定コード
HKONT   "総勘定元帳勘定
ZFBDT   "期日計算の支払基準日
ZTERM   "支払条件キー
SGTXT   "明細テキスト
FROM  BSID
INTO  CORRESPONDING FIELDS OF TABLE I_DOWNLOAD
WHERE  BUKRS = 'C001'
AND  KUNNR IN S_KUNNR
AND  UMSKS = SPACE
AND  UMSKZ = SPACE.
* 対象データ無し
IF SY-SUBRC <> 0.
MESSAGE S762(YN01).
EXIT.
ENDIF.

*-- 得意先マスタ名称取得
SELECT KNA1~KUNNR
ADRC~NAME1
FROM KNA1 INNER JOIN ADRC
ON KNA1~ADRNR = ADRC~ADDRNUMBER
INTO TABLE I_MASTER
FOR ALL ENTRIES IN I_DOWNLOAD
WHERE KNA1~KUNNR = I_DOWNLOAD-KUNNR.

IF SY-SUBRC = 0.
SORT I_MASTER.
ENDIF.

*-- 出力順ソート
SORT I_DOWNLOAD ASCENDING BY BUKRS KUNNR BUDAT HKONT.

*-- 編集
LOOP AT I_DOWNLOAD ASSIGNING <LF_DOWNLOAD>.

*-- 仕入先名称編集
CLEAR:W_MASTER.
READ TABLE I_MASTER INTO W_MASTER
WITH KEY KUNNR = <LF_DOWNLOAD>-KUNNR
BINARY SEARCH.

IF SY-SUBRC = 0.
CALL FUNCTION 'TEXT_SPLIT'
EXPORTING
LENGTH        = 35
TEXT          = W_MASTER-NAME1
IMPORTING
LINE          = LW_LINE.
<LF_DOWNLOAD>-NAME1 = LW_LINE.
ENDIF.

*-- 金額設定
IF ( <LF_DOWNLOAD>-SHKZG = 'H' ).  "貸方の場合金額移動
<LF_DOWNLOAD>-DMB_H = <LF_DOWNLOAD>-DMBTR.
<LF_DOWNLOAD>-WRB_H = <LF_DOWNLOAD>-WRBTR.
<LF_DOWNLOAD>-DMBTR = 0.
<LF_DOWNLOAD>-WRBTR = 0.
ENDIF.

ENDLOOP.

ENDFORM.                    " MAIN_SEC
*&---------------------------------------------------------------------*
*&      Form  END_SEC
*&---------------------------------------------------------------------*
*       終了処理
*----------------------------------------------------------------------*
FORM END_SEC.
DATA:L_PASS TYPE STRING.

L_PASS = P_PASS.

CHECK NOT ( I_DOWNLOAD[] IS INITIAL ).

CALL FUNCTION 'GUI_DOWNLOAD'
EXPORTING
*     BIN_FILESIZE                  =
FILENAME                      = L_PASS
FILETYPE                       = 'ASC'
*     APPEND                        = ' '
WRITE_FIELD_SEPARATOR         = 'X'
*     HEADER                        = '00'
*     TRUNC_TRAILING_BLANKS         = ' '
*     WRITE_LF                      = 'X'
*     COL_SELECT                    = ' '
*     COL_SELECT_MASK               = ' '
*   IMPORTING
*     FILELENGTH                    =
TABLES
DATA_TAB                      = I_DOWNLOAD
EXCEPTIONS
FILE_WRITE_ERROR              = 1
NO_BATCH                      = 2
GUI_REFUSE_FILETRANSFER       = 3
INVALID_TYPE                  = 4
NO_AUTHORITY                  = 5
UNKNOWN_ERROR                 = 6
HEADER_NOT_ALLOWED            = 7
SEPARATOR_NOT_ALLOWED         = 8
FILESIZE_NOT_ALLOWED          = 9
HEADER_TOO_LONG               = 10
DP_ERROR_CREATE               = 11
DP_ERROR_SEND                 = 12
DP_ERROR_WRITE                = 13
UNKNOWN_DP_ERROR              = 14
ACCESS_DENIED                 = 15
DP_OUT_OF_MEMORY              = 16
DISK_FULL                     = 17
DP_TIMEOUT                    = 18
FILE_NOT_FOUND                = 19
DATAPROVIDER_EXCEPTION        = 20
CONTROL_FLUSH_ERROR           = 21
OTHERS                        = 22
.
IF SY-SUBRC <> 0.
MESSAGE I000 WITH 'ダウンロードに失敗しました'.
ELSE.
MESSAGE I000 WITH 'ダウンロードが終了しました'.
ENDIF.


ENDFORM.                    " END_SEC
*&---------------------------------------------------------------------*
*&      Form  FRM_CHK_FILE
*&---------------------------------------------------------------------*
*       ファイルダウンロード検索ヘルプ
*----------------------------------------------------------------------*
FORM FRM_CHK_FILE.
CALL FUNCTION 'WS_FILENAME_GET'
EXPORTING
MASK     = '*.*,ALL Files,*.*. '
MODE     = 'S'
TITLE    = 'ダウンロード'
IMPORTING
FILENAME = P_PASS
EXCEPTIONS
OTHERS   = 5.
IF SY-SUBRC <> 0.
ENDIF.
ENDFORM.                    " FRM_CHK_FILE
