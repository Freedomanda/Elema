*----------------------------------------------------------------------*
*  勘定コード明細照会(部門用)                                          *
*    機能：勘定コード明細照会（FBL3N）について、                       *
*          各部門のみ情報照会を可能とする                              *
*                                                                      *
*    作成：2011/06/06 SOLFIS JO                                        *
*----------------------------------------------------------------------*
REPORT  ZF022300.

* 使用テーブル
TABLES SKB1.
TABLES BSIS.

* グローバル変数
DATA W_KOSTL TYPE CSKS-KOSTL.  "原価センタ
DATA W_OPSEL TYPE C.           "未決済明細
DATA W_AISEL TYPE C.           "全明細

* コンスタント
CONSTANTS C_BUKRS(4) TYPE C VALUE 'C001'.     "会社コード
CONSTANTS C_KOSTL(3) TYPE C VALUE 'KOS'.      "原価センタID
CONSTANTS C_X        TYPE C VALUE 'X'.
CONSTANTS C_VARI(8)  TYPE C VALUE 'ZF022300'. "バリアント

* 選択画面
* 勘定コード選択
SELECTION-SCREEN BEGIN OF BLOCK B01
* フレームのタイトル設定(『勘定コード選択』)
WITH FRAME TITLE TEXT-001.
* 勘定コード
SELECT-OPTIONS: S_SAKNR FOR SKB1-SAKNR.
SELECTION-SCREEN END   OF BLOCK B01.

* 明細選択
SELECTION-SCREEN BEGIN OF BLOCK B02
* フレームのタイトル設定(『明細選択』)
WITH FRAME TITLE TEXT-002.

* ステータス
SELECTION-SCREEN BEGIN OF BLOCK B03
* フレームのタイトル設定(『ステータス』)
WITH FRAME TITLE TEXT-003.

* 未決済明細
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: P_RAD01 RADIOBUTTON GROUP R1.
SELECTION-SCREEN COMMENT 3(20) TEXT-004 FOR FIELD P_RAD01.
SELECTION-SCREEN END   OF LINE.

* 基準日未決済明細
PARAMETERS: P_STIDA TYPE RFPDO-ALLGSTID DEFAULT SY-DATUM.

SELECTION-SCREEN SKIP.

* 全明細
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: P_RAD02 RADIOBUTTON GROUP R1
DEFAULT 'X'.
SELECTION-SCREEN COMMENT 3(20) TEXT-005 FOR FIELD P_RAD02.
SELECTION-SCREEN END   OF LINE.

* 転記日付
SELECT-OPTIONS: S_BUDAT FOR BSIS-BUDAT.

SELECTION-SCREEN END   OF BLOCK B03.

SELECTION-SCREEN END   OF BLOCK B02.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN.
* 勘定コードチェック
PERFORM F_CHECK_SAKNR.

* 原価センタ取得
PERFORM F_GET_KOSTL.

*----------------------------------------------------------------------*
START-OF-SELECTION.
* 勘定コード明細照会(RFITEMGL)起動
PERFORM F_SUBMIT.

*&---------------------------------------------------------------------*
*&      Form  F_CHECK_SAKNR
*&---------------------------------------------------------------------*
*       勘定コードチェック
*----------------------------------------------------------------------*
FORM F_CHECK_SAKNR.
DATA: L_SAKNR TYPE SKB1-SAKNR.

SELECT SINGLE SAKNR
INTO L_SAKNR
FROM SKB1
WHERE BUKRS = C_BUKRS      "会社コード
AND SAKNR IN S_SAKNR.    "勘定コード

* 勘定コードが存在しない場合
IF L_SAKNR IS INITIAL.
MESSAGE E400(Z1) WITH TEXT-M01.
ENDIF.

ENDFORM.                    " F_CHECK_SAKNR
*&---------------------------------------------------------------------*
*&      Form  F_GET_KOSTL
*&---------------------------------------------------------------------*
*       ユーザパラメータから原価センタ取得
*----------------------------------------------------------------------*
FORM F_GET_KOSTL.
CLEAR:
W_KOSTL.

* 原価センタ
GET PARAMETER ID C_KOSTL FIELD W_KOSTL.

* ユーザパラメータに原価センタが設定されていない場合
IF W_KOSTL IS INITIAL.
MESSAGE E400(Z1) WITH TEXT-M02.
ENDIF.

ENDFORM.                    " F_GET_KOSTL
*&---------------------------------------------------------------------*
*&      Form  F_SUBMIT
*&---------------------------------------------------------------------*
*       勘定コード明細照会(RFITEMGL)起動
*----------------------------------------------------------------------*
FORM F_SUBMIT.
CLEAR: W_OPSEL, W_AISEL.

* 明細選択
IF P_RAD01 = C_X.
W_OPSEL = C_X.  "未決済明細
ELSE.
W_AISEL = C_X.  "全明細
ENDIF.

* SUBMIT実行
SUBMIT RFITEMGL                    "レポート
USING SELECTION-SET C_VARI  "バリアント
WITH SD_SAKNR IN S_SAKNR    "勘定コード
WITH X_OPSEL  EQ W_OPSEL    "未決済明細
WITH PA_STIDA EQ P_STIDA    "基準日未決済明細
WITH X_AISEL  EQ W_AISEL    "全明細
WITH SO_BUDAT IN S_BUDAT    "転記日付
WITH X_NORM   EQ C_X        "タイプ(標準明細)
WITH SD_KOSTL EQ W_KOSTL    "原価センタ
AND RETURN.

ENDFORM.                    " F_SUBMIT
