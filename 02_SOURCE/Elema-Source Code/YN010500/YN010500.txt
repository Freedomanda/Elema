*&---------------------------------------------------------------------*
*& Report YN010500                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*
*& プログラム：支払保留解除                                            *
*& 作者      ：梁憶                                                    *
*& 作成日期  ：2006/06/22                                              *
*& 管理番号  ：                                                        *
*&---------------------------------------------------------------------*
*& 処理概要&                                                           *
*& 自社データの照合ステータスをもとに、未決済明細(会計伝票)の支払保留の*
*& オン/オフを行う                                                     *
*&---------------------------------------------------------------------*
*& 更新履歴 ：2007/02/28 Vertion2 H.Nakayama
*& 更新内容 ：Version2 (2007/02/28 H.Nakayama)
*&          ：・選択画面に入力された支払保留のみを対象とする
*&          ：・選択画面にコメント/照合番号/照合日/照合ユーザ追加
*& 変更履歴 ： 2009/02/02
*& 変更内容 ： テンプレート改善ＰＪ (2009/01/13 OODUCHI)
*&          ：・画面定義をYN010400に合わせる
*&---------------------------------------------------------------------*
REPORT YN010500 MESSAGE-ID YN01.

*-- 構築定義 --
* データエレメント取得用
TYPES: BEGIN OF GTS_DATAELEMENT,
FLDNM    LIKE DD03L-FIELDNAME,              "項目名
ROLLNM   LIKE DD03L-ROLLNAME,               "データエレメント
END OF GTS_DATAELEMENT.
* 画面項目名称取得用
TYPES: BEGIN OF GTS_SCRINFO,
FLDNM    LIKE DD03L-FIELDNAME,              "項目名
CAPNM    LIKE DFIES-SCRTEXT_L,              "画面項目名称
END OF GTS_SCRINFO.
* 会社コードチェック用
TYPES: BEGIN OF GTS_BUKRS,
BUKRS    LIKE T001-BUKRS,                   "会社コード
END OF GTS_BUKRS.
* 自社データ格納用
TYPES: BEGIN OF GTS_JISYADATA,
BUKRS    LIKE YN120-BUKRS,                  "会社コード
VRFCTON  LIKE YN120-VRFCTON,                "得意先コード
YNGJAHR  LIKE YN120-YNGJAHR,                "会計伝票会計年度
BELNR    LIKE YN120-BELNR,                  "会計伝票番号
BUZEI    LIKE YN120-BUZEI,                  "会計伝票明細番号
CSTS     LIKE YN120-CSTS,                   "照合ステータス
ZLSPR    LIKE BSID-ZLSPR,
END OF GTS_JISYADATA.
* 支払保留退避用
TYPES: BEGIN OF GTS_SHHRKJ,
BUKRS    LIKE YN120-BUKRS,                  "会社コード
VRFCTON  LIKE YN120-VRFCTON,                "得意先コード
YNGJAHR  LIKE YN120-YNGJAHR,                "会計伝票会計年度
BELNR    LIKE YN120-BELNR,                  "会計伝票番号
BUZEI    LIKE YN120-BUZEI,                  "会計伝票明細番号
SHHR_FLG LIKE BSIK-ZLSPR,                   "支払保留FLG
ZLSPR_OLD LIKE BSIK-ZLSPR,
END OF GTS_SHHRKJ.

*-- テーブル定義 --
TABLES: YN120,                                       "自社データ(売上)
BDCMSGCOLL.                         "SAPシステムのメッセージ収集

*-- 内部テーブル定義 --
DATA: GDT_DATAELEMENT TYPE STANDARD TABLE OF GTS_DATAELEMENT,
"データエレメント取得用
GDT_SCRINFO     TYPE STANDARD TABLE OF GTS_SCRINFO,
"画面項目名称取得用
GDT_JISYADATA   TYPE STANDARD TABLE OF GTS_JISYADATA,
"自社データ格納用
GDT_BUKRS       TYPE STANDARD TABLE OF GTS_BUKRS,
"会社コードチェック用
GDT_SHHRKJ      TYPE STANDARD TABLE OF GTS_SHHRKJ,
"支払保留退避用
GDT_BDCDATA     LIKE STANDARD TABLE OF BDCDATA,
"バッチインプット処理用
GDT_MSG         LIKE STANDARD TABLE OF BDCMSGCOLL.
"結果MSG格納用
*-- 20090202 INS STA
TYPES: TYP_T_DD03P TYPE STANDARD TABLE OF DD03P.

* エレメント内部テーブル
DATA: T_DD03P_YN110 TYPE TYP_T_DD03P,
T_DD03P_YN120 TYPE TYP_T_DD03P,
T_DD03P_YN210 TYPE TYP_T_DD03P,
T_DD03P_YN220 TYPE TYP_T_DD03P.
* 売上／仕入区分
CONSTANTS: C_MODE_SALES                 TYPE C VALUE '1',
C_MODE_PURCHASE              TYPE C VALUE '2'.
* 売上／仕入区分
DATA: P_MODE TYPE C.
*-- 20090202 INS END

*-- 作業領域定義 --
DATA: GDS_DATAELEMENT TYPE GTS_DATAELEMENT,      "データエレメント取得用
GDS_SCRINFO     TYPE GTS_SCRINFO,              "画面項目名称取得用
GDS_JISYADATA   TYPE GTS_JISYADATA,            "自社データ格納用
GDS_BUKRS       TYPE GTS_BUKRS,              "会社コードチェック用
GDS_SHHRKJ      TYPE GTS_SHHRKJ,               "支払保留退避用
GDS_BDCDATA     LIKE BDCDATA,              "バッチインプット処理用
GDS_MSG         LIKE BDCMSGCOLL.               "結果MSG格納用

*-- 変数定義 --
DATA: GDF_JISYA_TBL(5)        TYPE C,                "自社テーブル
GDF_NJSI_TBL(4)         TYPE C,        "会計管理: 二次索引テーブル
GDF_JISYA_LINE          LIKE SY-TABIX,         "自社情報件数
GDF_TAISYO_KENSU        LIKE SY-TABIX,         "対象件数
GDF_TAISYO_GAI_KENSU    LIKE SY-TABIX,         "対象外件数
GDF_SHHR_OFF_KENSU      LIKE SY-TABIX,         "支払保留オフ件数
GDF_SHHR_ON_KENSU       LIKE SY-TABIX,         "支払保留オン件数
GDF_ERROR_KENSU         LIKE SY-TABIX,         "エラー件数
GDF_ALL_TAISYO_KENSU    LIKE SY-TABIX,         "対象件数の合計
GDF_ALL_TAISYO_GAI_KENSU LIKE SY-TABIX,        "対象外件数の合計
GDF_ALL_SHHR_OFF_KENSU  LIKE SY-TABIX,     "支払保留オフ件数の合計
GDF_ALL_SHHR_ON_KENSU   LIKE SY-TABIX,     "支払保留オン件数の合計
GDF_ALL_ERROR_KENSU     LIKE SY-TABIX.         "エラー件数の合計

*-- 定数定義 --
CONSTANTS: GCF_C_X(1)         TYPE C VALUE 'X',      "'X'
GCF_C_N(1)         TYPE C VALUE 'N',      "'N'
GCF_C_Y(1)         TYPE C VALUE 'Y',      "'Y'
GCF_C_SEVEN(1)     TYPE C VALUE '7',      "'7'
GCF_C_EIGHT(1)     TYPE C VALUE '8',      "'8'
GCF_I_ZERO         TYPE I VALUE '0',      "0
GCF_I_ONE          TYPE I VALUE '1',      "1
GCF_I_TNY_SEVEN    TYPE I VALUE '27',     "27
GCF_MSGNO(3)       TYPE C VALUE '300',    "'300'
GCF_TBL_DD03L(5)   TYPE C VALUE 'DD03L',  "'DD03L'
GCF_JISYA_TBL_U(5) TYPE C VALUE 'YN120',  "自社データ(売上)
GCF_JISYA_TBL_S(5) TYPE C VALUE 'YN220',  "自社データ(仕入)
GCF_NJSI_TBL_U(4)  TYPE C VALUE 'BSID',
"会計管理: 得意先の二次索引
GCF_NJSI_TBL_S(4)  TYPE C VALUE 'BSIK',
"会計管理: 仕入先の二次索引
GCF_SAPMF05L(8)    TYPE C VALUE 'SAPMF05L',
"'SAPMF05L'
GCF_0102(4)        TYPE C VALUE '0102',   "'0102'
GCF_RF05LBELNR(11) TYPE C VALUE 'RF05L-BELNR',
"'RF05L-BELNR'
GCF_RF05LBUKRS(11) TYPE C VALUE 'RF05L-BUKRS',
"'RF05L-BUKRS'
GCF_RF05LGJAHR(11) TYPE C VALUE 'RF05L-GJAHR',
"'RF05L-GJAHR'
GCF_RF05LBUZEI(11) TYPE C VALUE 'RF05L-BUZEI',
"'RF05L-BUZEI'
GCF_RF05LXKKRE(11) TYPE C VALUE 'RF05L-XKKRE',
"'RF05L-XKKRE'
GCF_BDCOKCODE(10)  TYPE C VALUE 'BDC_OKCODE',
"'BDC_OKCODE'
GCF_00(3)          TYPE C VALUE '/00',    "'/00'
GCF_0301(4)        TYPE C VALUE '0301',   "'0301'
GCF_0302(4)        TYPE C VALUE '0302',   "'0302'
GCF_BSEGZLSPR(10)  TYPE C VALUE 'BSEG-ZLSPR',
"'BSEG-ZLSPR'
GCF_AE(3)          TYPE C VALUE '=AE',    "'=AE'
GCF_VRFCT_LOW      LIKE DYNPREAD-FIELDNAME
VALUE 'P_VRFCT-LOW',    "'P_VRFCT-LOW'
GCF_VRFCT_HIGH     LIKE DYNPREAD-FIELDNAME
VALUE 'P_VRFCT-HIGH',   "'P_VRFCT-HIGH'
GCF_VRFCTON(7)     TYPE C VALUE 'VRFCTON'."'VRFCTON'


*-- 20090202 UPD STA 画面項目定義方法をYN010400と合わせる
*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
* 売上/仕入区分
SELECTION-SCREEN BEGIN OF BLOCK BK1 WITH FRAME TITLE TEXT-S01.
SELECTION-SCREEN BEGIN OF LINE.
* 売上照合
PARAMETERS P_SALES RADIOBUTTON GROUP RAD1 DEFAULT 'X'
USER-COMMAND RADIO.
SELECTION-SCREEN COMMENT 4(18) TEXT-S02.
* 仕入照合
PARAMETERS P_PRCHS RADIOBUTTON GROUP RAD1.
SELECTION-SCREEN COMMENT 26(18) TEXT-S03.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK BK1.

* パラメータ定義、照合対象データの取得
INCLUDE YN01_YN010500_0000.

* 処理オプション
SELECTION-SCREEN BEGIN OF BLOCK BK3 WITH FRAME TITLE TEXT-S05.
* 支払保留
PARAMETERS P_ZLSPR LIKE BSIK-ZLSPR DEFAULT GCF_C_Y
OBLIGATORY.
SELECTION-SCREEN END OF BLOCK BK3.
* バッチインプット
SELECTION-SCREEN BEGIN OF BLOCK BK4 WITH FRAME TITLE TEXT-S07.
* 実行モード
PARAMETERS P_DMODE LIKE CTU_PARAMS-DISMODE DEFAULT GCF_C_N.
* メッセージ番号
SELECT-OPTIONS P_MSGNO FOR BDCMSGCOLL-MSGNR DEFAULT GCF_MSGNO.
SELECTION-SCREEN END OF BLOCK BK4.
*-- 20090202 UPD END 画面項目定義方法をYN010400と合わせる

*-- 20090202 INS STA 画面項目定義方法をYN010400と合わせる
*&---------------------------------------------------------------------*
*&   Event LOAD-Of-PROGRAM
*&---------------------------------------------------------------------*
LOAD-Of-PROGRAM.

* 出力項目定義取得
PERFORM GET_TABLE_FIELDS.
*-- 20090202 INS END 画面項目定義方法をYN010400と合わせる

*&---------------------------------------------------------------------*
*& EVENT INITIALIZATION                                                *
*&---------------------------------------------------------------------*
INITIALIZATION.
* 初期処理
PERFORM INIT.

*&---------------------------------------------------------------------*
*&   Event AT SELECTION-SCREEN ON VALUE-REQUEST
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VRFCT-LOW.
PERFORM RUNTIME_VRFCT_F4HELP_CHANGE USING GCF_VRFCT_LOW.

*&---------------------------------------------------------------------*
*&   Event AT SELECTION-SCREEN ON VALUE-REQUEST
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VRFCT-HIGH.
PERFORM RUNTIME_VRFCT_F4HELP_CHANGE USING GCF_VRFCT_HIGH.

*&---------------------------------------------------------------------*
*& EVENT AT SELECTION-SCREEN                                           *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
CASE SY-UCOMM.
WHEN 'RADIO'.
*-- 20090202 UPD STA 画面項目定義方法をYN010400と合わせる
* 選択画面の項目名称の再設定
*      PERFORM CAPTION_SET.
*-- 20090202 UPD END 画面項目定義方法をYN010400と合わせる
WHEN OTHERS.
* 入力チェック
PERFORM CHECK.
ENDCASE.

*&---------------------------------------------------------------------*
*& EVENT AT SELECTION-SCREEN OUTPUT                                    *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
*-- 20090202 UPD STA 画面項目定義方法をYN010400と合わせる
IF P_SALES = 'X'.
P_MODE = C_MODE_SALES.
ELSEIF P_PRCHS = 'X'.
P_MODE = C_MODE_PURCHASE.
ENDIF.
* 選択画面の項目名称の初期設定
*  PERFORM CAPTION_SET.
PERFORM SET_DYNPRO_TEXT.
*-- 20090202 UPD END 画面項目定義方法をYN010400と合わせる

*&---------------------------------------------------------------------*
*& EVENT AT START-OF-SELECTION                                         *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* 照合対象データの取得
PERFORM SYOGO_DATA_GET.
* 支払保留解除処理
PERFORM SHHRKJ_PROCESS.

*&---------------------------------------------------------------------*
*& EVENT TOP-OF-PAGE                                                   *
*&---------------------------------------------------------------------*
TOP-OF-PAGE.
* タイトルの出力
PERFORM TITLE_PRINT.

*&---------------------------------------------------------------------*
*& EVENT END-OF-PAGE                                                   *
*&---------------------------------------------------------------------*
END-OF-PAGE.
* ページ数の出力
PERFORM PAGE_CNT_PRINT.

*&---------------------------------------------------------------------*
*&      Form  INIT
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INIT.
* 初期化
CLEAR: GDS_DATAELEMENT,
GDS_SCRINFO,
GDS_JISYADATA,
GDS_BUKRS,
GDS_SHHRKJ,
GDS_BDCDATA,
GDS_MSG.

REFRESH: GDT_DATAELEMENT,
GDT_SCRINFO,
GDT_JISYADATA,
GDT_BUKRS,
GDT_SHHRKJ,
GDT_BDCDATA,
GDT_MSG.

GDF_JISYA_LINE          = GCF_I_ZERO.
GDF_TAISYO_KENSU        = GCF_I_ZERO.
GDF_SHHR_OFF_KENSU      = GCF_I_ZERO.
GDF_SHHR_ON_KENSU       = GCF_I_ZERO.
GDF_ERROR_KENSU         = GCF_I_ZERO.
GDF_ALL_TAISYO_KENSU    = GCF_I_ZERO.
GDF_ALL_SHHR_OFF_KENSU  = GCF_I_ZERO.
GDF_ALL_SHHR_ON_KENSU   = GCF_I_ZERO.
GDF_ALL_ERROR_KENSU     = GCF_I_ZERO.
ENDFORM.                    " INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK
*&---------------------------------------------------------------------*
*       入力チェック
*----------------------------------------------------------------------*
FORM CHECK.
* 会社コードのチェック
IF P_BUKRS-LOW <> SPACE.
SELECT COUNT(*)                                  "会社コード
FROM T001
WHERE BUKRS = P_BUKRS-LOW.
IF SY-SUBRC <> 0.
MESSAGE E750 WITH P_BUKRS-LOW.
ENDIF.
ENDIF.
SELECT BUKRS                                       "会社コード
FROM T001
INTO TABLE GDT_BUKRS
WHERE BUKRS IN P_BUKRS.
IF SY-SUBRC <> 0.
MESSAGE E762.
ENDIF.
* 権限チェック
IF P_SALES = GCF_C_X.
LOOP AT GDT_BUKRS
INTO GDS_BUKRS.
AUTHORITY-CHECK OBJECT 'F_KNA1_BUK'
ID 'BUKRS' FIELD GDS_BUKRS-BUKRS
ID 'ACTVT' DUMMY.
IF SY-SUBRC <> 0.
MESSAGE E751 WITH GDS_BUKRS-BUKRS.
ENDIF.
ENDLOOP.
ENDIF.
IF P_PRCHS = GCF_C_X.
LOOP AT GDT_BUKRS
INTO GDS_BUKRS.
AUTHORITY-CHECK OBJECT 'F_LFA1_BUK'
ID 'BUKRS' FIELD GDS_BUKRS-BUKRS
ID 'ACTVT' DUMMY.
IF SY-SUBRC <> 0.
MESSAGE E751 WITH GDS_BUKRS-BUKRS.
ENDIF.
ENDLOOP.
ENDIF.
* 取引先コードのチェック
DATA: L_VRFCT LIKE KNB1-KUNNR.
PERFORM CHECK_VRFCTON_RANGE TABLES   P_VRFCT
CHANGING L_VRFCT.
IF NOT L_VRFCT IS INITIAL.
IF P_SALES = 'X'.
MESSAGE E752 WITH L_VRFCT.
ENDIF.
IF P_PRCHS = 'X'.
MESSAGE E753 WITH L_VRFCT.
ENDIF.
ENDIF.
* 利益センタのチェック
IF P_PRCTR-LOW <> SPACE.
SELECT COUNT(*)                                  "利益センタ
FROM CEPC AS A
INNER JOIN TKA02 AS B
ON A~KOKRS = B~KOKRS
WHERE A~PRCTR = P_PRCTR-LOW
AND B~BUKRS IN P_BUKRS.
IF SY-SUBRC <> 0.
MESSAGE E761 WITH P_PRCTR-LOW.
ENDIF.
ENDIF.
ENDFORM.                    " CHECK

*&---------------------------------------------------------------------*
*&      Form  SHHRKJ_PROCESS
*&---------------------------------------------------------------------*
*       支払保留解除処理
*----------------------------------------------------------------------*
FORM SHHRKJ_PROCESS.
* ソート
SORT GDT_JISYADATA BY BUKRS
VRFCTON
YNGJAHR
BELNR
BUZEI.
* 支払保留退避用内部テーブルの作成
LOOP AT GDT_JISYADATA
INTO GDS_JISYADATA.
AT NEW BUZEI.
CLEAR GDS_SHHRKJ.
GDS_SHHRKJ-BUKRS   = GDS_JISYADATA-BUKRS.
GDS_SHHRKJ-VRFCTON = GDS_JISYADATA-VRFCTON.
GDS_SHHRKJ-YNGJAHR = GDS_JISYADATA-YNGJAHR.
GDS_SHHRKJ-BELNR   = GDS_JISYADATA-BELNR.
GDS_SHHRKJ-BUZEI   = GDS_JISYADATA-BUZEI.
ENDAT.

GDS_SHHRKJ-ZLSPR_OLD = GDS_JISYADATA-ZLSPR.
IF GDS_JISYADATA-CSTS <> GCF_C_SEVEN AND
GDS_JISYADATA-CSTS <> GCF_C_EIGHT.
*&Ver2 対応 2007/02/28 >>>
*      GDS_SHHRKJ-SHHR_FLG = P_ZLSPR.
IF GDS_JISYADATA-ZLSPR = ''.
GDS_SHHRKJ-SHHR_FLG = P_ZLSPR.
ELSE.
GDS_SHHRKJ-SHHR_FLG = GDS_JISYADATA-ZLSPR.
ENDIF.
*&Ver2 対応 2007/02/28 <<<
ENDIF.

AT END OF BUZEI.
IF GDS_SHHRKJ-SHHR_FLG <> GDS_JISYADATA-ZLSPR.
APPEND GDS_SHHRKJ TO GDT_SHHRKJ.
ENDIF.
ENDAT.
ENDLOOP.
* バッチインプット処理を行う
LOOP AT GDT_SHHRKJ
INTO GDS_SHHRKJ.
AT NEW VRFCTON.
GDF_TAISYO_KENSU     = GCF_I_ZERO.
GDF_TAISYO_GAI_KENSU = GCF_I_ZERO.
GDF_SHHR_OFF_KENSU   = GCF_I_ZERO.
GDF_SHHR_ON_KENSU    = GCF_I_ZERO.
GDF_ERROR_KENSU      = GCF_I_ZERO.
ENDAT.

*&Ver2 対応 2007/02/28 >>>
*    IF GDS_SHHRKJ-SHHR_FLG <> GDS_SHHRKJ-ZLSPR_OLD.
IF GDS_SHHRKJ-SHHR_FLG <> GDS_SHHRKJ-ZLSPR_OLD AND
( GDS_SHHRKJ-ZLSPR_OLD = P_ZLSPR OR
GDS_SHHRKJ-ZLSPR_OLD = '' ).
*&Ver2 対応 2007/02/28 <<<

*   BDCデータ作成
REFRESH GDT_BDCDATA.

GDS_BDCDATA-PROGRAM   = GCF_SAPMF05L.
GDS_BDCDATA-DYNPRO    = GCF_0102.
GDS_BDCDATA-DYNBEGIN  = GCF_C_X.
GDS_BDCDATA-FNAM      = ''.
GDS_BDCDATA-FVAL      = ''.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM      = GCF_RF05LBELNR.
GDS_BDCDATA-FVAL      = GDS_SHHRKJ-BELNR.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM      = GCF_RF05LBUKRS.
GDS_BDCDATA-FVAL      = GDS_SHHRKJ-BUKRS.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM      = GCF_RF05LGJAHR.
GDS_BDCDATA-FVAL      = GDS_SHHRKJ-YNGJAHR.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM      = GCF_RF05LBUZEI.
GDS_BDCDATA-FVAL      = GDS_SHHRKJ-BUZEI.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM      = GCF_RF05LXKKRE.
GDS_BDCDATA-FVAL      = GCF_C_X.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM = GCF_BDCOKCODE.
GDS_BDCDATA-FVAL = GCF_00.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = GCF_SAPMF05L.
IF P_SALES = GCF_C_X.
GDS_BDCDATA-DYNPRO  = GCF_0301.
ENDIF.
IF P_PRCHS = GCF_C_X.
GDS_BDCDATA-DYNPRO  = GCF_0302.
ENDIF.
GDS_BDCDATA-DYNBEGIN  = GCF_C_X.
GDS_BDCDATA-FNAM      = ''.
GDS_BDCDATA-FVAL      = ''.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM = GCF_BSEGZLSPR.
GDS_BDCDATA-FVAL = GDS_SHHRKJ-SHHR_FLG.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

CLEAR GDS_BDCDATA.
GDS_BDCDATA-PROGRAM   = ''.
GDS_BDCDATA-DYNPRO    = ''.
GDS_BDCDATA-DYNBEGIN  = ''.
GDS_BDCDATA-FNAM = GCF_BDCOKCODE.
GDS_BDCDATA-FVAL = GCF_AE.
APPEND GDS_BDCDATA TO GDT_BDCDATA.

REFRESH GDT_MSG.
*   トランザクション起動
CALL TRANSACTION 'FB09' USING GDT_BDCDATA
MODE P_DMODE
UPDATE 'S'               "同期モードのみ
MESSAGES INTO GDT_MSG.
*   件数のカウント
LOOP AT GDT_MSG
INTO GDS_MSG.
IF NOT GDS_MSG-MSGNR IN P_MSGNO.
GDF_ERROR_KENSU = GDF_ERROR_KENSU + 1.
GDF_ALL_ERROR_KENSU = GDF_ALL_ERROR_KENSU + 1.
ELSE.
IF GDS_SHHRKJ-SHHR_FLG IS INITIAL.
GDF_SHHR_OFF_KENSU = GDF_SHHR_OFF_KENSU + 1.
GDF_ALL_SHHR_OFF_KENSU = GDF_ALL_SHHR_OFF_KENSU + 1.
ELSE.
GDF_SHHR_ON_KENSU = GDF_SHHR_ON_KENSU + 1.
GDF_ALL_SHHR_ON_KENSU = GDF_ALL_SHHR_ON_KENSU + 1.
ENDIF.
ENDIF.

EXIT.
ENDLOOP.

GDF_TAISYO_KENSU = GDF_TAISYO_KENSU + 1.
GDF_ALL_TAISYO_KENSU = GDF_ALL_TAISYO_KENSU + 1.

ELSE.
GDF_TAISYO_GAI_KENSU = GDF_TAISYO_GAI_KENSU + 1.
GDF_ALL_TAISYO_GAI_KENSU = GDF_ALL_TAISYO_GAI_KENSU + 1.
ENDIF.

AT END OF VRFCTON.
* 結果出力
WRITE: /04 GDS_SHHRKJ-BUKRS,                   "会社コード
15 GDS_SHHRKJ-VRFCTON,                 "取引先コード
28 GDF_TAISYO_KENSU,                   "対象件数
41 GDF_TAISYO_GAI_KENSU,               "対象外件数
56 GDF_SHHR_ON_KENSU,                  "支払保留オン件数
69 GDF_SHHR_OFF_KENSU,                 "支払保留オフ件数
80 GDF_ERROR_KENSU.                    "エラー件数
ENDAT.
ENDLOOP.

SKIP.
WRITE: /21 TEXT-S17,                               "合計：
28 GDF_ALL_TAISYO_KENSU,                   "対象件数の合計
41 GDF_ALL_TAISYO_GAI_KENSU,               "対象外件数の合計
56 GDF_ALL_SHHR_ON_KENSU,              "支払保留オン件数の合計
69 GDF_ALL_SHHR_OFF_KENSU,             "支払保留オフ件数の合計
80 GDF_ALL_ERROR_KENSU.                    "エラー件数の合計

*  RESERVE GCF_I_TNY_SEVEN LINES.                     "改ページ

MESSAGE S765.
ENDFORM.                    " SHHRKJ_PROCESS

*&---------------------------------------------------------------------*
*&      Form TITLE_PRINT
*&---------------------------------------------------------------------*
*       タイトルの出力
*----------------------------------------------------------------------*
FORM TITLE_PRINT.
WRITE: /04 TEXT-S10,                               "'会社コード'
15 TEXT-S11,                               "'取引先コード'
30 TEXT-S12,                               "'対象件数'
41 TEXT-S18,                               "'対象外件数'
54 TEXT-S13,                               "'支払保留オン'
67 TEXT-S14,                               "'支払保留オフ'
80 TEXT-S15.                               "'エラー件数'
ULINE.
ENDFORM.                    " TITLE_PRINT

*&---------------------------------------------------------------------*
*&      Form PAGE_CNT_PRINT
*&---------------------------------------------------------------------*
*       ページ数の出力
*----------------------------------------------------------------------*
FORM PAGE_CNT_PRINT.
WRITE: /102 TEXT-S16,                              "'ページ:'
110 SY-PAGNO.                              "ページ数
ENDFORM.                    " PAGE_CNT_PRINT

*&---------------------------------------------------------------------*
*&      Form RUNTIME_VRFCT_F4HELP_CHANGE
*&---------------------------------------------------------------------*
*       ランタイム環境での得意先コード/仕入先コードＦ４ヘルプの変更
*----------------------------------------------------------------------*
FORM RUNTIME_VRFCT_F4HELP_CHANGE USING LDF_FLD_NM.
* ローカル変数
DATA: LDF_TABNAME   LIKE DFIES-TABNAME,
LDF_FIELDNAME LIKE DFIES-FIELDNAME,
LDT_RTN       LIKE STANDARD TABLE OF DDSHRETVAL,
LDS_RTN       LIKE DDSHRETVAL,
LDF_VALUE     LIKE DYNPREAD-FIELDVALUE,
LDT_DYFIELDS  LIKE STANDARD TABLE OF DYNPREAD,
LDS_DYFIELDS  LIKE DYNPREAD.
DATA: L_DFNAME TYPE HELP_INFO-DYNPROFLD,
L_REPID LIKE SY-REPID,
L_DYNNR LIKE SY-DYNNR.
*-- 20090202 INS STA
* 自社データのテーブルＩＤの取得
IF P_SALES = GCF_C_X.
GDF_JISYA_TBL = GCF_JISYA_TBL_U.
ENDIF.
IF P_PRCHS = GCF_C_X.
GDF_JISYA_TBL = GCF_JISYA_TBL_S.
ENDIF.
*-- 20090202 INS END
LDF_TABNAME = GDF_JISYA_TBL.
LDF_FIELDNAME = GCF_VRFCTON.
L_REPID = SY-REPID.
L_DYNNR = SY-DYNNR.
L_DFNAME = LDF_FLD_NM.

CALL FUNCTION 'F4IF_FIELD_VALUE_REQUEST'
EXPORTING
TABNAME                   = LDF_TABNAME
FIELDNAME                 = LDF_FIELDNAME
DYNPPROG                  = L_REPID
DYNPNR                    = L_DYNNR
DYNPROFIELD               = L_DFNAME
TABLES
RETURN_TAB                = LDT_RTN
EXCEPTIONS
FIELD_NOT_FOUND           = 1
NO_HELP_FOR_FIELD         = 2
INCONSISTENT_HELP         = 3
NO_VALUES_FOUND           = 4
OTHERS                    = 5.

IF SY-SUBRC = 0.
READ TABLE LDT_RTN
INTO LDS_RTN
INDEX GCF_I_ONE.
LDF_VALUE = LDS_RTN-FIELDVAL.
ENDIF.

ENDFORM.                    " RUNTIME_VRFCT_F4HELP_CHANGE
*&---------------------------------------------------------------------*
*&      Form  CHECK_VRFCTON_RANGE
*&---------------------------------------------------------------------*
*       取引先コード存在チェック
*----------------------------------------------------------------------*
*      -->PT_VRFCTON  取引先コード
*      <--P_EVAL  text
*----------------------------------------------------------------------*
FORM CHECK_VRFCTON_RANGE  TABLES   PT_VRFCTON
CHANGING P_EVAL.

FIELD-SYMBOLS: <FS1>, <FS2>, <FS3>.

CLEAR: P_EVAL.
LOOP AT PT_VRFCTON.
ASSIGN COMPONENT 1 OF STRUCTURE PT_VRFCTON TO <FS1>.
ASSIGN COMPONENT 2 OF STRUCTURE PT_VRFCTON TO <FS2>.
CHECK <FS1> = 'I' AND <FS2> = 'EQ'.
ASSIGN COMPONENT 3 OF STRUCTURE PT_VRFCTON TO <FS3>.
* [売上照合]が選択された場合
IF P_SALES = 'X'.
SELECT COUNT(*) FROM KNB1
WHERE BUKRS IN P_BUKRS AND KUNNR = <FS3>.
ENDIF.
* [仕入照合]が選択された場合
IF P_PRCHS = 'X'.
SELECT COUNT(*) FROM LFB1
WHERE BUKRS IN P_BUKRS AND LIFNR = <FS3>.
ENDIF.
IF SY-SUBRC <> 0.
P_EVAL = <FS3>.
EXIT.
ENDIF.
ENDLOOP.

ENDFORM.                    " CHECK_VRFCTON_RANGE
*&---------------------------------------------------------------------*
*&      Form  SET_DYNPRO_TEXT
*&---------------------------------------------------------------------*
*       選択画面テキスト動的に設定
*----------------------------------------------------------------------*
FORM SET_DYNPRO_TEXT .

DATA: L_HELP_ID(20) TYPE C,
L_SNAME TYPE STRING,
L_FNAME TYPE STRING,
L_SCR_NAME TYPE STRING,
L_TEXT TYPE STRING.

FIELD-SYMBOLS: <FS> TYPE ANY.

FIELD-SYMBOLS: <FS_T_DD03P> TYPE TYP_T_DD03P.
DATA: L_H_DD03P LIKE LINE OF <FS_T_DD03P>.

CHECK SY-DYNNR = '1000'.
LOOP AT SCREEN.
IF SCREEN-GROUP1 = '020' OR SCREEN-GROUP1 = '010'.
CASE SCREEN-GROUP3.
WHEN 'TXT'.
L_SCR_NAME = SCREEN-NAME.
WHEN 'LOW'.
ASSIGN (SCREEN-NAME) TO <FS>.
DESCRIBE FIELD <FS> HELP-ID L_HELP_ID.
SPLIT L_HELP_ID AT '-' INTO L_SNAME L_FNAME.
CONCATENATE 'T_DD03P_YN' P_MODE SCREEN-GROUP1+1(2)
INTO L_SNAME.
ASSIGN (L_SNAME) TO <FS_T_DD03P>.
READ TABLE <FS_T_DD03P> INTO L_H_DD03P
WITH KEY FIELDNAME = L_FNAME.
IF SY-SUBRC = 0.
ASSIGN (L_SCR_NAME) TO <FS>.
<FS> = L_H_DD03P-DDTEXT.
ELSE.
MESSAGE E764.
ENDIF.
ENDCASE.
ENDIF.
ENDLOOP.

ENDFORM.                    " SET_DYNPRO_TEXT
*&---------------------------------------------------------------------*
*&      Form  GET_TABLE_FIELDS
*&---------------------------------------------------------------------*
*       項目テキスト取得
*----------------------------------------------------------------------*
FORM GET_TABLE_FIELDS .

PERFORM GET_DDIF_TABL TABLES T_DD03P_YN110
USING  'YN110'.
PERFORM GET_DDIF_TABL TABLES T_DD03P_YN120
USING  'YN120'.
PERFORM GET_DDIF_TABL TABLES T_DD03P_YN210
USING  'YN210'.
PERFORM GET_DDIF_TABL TABLES T_DD03P_YN220
USING  'YN220'.

ENDFORM.                    " GET_TABLE_FIELDS
*&---------------------------------------------------------------------*
*&      Form  GET_DDIF_TBL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_T_DD03P  text
*      -->P_TABNAME   text
*----------------------------------------------------------------------*
FORM GET_DDIF_TABL  TABLES   P_T_DD03P STRUCTURE DD03P
USING    P_TABNAME.

DATA: L_NAME TYPE DDOBJNAME.

REFRESH P_T_DD03P.
L_NAME = P_TABNAME.
CALL FUNCTION 'DDIF_TABL_GET'
EXPORTING
NAME          = L_NAME
LANGU         = SY-LANGU
TABLES
DD03P_TAB     = P_T_DD03P
EXCEPTIONS
ILLEGAL_INPUT = 1
OTHERS        = 2.

IF SY-SUBRC <> 0.
MESSAGE E754 WITH P_TABNAME.
ENDIF.

ENDFORM.                    " GET_DDIF_TBL
*Text symbol text・
*S01:Sales/Purches
*S02:Sales Checking
*S03:Purchase Checking
*S04:Own Data Selection
*S05:Process Option
*S06:Payment Block
*S07:Batch Input
*S08:Execution Mode
*S09:Message Number
*S10:Company
*S11:Partner
*S12:Applied
*S13:Block On
*S14:Block Off
*S15:Error
*S16:Pages:
*S17:Total:
*S18:Not apply
*Selection text・
*P_DMODE:        Execution Mode
*P_MSGNO:        Message Number
*P_ZLSPR:        Payment Block
