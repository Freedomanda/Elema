*&---------------------------------------------------------------------*
*&  REPORT ZM011900                                                    *
*&         当日入庫一覧　                                              *
*&---------------------------------------------------------------------*
*&  機能     :当日入庫一覧表を作成する
*&  作成日   :2010/09/01
*&  作成者   :S.KURATA(SOLFIS)
*&  変更履歴 :
*&   No   DATE       MODIFYED BY SUMMARY
*&   0001 2014/09/24 ISID11      グローバル化の対応
*&   0002 2014/11/13 ISID11      レポートの通貨コード表示対応
*&---------------------------------------------------------------------*
REPORT ZM011900 MESSAGE-ID  Z1
NO STANDARD PAGE HEADING.

TABLES:MSEG,MKPF,EKKO.
TYPE-POOLS SLIS.
*----------------------------------------------------------------------*
*  型定義
*----------------------------------------------------------------------*
* 入庫伝票ヘッダ取得用
TYPES:BEGIN OF T_MKPF,
MBLNR            TYPE MKPF-MBLNR,         "入出庫伝票番号
MJAHR            TYPE MKPF-MJAHR,         "入出庫伝票年度
BLDAT            TYPE MKPF-BLDAT,         "伝票日付
BUDAT            TYPE MKPF-BUDAT,         "転記日付
CPUDT            TYPE MKPF-CPUDT,         "会計伝票登録日
CPUTM            TYPE MKPF-CPUTM,         "登録時刻
AEDAT            TYPE MKPF-AEDAT,         "変更日
USNAM            TYPE MKPF-USNAM,         "ユーザ名
END OF T_MKPF.

* 入出庫伝票明細取得用
TYPES:BEGIN OF T_MSEG,
MBLNR            TYPE MSEG-MBLNR,         "入出庫伝票番号
MJAHR            TYPE MSEG-MJAHR,         "入出庫伝票年度
ZEILE            TYPE MSEG-ZEILE,         "入出庫伝票明細
BWART            TYPE MSEG-BWART,         "移動タイプ
MATNR            TYPE MSEG-MATNR,         "品目コード
WERKS            TYPE MSEG-WERKS,         "プラント
LIFNR            TYPE MSEG-LIFNR,         "仕入先
KUNNR            TYPE MSEG-KUNNR,         "得意先
KDAUF            TYPE MSEG-KDAUF,         "受注伝票番号
DMBTR            TYPE MSEG-DMBTR,         "国内通貨額
ERFMG            TYPE MSEG-ERFMG,         "入力単位での数量
EBELN            TYPE MSEG-EBELN,         "購買発注伝票番号
END OF T_MSEG.

* 購買伝票情報取得用
TYPES:BEGIN OF T_EKKO,
EBELN            TYPE EKKO-EBELN,         "購買伝票番号
EKGRP            TYPE EKKO-EKGRP,         "購買グループ
BEDAT            TYPE EKKO-BEDAT,         "発注伝票日付
NETPR            TYPE EKPO-NETPR,         "正味価格
PEINH            TYPE EKPO-PEINH,         "価格単位
END OF T_EKKO.

* 通貨・仕入先名称取得用
TYPES:BEGIN OF T_LFA1,
LIFNR            TYPE LFM1-LIFNR,         "仕入先
WAERS            TYPE LFM1-WAERS,         "購買発注通貨
NAME1            TYPE LFA1-NAME1,         "名称１
END OF T_LFA1.

* 品目テキスト取得用
TYPES:BEGIN OF T_MAKT,
MATNR            TYPE MAKT-MATNR,         "品目コード
MAKTX            TYPE MAKT-MAKTX,         "品目テキスト
END OF T_MAKT.

* 棚番取得用
TYPES:BEGIN OF T_MARD,
MATNR            TYPE MARD-MATNR,         "品目コード
WERKS            TYPE MARD-WERKS,         "プラント
LGPBE            TYPE MARD-LGPBE,         "棚番
END OF T_MARD.

* ALV出力用
TYPES:BEGIN OF T_ALV,
**** START ADD 2015/03/03 ISID13 ****
XBLNR            TYPE MKPF-XBLNR,         "Invoice番号
**** END ADD 2015/03/03 ISID13 ****
WERKS            TYPE MSEG-WERKS,         "プラント
**** START ADD 2015/03/03 ISID13 ****
LGORT            TYPE MSEG-LGORT,         "保管場所
**** END ADD 2015/03/03 ISID13 ****
MATNR            TYPE MSEG-MATNR,         "品目コード
MAKTX            TYPE MAKT-MAKTX,         "品名
EKGRP            TYPE EKKO-EKGRP,         "購買グループ
BWART            TYPE MSEG-BWART,         "移動タイプ
BUDAT            TYPE MKPF-BUDAT,         "転記日付
MBLNR            TYPE MSEG-MBLNR,         "入出庫伝票番号
**** START ADD 2015/03/03 ISID13 ****
ZEILE            TYPE MSEG-ZEILE,         "入出庫伝票明細番号
**** END ADD 2015/03/03 ISID13 ****
ERFMG            TYPE MSEG-ERFMG,         "数量
NETPR(13)        TYPE P DECIMALS 2,       "正味価格
WAERS            TYPE LFM1-WAERS,         "通貨コード
PEINH            TYPE EKPO-PEINH,         "価格単位
INGAKU           TYPE P DECIMALS 2,       "入庫金額
INGAKU2          TYPE P DECIMALS 2,       "ローカル入庫金額
DMBTR            TYPE P DECIMALS 2,       "国内通貨額
EBELN            TYPE EKKO-EBELN,         "購買伝票番号
**** START ADD 2015/03/03 ISID13 ****
EBELP            TYPE MSEG-EBELP,         "購買伝票明細番号
**** END ADD 2015/03/03 ISID13 ****
LIFNR            TYPE MSEG-LIFNR,         "仕入先勘定コード
NAME1            TYPE LFA1-NAME1,         "仕入先名
KDAUF            TYPE MSEG-KDAUF,         "受注伝票番号
**** START ADD 2015/03/03 ISID13 ****
KDPOS            TYPE MSEG-KDPOS,         "受注伝票明細番号
**** END ADD 2015/03/03 ISID13 ****
UKURS            TYPE TCURR-UKURS,        "換算レート
MJAHR            TYPE MSEG-MJAHR,         "入出庫伝票年度
KUNNR            TYPE MSEG-KUNNR,         "得意先コード
BLDAT            TYPE MKPF-BLDAT,         "伝票日付
CPUDT            TYPE MKPF-CPUDT,         "伝票登録日
CPUTM            TYPE MKPF-CPUTM,         "登録時間
AEDAT            TYPE MKPF-AEDAT,         "変更日
USNAM            TYPE MKPF-USNAM,         "ユーザ名
LGPBE            TYPE MARD-LGPBE,         "棚番
BEDAT            TYPE EKKO-BEDAT,         "発注伝票日付

END OF T_ALV.

*----------------------------------------------------------------------*
*  内部テーブル・構造定義
*----------------------------------------------------------------------*
* 入出庫伝票ヘッダ取得用
DATA: IT_MKPF           TYPE HASHED TABLE OF T_MKPF
WITH UNIQUE KEY MBLNR MJAHR,
WA_MKPF           TYPE T_MKPF.

* 入出庫伝票明細取得用
DATA: IT_MSEG           TYPE STANDARD TABLE OF T_MSEG,
WA_MSEG           TYPE T_MSEG.

* 購買伝票情報取得用
DATA: IT_EKKO           TYPE HASHED TABLE OF T_EKKO
WITH UNIQUE KEY EBELN,
WA_EKKO           TYPE T_EKKO.

* 通貨・仕入先名称取得用
DATA: IT_LFA1           TYPE HASHED TABLE OF T_LFA1
WITH UNIQUE KEY LIFNR,
WA_LFA1           TYPE T_LFA1.

* 品目テキスト取得用
DATA: IT_MAKT           TYPE HASHED TABLE OF T_MAKT
WITH UNIQUE KEY MATNR,
WA_MAKT           TYPE T_MAKT.

* 棚番取得用
DATA: IT_MARD           TYPE HASHED TABLE OF T_MARD
WITH UNIQUE KEY MATNR WERKS,
WA_MARD           TYPE T_MARD.

* ALV出力用
DATA: IT_ALV            TYPE STANDARD TABLE OF T_ALV,
WA_ALV            TYPE T_ALV.

* ALV項目カタログ用
DATA: IT_FIELDCAT       TYPE SLIS_T_FIELDCAT_ALV,
WA_FIELDCAT       LIKE LINE OF IT_FIELDCAT.

* ALVレイアウト用
DATA: WA_LAYOUT         TYPE SLIS_LAYOUT_ALV.

* ALVバリアント用
DATA: WA_VARIANT        TYPE DISVARIANT.

* ALVイベント用
DATA: IT_EVENT          TYPE SLIS_T_EVENT,
WA_EVENT          LIKE LINE OF IT_EVENT.

* ALV印刷用
DATA: WA_PRINT          TYPE SLIS_PRINT_ALV.

**** START ADD 2014/09/24 ISID11 ****
DATA:
WA_WAERS_LOCAL        TYPE T001-WAERS,     "通貨コード
WA_RNG_WERKS          TYPE RANGE_T_WERKS.  "プラント
**** END ADD 2014/09/24 ISID11 ****

*----------------------------------------------------------------------*
*  変数定義
*----------------------------------------------------------------------*

*----------------------------------------------------------------------*
*  定数定義
*----------------------------------------------------------------------*
CONSTANTS:
**** START DEL 2014/09/24 ISID11 ****
*  CNS_EKORG(4)          TYPE C VALUE '1000',
*  CNS_SPRAS_J(1)        TYPE C VALUE 'J',
**** END DEL 2014/09/24 ISID11 ****

CNS_DAY_FIRST(2)      TYPE C VALUE '01',
**** START DEL 2014/09/24 ISID11 ****
*  CNS_WAERS_J(3)        TYPE C VALUE 'JPY',
**** END DEL 2014/09/24 ISID11 ****

CNS_ON(1)             TYPE C VALUE 'X'.

*----------------------------------------------------------------------*
*  選択画面定義
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK BK01 WITH FRAME.
**** START ADD 2015/03/03 ISID13 ****
*Invoice番号
SELECT-OPTIONS S_XBLNR  FOR MKPF-XBLNR.
**** END ADD 2015/03/03 ISID13 ****
**** START ADD 2014/09/24 ISID11 ****
*会社コード
PARAMETERS     P_BUKRS  TYPE T001-BUKRS OBLIGATORY.
**** END ADD 2014/09/24 ISID11 ****
*品目コード
SELECT-OPTIONS S_MATNR  FOR MSEG-MATNR.
*プラント
SELECT-OPTIONS S_WERKS  FOR MSEG-WERKS.
**** START ADD 2015/03/03 ISID13 ****
*保管場所
SELECT-OPTIONS S_LGORT  FOR MSEG-LGORT.
**** END ADD 2015/03/03 ISID13 ****
*仕入先勘定コード
SELECT-OPTIONS S_LIFNR  FOR MSEG-LIFNR.
*得意先コード
SELECT-OPTIONS S_KUNNR  FOR MSEG-KUNNR.
*移動タイプ
SELECT-OPTIONS S_BWART  FOR MSEG-BWART.
*転記日付
SELECT-OPTIONS S_BUDAT  FOR MKPF-BUDAT DEFAULT SY-DATUM OBLIGATORY.
*伝票日付
SELECT-OPTIONS S_BLDAT  FOR MKPF-BLDAT.
*会計伝票登録日
SELECT-OPTIONS S_CPUDT  FOR MKPF-CPUDT.
*登録時刻
SELECT-OPTIONS S_CPUTM  FOR MKPF-CPUTM.
*変更日
SELECT-OPTIONS S_AEDAT  FOR MKPF-AEDAT.
*ユーザ名
SELECT-OPTIONS S_USNAM  FOR MKPF-USNAM.
*購買グループ
SELECT-OPTIONS S_EKGRP  FOR EKKO-EKGRP.
*購買伝票番号
SELECT-OPTIONS S_EBELN  FOR EKKO-EBELN.
**** START ADD 2015/03/03 ISID13 ****
*購買伝票明細番号
SELECT-OPTIONS S_EBELP  FOR MSEG-EBELP.
**** END ADD 2015/03/03 ISID13 ****
*入出庫伝票番号
SELECT-OPTIONS S_MBLNR  FOR MSEG-MBLNR.
**** START ADD 2015/03/03 ISID13 ****
*入出庫伝票明細番号
SELECT-OPTIONS S_ZEILE  FOR MSEG-ZEILE.
**** END ADD 2015/03/03 ISID13 ****
*入出庫伝票年度
SELECT-OPTIONS S_MJAHR  FOR MSEG-MJAHR.
SELECTION-SCREEN END OF BLOCK BK01.

**** START ADD 2014/09/24 ISID11 ****
*----------------------------------------------------------------------*
*  INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.
GET PARAMETER ID 'BUK' FIELD P_BUKRS.
**** END ADD 2014/09/24 ISID11 ****

*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

* 入力チェック
IF SY-UCOMM+0(1) <> '%'.
**** START ADD 2014/09/24 ISID11 ****
*   会社コード
PERFORM CHECK_BUKRS CHANGING WA_WAERS_LOCAL.
**** END ADD 2014/09/24 ISID11 ****
*   品目コード
PERFORM CHECK_MATNR.
*   プラント
PERFORM CHECK_WERKS.
*   仕入先勘定コード
PERFORM CHECK_LIFNR.
*   得意先コード
PERFORM CHECK_KUNNR.
*   移動タイプ
PERFORM CHECK_BWART.
*   購買グループ
PERFORM CHECK_EKGRP.
ENDIF.

*----------------------------------------------------------------------*
*  START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* データ取得
PERFORM GET_DATA.

* 編集
PERFORM EDIT_DATA.

* ALV出力
PERFORM OUT_DATA.

*&---------------------------------------------------------------------*
*&      Form  CHECK_MATNR
*&---------------------------------------------------------------------*
*       入力チェック：品目コード
*----------------------------------------------------------------------*
FORM CHECK_MATNR.

DATA : LV_MATNR TYPE MARA-MATNR.

SELECT MATNR
INTO LV_MATNR
FROM MARA
UP TO 1 ROWS
WHERE MATNR IN S_MATNR.
ENDSELECT.

IF SY-SUBRC <> 0.
*   品目コードがありません
MESSAGE E606 WITH TEXT-M01.
ENDIF.

ENDFORM.                    " CHECK_MATNR
*&---------------------------------------------------------------------*
*&      Form  CHECK_WERKS
*&---------------------------------------------------------------------*
*       入力チェック：プラント
*----------------------------------------------------------------------*
FORM CHECK_WERKS.

**** START UPD 2014/09/24 ISID11 ****
*  DATA : LV_WERKS TYPE T001W-WERKS.
*
*  SELECT WERKS
*    INTO LV_WERKS
*    FROM T001W
*      UP TO 1 ROWS
*   WHERE WERKS IN S_WERKS.
*  ENDSELECT.
*
*  IF SY-SUBRC <> 0.
**   プラントがありません
*    MESSAGE E606 WITH TEXT-M02.
*  ENDIF.
DATA:
LV_RANGE_WERKS TYPE RANGE_T_WERKS.
CLEAR LV_RANGE_WERKS.
LV_RANGE_WERKS = S_WERKS[].
* プラントチェック
CALL FUNCTION 'ZEG_ZZ_WERKS_CHK'
EXPORTING
IMPBUKRS           = P_BUKRS
IMPRNGWERKS        = LV_RANGE_WERKS
IMPORTING
EXPWERKS           = WA_RNG_WERKS
EXCEPTIONS
WERKS_NOT_EXIST    = 1
WERKS_NO_AUTHORITY = 2
WERKS_BUKRS_ERROR  = 3
OTHERS             = 4.
IF SY-SUBRC <> 0.
SET CURSOR FIELD 'P_BUKRS' .
MESSAGE ID SY-MSGID TYPE 'E'  NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.
**** END UPD 2014/09/24 ISID11 ****

ENDFORM.                    " CHECK_WERKS
*&---------------------------------------------------------------------*
*&      Form  CHECK_LIFNR
*&---------------------------------------------------------------------*
*       入力チェック：仕入先勘定コード
*----------------------------------------------------------------------*
FORM CHECK_LIFNR.

DATA : LV_LIFNR TYPE LFA1-LIFNR.

SELECT LIFNR
INTO LV_LIFNR
FROM LFA1
UP TO 1 ROWS
WHERE LIFNR IN S_LIFNR.
ENDSELECT.

IF SY-SUBRC <> 0.
*   仕入先勘定コードがありません
MESSAGE E606 WITH TEXT-M03.
ENDIF.

ENDFORM.                    " CHECK_LIFNR
*&---------------------------------------------------------------------*
*&      Form  CHECK_KUNNR
*&---------------------------------------------------------------------*
*       入力チェック：得意先コード
*----------------------------------------------------------------------*
FORM CHECK_KUNNR.

DATA : LV_KUNNR TYPE KNA1-KUNNR.

SELECT KUNNR
INTO LV_KUNNR
FROM KNA1
UP TO 1 ROWS
WHERE KUNNR IN S_KUNNR.
ENDSELECT.

IF SY-SUBRC <> 0.
*   得意先コードがありません
MESSAGE E606 WITH TEXT-M04.
ENDIF.

ENDFORM.                    " CHECK_KUNNR
*&---------------------------------------------------------------------*
*&      Form  CHECK_BWART
*&---------------------------------------------------------------------*
*       入力チェック：移動タイプ
*----------------------------------------------------------------------*
FORM CHECK_BWART.

DATA : LV_BWART TYPE T156-BWART.

SELECT BWART
INTO LV_BWART
FROM T156
UP TO 1 ROWS
WHERE BWART IN S_BWART.
ENDSELECT.

IF SY-SUBRC <> 0.
*   移動タイプがありません
MESSAGE E606 WITH TEXT-M05.
ENDIF.

ENDFORM.                    " CHECK_BWART
*&---------------------------------------------------------------------*
*&      Form  CHECK_EKGRP
*&---------------------------------------------------------------------*
*       入力チェック：購買グループ
*----------------------------------------------------------------------*
FORM CHECK_EKGRP.

DATA : LV_EKGRP TYPE T024-EKGRP.

SELECT EKGRP
INTO LV_EKGRP
FROM T024
UP TO 1 ROWS
WHERE EKGRP IN S_EKGRP.
ENDSELECT.

IF SY-SUBRC <> 0.
*   購買グループがありません
MESSAGE E606 WITH TEXT-M06.
ENDIF.

ENDFORM.                    " CHECK_EKGRP
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       データ取得
*----------------------------------------------------------------------*
FORM GET_DATA.

* 入出庫伝票情報：ヘッダ
PERFORM GET_MKPF.

* 入出庫伝票情報：明細
PERFORM GET_MSEG.

* 購買伝票情報
PERFORM GET_EKKO.

* 通貨・仕入先名称
PERFORM GET_LFA1.

* 品目テキスト
PERFORM GET_MAKT.

* 棚番
PERFORM GET_MARD.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_MKPF
*&---------------------------------------------------------------------*
*       入出庫伝票情報：ヘッダ
*----------------------------------------------------------------------*
FORM GET_MKPF.

REFRESH IT_MKPF.
SELECT MBLNR         "入出庫伝票番号
MJAHR         "入出庫伝票年度
BLDAT         "伝票日付
BUDAT         "転記日付
CPUDT         "会計伝票登録日
CPUTM         "登録時刻
AEDAT         "変更日
USNAM         "ユーザ名
INTO TABLE IT_MKPF
FROM MKPF
WHERE BUDAT IN S_BUDAT
AND MBLNR IN S_MBLNR
AND MJAHR IN S_MJAHR
AND BLDAT IN S_BLDAT
AND CPUDT IN S_CPUDT
AND CPUTM IN S_CPUTM
AND AEDAT IN S_AEDAT
AND USNAM IN S_USNAM
**** START ADD 2015/03/03 ISID13 ****
AND XBLNR IN S_XBLNR.
**** END ADD 2015/03/03 ISID13 ****

IF SY-SUBRC <> 0.
*   処理対象データがありません
MESSAGE S204.
STOP.
ENDIF.

ENDFORM.                    " GET_MKPF
*&---------------------------------------------------------------------*
*&      Form  GET_MSEG
*&---------------------------------------------------------------------*
*       入出庫伝票情報：明細
*----------------------------------------------------------------------*
FORM GET_MSEG.

REFRESH IT_MSEG.
SELECT MBLNR         "入出庫伝票番号
MJAHR         "入出庫伝票年度
ZEILE         "入出庫伝票明細
BWART         "移動タイプ
MATNR         "品目コード
WERKS         "プラント
**** START ADD 2015/03/03 ISID13 ****
LGORT         "保管場所
**** END ADD 2015/03/03 ISID13 ****
LIFNR         "仕入先
KUNNR         "得意先
KDAUF         "受注伝票番号
DMBTR         "国内通貨額
ERFMG         "入力単位での数量
EBELN         "購買発注伝票番号
**** START ADD 2015/03/03 ISID13 ****
EBELP         "入出庫伝票の明細
**** END ADD 2015/03/03 ISID13 ****
INTO TABLE IT_MSEG
FROM MSEG
FOR ALL ENTRIES IN IT_MKPF
WHERE MBLNR =  IT_MKPF-MBLNR
AND MJAHR =  IT_MKPF-MJAHR
AND MATNR IN S_MATNR
**** START UPD 2014/09/24 ISID11 ****
*     AND WERKS IN S_WERKS
AND WERKS IN WA_RNG_WERKS
**** END UPD 2014/09/24 ISID11 ****
AND BWART IN S_BWART
AND LIFNR IN S_LIFNR
AND KUNNR IN S_KUNNR
AND EBELN IN S_EBELN
**** START ADD 2015/03/03 ISID13 ****
AND EBELP IN S_EBELP
AND ZEILE IN S_ZEILE.
**** END ADD 2015/03/03 ISID13 ****

IF SY-SUBRC <> 0.
*   処理対象データがありません
MESSAGE S204.
STOP.
ENDIF.

ENDFORM.                    " GET_MSEG
*&---------------------------------------------------------------------*
*&      Form  GET_EKKO
*&---------------------------------------------------------------------*
*       購買伝票情報
*----------------------------------------------------------------------*
FORM GET_EKKO.

REFRESH IT_EKKO.
SELECT EKKO~EBELN         "購買伝票番号
EKKO~EKGRP         "購買グループ
EKKO~BEDAT         "発注伝票日付
EKPO~NETPR         "正味価格
EKPO~PEINH         "価格単位
INTO TABLE IT_EKKO
FROM EKKO
INNER JOIN EKPO
ON EKKO~EBELN = EKPO~EBELN
AND EKKO~LPONR = EKPO~EBELP
FOR ALL ENTRIES IN IT_MSEG
WHERE EKKO~EBELN =  IT_MSEG-EBELN.

ENDFORM.                    " GET_EKKO
*&---------------------------------------------------------------------*
*&      Form  GET_LFA1
*&---------------------------------------------------------------------*
*       通貨・仕入先名称
*----------------------------------------------------------------------*
FORM GET_LFA1.

DATA : LT_MSEG TYPE STANDARD TABLE OF T_MSEG.
**** START ADD 2014/09/24 ISID11 ****
DATA:
LV_EKORG    TYPE T024E-EKORG.
SELECT EKORG
FROM T024E
INTO LV_EKORG
UP TO 1 ROWS
WHERE BUKRS = P_BUKRS.
ENDSELECT.
**** END ADD 2014/09/24 ISID11 ****

* 仕入先の重複を削除
LT_MSEG = IT_MSEG.
SORT LT_MSEG BY LIFNR.
DELETE ADJACENT DUPLICATES FROM LT_MSEG COMPARING LIFNR.

REFRESH IT_LFA1.
SELECT LFM1~LIFNR
LFM1~WAERS
LFA1~NAME1
INTO TABLE IT_LFA1
FROM LFM1
INNER JOIN LFA1
ON LFM1~LIFNR = LFA1~LIFNR
FOR ALL ENTRIES IN LT_MSEG
WHERE LFM1~LIFNR = LT_MSEG-LIFNR
**** START UPD 2014/09/24 ISID11 ****
*     AND LFM1~EKORG = CNS_EKORG.
AND LFM1~EKORG = LV_EKORG.
**** END UPD 2014/09/24 ISID11 ****

ENDFORM.                    " GET_LFA1
*&---------------------------------------------------------------------*
*&      Form  GET_MAKT
*&---------------------------------------------------------------------*
*       品目テキスト
*----------------------------------------------------------------------*
FORM GET_MAKT.

DATA : LT_MSEG TYPE STANDARD TABLE OF T_MSEG.

* 品目コードの重複を削除
LT_MSEG = IT_MSEG.
SORT LT_MSEG BY MATNR.
DELETE ADJACENT DUPLICATES FROM LT_MSEG COMPARING MATNR.

REFRESH IT_MAKT.
SELECT MATNR         "品目コード
MAKTX         "品目テキスト
INTO TABLE IT_MAKT
FROM MAKT
FOR ALL ENTRIES IN LT_MSEG
WHERE MATNR = LT_MSEG-MATNR
**** START UPD 2014/09/24 ISID11 ****
*     AND SPRAS = CNS_SPRAS_J.
AND SPRAS = SY-LANGU.
**** END UPD 2014/09/24 ISID11 ****

ENDFORM.                    " GET_MAKT
*&---------------------------------------------------------------------*
*&      Form  GET_MARD
*&---------------------------------------------------------------------*
*       棚番
*----------------------------------------------------------------------*
FORM GET_MARD.

DATA : LT_MSEG TYPE STANDARD TABLE OF T_MSEG.

* 品目・プラントの重複を削除
LT_MSEG = IT_MSEG.
SORT LT_MSEG BY MATNR WERKS.
DELETE ADJACENT DUPLICATES FROM LT_MSEG COMPARING MATNR WERKS.

REFRESH IT_MARD.
SELECT MATNR         "品目コード
WERKS         "プラント
LGPBE         "棚番
INTO TABLE IT_MARD
FROM MARD
FOR ALL ENTRIES IN LT_MSEG
WHERE MATNR = LT_MSEG-MATNR
AND WERKS = LT_MSEG-WERKS.

ENDFORM.                    " GET_MARD
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       編集
*----------------------------------------------------------------------*
FORM EDIT_DATA.

DATA : LV_KIN_C(20) TYPE C,
LV_KIN_P     TYPE P DECIMALS 2.

REFRESH IT_ALV.
LOOP AT IT_MSEG INTO WA_MSEG.
CLEAR: WA_ALV.

*   入出庫伝票情報：ヘッダ
CLEAR WA_MKPF.
READ TABLE IT_MKPF INTO WA_MKPF
WITH TABLE KEY MBLNR = WA_MSEG-MBLNR
MJAHR = WA_MSEG-MJAHR.
CHECK SY-SUBRC = 0.

*   購買伝票情報
CLEAR WA_EKKO.
READ TABLE IT_EKKO INTO WA_EKKO
WITH TABLE KEY EBELN = WA_MSEG-EBELN.
*   購買伝票が存在する場合、
*   購買グループが選択画面指定値に該当しないものは除外
IF SY-SUBRC      = 0.
IF WA_EKKO-EKGRP IN S_EKGRP.
ELSE.
*       処理対象外
CONTINUE.
ENDIF.
ENDIF.

*   通貨コード・仕入先名
CLEAR WA_LFA1.
READ TABLE IT_LFA1 INTO WA_LFA1
WITH TABLE KEY LIFNR = WA_MSEG-LIFNR.
*   品目テキスト
CLEAR WA_MAKT.
READ TABLE IT_MAKT INTO WA_MAKT
WITH TABLE KEY MATNR = WA_MSEG-MATNR.
*   棚番
CLEAR WA_MARD.
READ TABLE IT_MARD INTO WA_MARD
WITH TABLE KEY MATNR = WA_MSEG-MATNR
WERKS = WA_MSEG-WERKS.

WA_ALV-WERKS   = WA_MSEG-WERKS.                   "プラント
WA_ALV-MATNR   = WA_MSEG-MATNR.                   "品目コード
WA_ALV-MAKTX   = WA_MAKT-MAKTX.                   "品名
WA_ALV-EKGRP   = WA_EKKO-EKGRP.                   "購買グループ
WA_ALV-BWART   = WA_MSEG-BWART.                   "移動タイプ
WA_ALV-BUDAT   = WA_MKPF-BUDAT.                   "転記日付
WA_ALV-MBLNR   = WA_MKPF-MBLNR.                   "入出庫伝票番号
WA_ALV-ERFMG   = WA_MSEG-ERFMG.                   "数量
WA_ALV-WAERS   = WA_LFA1-WAERS.                   "通貨コード
CLEAR LV_KIN_C.
WRITE WA_EKKO-NETPR TO LV_KIN_C
CURRENCY WA_ALV-WAERS NO-GROUPING.
WA_ALV-NETPR   = LV_KIN_C.                        "正味価格
WA_ALV-PEINH   = WA_EKKO-PEINH.                   "価格単位
**** START UPD 2014/09/24 ISID11 ****
*    WA_ALV-DMBTR   = WA_MSEG-DMBTR * 100.             "国内通貨額
*   通貨金額換算IO
PERFORM GET_DMBTR_EXTERNAL USING WA_MSEG-DMBTR
WA_WAERS_LOCAL
CHANGING WA_ALV-DMBTR.
**** END UPD 2014/09/24 ISID11 ****

WA_ALV-EBELN   = WA_EKKO-EBELN.                   "購買伝票番号
WA_ALV-LIFNR   = WA_MSEG-LIFNR.                   "仕入先勘定コード
WA_ALV-NAME1   = WA_LFA1-NAME1.                   "仕入先名
WA_ALV-KDAUF   = WA_MSEG-KDAUF.                   "受注伝票番号
WA_ALV-MJAHR   = WA_MKPF-MJAHR.                   "入出庫伝票年度
WA_ALV-KUNNR   = WA_MSEG-KUNNR.                   "得意先コード
WA_ALV-BLDAT   = WA_MKPF-BLDAT.                   "伝票日付
WA_ALV-CPUDT   = WA_MKPF-CPUDT.                   "伝票登録日
WA_ALV-CPUTM   = WA_MKPF-CPUTM.                   "登録時間
WA_ALV-AEDAT   = WA_MKPF-AEDAT.                   "変更日
WA_ALV-USNAM   = WA_MKPF-USNAM.                   "ユーザ名
WA_ALV-LGPBE   = WA_MARD-LGPBE.                   "棚番
WA_ALV-BEDAT   = WA_EKKO-BEDAT.                   "発注伝票日付
PERFORM GET_UKURS USING    WA_ALV-WAERS
WA_ALV-BUDAT
CHANGING WA_ALV-UKURS.          "換算レート
CLEAR : LV_KIN_C,LV_KIN_P.
IF WA_EKKO-PEINH = 0.
LV_KIN_P = 0.
ELSE.
LV_KIN_P       = WA_EKKO-NETPR / WA_EKKO-PEINH
* WA_MSEG-ERFMG.
ENDIF.
WRITE LV_KIN_P TO LV_KIN_C CURRENCY WA_ALV-WAERS
NO-GROUPING.
WA_ALV-INGAKU  = LV_KIN_C.                        "入庫金額
WA_ALV-INGAKU2 = WA_ALV-INGAKU * WA_ALV-UKURS.    "ローカル入庫金額
APPEND WA_ALV TO IT_ALV.
ENDLOOP.

* 出力対象データ有無チェック
IF IT_ALV IS INITIAL.
*   処理対象データがありません
MESSAGE S204.
STOP.
ENDIF.
FREE: IT_MKPF,IT_MSEG,IT_EKKO,IT_LFA1,IT_MAKT,IT_MARD.

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_UKURS
*&---------------------------------------------------------------------*
*       換算レート
*----------------------------------------------------------------------*
*      -->I_WAERS  通貨コード
*      -->I_BUDAT  転記日付
*      <--O_UKURS  換算レート
*----------------------------------------------------------------------*
FORM GET_UKURS USING    I_WAERS
I_BUDAT
CHANGING O_UKURS.

DATA : LV_DATE      TYPE SY-DATUM,
LV_AMOUNT(8) TYPE P DECIMALS 2.

**** START UPD 2014/09/24 ISID11 ****
* 通貨コードがローカル
*  IF I_WAERS = CNS_WAERS_J.
IF I_WAERS = WA_WAERS_LOCAL.
**** END UPD 2014/09/24 ISID11 ****
*   換算レートは1固定
O_UKURS = 1.

* 換算レートがローカル以外
ELSE.
IF I_WAERS IS INITIAL.
EXIT.
ENDIF.
CONCATENATE I_BUDAT+0(6) CNS_DAY_FIRST
INTO LV_DATE.

CALL FUNCTION 'CONVERT_TO_LOCAL_CURRENCY'
EXPORTING
DATE             = LV_DATE
FOREIGN_AMOUNT   = LV_AMOUNT
FOREIGN_CURRENCY = I_WAERS
**** START UPD 2014/09/24 ISID11 ****
*        LOCAL_CURRENCY   = CNS_WAERS_J
LOCAL_CURRENCY   = WA_WAERS_LOCAL
**** END UPD 2014/09/24 ISID11 ****
TYPE_OF_RATE     = 'M'
IMPORTING
EXCHANGE_RATE    = O_UKURS
EXCEPTIONS
NO_RATE_FOUND    = 1
OVERFLOW         = 2
NO_FACTORS_FOUND = 3
NO_SPREAD_FOUND  = 4
DERIVED_2_TIMES  = 5
OTHERS           = 6.

IF SY-SUBRC <> 0.
**** START UPD 2014/09/24 ISID11 ****
*      MESSAGE ID 'S' TYPE SY-MSGTY NUMBER SY-MSGNO
MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO
**** END UPD 2014/09/24 ISID11 ****
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
STOP.
ENDIF.
ENDIF.

ENDFORM.                    " GET_UKURS
*&---------------------------------------------------------------------*
*&      Form  OUT_DATA
*&---------------------------------------------------------------------*
*       ALV出力
*----------------------------------------------------------------------*
FORM OUT_DATA.

DATA LV_PGM  TYPE SY-REPID.

* 項目カタログの設定
PERFORM SET_FIELDCAT.

* レイアウトの設定
CLEAR WA_LAYOUT.
WA_LAYOUT-COLWIDTH_OPTIMIZE = SPACE.
WA_LAYOUT-ZEBRA             = CNS_ON.
WA_LAYOUT-GROUP_CHANGE_EDIT = CNS_ON.

* バリアントの設定
CLEAR WA_VARIANT.
WA_VARIANT-VARIANT = '/ZM011900_01'.

* イベントの設定
PERFORM GET_EVENT.

* 印刷の設定
CLEAR WA_PRINT.
WA_PRINT-NO_PRINT_LISTINFOS = CNS_ON.

* プログラムIDの設定
CLEAR LV_PGM.
LV_PGM = SY-REPID.

CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM = LV_PGM
IS_LAYOUT          = WA_LAYOUT
IT_FIELDCAT        = IT_FIELDCAT
I_SAVE             = 'A'
IS_VARIANT         = WA_VARIANT
IT_EVENTS          = IT_EVENT
IS_PRINT           = WA_PRINT
TABLES
T_OUTTAB           = IT_ALV
EXCEPTIONS
PROGRAM_ERROR      = 1
OTHERS             = 2.

ENDFORM.                    " OUT_DATA
*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       項目カタログの設定
*----------------------------------------------------------------------*
FORM SET_FIELDCAT.

REFRESH IT_FIELDCAT.

* プラント
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'WERKS'.
WA_FIELDCAT-SELTEXT_M = TEXT-H01.
WA_FIELDCAT-COL_POS   = 1.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 品目コード
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'MATNR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H02.
WA_FIELDCAT-COL_POS   = 2.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 品名
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'MAKTX'.
WA_FIELDCAT-SELTEXT_M = TEXT-H03.
WA_FIELDCAT-COL_POS   = 3.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 購買グループ
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'EKGRP'.
WA_FIELDCAT-SELTEXT_M = TEXT-H04.
WA_FIELDCAT-COL_POS   = 4.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 移動タイプ
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'BWART'.
WA_FIELDCAT-SELTEXT_M = TEXT-H05.
WA_FIELDCAT-COL_POS   = 5.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 転記日付
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'BUDAT'.
WA_FIELDCAT-SELTEXT_M = TEXT-H06.
WA_FIELDCAT-COL_POS   = 6.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 入出庫伝票番号
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'MBLNR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H07.
WA_FIELDCAT-COL_POS   = 7.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 数量
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'ERFMG'.
WA_FIELDCAT-SELTEXT_M = TEXT-H08.
WA_FIELDCAT-COL_POS   = 8.
WA_FIELDCAT-JUST      = 'R'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 正味価格
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'NETPR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H09.
WA_FIELDCAT-COL_POS   = 9.
WA_FIELDCAT-JUST      = 'R'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 通貨コード
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'WAERS'.
WA_FIELDCAT-SELTEXT_M = TEXT-H10.
WA_FIELDCAT-COL_POS   = 10.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 価格単位
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'PEINH'.
WA_FIELDCAT-SELTEXT_M = TEXT-H11.
WA_FIELDCAT-COL_POS   = 11.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 入庫金額
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'INGAKU'.
WA_FIELDCAT-SELTEXT_M = TEXT-H12.
WA_FIELDCAT-COL_POS   = 12.
WA_FIELDCAT-JUST      = 'R'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* ローカル入庫金額
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'INGAKU2'.
WA_FIELDCAT-SELTEXT_M = TEXT-H13.
WA_FIELDCAT-COL_POS   = 13.
WA_FIELDCAT-JUST      = 'R'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 国内通貨額
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'DMBTR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H14.
WA_FIELDCAT-COL_POS   = 14.
WA_FIELDCAT-JUST      = 'R'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 購買伝票番号
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'EBELN'.
WA_FIELDCAT-SELTEXT_M = TEXT-H15.
WA_FIELDCAT-COL_POS   = 15.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 仕入先勘定コード
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'LIFNR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H16.
WA_FIELDCAT-COL_POS   = 16.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 仕入先名
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'NAME1'.
WA_FIELDCAT-SELTEXT_M = TEXT-H17.
WA_FIELDCAT-COL_POS   = 17.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 受注伝票番号
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'KDAUF'.
WA_FIELDCAT-SELTEXT_M = TEXT-H18.
WA_FIELDCAT-COL_POS   = 18.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 換算レート
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'UKURS'.
WA_FIELDCAT-SELTEXT_M = TEXT-H19.
WA_FIELDCAT-COL_POS   = 19.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 入出庫伝票年度
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'MJAHR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H20.
WA_FIELDCAT-COL_POS   = 20.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 得意先コード
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'KUNNR'.
WA_FIELDCAT-SELTEXT_M = TEXT-H21.
WA_FIELDCAT-COL_POS   = 21.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 伝票日付
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'BLDAT'.
WA_FIELDCAT-SELTEXT_M = TEXT-H22.
WA_FIELDCAT-COL_POS   = 22.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 伝票登録日
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'CPUDT'.
WA_FIELDCAT-SELTEXT_M = TEXT-H23.
WA_FIELDCAT-COL_POS   = 23.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 登録時刻
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'CPUTM'.
WA_FIELDCAT-SELTEXT_M = TEXT-H24.
WA_FIELDCAT-COL_POS   = 24.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 変更日
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'AEDAT'.
WA_FIELDCAT-SELTEXT_M = TEXT-H25.
WA_FIELDCAT-COL_POS   = 25.
WA_FIELDCAT-DATATYPE  = 'D'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* ユーザ名
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'USNAM'.
WA_FIELDCAT-SELTEXT_M = TEXT-H26.
WA_FIELDCAT-COL_POS   = 26.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 棚番
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'LGPBE'.
WA_FIELDCAT-SELTEXT_M = TEXT-H27.
WA_FIELDCAT-COL_POS   = 27.
APPEND WA_FIELDCAT TO IT_FIELDCAT.
* 発注伝票日付
CLEAR WA_FIELDCAT.
WA_FIELDCAT-FIELDNAME = 'BEDAT'.
WA_FIELDCAT-SELTEXT_M = TEXT-H28.
WA_FIELDCAT-COL_POS   = 28.
WA_FIELDCAT-DATATYPE  = 'D'.
APPEND WA_FIELDCAT TO IT_FIELDCAT.

ENDFORM.                    " SET_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  GET_EVENT
*&---------------------------------------------------------------------*
*       イベントの設定
*----------------------------------------------------------------------*
FORM GET_EVENT.

REFRESH IT_EVENT.
CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
EXPORTING
I_LIST_TYPE     = 0
IMPORTING
ET_EVENTS       = IT_EVENT
EXCEPTIONS
LIST_TYPE_WRONG = 1
OTHERS          = 2.

* 実行するサブルーチン名を設定
LOOP AT IT_EVENT INTO WA_EVENT.
CASE WA_EVENT-NAME.
WHEN 'TOP_OF_PAGE'.
WA_EVENT-FORM = 'OUT_HEADER'.
MODIFY IT_EVENT FROM WA_EVENT.

WHEN 'CALLER_EXIT'.
WA_EVENT-FORM = 'CALLER_EXIT'.
MODIFY IT_EVENT FROM WA_EVENT.

WHEN OTHERS.
ENDCASE.
ENDLOOP.

ENDFORM.                    " GET_EVENT
*&---------------------------------------------------------------------*
*&      Form  OUT_HEADER
*&---------------------------------------------------------------------*
*       ヘッダ出力(ALVで呼び出されるサブルーチン)
*----------------------------------------------------------------------*
FORM OUT_HEADER.

WRITE:  050 TEXT-001,           "一覧タイトル
**** START UPD 2014/09/24 ISID11 ****
*          140 TEXT-002,SY-DATUM,  "日付
140 TEXT-002,SY-DATLO,  "日付
**** END UPD 2014/09/24 ISID11 ****

**** START ADD 2014/11/13 ISID11 ****
/1 TEXT-004,            "国内通貨コード
15 ':',
17 WA_WAERS_LOCAL,
**** END ADD 2014/11/13 ISID11 ****

140 TEXT-003,SY-PAGNO.  "ページ数

ENDFORM.                    " OUT_HEADER
*&---------------------------------------------------------------------*
*&      Form  CALLER_EXIT
*&---------------------------------------------------------------------*
*       CALLER_EXIT(ALVで呼び出されるサブルーチン)
*----------------------------------------------------------------------*
FORM CALLER_EXIT USING RS_DATA TYPE SLIS_DATA_CALLER_EXIT.

RS_DATA-CALLBACK_HEADER_TRANSPORT = 'FILITEXTS_TEXT-TRANSPORT'.

ENDFORM.                    " CALLER_EXIT
**** START ADD 2014/09/24 ISID11 ****
*&---------------------------------------------------------------------*
*&      Form  CHECK_BUKRS
*&---------------------------------------------------------------------*
*       入力チェック：会社コード
*&---------------------------------------------------------------------*
*       <- P_WAERS    通貨コード
*----------------------------------------------------------------------*
FORM CHECK_BUKRS CHANGING O_WAERS    TYPE T001-WAERS.
CLEAR O_WAERS.
* 選択画面の会社コードの存在チェックを行う
SELECT SINGLE WAERS
INTO O_WAERS
FROM T001
WHERE BUKRS = P_BUKRS.
IF SY-SUBRC <> 0.
SET CURSOR FIELD 'P_BUKRS'.
MESSAGE E016(Z3) WITH P_BUKRS.
*   会社コード & が存在しません
ENDIF.

* 選択画面の会社コードの権限チェックを行う
AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
ID 'BUKRS' FIELD P_BUKRS          "対象会社コード
ID 'ACTVT' FIELD '03'.

IF SY-SUBRC <> 0.
SET CURSOR FIELD 'P_BUKRS'.
MESSAGE E015(Z3) WITH P_BUKRS.
*   会社コード & では実行する権限がありません。
ENDIF.
ENDFORM.                    " CHECK_BUKRS
*&---------------------------------------------------------------------*
*&      Form  GET_DMBTR_EXTERNAL
*&---------------------------------------------------------------------*
*       通貨金額換算IO
*----------------------------------------------------------------------*
*      -->I_DMBTR        国内通貨額
*      -->I_WAERS_LOCAL  国内通貨
*      <--O_DMBTR        国内通貨額
*----------------------------------------------------------------------*
FORM GET_DMBTR_EXTERNAL  USING    I_DMBTR       TYPE MSEG-DMBTR
I_WAERS_LOCAL TYPE T001-WAERS
CHANGING O_DMBTR.
DATA:
LV_DMBTR LIKE BAPICURR-BAPICURR.
CALL FUNCTION 'BAPI_CURRENCY_CONV_TO_EXTERNAL'
EXPORTING
CURRENCY        = I_WAERS_LOCAL
AMOUNT_INTERNAL = I_DMBTR
IMPORTING
AMOUNT_EXTERNAL = LV_DMBTR.
O_DMBTR = LV_DMBTR.

ENDFORM.                    " GET_DMBTR_EXTERNAL
**** END UPD 2014/09/24 ISID11 ****
*Text symbol text・
*001:**Cur. Day Goods Receipt List**
*002:DATE
*003:PAGE
*004:Local Currency
*H01:Plant
*H02:Mat.No.
*H03:Mat.
*H04:Pur. Group
*H05:Move Type
*H06:Post DT
*H07:Mat. Doc. No.
*H08:Qty
*H09:Net Pri.
*H10:Currency
*H11:Pr. Unit
*H12:Rec. Amt
*H13:Loc Rec. Amt
*H14:Loc.Cu.Amt
*H15:Pur.Doc.No.
*H16:Acc. No. of Ven.
*H17:Ven. NM
*H18:SO Number
*H19:Ex. Rate
*H20:Mat. Doc. Year
*H21:Customer CD
*H22:Doc. DT
*H23:Cre.Doc.DT
*H24:Cre.Time
*H25:Chg DT
*H26:User NM
*H27:Bins
*H28:Order No. DT
*M01:Mat. Code
*M03:Acc. No. of Ven.
*M04:Customer CD
*M05:Move Type
*M06:Pur. Group
*Selection text・
*P_BUKRS:        Company Code
*S_AEDAT:        Change Date
*S_BLDAT:        Posting Date
*S_BUDAT:        Document Date
*S_BWART:        Movement Type
*S_CPUDT:        Accounting Doc. Create Date
*S_CPUTM:        Create Time
*S_EBELN:        Purchasing Document No.
*S_EBELP:        Item
*S_EKGRP:        Purchasing Group
*S_KUNNR:        Customer No.
*S_LGORT:        Storage Location
*S_LIFNR:        Account Number Of Vendor
*S_MATNR:        Material Number
*S_MBLNR:        Material Document Number
*S_MJAHR:        Material Document Year
*S_USNAM:        User Name
*S_WERKS:        Plant
*S_XBLNR:        Reference Document Number
*S_ZEILE:        Item in Material Document
