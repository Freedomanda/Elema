*&---------------------------------------------------------------------*
*&  REPORT ZN030900
*&         【移行】自社データ（売上）（未請求）【セットアップ】
*&---------------------------------------------------------------------*
*&  機能     : YK520をYN120に変換する
*&
*&  作成日   : 2012/04/03
*&  作成者   : K.KAJISA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/04/09 K.FURUYA
*&  変更内容 : 会計年度取得方法修正
*&---------------------------------------------------------------------*
REPORT ZN030900
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.


*----------------------------------------------------------------------*
*   テーブル
*----------------------------------------------------------------------*
TABLES:YK520,YN120.

*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_IKO   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(10) TEXT-003 FOR FIELD P_R_IKO.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-003.
* 移行のみ
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_UP   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(14) TEXT-007 FOR FIELD P_R_UP.
SELECTION-SCREEN:END   OF LINE.
* 移行とダウンロード
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_DUP   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(24) TEXT-008 FOR FIELD P_R_DUP.
SELECTION-SCREEN:END   OF LINE.
* ダウンロードのみ
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_DOWN   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(18) TEXT-009 FOR FIELD P_R_DOWN.
SELECTION-SCREEN:END   OF LINE.
* 条件
SELECTION-SCREEN:BEGIN OF BLOCK BL1 WITH FRAME TITLE TEXT-001.
SELECT-OPTIONS:P_BUKRS   FOR YK520-BUKRS.
SELECT-OPTIONS:P_KUNNR   FOR YK520-KUNNR.
SELECT-OPTIONS:P_VKORG   FOR YK520-VKORG.
SELECT-OPTIONS:P_VTWEG   FOR YK520-VTWEG.
SELECT-OPTIONS:P_SPART   FOR YK520-SPART.

SELECTION-SCREEN:BEGIN OF BLOCK BL2 WITH FRAME TITLE TEXT-010.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_31 RADIOBUTTON GROUP GR01 DEFAULT 'X'.
SELECTION-SCREEN:COMMENT 3(8) TEXT-011 FOR FIELD R_31 .
PARAMETERS: R_05 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-012 FOR FIELD R_05 .
PARAMETERS: R_10 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-013 FOR FIELD R_10 .
SELECTION-SCREEN:END OF LINE.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_15 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 3(8) TEXT-014 FOR FIELD R_15 .
PARAMETERS: R_20 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-015 FOR FIELD R_20 .
PARAMETERS: R_25 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-016 FOR FIELD R_25.
SELECTION-SCREEN:END OF LINE.
SELECT-OPTIONS:P_ZFBDT   FOR YK520-ZFBDT.
SELECTION-SCREEN:END   OF BLOCK BL2.

SELECT-OPTIONS:P_VBELN    FOR YK520-VBELN.
SELECT-OPTIONS:P_POSNR    FOR YK520-POSNR.
SELECT-OPTIONS:P_HEAD     FOR YK520-HEADFLG.
SELECT-OPTIONS:P_BRTWR    FOR YK520-BRTWR.
PARAMETERS    :P_PASS(255) TYPE C LOWER CASE.
SELECTION-SCREEN:END   OF BLOCK BL1.
SELECTION-SCREEN:END   OF BLOCK BL01.

SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_DEL   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(14) TEXT-004 FOR FIELD P_R_DEL.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-002.
SELECT-OPTIONS:P_GJAHR   FOR YN120-GJAHR.
SELECT-OPTIONS:P_SLPDOC  FOR YN120-SLPDOC.
SELECT-OPTIONS:P_DTLDOC  FOR YN120-DTLDOC.
SELECT-OPTIONS:P_VRF     FOR YN120-VRFCTON.
SELECT-OPTIONS:P_ZFB     FOR YN120-ZFBDT.
SELECTION-SCREEN:END   OF BLOCK BL02.
*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
TYPES:BEGIN OF TYP_YK520_O.
INCLUDE STRUCTURE YK520.
TYPES:GJAHR    TYPE BSID-GJAHR,
END OF TYP_YK520_O.

* 得意先締日
TYPES:BEGIN OF TYP_KNVV,
KUNNR TYPE KNVV-KUNNR,
VKORG TYPE KNVV-VKORG,
VTWEG TYPE KNVV-VTWEG,
SPART TYPE KNVV-SPART,
ZTERM TYPE KNVV-ZTERM,
ZFAEL TYPE T052-ZFAEL,
END OF TYP_KNVV.

* ファイルダウンロード
TYPES:BEGIN OF  TYP_DOWNLOAD,
BUKRS(4)    TYPE  C,"会社コード
KUNNR(10)   TYPE  C,"得意先コード
VKORG(4)    TYPE  C,"販売組織
VTWEG(2)    TYPE  C,"流通チャネル
SPART(2)    TYPE  C,"製品部門
ZFBDT(8)    TYPE  C,"締日
VBELN(10)   TYPE  C,"請求伝票
POSNR(6)    TYPE  C,"請求明細
UEPOS(6)    TYPE  C,"上位レベル明細
FKDAT(8)    TYPE  C,"請求索引および印刷に対する請求日
FKIMG(15)   TYPE  C,"請求数量 (実績)
VRKME(3)    TYPE  C,"販売単位
NETWR(21)   TYPE  C,"伝票通貨での請求明細の正味額
MWSBP(21)   TYPE  C,"伝票通貨での税額
BRTWR(21)   TYPE  C,"伝票通貨における請求伝票の総額
AUBEL(10)   TYPE  C,"販売伝票
AUPOS(6)    TYPE  C,"販売伝票明細
KURRF(12)   TYPE  C,"FI 転記における換算レート
BSTKD(35)   TYPE  C,"得意先発注番号
MATNR(18)   TYPE  C,"品目コード
KDMAT(35)   TYPE  C,"得意先品目コード
ARKTX(40)   TYPE  C,"テキスト (短): 受注明細
WAERS(5)    TYPE  C,"通貨コード
INSDAT(8)   TYPE  C,"登録日
INSTIM(6)   TYPE  C,"登録時刻
INSUSR(12)  TYPE  C,"登録ユーザ
UPDDAT(8)   TYPE  C,"変更日
UPDTIM(6)   TYPE  C,"変更時刻
UPDUSR(12)  TYPE  C,"変更ユーザ
HEADFLG(1)  TYPE  C,"ヘッダ処理済フラグ
KUNAG(10)   TYPE  C,"受注先
END  OF TYP_DOWNLOAD,
T_TYP_DOWNLOAD TYPE STANDARD TABLE OF TYP_DOWNLOAD.

* エラーデータ
TYPES:BEGIN OF TYP_ERR,
VBELN    TYPE YK520-VBELN,"暦年
POSNR    TYPE YK520-POSNR,"検収番号
MESSAGE  TYPE CHAR255,    "エラー内容
END   OF TYP_ERR.

DATA:T_ERR TYPE STANDARD TABLE OF TYP_ERR,
W_ERR TYPE TYP_ERR.

* VBRP
TYPES:BEGIN OF TYP_VBRP,
VBELN    TYPE VBRP-VBELN,
POSNR    TYPE VBRP-POSNR,
VKGRP    TYPE VBRP-VKGRP,
VKBUR    TYPE VBRP-VKBUR,
END OF TYP_VBRP.

* VBKD
*TYPES:BEGIN OF TYP_VBKD,
*      VBELN    TYPE VBKD-VBELN,
*      POSNR    TYPE VBKD-POSNR,
*      BSTKD    TYPE VBKD-BSTKD,
*      END OF   TYP_VBKD.

* VBRK
TYPES:BEGIN OF TYP_VBRK,
VBELN    TYPE VBRK-VBELN,
FKDAT    TYPE VBRK-FKDAT,
END OF TYP_VBRK.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*

DATA:T_YK520 TYPE STANDARD TABLE OF YK520,
W_YK520 TYPE YK520.

DATA:T_YN120 TYPE STANDARD TABLE OF YN120,
W_YN120 TYPE YN120.

DATA:T_YK520_O TYPE STANDARD TABLE OF TYP_YK520_O,
W_YK520_O TYPE TYP_YK520_O.

DATA:T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART,
W_KNVV TYPE TYP_KNVV.

DATA:T_VBRP TYPE SORTED TABLE OF TYP_VBRP
WITH UNIQUE KEY VBELN POSNR,
W_VBRP TYPE TYP_VBRP.

*DATA:T_VBKD TYPE SORTED TABLE OF TYP_VBKD
*     WITH UNIQUE KEY VBELN POSNR,
*     W_VBKD TYPE TYP_VBKD.

DATA:T_VBRK TYPE SORTED TABLE OF TYP_VBRK
WITH UNIQUE KEY VBELN ,
W_VBRK TYPE TYP_VBRK.

* BSID
TYPES:BEGIN OF TYP_BSID,
GJAHR  TYPE BSID-GJAHR, "会計年度
BELNR  TYPE BSID-BELNR, "会計伝票番号
BUKRS  TYPE BSID-BUKRS, "会社コード
KUNNR  TYPE BSID-KUNNR, "得意先コード
BUZEI  TYPE BSID-BUZEI, "会計伝票明細番号
UMSKS  TYPE BSID-UMSKS, "特殊仕訳取引タイプ
UMSKZ  TYPE BSID-UMSKZ, "特殊仕訳コード
AUGDT  TYPE BSID-AUGDT, "決済日付
AUGBL  TYPE BSID-AUGBL, "決済伝票番号
ZUONR  TYPE BSID-ZUONR, "ソートキー
END OF TYP_BSID.
DATA:T_BSID TYPE SORTED TABLE OF TYP_BSID
WITH UNIQUE KEY  GJAHR BELNR BUKRS KUNNR BUZEI
UMSKS UMSKZ AUGDT AUGBL ZUONR,
W_BSID TYPE TYP_BSID.

*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_INIT_POS   TYPE VBRP-POSNR VALUE '000000',
C_ZFAEL_05   TYPE T052-ZFAEL VALUE '05',
C_ZFAEL_10   TYPE T052-ZFAEL VALUE '10',
C_ZFAEL_15   TYPE T052-ZFAEL VALUE '15',
C_ZFAEL_20   TYPE T052-ZFAEL VALUE '20',
C_ZFAEL_25   TYPE T052-ZFAEL VALUE '25',
C_ZFAEL_31   TYPE T052-ZFAEL VALUE '31',
C_UNMACH     TYPE YK130-KEKKBN VALUE '3',
C_JPY        TYPE YK130-WAERS  VALUE 'JPY',
C_ON         TYPE C     VALUE 'X',
C_SPRAS      TYPE SPRAS VALUE 'J'.   "言語キー
*----------------------------------------------------------------------*
*   変数定義
*----------------------------------------------------------------------*
DATA:G_ZFAEL    TYPE T052-ZFAEL,
G_ERR(1)   TYPE C,
G_BIKO(40) TYPE C.

DATA:G_CNT_YK520   TYPE I,
G_CNT_YN120   TYPE I,
G_CNT_ERR     TYPE I.
*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_PASS.
*-- ファイルダウンロード検索ヘルプ
PERFORM FRM_CHK_FILE.


*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.
IF ( P_R_DOWN = C_ON
OR   P_R_DUP  = C_ON )
AND P_PASS IS INITIAL.
MESSAGE E999(YN01) WITH 'ダウンロードファイル名を入力してください'.
ENDIF.



*----------------------------------------------------------------------*
*  START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM INIT_PROC.

*---データ移行処理
IF P_R_IKO = C_ON.
*   移行用データ取得
PERFORM GET_YK520.
CHECK NOT T_YK520_O[] IS INITIAL.
IF P_R_DOWN = C_ON
OR P_R_DUP  = C_ON.
*     ファイルダウンロード
CLEAR G_ERR.
PERFORM DOWNLOAD.
CHECK G_ERR IS INITIAL.
ENDIF.
CHECK P_R_DOWN IS INITIAL.  "ダウンロードのみ以外処理続行
*   標準ＤＢデータ取得
PERFORM GET_DB.
*   編集
PERFORM EDIT_YN520.
*   登録
PERFORM INSERT_DATA.
*   処理結果出力
PERFORM WRITE_IKO.
ELSE.
*   データ取得
PERFORM DEL_DATA_GET.
CHECK NOT T_YN120[] IS INITIAL.
*   削除
PERFORM DEL_DATA.
*   処理結果出力
PERFORM WRITE_DEL.
ENDIF.

*&---------------------------------------------------------------------*
*&      Form  INIT_PROC
*&---------------------------------------------------------------------*
*       初期化処理
*----------------------------------------------------------------------*
FORM INIT_PROC.
REFRESH:T_YN120,
T_YK520,
T_YK520_O,
T_KNVV,
T_VBRP,
T_VBRK,
T_ERR,
T_BSID.
*          T_VBKD.

CLEAR:G_ERR,
G_CNT_YK520,
G_CNT_YN120,
G_CNT_ERR.


ENDFORM.                    " INIT_PROC
*&---------------------------------------------------------------------*
*&      Form  GET_YK520
*&---------------------------------------------------------------------*
*       移行用データ取得
*----------------------------------------------------------------------*
FORM GET_YK520.

SELECT *
INTO TABLE T_YK520
FROM YK520
WHERE BUKRS    IN P_BUKRS
AND KUNNR    IN P_KUNNR
AND VKORG    IN P_VKORG
AND VTWEG    IN P_VTWEG
AND SPART    IN P_SPART
AND ZFBDT    IN P_ZFBDT
AND VBELN    IN P_VBELN
AND POSNR    IN P_POSNR
AND BRTWR    IN P_BRTWR
AND HEADFLG  IN P_HEAD.

IF SY-SUBRC = 0.
PERFORM CHECK_SIMEBI.
ELSE.
MESSAGE S762(YN01).
ENDIF.
ENDFORM.                    " GET_YK520
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD
*&---------------------------------------------------------------------*
*       ファイルダウンロード
*----------------------------------------------------------------------*
FORM DOWNLOAD.
DATA:L_PASS TYPE STRING,
LT_DOWNLOAD TYPE T_TYP_DOWNLOAD,
LW_DOWNLOAD TYPE TYP_DOWNLOAD.

L_PASS = P_PASS.
CHECK NOT ( T_YK520_O[] IS INITIAL ).
* 変換処理
LOOP AT T_YK520_O INTO W_YK520_O.
MOVE-CORRESPONDING W_YK520_O TO LW_DOWNLOAD.
*   後ろマイナスの変換
IF W_YK520_O-FKIMG < 0.         "数量
SHIFT LW_DOWNLOAD-FKIMG CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-FKIMG NO-GAPS.

IF W_YK520_O-NETWR < 0.         "税抜
SHIFT LW_DOWNLOAD-NETWR CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-NETWR NO-GAPS.

IF W_YK520_O-MWSBP < 0.         "税額
SHIFT LW_DOWNLOAD-MWSBP CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-MWSBP NO-GAPS.

IF W_YK520_O-BRTWR < 0.         "税込
SHIFT LW_DOWNLOAD-BRTWR CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-BRTWR NO-GAPS.

IF W_YK520_O-KURRF < 0.         "レート
SHIFT LW_DOWNLOAD-KURRF CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-KURRF NO-GAPS.

*   文字化け対策
CLEAR:LW_DOWNLOAD-ARKTX.
APPEND LW_DOWNLOAD TO LT_DOWNLOAD.
ENDLOOP.

CALL FUNCTION 'GUI_DOWNLOAD'
EXPORTING
*     BIN_FILESIZE                  =
FILENAME                      = L_PASS
FILETYPE                       = 'ASC'
*     APPEND                        = ' '
WRITE_FIELD_SEPARATOR         = 'X'
*     HEADER                        = '00'
*     TRUNC_TRAILING_BLANKS         = ' '
*     WRITE_LF                      = 'X'
*     COL_SELECT                    = ' '
*     COL_SELECT_MASK               = ' '
*   IMPORTING
*     FILELENGTH                    =
TABLES
DATA_TAB                      = LT_DOWNLOAD
EXCEPTIONS
FILE_WRITE_ERROR              = 1
NO_BATCH                      = 2
GUI_REFUSE_FILETRANSFER       = 3
INVALID_TYPE                  = 4
NO_AUTHORITY                  = 5
UNKNOWN_ERROR                 = 6
HEADER_NOT_ALLOWED            = 7
SEPARATOR_NOT_ALLOWED         = 8
FILESIZE_NOT_ALLOWED          = 9
HEADER_TOO_LONG               = 10
DP_ERROR_CREATE               = 11
DP_ERROR_SEND                 = 12
DP_ERROR_WRITE                = 13
UNKNOWN_DP_ERROR              = 14
ACCESS_DENIED                 = 15
DP_OUT_OF_MEMORY              = 16
DISK_FULL                     = 17
DP_TIMEOUT                    = 18
FILE_NOT_FOUND                = 19
DATAPROVIDER_EXCEPTION        = 20
CONTROL_FLUSH_ERROR           = 21
OTHERS                        = 22
.
IF SY-SUBRC <> 0.
MESSAGE I000 WITH 'ダウンロードに失敗しました'.
G_ERR = C_ON.
ELSE.
MESSAGE S000 WITH 'ダウンロードしました'.
ENDIF.


ENDFORM.                    " DOWNLOAD
*----------------------------------------------------------------------*
*       締日チェック
*----------------------------------------------------------------------*
FORM CHECK_SIMEBI.
DATA:L_T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART ZTERM,
L_W_KNVV      TYPE TYP_KNVV,
L_ID          TYPE THEAD-TDID,
L_LANGU       TYPE THEAD-TDSPRAS,
L_NAME        TYPE THEAD-TDNAME,
L_OBJECT      TYPE THEAD-TDOBJECT,
LT_LINES      TYPE STANDARD TABLE OF TLINE,
LW_LINES      TYPE TLINE.

* 選択画面から、今回の締日を取得
CLEAR:G_ZFAEL.
IF R_05 = C_ON.
G_ZFAEL = C_ZFAEL_05.
ELSEIF R_10 = C_ON.
G_ZFAEL = C_ZFAEL_10.
ELSEIF R_15 = C_ON.
G_ZFAEL = C_ZFAEL_15.
ELSEIF R_20 = C_ON.
G_ZFAEL = C_ZFAEL_20.
ELSEIF R_25 = C_ON.
G_ZFAEL = C_ZFAEL_25.
ELSEIF R_31 = C_ON.
G_ZFAEL = C_ZFAEL_31.
ENDIF.

* 今回処理対象のうち、得意先の締日が選択条件に合致する得意先を取得
SELECT KNVV~KUNNR
KNVV~VKORG
KNVV~VTWEG
KNVV~SPART
KNVV~ZTERM
T052~ZFAEL
FROM KNVV
INNER JOIN T052
ON T052~ZTERM = KNVV~ZTERM
INTO TABLE T_KNVV
FOR ALL ENTRIES IN T_YK520
WHERE KNVV~KUNNR = T_YK520-KUNNR
AND KNVV~VKORG = T_YK520-VKORG
AND KNVV~VTWEG = T_YK520-VTWEG
AND KNVV~SPART = T_YK520-SPART
AND T052~ZFAEL = G_ZFAEL.

* 処理対象の得意先を、得意先照合対象の得意先だけにする
REFRESH:L_T_KNVV.
L_T_KNVV[] = T_KNVV[].

L_LANGU   = C_SPRAS.
L_ID      = 'YN01'.
L_OBJECT  = 'KNB1'.

LOOP AT L_T_KNVV INTO L_W_KNVV.
AT END OF KUNNR.
CLEAR:LW_LINES.
REFRESH: LT_LINES.
L_NAME = L_W_KNVV-KUNNR.
CONCATENATE  L_NAME  'C001'  INTO  L_NAME.

CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                            = L_ID
LANGUAGE                      = L_LANGU
NAME                          = L_NAME
OBJECT                        = L_OBJECT
TABLES
LINES                         = LT_LINES
EXCEPTIONS
ID                            = 1
LANGUAGE                      = 2
NAME                          = 3
*       NOT_FOUND                     = 4
OBJECT                        = 5
REFERENCE_CHECK               = 6
WRONG_ACCESS_TO_ARCHIVE       = 7
OTHERS                        = 8.
IF SY-SUBRC = 0.
READ TABLE LT_LINES INTO LW_LINES INDEX 1.
ENDIF.
IF  LW_LINES-TDLINE <> 'Y'
AND LW_LINES-TDLINE <> 'Z'.
DELETE T_KNVV WHERE KUNNR = L_W_KNVV-KUNNR.
ENDIF.
ENDAT.
ENDLOOP.

*-- 取得したYK520の得意先コードをチェック
LOOP AT T_YK520 INTO W_YK520.
CLEAR:W_KNVV.
READ TABLE T_KNVV INTO W_KNVV
WITH TABLE KEY KUNNR = W_YK520-KUNNR
VKORG = W_YK520-VKORG
VTWEG = W_YK520-VTWEG
SPART = W_YK520-SPART.
IF SY-SUBRC = 0.
CLEAR:W_YK520_O.
MOVE-CORRESPONDING W_YK520 TO W_YK520_O.
CALL FUNCTION 'FI_PERIOD_DETERMINE'
EXPORTING
*2012/04/09 MOD START
I_BUDAT             = W_YK520_O-FKDAT "請求日
*          I_BUDAT             = W_YK520_O-ZFBDT "締日
*2012/04/09 MOD END
I_BUKRS             = 'C001'
IMPORTING
E_GJAHR              = W_YK520_O-GJAHR
EXCEPTIONS
FISCAL_YEAR          = 1
PERIOD               = 2
PERIOD_VERSION       = 3
POSTING_PERIOD       = 4
SPECIAL_PERIOD       = 5
VERSION              = 6
POSTING_DATE         = 7
OTHERS               = 8.
APPEND W_YK520_O TO T_YK520_O.
G_CNT_YK520 = G_CNT_YK520 + 1.
ELSE.
DELETE T_YK520  WHERE KUNNR = W_YK520-KUNNR
AND VKORG = W_YK520-VKORG
AND VTWEG = W_YK520-VTWEG
AND SPART = W_YK520-SPART.
ENDIF.
ENDLOOP.

IF T_YK520_O[] IS INITIAL.
MESSAGE S762(YN01).
ENDIF.

FREE:T_YK520,T_KNVV.

ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  GET_DB
*&---------------------------------------------------------------------*
*       標準ＤＢデータ取得(VBRK,VBRP,VBKD)
*----------------------------------------------------------------------*
FORM GET_DB.

CHECK NOT T_YK520_O[] IS INITIAL.
* VBRP
SELECT VBELN
POSNR
VKGRP
VKBUR
INTO TABLE T_VBRP
FROM VBRP
FOR ALL ENTRIES IN T_YK520_O
WHERE VBELN = T_YK520_O-VBELN
AND POSNR = T_YK520_O-POSNR.

** VBKD
*  SELECT VBELN
*         POSNR
*         BSTKD
*    INTO TABLE T_VBKD
*    FROM VBKD
*     FOR ALl ENTRIES IN T_YK520_O
*   WHERE VBELN = T_YK520_O-AUBEL   "販売伝票
*     AND POSNR = C_INIT_POS.       "明細ゼロ

* VBRK
SELECT VBELN
FKDAT
INTO TABLE T_VBRK
FROM VBRK
FOR ALL ENTRIES IN T_YK520_O
WHERE VBELN = T_YK520_O-VBELN.

* BSID
SELECT GJAHR
BELNR
BUKRS
KUNNR
BUZEI
UMSKS
UMSKZ
AUGDT
AUGBL
ZUONR
INTO TABLE T_BSID
FROM BSID
FOR ALL ENTRIES IN T_YK520_O
WHERE KUNNR = T_YK520_O-KUNNR
AND BUKRS = T_YK520_O-BUKRS
AND GJAHR = T_YK520_O-GJAHR
AND BELNR = T_YK520_O-VBELN.

ENDFORM.                    " GET_DB
*&---------------------------------------------------------------------*
*&      Form  EDIT_YN520
*&---------------------------------------------------------------------*
*       チェックと編集
*----------------------------------------------------------------------*
FORM EDIT_YN520.
DATA:L_W_YK520 TYPE TYP_YK520_O.

SORT T_YK520_O BY BUKRS KUNNR VKORG VTWEG SPART ZFBDT
VBELN POSNR.
LOOP AT T_YK520_O INTO W_YK520_O.
L_W_YK520 = W_YK520_O.
AT NEW VBELN.
PERFORM GET_BIKO USING W_YK520_O-VBELN.
CLEAR: W_BSID.
LOOP AT T_BSID INTO W_BSID
WHERE GJAHR = L_W_YK520-GJAHR
AND BELNR = L_W_YK520-VBELN
AND KUNNR = L_W_YK520-KUNNR
AND BUKRS = L_W_YK520-BUKRS.
EXIT.
ENDLOOP.
ENDAT.
CLEAR:W_YN120.
W_YN120-GJAHR     = W_YK520_O-GJAHR.  "伝票会計年度
W_YN120-SLPDOC    = W_YK520_O-VBELN.  "伝票番号
W_YN120-DTLDOC    = W_YK520_O-POSNR.  "伝票明細番号
W_YN120-VRFCTON   = W_YK520_O-KUNNR.  "得意先コード(支払人)
W_YN120-BUKRS     = W_YK520_O-BUKRS.  "会社コード

CLEAR:W_VBRK,W_VBRP.
"W_VBKD.
READ TABLE T_VBRK INTO W_VBRK WITH TABLE KEY
VBELN = W_YK520_O-VBELN.
READ TABLE T_VBRP INTO W_VBRP WITH TABLE KEY
VBELN = W_YK520_O-VBELN
POSNR = W_YK520_O-POSNR.
*    READ TABLE T_VBKD INTO W_VBKD WITH TABLE KEY
*                      VBELN = W_YK520_O-AUBEL
*                      POSNR = C_INIT_POS.

* エラーチェック
CLEAR:W_ERR.
PERFORM ERR_CHECK.
IF NOT W_ERR IS INITIAL.
CONTINUE.
ENDIF.

W_YN120-ZFBDT     = W_YK520_O-ZFBDT.    "締日
*   W_YN120-PRCTR     = 初期値.             "利益センタ
W_YN120-PERNR     = SY-UNAME.           "担当者
W_YN120-DVSON     = W_VBRP-VKBUR.       "営業所
*   W_YN120-TGTFLG    = 初期値.             "照合対象フラグ
W_YN120-CSTS      = '1'.                "照合ステータス
W_YN120-COMMT     = TEXT-022.           "コメント(未請求)
W_YN120-YNGJAHR   = W_BSID-GJAHR.       "会計伝票会計年度
W_YN120-BELNR     = W_BSID-BELNR.       "会計伝票番号
W_YN120-BUZEI     = W_BSID-BUZEI.       "会計伝票明細番号
W_YN120-KNQUAN    = W_YK520_O-FKIMG.    "数量
W_YN120-KNUNIT    = W_YK520_O-VRKME.    "単位
*   W_YN120-KNUNTPRC  = 通貨換算後の税抜額÷数量. "単価
PERFORM GET_TANKA.
W_YN120-KNTAXAMT  = W_YK520_O-MWSBP.    "消費税額
W_YN120-KNITXAMT  = W_YK520_O-NETWR.    "税抜金額
W_YN120-KNETXAMT  = W_YK520_O-BRTWR.    "税込金額
W_YN120-WAERS     = W_YK520_O-WAERS.    "通貨コード
*   W_YN120-CHKDOC    = 初期値.             "照合番号
*   W_YN120-CHKDAT    = 初期値.             "照合日
*   W_YN120-CHKUSR    = 初期値.             "照合ユーザ
W_YN120-UPDDAT    = SY-DATUM.           "更新日
W_YN120-UPDTIM    = SY-UZEIT.           "更新時刻
W_YN120-UPDUSR    = SY-UNAME.           "更新ユーザ
W_YN120-UPDPRG    = SY-REPID.           "更新プログラム

*2012/05/22 MOD START
*必ず得意先発注番号を設定
W_YN120-CKEY1   = W_YK520_O-BSTKD.    "照合キー1：得意先発注番号

*    IF W_YK520_O-WAERS  = C_JPY.
*     W_YN120-CKEY1   = W_YK520_O-BSTKD.    "照合キー1：得意先発注番号
*    ELSE.
*      W_YN120-CKEY2   = G_BIKO.             "照合キー2：インボイスNo
*    ENDIF.
*2012/05/22 MOD END

*   W_YN120-CKEY3     = 初期値.             "照合キー3
*   W_YN120-CKEY4     = 初期値.             "照合キー4
*   W_YN120-CKEY5     = 初期値.             "照合キー5
*   W_YN120-CKEY6     = 初期値.             "照合キー6
*   W_YN120-CKEY7     = 初期値.             "照合キー7
*   W_YN120-CKEY8     = 初期値.             "照合キー8
W_YN120-LIST1     = W_YK520_O-KUNNR.    "得意先コード(受注先)
W_YN120-LIST2     = W_YK520_O-BSTKD.    "得意先発注番号
W_YN120-LIST3     = W_YK520_O-AUBEL.    "受注伝票
W_YN120-LIST4     = W_VBRP-VKGRP.       "営業グループ
W_YN120-LIST5     = W_YK520_O-MATNR.    "品目コード
W_YN120-LIST6     = W_YK520_O-ARKTX.    "品目テキスト
W_YN120-LIST7     = W_YK520_O-KDMAT.    "得意先品目
W_YN120-LIST8     = G_BIKO.             "インボイスNo
*   W_YN120-LIST9     = 初期値.             "一覧表示項目9
*   W_YN120-LIST10    = 初期値.             "一覧表示項目10
W_YN120-INSDT     = SY-DATUM.           "登録日
W_YN120-LDATE1    = W_VBRK-FKDAT.       "請求日
*   W_YN120-CZFBDT    = 初期値.             "照合締日
*   W_YN120-AUTOFLG   = 初期値.             "自動生成明細（部分照合）
*   W_YN120-OUTMNTH3  = 初期値.             "月締請求書出力月
*   W_YN120-PRINTDT3  = 初期値.             "月締請求書出力日
*   W_YN120-DUMMYMNTH = 初期値.             "ダミー月
*   W_YN120-DUMMYDT   = 初期値.             "ダミー日付
APPEND W_YN120 TO T_YN120.
G_CNT_YN120 = G_CNT_YN120 + 1.
ENDLOOP.

ENDFORM.                    " EDIT_YN520
*&---------------------------------------------------------------------*
*&      Form  ERR_CHECK
*&---------------------------------------------------------------------*
*       エラーチェック
*----------------------------------------------------------------------*
FORM ERR_CHECK.
DATA:L_MESSAGE TYPE TYP_ERR-MESSAGE,
L_SLPDOC  TYPE YNSLPDOC.

CLEAR:L_MESSAGE.
* 会計年度取得エラー
IF W_YK520_O-GJAHR IS INITIAL.
*   会計年度が取得できませんでした
L_MESSAGE = TEXT-017.
ELSEIF W_VBRK-VBELN IS INITIAL.
*   VBRKが取得できませんでした
L_MESSAGE = TEXT-019.
ELSEIF W_VBRP-VBELN IS INITIAL.
*   VBRPが取得できませんでした
L_MESSAGE = TEXT-020.
*  ELSEIF W_VBKD-VBELN IS INITIAL.
**   VBKDが取得できませんでした
*    L_MESSAGE = TEXT-021.
ENDIF.

IF L_MESSAGE IS INITIAL.
CLEAR:L_SLPDOC.
SELECT SINGLE SLPDOC
INTO L_SLPDOC
FROM YN120
WHERE GJAHR   = W_YN120-GJAHR
AND SLPDOC  = W_YN120-SLPDOC
AND DTLDOC  = W_YN120-DTLDOC
AND VRFCTON = W_YN120-VRFCTON
AND BUKRS   = W_YN120-BUKRS.
IF SY-SUBRC = 0.
*      すでに同じキーが存在します
L_MESSAGE = TEXT-018.
ENDIF.
ENDIF.

IF NOT L_MESSAGE IS INITIAL.
W_ERR-VBELN  = W_YK520_O-VBELN. "暦年
W_ERR-POSNR  = W_YK520_O-POSNR. "検収番号
W_ERR-MESSAGE = L_MESSAGE.
APPEND W_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
ENDIF.


ENDFORM.                    " ERR_CHECK
*&---------------------------------------------------------------------*
*&      Form  GET_TANKA
*&---------------------------------------------------------------------*
*       単価計算(通貨換算後の税抜額÷数量)
*----------------------------------------------------------------------*
FORM GET_TANKA.
DATA:L_KEKKA    TYPE BAPICURR-BAPICURR.

CLEAR: L_KEKKA.
IF W_YK520_O-FKIMG = 0.
W_YN120-KNUNTPRC  = 0.
ELSE.
CALL FUNCTION 'BAPI_CURRENCY_CONV_TO_EXTERNAL'
EXPORTING
CURRENCY              = W_YK520_O-WAERS
AMOUNT_INTERNAL       = W_YK520_O-NETWR
IMPORTING
AMOUNT_EXTERNAL        = L_KEKKA.
W_YN120-KNUNTPRC  = L_KEKKA / W_YK520_O-FKIMG.
ENDIF.
ENDFORM.                    " GET_TANKA
*&---------------------------------------------------------------------*
*&      Form  GET_BIKO
*&---------------------------------------------------------------------*
*       テキスト取得
*----------------------------------------------------------------------*
*      -->P_VBELN  請求伝票番号
*----------------------------------------------------------------------*
FORM GET_BIKO USING P_VBELN.
DATA:L_ID          TYPE THEAD-TDID,
L_LANGU       TYPE THEAD-TDSPRAS,
L_NAME        TYPE THEAD-TDNAME,
L_OBJECT      TYPE THEAD-TDOBJECT,
LT_LINES      TYPE STANDARD TABLE OF TLINE,
LW_LINES      TYPE TLINE.

L_LANGU   = C_SPRAS.
L_ID      = '0001'.
L_OBJECT  = 'VBBK'.
L_NAME    = P_VBELN.

CLEAR: G_BIKO.

REFRESH: LT_LINES.
CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                            = L_ID
LANGUAGE                      = L_LANGU
NAME                          = L_NAME
OBJECT                        = L_OBJECT
TABLES
LINES                         = LT_LINES
EXCEPTIONS
ID                            = 1
LANGUAGE                      = 2
NAME                          = 3
*    NOT_FOUND                     = 4
OBJECT                        = 5
REFERENCE_CHECK               = 6
WRONG_ACCESS_TO_ARCHIVE       = 7
OTHERS                        = 8.
IF SY-SUBRC = 0.
CLEAR:LW_LINES.
READ TABLE LT_LINES INTO LW_LINES INDEX 1.

CALL FUNCTION 'TEXT_SPLIT'
EXPORTING
LENGTH        = 40
TEXT          = LW_LINES-TDLINE
IMPORTING
LINE          = G_BIKO.
ENDIF.

ENDFORM.                    " GET_BIKO
*&---------------------------------------------------------------------*
*&      Form  INSERT_DATA
*&---------------------------------------------------------------------*
*       データ登録
*----------------------------------------------------------------------*
FORM INSERT_DATA.

IF T_YN120[] IS INITIAL.
EXIT.
ENDIF.

INSERT YN120 FROM TABLE T_YN120
ACCEPTING DUPLICATE KEYS.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.

ENDFORM.                    " INSERT_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_IKO
*&---------------------------------------------------------------------*
*      処理結果出力【移行】
*----------------------------------------------------------------------*
FORM WRITE_IKO.
DATA:LW_CNT(10)  TYPE C,
LW_ERR      TYPE TYP_ERR.

WRITE G_CNT_YK520 TO LW_CNT.
WRITE: /001 '<データ移行>',
/003 '一括請求書明細データ(YK520)抽出件数：',
040 LW_CNT,
050 '件'.
SKIP 1.

WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)登録件数：',
040 LW_CNT,
050 '件'.
WRITE G_CNT_ERR TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)エラー件数：',
040 LW_CNT,
050 '件'.
CHECK NOT T_ERR[] IS INITIAL.
SKIP 1.
WRITE: /003 '伝票番号',
014 '明細',
029 'エラー内容'.
ULINE.
LOOP AT T_ERR INTO LW_ERR.
WRITE: /003 LW_ERR-VBELN,
014 LW_ERR-POSNR,
029 LW_ERR-MESSAGE.
ENDLOOP.

ENDFORM.                    " WRITE_IKO
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA_GET
*&---------------------------------------------------------------------*
*       YN120データ取得【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA_GET.

SELECT *
INTO TABLE T_YN120
FROM YN120
WHERE GJAHR   IN P_GJAHR
AND SLPDOC  IN P_SLPDOC
AND DTLDOC  IN P_DTLDOC
AND VRFCTON IN P_VRF
AND ZFBDT   IN P_ZFB.

IF SY-SUBRC = 0.
DESCRIBE TABLE T_YN120 LINES G_CNT_YN120.
ELSE.
MESSAGE S762(YN01).
ENDIF.

ENDFORM.                    " DEL_DATA_GET
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA
*&---------------------------------------------------------------------*
*       YN120削除【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA.
CHECK NOT T_YN120[] IS INITIAL.

DELETE YN120 FROM TABLE T_YN120.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.

ENDFORM.                    " DEL_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_DEL
*&---------------------------------------------------------------------*
*       処理結果出力【削除】
*----------------------------------------------------------------------*
FORM WRITE_DEL.
DATA:LW_CNT(10)  TYPE C.

WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /001 '<移行データ削除>',
/003 '自社データ(売上)(YN120)削除件数：',
030 LW_CNT,
040 '件'.

ENDFORM.                    " WRITE_DEL
*&---------------------------------------------------------------------*
*&      Form  FRM_CHK_FILE
*&---------------------------------------------------------------------*
*       ファイルダウンロード検索ヘルプ
*----------------------------------------------------------------------*
FORM FRM_CHK_FILE.
CALL FUNCTION 'WS_FILENAME_GET'
EXPORTING
MASK     = '*.*,ALL Files,*.*. '
MODE     = 'S'
TITLE    = 'ダウンロード'
IMPORTING
FILENAME = P_PASS
EXCEPTIONS
OTHERS   = 5.
IF SY-SUBRC <> 0.
ENDIF.

ENDFORM.                    " FRM_CHK_FILE
