*&---------------------------------------------------------------------*
*&  プログラムＩＤ ： /A1F/YXT120IN001
*&  プログラム名称 ：【共通】INCLUDE処理
*&  処　理　概　要 ：
*&
*&  作　  成  　者 ： H.Kimura (MIS)
*&  作　  成  　日 ： 2009.04.02
*&---------------------------------------------------------------------*
*& 【変更履歴】
*&  更新日     管理番号     更新者               更新内容
*& ----------  --------  ---------------  -----------------------------*
*& 2007/12/03  XXX〜XXX  H.KAJIHARA       発注者・受注者コード変換追加
*& ----------  --------  ---------------  -----------------------------*
*& 2008/10/01  ZZ0188    T.AIDA           ｼﾝｸﾞﾙｸｫｰﾃｰｼｮﾝ付与処理追加
*& ----------  --------  ---------------  -----------------------------*
*& 2008/10/20  ZZ0202    Y.FUJIKAKE       バリアント検索ヘルプ不正対応
*& 2012/08/29            ISID             ES-UP
*& 2014/08/29            ISID18           コードページを"UTF-8"に修正
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  A1F_OPEN_FILE_INC
*&---------------------------------------------------------------------*
*       FILE OPEN 処 理
*----------------------------------------------------------------------*
*       <--  EX_FILENAME  ファイル名（フルパス）
*----------------------------------------------------------------------*
FORM A1F_OPEN_FILE_INC                                      "#EC CALLED
CHANGING VALUE(EX_FILENAME) TYPE C.
*----------------------------------------------------------------------*
* Local Data
*----------------------------------------------------------------------*
DATA : A1F_TL_FILETAB TYPE FILETABLE,
A1F_WL_FILETAB LIKE FILE_TABLE,
A1F_WL_RTNCD   TYPE I.
**
CLEAR EX_FILENAME.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
CHANGING
FILE_TABLE              = A1F_TL_FILETAB
RC                      = A1F_WL_RTNCD
EXCEPTIONS
FILE_OPEN_DIALOG_FAILED = 1
CNTL_ERROR              = 2
ERROR_NO_GUI            = 3
OTHERS                  = 5.

* エラー判定
IF SY-SUBRC <> 0.       " エラー発生時
CLEAR EX_FILENAME.    " Changingパラメータを初期化
EXIT.                 " サブルーチン処理を終了。
ENDIF.

LOOP AT A1F_TL_FILETAB INTO A1F_WL_FILETAB.
*   フルパスを戻り値にセット。
MOVE A1F_WL_FILETAB-FILENAME TO EX_FILENAME.
EXIT.
ENDLOOP.

ENDFORM.                    " A1F_OPEN_FILE_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_SAVE_FILE_INC
*&---------------------------------------------------------------------*
*       FILE SAVE 処 理
*----------------------------------------------------------------------*
*       <--  EX_FILENAME  ファイル名（フルパス）
*----------------------------------------------------------------------*
FORM A1F_SAVE_FILE_INC                                      "#EC CALLED
CHANGING VALUE(EX_FILENAME) TYPE C.
*----------------------------------------------------------------------*
* Local Data
*----------------------------------------------------------------------*
DATA : A1F_WL_FILENAME TYPE STRING,
A1F_WL_PATH     TYPE STRING,
A1F_WL_FULLPATH TYPE STRING.
**
CLEAR EX_FILENAME.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_SAVE_DIALOG
CHANGING
FILENAME     = A1F_WL_FILENAME
PATH         = A1F_WL_PATH
FULLPATH     = A1F_WL_FULLPATH
EXCEPTIONS
CNTL_ERROR   = 1
ERROR_NO_GUI = 2
OTHERS       = 4.

* エラー判定
IF SY-SUBRC <> 0.       " エラー発生時
CLEAR EX_FILENAME.    " Changingパラメータを初期化
EXIT.                 " サブルーチン処理を終了。
ENDIF.

* フルパスを戻り値にセット。
MOVE A1F_WL_FULLPATH TO EX_FILENAME.

ENDFORM.                    " A1F_SAVE_FILE_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_UPLOAD_FILE_DAT_INC
*&---------------------------------------------------------------------*
*       FILE UPLOAD 処理  （タブ区切り）
*----------------------------------------------------------------------*
*  -->  IM_FILENAME  ファイル名（フルパス）
*  <--  EX_ITAB      ファイルデータ格納内部テーブル
*  <--  EX_FLG_ERR   エラー判定フラグ
*----------------------------------------------------------------------*
FORM A1F_UPLOAD_FILE_DAT_INC                                "#EC CALLED
USING    VALUE(IM_FILENAME) TYPE C
**** START ADD 2015/02/04 ISID11 ****
VALUE(IM_BUKRS)    TYPE T001-BUKRS
**** END ADD 2015/02/04 ISID11 ****
CHANGING       EX_ITAB      TYPE TABLE
VALUE(EX_FLG_ERR)  TYPE C.
*----------------------------------------------------------------------*
*  Local Data
*----------------------------------------------------------------------*
DATA : A1F_WL_FILENAME TYPE STRING.
* Add ES-UP 2012/08/29 -->
DATA L_CODEPAGE TYPE ABAP_ENCODING.

**** START ADD 2015/02/04 ISID11 ****
DATA:
L_Z_OUTPUT_CP TYPE  ZTEGZZM001-Z_OUTPUT_CP,
L_SAPCODEPAGE TYPE STRING.

CALL FUNCTION 'ZEG_ZZ_GLOBAL_PGM_CONFIG_GET'
EXPORTING
IMPPGM      = SY-REPID
IMPBUKRS    = IM_BUKRS
IMPORTING
EXPCODEPAGE = L_Z_OUTPUT_CP.
**** END ADD 2015/02/04 ISID11 ****

****START UPD 2014/08/29 ISID18****
*  L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( `shift_jis` ).
**** START UPD 2015/02/04 ISID11 ****
*  L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( `UTF-8` ).
L_SAPCODEPAGE = L_Z_OUTPUT_CP.
IF L_SAPCODEPAGE IS NOT INITIAL.

L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( L_SAPCODEPAGE ).

ENDIF.
**** END UPD 2015/02/04 ISID11 ****
****END UPD 2014/08/29 ISID18****
* Add ES-UP 2012/08/29 <--
CLEAR EX_FLG_ERR.

MOVE IM_FILENAME TO A1F_WL_FILENAME.

CALL FUNCTION 'GUI_UPLOAD'
EXPORTING
FILENAME                = A1F_WL_FILENAME
HAS_FIELD_SEPARATOR     = 'X'
* Add ES-UP 2012/08/29 -->
CODEPAGE                  = L_CODEPAGE
* Add ES-UP 2012/08/29 <--
TABLES
DATA_TAB                = EX_ITAB[]
EXCEPTIONS
FILE_OPEN_ERROR         = 1
FILE_READ_ERROR         = 2
NO_BATCH                = 3
GUI_REFUSE_FILETRANSFER = 4
INVALID_TYPE            = 5
NO_AUTHORITY            = 6
UNKNOWN_ERROR           = 7
BAD_DATA_FORMAT         = 8
HEADER_NOT_ALLOWED      = 9
SEPARATOR_NOT_ALLOWED   = 10
HEADER_TOO_LONG         = 11
UNKNOWN_DP_ERROR        = 12
ACCESS_DENIED           = 13
DP_OUT_OF_MEMORY        = 14
DISK_FULL               = 15
DP_TIMEOUT              = 16
OTHERS                  = 17.

IF SY-SUBRC <> 0.
MOVE 'X' TO EX_FLG_ERR.
ENDIF.

ENDFORM.                    " A1F_UPLOAD_FILE_DAT_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_DOWNLOAD_FILE_DAT_INC
*&---------------------------------------------------------------------*
*       FILE DOWNLOAD 処理  （タブ区切り）
*----------------------------------------------------------------------*
*  -->  IM_FILENAME  ファイル名（フルパス）
*  <--  EX_ITAB      ファイルデータ格納内部テーブル
*  <--  EX_FLG_ERR   エラー判定フラグ
*----------------------------------------------------------------------*
FORM A1F_DOWNLOAD_FILE_DAT_INC                              "#EC CALLED
USING    VALUE(IM_FILENAME) TYPE C
**** START ADD 2015/02/04 ISID11 ****
VALUE(IM_BUKRS)    TYPE T001-BUKRS
**** END ADD 2015/02/04 ISID11 ****
CHANGING       EX_ITAB      TYPE TABLE
VALUE(EX_FLG_ERR)  TYPE C.
*----------------------------------------------------------------------*
*  Local Data
*----------------------------------------------------------------------*
DATA : A1F_WL_FILENAME TYPE STRING.
* Add ES-UP 2012/08/29 -->
DATA L_CODEPAGE TYPE ABAP_ENCODING.

**** START ADD 2015/02/04 ISID11 ****
DATA:
L_Z_OUTPUT_CP TYPE  ZTEGZZM001-Z_OUTPUT_CP,
L_SAPCODEPAGE TYPE STRING.

CALL FUNCTION 'ZEG_ZZ_GLOBAL_PGM_CONFIG_GET'
EXPORTING
IMPPGM      = SY-REPID
IMPBUKRS    = IM_BUKRS
IMPORTING
EXPCODEPAGE = L_Z_OUTPUT_CP.
**** END ADD 2015/02/04 ISID11 ****
****START UPD 2014/08/29 ISID18****
*  L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( `shift_jis` ).
**** START UPD 2015/02/04 ISID11 ****
*  L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( `UTF-8` ).
L_SAPCODEPAGE = L_Z_OUTPUT_CP.
IF L_SAPCODEPAGE IS NOT INITIAL.

L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( L_SAPCODEPAGE ).

ENDIF.
**** END UPD 2015/02/04 ISID11 ****
****END UPD 2014/08/29 ISID18****
* Add ES-UP 2012/08/29 <--
CLEAR EX_FLG_ERR.

MOVE IM_FILENAME TO A1F_WL_FILENAME.

CALL FUNCTION 'GUI_DOWNLOAD'
EXPORTING
FILENAME                = A1F_WL_FILENAME
WRITE_FIELD_SEPARATOR   = 'X'
* Add ES-UP 2012/08/29 -->
CODEPAGE                  = L_CODEPAGE
* Add ES-UP 2012/08/29 <--
TABLES
DATA_TAB                = EX_ITAB
EXCEPTIONS
FILE_WRITE_ERROR        = 1
NO_BATCH                = 2
GUI_REFUSE_FILETRANSFER = 3
INVALID_TYPE            = 4
NO_AUTHORITY            = 5
UNKNOWN_ERROR           = 6
HEADER_NOT_ALLOWED      = 7
SEPARATOR_NOT_ALLOWED   = 8
FILESIZE_NOT_ALLOWED    = 9
HEADER_TOO_LONG         = 10
DP_ERROR_CREATE         = 11
DP_ERROR_SEND           = 12
DP_ERROR_WRITE          = 13
UNKNOWN_DP_ERROR        = 14
ACCESS_DENIED           = 15
DP_OUT_OF_MEMORY        = 16
DISK_FULL               = 17
DP_TIMEOUT              = 18
FILE_NOT_FOUND          = 19
DATAPROVIDER_EXCEPTION  = 20
CONTROL_FLUSH_ERROR     = 21
OTHERS                  = 22.

IF SY-SUBRC <> 0.
MOVE 'X' TO EX_FLG_ERR.
ENDIF.

ENDFORM.                    " A1F_DOWNLOAD_FILE_DAT_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_GET_VARIANT_INC
*&---------------------------------------------------------------------*
*       検索ヘルプ：ＡＬＶ出力バリアント
*----------------------------------------------------------------------*
*       -->  IM_SAVE     バリアント保存形式
*       -->  IM_REPID    プログラムID
*       -->  EX_VARIANT  レイアウト
*----------------------------------------------------------------------*
FORM A1F_GET_VARIANT_INC                                    "#EC CALLED
USING    VALUE(IM_SAVE)    TYPE C
VALUE(IM_REPID)   TYPE SYST-REPID
CHANGING VALUE(EX_VARIANT) TYPE DISVARIANT.
*----------------------------------------------------------------------*
*  Local Data
*----------------------------------------------------------------------*
DATA : A1F_WL_VARIANT TYPE DISVARIANT,      " Display Variant
A1F_WL_EXIT(1) TYPE C.                             "#EC CALLED

**
CLEAR EX_VARIANT.
MOVE IM_REPID TO A1F_WL_VARIANT-REPORT.

CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
EXPORTING
IS_VARIANT    = A1F_WL_VARIANT
I_SAVE        = IM_SAVE
IMPORTING
E_EXIT        = A1F_WL_EXIT
ES_VARIANT    = A1F_WL_VARIANT
EXCEPTIONS
NOT_FOUND     = 1
PROGRAM_ERROR = 2.

IF SY-SUBRC = 0.
MOVE A1F_WL_VARIANT TO EX_VARIANT.
ELSE.
CLEAR EX_VARIANT.
ENDIF.

*----- INS --- ZZ0202 --- 2008.10.20 --- BY Y.FUJIKAKE ------ START >>>
* 選択されなかった場合
IF A1F_WL_EXIT = 'X'.
*   再度プログラムIDの設定
MOVE IM_REPID TO A1F_WL_VARIANT-REPORT.
*   戻り値設定
MOVE A1F_WL_VARIANT TO EX_VARIANT.
ENDIF.
*----- INS --- ZZ0202 --- 2008.10.20 --- BY Y.FUJIKAKE ------ E N D -<<<

ENDFORM.                    " A1F_GET_VARIANT_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_CONV_WEEKDAY_INC
*&---------------------------------------------------------------------*
*       日付項目算出処理（稼働日、曜日）
*----------------------------------------------------------------------*
*      --> IM_IDATE  照会日付
*      --> IM_IDENT  祝日カレンダ
*      --> IM_SPRAS  言語キー
*      --> EX_YOBI   曜日
*                  　【戻り値】 0:月 1:火 2:水 3:木 4:金 5:土 6:日
*      --> EX_KADOU  稼動【戻り値】 0:非稼働日 1:稼働日
*      --> EX_KTEXT  祝日テキスト
*      --> EX_SUBRC  エラー判定
*----------------------------------------------------------------------*
FORM A1F_CONV_WEEKDAY_INC                                   "#EC NEEDED
USING    VALUE(IM_IDATE) TYPE SYST-DATUM
VALUE(IM_IDENT) TYPE THOC-IDENT
VALUE(IM_SPRAS) TYPE THOLT-SPRAS
CHANGING VALUE(EX_YOBI)  TYPE N
VALUE(EX_KADOU) TYPE N
VALUE(EX_KTEXT) TYPE C
VALUE(EX_SUBRC) TYPE SYST-SUBRC.
*----------------------------------------------------------------------*
* Local Tables
*----------------------------------------------------------------------*
TABLES : TFACS,      " 稼働日カレンダ (照会)
THOC,       " 祝日カレンダ
THOLT.      " 祝日テキスト
*----------------------------------------------------------------------*
* Local Data
*----------------------------------------------------------------------*
DATA : A1F_WL_WEEK    LIKE SCAL-WEEK,                     "#EC NEEDED
A1F_WL_MDATE   LIKE SY-DATUM,
A1F_WL_SDATE   LIKE SY-DATUM,                      "#EC NEEDED
A1F_WL_YYYY(4) TYPE N,
A1F_WL_MM(2)   TYPE N,
A1F_WL_DD(2)   TYPE N,
A1F_WL_DAY(31).
*----------------------------------------------------------------------*
* Main Process
*----------------------------------------------------------------------*
CALL FUNCTION 'GET_WEEK_INFO_BASED_ON_DATE'
EXPORTING
DATE   = IM_IDATE
IMPORTING
WEEK   = A1F_WL_WEEK
MONDAY = A1F_WL_MDATE
SUNDAY = A1F_WL_SDATE.

EX_YOBI = IM_IDATE - A1F_WL_MDATE.

MOVE   IM_IDATE(4)    TO   A1F_WL_YYYY.
MOVE   IM_IDATE+4(2)  TO   A1F_WL_MM.
MOVE   IM_IDATE+6(2)  TO   A1F_WL_DD.

SELECT   SINGLE  *  FROM  TFACS
WHERE   IDENT   =  'JP'
AND   JAHR    =  A1F_WL_YYYY.
IF      SY-SUBRC   <>  0.
MOVE SY-SUBRC   TO  EX_SUBRC.
ELSE.
CASE   A1F_WL_MM.
WHEN  1.
MOVE  TFACS-MON01   TO  A1F_WL_DAY.
WHEN  2.
MOVE  TFACS-MON02   TO  A1F_WL_DAY.
WHEN  3.
MOVE  TFACS-MON03   TO  A1F_WL_DAY.
WHEN  4.
MOVE  TFACS-MON04   TO  A1F_WL_DAY.
WHEN  5.
MOVE  TFACS-MON05   TO  A1F_WL_DAY.
WHEN  6.
MOVE  TFACS-MON06   TO  A1F_WL_DAY.
WHEN  7.
MOVE  TFACS-MON07   TO  A1F_WL_DAY.
WHEN  8.
MOVE  TFACS-MON08   TO  A1F_WL_DAY.
WHEN  9.
MOVE  TFACS-MON09   TO  A1F_WL_DAY.
WHEN 10.
MOVE  TFACS-MON10   TO  A1F_WL_DAY.
WHEN 11.
MOVE  TFACS-MON11   TO  A1F_WL_DAY.
WHEN 12.
MOVE  TFACS-MON12   TO  A1F_WL_DAY.
ENDCASE.
A1F_WL_DD   =  A1F_WL_DD  -  1.
MOVE    A1F_WL_DAY+A1F_WL_DD(1)   TO  EX_KADOU.
ENDIF.

SELECT  *  FROM  THOC
UP TO 1 ROWS
WHERE  IDENT   =  IM_IDENT
AND  DATUM   =  IM_IDATE.
ENDSELECT.

IF      SY-SUBRC  =  0.
SELECT  SINGLE  *  FROM  THOLT
WHERE  FTGID   =  THOC-FTGID
AND  SPRAS   =  IM_SPRAS.
IF      SY-SUBRC  =  0.
MOVE THOLT-KTEXT   TO  EX_KTEXT.
ENDIF.
ENDIF.

ENDFORM.                    " A1F_CONV_WEEKDAY_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_CHECK_FILE_INC
*&---------------------------------------------------------------------*
*       ファイル存在チェック
*----------------------------------------------------------------------*
*  -->  IM_FILENAME  ファイル名（フルパス）
*  <--  EX_FLG       判定フラグ変数
*----------------------------------------------------------------------*
FORM A1F_CHECK_FILE_INC                                     "#EC NEEDED
USING    VALUE(IM_FILENAME) TYPE C
CHANGING VALUE(EX_FLG)      TYPE C.
*----------------------------------------------------------------------*
* Local Data
*----------------------------------------------------------------------*

DATA : A1F_WL_RESULT   TYPE ABAP_BOOL,
A1F_WL_FILENAME TYPE STRING.
***
CLEAR EX_FLG.
MOVE IM_FILENAME TO A1F_WL_FILENAME.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_EXIST
EXPORTING
FILE            = A1F_WL_FILENAME
RECEIVING
RESULT          = A1F_WL_RESULT
EXCEPTIONS
CNTL_ERROR      = 1
ERROR_NO_GUI    = 2
WRONG_PARAMETER = 3
OTHERS          = 5.

IF SY-SUBRC <> 0.
MOVE 'X' TO EX_FLG.
ENDIF.

IF A1F_WL_RESULT EQ SPACE.
MOVE 'X' TO EX_FLG.
ENDIF.

ENDFORM.                    " A1F_CHECK_FILE_INC
*&---------------------------------------------------------------------*
*&      Form  A1F_CONV_TAX_INC
*&---------------------------------------------------------------------*
*       税額計算
*----------------------------------------------------------------------*
*      --> IM_KUNNR  得意先コード
*      --> IM_MATNR  品目コード
*      --> IM_NETPR  金額
*      --> IM_BUKRS  会社コード
*      --> IM_JPY    通貨単位
*      <-- IM_TAX    税込価格
*----------------------------------------------------------------------*
FORM A1F_CONV_TAX_INC                                       "#EC NEEDED
USING    VALUE(IM_KUNNR) TYPE KNA1-KUNNR
VALUE(IM_MATNR) TYPE MARA-MATNR
VALUE(IM_BUKRS) TYPE T001-BUKRS
VALUE(IM_NETPR) TYPE NETPR
VALUE(IM_WAERK) TYPE VBAP-WAERK
CHANGING VALUE(EX_TAX)   TYPE NETPR.

*----------------------------------------------------------------------*
*  Local Data
*----------------------------------------------------------------------*
DATA : A1F_TL_MWDAT TYPE TABLE OF RTAX1U15,
A1F_WL_MWDAT LIKE LINE  OF A1F_TL_MWDAT.

DATA : A1F_WL_TAXM1 TYPE MLAN-TAXM1,      " 品目の税分類
A1F_WL_TAXKD TYPE KNVI-TAXKD,      " 品目の税分類
A1F_WL_TATYP TYPE KNVI-TATYP,      " 税カテゴリ (消費税等)
A1F_WL_WRBTR TYPE BSEG-WRBTR,      " 金額
A1F_WL_WAERS TYPE BKPF-WAERS,      " 通貨単位
A1F_WL_KNUMH TYPE KNUMH,           " 条件レコード番号
A1F_WL_MWSK1 TYPE BSEG-MWSKZ.      " 消費税コード
*----------------------------------------------------------------------*
*  Local Constants
*----------------------------------------------------------------------*
CONSTANTS : A1F_CNS_ALAND TYPE ALAND VALUE 'JP',
A1F_CNS_KAPPL TYPE KAPPL VALUE 'V'.

*---<<< Main Process >>>-----------------------------------------------*
CLEAR EX_TAX.

MOVE : IM_NETPR TO A1F_WL_WRBTR,
IM_WAERK TO A1F_WL_WAERS.

* 品目マスタの税分類を取得
SELECT SINGLE TAXM1
FROM MLAN
INTO A1F_WL_TAXM1
WHERE MATNR EQ IM_MATNR
AND ALAND EQ A1F_CNS_ALAND.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

* 得意先マスタの税分類を取得
SELECT TAXKD TATYP
FROM KNVI
INTO (A1F_WL_TAXKD,A1F_WL_TATYP)
UP TO 1 ROWS
WHERE KUNNR EQ IM_KUNNR
AND ALAND EQ A1F_CNS_ALAND.
ENDSELECT.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

* A002
SELECT KNUMH
FROM A002 UP TO 1 ROWS
INTO A1F_WL_KNUMH
WHERE KAPPL EQ A1F_CNS_KAPPL
AND KSCHL EQ A1F_WL_TATYP
AND ALAND EQ A1F_CNS_ALAND
AND TAXK1 EQ A1F_WL_TAXKD
AND TAXM1 EQ A1F_WL_TAXM1.
ENDSELECT.

* KONP
SELECT MWSK1
FROM KONP UP TO 1 ROWS
INTO A1F_WL_MWSK1
WHERE KNUMH EQ A1F_WL_KNUMH.
ENDSELECT.

CALL FUNCTION 'CALCULATE_TAX_FROM_NET_AMOUNT'
EXPORTING
I_BUKRS           = IM_BUKRS
I_MWSKZ           = A1F_WL_MWSK1
I_WAERS           = A1F_WL_WAERS
I_WRBTR           = A1F_WL_WRBTR
TABLES
T_MWDAT           = A1F_TL_MWDAT
EXCEPTIONS
BUKRS_NOT_FOUND   = 1
COUNTRY_NOT_FOUND = 2
MWSKZ_NOT_DEFINED = 3
MWSKZ_NOT_VALID   = 4
KTOSL_NOT_FOUND   = 5
KALSM_NOT_FOUND   = 6
PARAMETER_ERROR   = 7
KNUMH_NOT_FOUND   = 8
KSCHL_NOT_FOUND   = 9
UNKNOWN_ERROR     = 10
ACCOUNT_NOT_FOUND = 11
TXJCD_NOT_VALID   = 12
OTHERS            = 13.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

READ TABLE A1F_TL_MWDAT INTO A1F_WL_MWDAT INDEX 1.

EX_TAX = IM_NETPR + A1F_WL_MWDAT-WMWST.

ENDFORM.                    " A1F_CONV_TAX_INC
