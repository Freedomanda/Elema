REPORT  ZTEST_ISID06_001.
*&---------------------------------------------------------------------*
*&      選択画面
*&---------------------------------------------------------------------*
TABLES TRDIR.
SELECT-OPTIONS P_OBJ FOR TRDIR-NAME.
PARAMETERS P_PATH TYPE STRING MEMORY ID GR8.
*&---------------------------------------------------------------------*
*&      グローバルデータ
*&---------------------------------------------------------------------*
DATA FILE_SEPARATOR TYPE C LENGTH 1.
*&---------------------------------------------------------------------*
*&      LOAD-OF-PROGRAM
*&---------------------------------------------------------------------*
LOAD-OF-PROGRAM.
* pcの区切り文字を取得する。（unix→/, windows→\ など）
CL_GUI_FRONTEND_SERVICES=>GET_FILE_SEPARATOR(
CHANGING FILE_SEPARATOR = FILE_SEPARATOR ).
*&---------------------------------------------------------------------*
*&      INITIALIZATION
*&---------------------------------------------------------------------*
INITIALIZATION.
%_P_OBJ_%_APP_%-TEXT = 'プログラム名'.
%_P_PATH_%_APP_%-TEXT = 'ダウンロードフォルダ'.
PERFORM INITIALIZATION.
*&---------------------------------------------------------------------*
*&      AT SELECTION-SCREEN
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON P_PATH.
CHECK CL_GUI_FRONTEND_SERVICES=>DIRECTORY_EXIST( P_PATH ) = ABAP_FALSE.
MESSAGE 'フォルダが見つかりません' TYPE 'E'.
*--- POV
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_PATH.
CL_GUI_FRONTEND_SERVICES=>DIRECTORY_BROWSE(
CHANGING SELECTED_FOLDER = P_PATH ).
*&---------------------------------------------------------------------*
*&      START-OF-SELECTION
*&---------------------------------------------------------------------*
START-OF-SELECTION.
PERFORM STARTOFSELECTION.
*&---------------------------------------------------------------------*
*&      Form  INITIALIZATION
*&---------------------------------------------------------------------*
FORM INITIALIZATION.
DATA UPLOAD_PATH TYPE STRING.
* 最後にダウンロードしたフォルダを初期値にする
CL_GUI_FRONTEND_SERVICES=>GET_UPLOAD_DOWNLOAD_PATH(
CHANGING   UPLOAD_PATH   = UPLOAD_PATH
DOWNLOAD_PATH = P_PATH
EXCEPTIONS OTHERS        = 0 ).
ENDFORM.                    "initialization
*&---------------------------------------------------------------------*
*&      Form  STARTOFSELECTION
*&---------------------------------------------------------------------*
FORM STARTOFSELECTION.
DATA: I_NAME   TYPE TABLE OF TRDIR-NAME,
L_NAME   TYPE REF TO TRDIR-NAME,
L_PATH   TYPE STRING,
REGEX    TYPE STRING,
SOC      TYPE TABLE OF STRING,
FILENAME TYPE STRING.
* パスの最後が使用PCの区切文字でなければ付加する
CONCATENATE FILE_SEPARATOR '$' INTO REGEX.
FIND REGEX REGEX IN P_PATH.
IF SY-SUBRC = 0.
L_PATH = P_PATH.
ELSE.
CONCATENATE P_PATH FILE_SEPARATOR INTO L_PATH.
ENDIF.
* 一覧取得
SELECT NAME INTO TABLE I_NAME FROM TRDIR
WHERE NAME IN P_OBJ.
* ダウンロード
LOOP AT I_NAME REFERENCE INTO L_NAME.
READ REPORT L_NAME->* INTO SOC.
CHECK SY-SUBRC = 0.
CONCATENATE L_PATH L_NAME->* '.TXT' INTO FILENAME.
CL_GUI_FRONTEND_SERVICES=>GUI_DOWNLOAD(
EXPORTING  FILENAME  = FILENAME
CHANGING   DATA_TAB  = SOC
EXCEPTIONS OTHERS    = 0 ).
ENDLOOP.
ENDFORM.                    "STARTOFSELECTION
