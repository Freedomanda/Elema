************************************************************************
* プログラムID  :ZF024100
* プログラム名  :電子記録債務IF
* 作成日        :2013/11/12
* 作成者        :GSL(morisawa)
* 変更履歴      :
*& YYYY/MM/DD  Programar         Description
*& 2013/12/13  GSL(k.tsunose)    文字コード修正
************************************************************************
REPORT ZF021900 MESSAGE-ID Z1
LINE-SIZE   120
LINE-COUNT  90
NO STANDARD PAGE HEADING.

************************************************************************
*     宣言部
************************************************************************
*----------------------------------------------------------------------*
* TABLES定義
*----------------------------------------------------------------------*
TABLES:BSIK,
USR01
.
*----------------------------------------------------------------------*
* TYPES定義
*----------------------------------------------------------------------*
TYPE-POOLS ABAP.

*支払企業銀行情報
TYPES:BEGIN OF T_JBANK,
BUKRS TYPE T001-BUKRS,    "会社コード
WAERS TYPE T001-WAERS,    "通貨コード
LAND1 TYPE T001-LAND1,    "国コード
BANKS TYPE T012-BANKS,    "銀行国コード
BANKL TYPE T012-BANKL,    "銀行コード
BANKA TYPE CHAR15,        "銀行名
BRNCH TYPE CHAR15,        "銀行支店名
HKTID TYPE T012K-HKTID,   "口座 ID 詳細
BKONT TYPE T012K-BKONT,   "預金種別
BANKN TYPE T012K-BANKN,   "口座番号
KGYCD TYPE CHAR10,        "企業コード
KGYTX TYPE CHAR40,        "企業名
END OF T_JBANK.

*仕入先取得用
TYPES:BEGIN OF T_SHIIRE,
LIFNR TYPE LFB1-LIFNR,    "仕入先コード
BUKRS TYPE LFB1-BUKRS,    "会社コード
ZWELS TYPE LFB1-ZWELS,    "支払方法
BANKS TYPE LFBK-BANKS,    "銀行国コード
BANKL TYPE LFBK-BANKL,    "銀行コード
BKONT TYPE LFBK-BKONT,    "預金種別
BANKN TYPE LFBK-BANKN,    "口座番号
KOINH TYPE LFBK-KOINH,    "口座名義人名
SORTL TYPE LFA1-SORTL,    "仕入先名
END OF T_SHIIRE,
TI_SHIIRE TYPE HASHED TABLE OF T_SHIIRE
WITH UNIQUE KEY LIFNR     "仕入先コード
.

*電子記録債務明細取得用
TYPES:BEGIN OF T_MEISAI,
ZFBDT TYPE BSIK-ZFBDT,    "決済日
BUKRS TYPE BSIK-BUKRS,    "会社コード
LIFNR TYPE BSIK-LIFNR,    "仕入先コード
WRBTR TYPE BSIK-WRBTR,    "伝票通貨額
GJAHR TYPE BSIK-GJAHR,    "会計年度
BELNR TYPE BSIK-BELNR,    "会計伝票番号
BUZEI TYPE BSIK-BUZEI,    "会計伝票明細番号
WAERS TYPE BSIK-WAERS,    "通貨コード
SHKZG TYPE BSIK-SHKZG,    "貸借
WDATE TYPE BSED-WDATE,    "振出日
END OF T_MEISAI,
TI_MEISAI TYPE STANDARD TABLE OF T_MEISAI
.

*電子記録債務明細出力用
TYPES:BEGIN OF T_MSAI_OUT,
ZFBDT TYPE NUMC08,     "決済日
LIFNR TYPE NUMC10,     "仕入先コード
WRBTR TYPE NUMC10,     "伝票通貨額
BELNR TYPE NUMC10,     "会計伝票番号
WDATE TYPE NUMC08,     "振出日
BANKA TYPE CHAR15,     "仕入先銀行名
BRNCH TYPE CHAR15,     "仕入先銀行支店名
BANKL TYPE CHAR08,     "仕入先銀行コード
BKONT TYPE NUMC01,     "仕入先預金種別
BANKN TYPE NUMC07,     "仕入先口座番号
KOINH TYPE CHAR30,     "仕入先口座名義人名
RIYNO TYPE CHAR9,      "利用者番号
SORTL TYPE LFA1-SORTL, "仕入先名
END OF T_MSAI_OUT,
TI_MSAI_OUT TYPE STANDARD TABLE OF T_MSAI_OUT
.

*該当振出日の伝票取得用
TYPES:BEGIN OF T_FURIDEN,
BUKRS TYPE BSIK-BUKRS,    "会社コード
BELNR TYPE BSIK-BELNR,    "会計伝票番号
GJAHR TYPE BSIK-GJAHR,    "会計年度
BUZEI TYPE BSIK-BUZEI,    "会計伝票明細番号
WDATE TYPE BSED-WDATE,    "振出日
END OF T_FURIDEN,
TI_FURIDEN TYPE HASHED TABLE OF T_FURIDEN
WITH UNIQUE KEY BUKRS BELNR GJAHR BUZEI
.
*ログヘッダ用
TYPES:BEGIN OF T_LOGHEAD,
MNGN01(50) TYPE C,
MNGN02(50) TYPE C,
MNGN03(50) TYPE C,
MNGN04(50) TYPE C,
KMKM01(10) TYPE C,
KMKM02(10) TYPE C,
KMKM03(10) TYPE C,
KMKM04(10) TYPE C,
KMKM05(10) TYPE C,
END OF T_LOGHEAD.

*エラーログ取得用
TYPES:BEGIN OF T_ERRLOG,
BUKRS TYPE BSIK-BUKRS,    "会社コード
LIFNR TYPE BSIK-LIFNR,    "仕入先コード
GJAHR TYPE BSIK-GJAHR,    "会計年度
BELNR TYPE BSIK-BELNR,    "会計伝票番号
ERRTX(50) TYPE C,         "内容
END OF T_ERRLOG,
TI_ERRLOG TYPE STANDARD TABLE OF T_ERRLOG
.

*「電手」ダウンロードファイル
TYPES:BEGIN OF T_DTE_F,
FIELD(120) TYPE C,
END OF T_DTE_F,
TI_DTE_F TYPE STANDARD TABLE OF T_DTE_F
.

*「でんさい」ダウンロードファイル
TYPES:BEGIN OF T_DSAI_F,
FIELD(252) TYPE C,
END OF T_DSAI_F,
TI_DSAI_F TYPE STANDARD TABLE OF T_DSAI_F
.

*----------------------------------------------------------------------*
* 内部テーブル定義
*----------------------------------------------------------------------*
DATA:
*仕入先内部テーブル
GT_SHIIRE   TYPE TI_SHIIRE,
*電子記録債務明細内部テーブル
GT_MEISAI   TYPE TI_MEISAI,

*電子記録債務明細出力内部テーブル
GT_MSAI_OUT TYPE TI_MSAI_OUT,

*エラーログ内部テーブル
GT_ERRLOG TYPE TABLE OF T_ERRLOG,

*出力ファイル内部テーブル
GT_DTE_F  TYPE TI_DTE_F,
GT_DSAI_F TYPE TI_DSAI_F
.

*----------------------------------------------------------------------*
* ワークエリア定義
*----------------------------------------------------------------------*
DATA:
*支払企業銀行情報
GW_JBANK TYPE T_JBANK,

*禁則文字チェック用
GW_KINSOKU(96) TYPE C,

*エラー存在チェック用
GW_ERRFLG(1) TYPE C,

*出力ログヘッダ情報保持用
GW_LOGHEAD TYPE T_LOGHEAD.

*----------------------------------------------------------------------*
*CONSTANTS定義
*----------------------------------------------------------------------*
CONSTANTS:
*F4検索ヘルプ
CNS_WTITLE(40)     TYPE C VALUE '出力先のフォルダを選択してください',
CNS_IFOLDER(50)    TYPE C VALUE '', "選択フォルダの初期値

*企業コード・禁則文字チェック
CNS_SUJI(10) TYPE C VALUE '0123456789',                    "半角数字
CNS_EIJI(26) TYPE C VALUE
'ABCDEFGHIJKLMNOPQRSTUVWXYZ',                          "半角英字
CNS_KANA(48) TYPE C VALUE
'ｱｲｳｴｵｶｷｸｹｺｻｼｽｾｿﾀﾁﾂﾃﾄﾅﾆﾇﾈﾉﾊﾋﾌﾍﾎﾏﾐﾑﾒﾓﾔﾕﾖﾗﾘﾙﾚﾛﾜｦﾝﾞﾟ',
"半角カナ
CNS_KIGO(12) TYPE C VALUE ''':()+-\?,./ ',                 "半角記号, SPACE

*電子記録債務明細の貸借チェック用
CNS_DC(1) TYPE C VALUE 'S',       "貸方

*ファイルダウンロード
CNS_FIL(7)     TYPE C VALUE 'fb_for_' ,           "ファイル名（頭）
CNS_A(1)       TYPE C VALUE '_' ,                 "ファイル名（頭）
CNS_DTE(6)     TYPE C VALUE  'dente ',            "電手
CNS_DSAI(6)    TYPE C VALUE  'densai',            "でんさい
CNS_FTYPE(4)   TYPE C VALUE '.txt'.               "ファイル形式
* 2013/12/13 ADD START
* コードページS-JIS用
CONSTANTS CNS_SJIS TYPE STRING VALUE `shift_jis`.
* 2013/12/13 ADD END
************************************************************************
* 選択画面定義
************************************************************************
*共通選択
SELECTION-SCREEN BEGIN OF BLOCK BL1 WITH FRAME TITLE TEXT-S01.
PARAMETERS:     P_BUKRS TYPE T001-BUKRS. "会社コード
SELECT-OPTIONS: S_LIFNR FOR BSIK-LIFNR.  "仕入先コード
SELECTION-SCREEN END OF BLOCK BL1.

*仕入先明細選択
SELECTION-SCREEN BEGIN OF BLOCK BL2 WITH FRAME TITLE TEXT-S02.
SELECT-OPTIONS: S_UMSKZ FOR BSIK-UMSKZ.   "特殊仕訳コード
PARAMETERS: P_WDATE TYPE BSED-WDATE.      "振出日
SELECT-OPTIONS: S_ZFBDT FOR BSIK-ZFBDT  NO INTERVALS.  "決済日
SELECTION-SCREEN END OF BLOCK BL2.

*仕入先マスタ選択
SELECTION-SCREEN BEGIN OF BLOCK BL3 WITH FRAME TITLE TEXT-S03.
PARAMETERS: P_ZWELS TYPE LFB1-ZWELS.      "支払方法
SELECTION-SCREEN END OF BLOCK BL3.

*入力関連
SELECTION-SCREEN BEGIN OF BLOCK BL4 WITH FRAME TITLE TEXT-S04.
PARAMETERS: P_KGYCD(09) TYPE C.           "企業コード
PARAMETERS: P_KGYTX(40) TYPE C.           "企業名
PARAMETERS: P_SIGHT(3) TYPE N.            "サイト
SELECTION-SCREEN END OF BLOCK BL4.

*出力関連
SELECTION-SCREEN BEGIN OF BLOCK BL5 WITH FRAME TITLE TEXT-S05.
PARAMETERS: P_FOLDER(50) TYPE C.          "出力フォルダ
PARAMETERS: P_HBKID TYPE T012-HBKID.      "取引銀行ID

SELECTION-SCREEN BEGIN OF LINE.           "出力レイアウト
SELECTION-SCREEN    COMMENT 01(14)  TEXT-R00.
PARAMETERS P_DTE  RADIOBUTTON  GROUP RB1  USER-COMMAND RB1
DEFAULT 'X'.
SELECTION-SCREEN    COMMENT 20(04)  TEXT-R01
FOR FIELD P_DTE.
PARAMETERS P_DSAI RADIOBUTTON  GROUP RB1.
SELECTION-SCREEN    COMMENT 30(08)  TEXT-R02
FOR FIELD P_DSAI.
SELECTION-SCREEN END OF LINE.
PARAMETERS: P_OCNT(6) TYPE N.            "最大出力件数
SELECTION-SCREEN END OF BLOCK BL5.

************************************************************************
*INITIALIZATION.
************************************************************************

************************************************************************
* AT SELECTION-SCREEN
************************************************************************
AT SELECTION-SCREEN.
*入力パラメータのチェック
PERFORM CHECK_INPUT.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_FOLDER.
*出力先フォルダF4検索ヘルプ
PERFORM F4_HELP.  "call function 'TMP_GUI_BROWSE_FOR_FOLDER '

************************************************************************
*START-OF-SELECTION
************************************************************************
START-OF-SELECTION.

* 変数の初期化
PERFORM WORK_INIT.

* 支払企業銀行情報取得
PERFORM GET_JBANK USING    GW_KINSOKU
CHANGING GW_JBANK
.
* データの取得
PERFORM GET_DATA  USING    GW_JBANK
CHANGING GT_SHIIRE
GT_MEISAI
.
* 電子記録債務明細のチェック＆編集
PERFORM CHECK_MEISAI USING    GW_KINSOKU
GW_JBANK
GT_SHIIRE
GT_MEISAI
CHANGING GT_MSAI_OUT
GT_ERRLOG
GW_ERRFLG
.
FREE:GT_SHIIRE,
GT_MEISAI.
************************************************************************
* 出力
************************************************************************
*ファイル編集・出力
PERFORM OUTPUT_FILE.

*ログ編集・出力
PERFORM OUTPUT_LOG
.
FREE: GT_MSAI_OUT,
GT_ERRLOG
.
************************************************************************
*TOP-OF-PAGE.
************************************************************************
TOP-OF-PAGE.
*一覧ヘッダの作成
PERFORM WRITE_HEAD.


*<<<<<<<<<<<<<<<<<<<<<<<<<以下サブルーチン>>>>>>>>>>>>>>>>>>>>>>>>>>>>*
*---------------------------------------------------------------------*
*       FORM work_init                                                *
*---------------------------------------------------------------------*
*       変数の初期化                                                  *
*---------------------------------------------------------------------*
FORM WORK_INIT.
CLEAR: GW_KINSOKU, GW_ERRFLG, GW_LOGHEAD.

REFRESH:GT_SHIIRE,
GT_MEISAI,
GT_ERRLOG
.
*禁則変数
CONCATENATE CNS_SUJI CNS_EIJI CNS_KANA CNS_KIGO INTO GW_KINSOKU.

ENDFORM.                    "WORK_INIT
*---------------------------------------------------------------------*
*       FORM check_input                                              *
*---------------------------------------------------------------------*
*       入力パラメータのチェック                                      *
*---------------------------------------------------------------------*
FORM CHECK_INPUT.
DATA  LW_ZWELS   TYPE LFB1-ZWELS
.

CLEAR GW_JBANK.

* 禁則変数
CONCATENATE CNS_SUJI CNS_EIJI CNS_KANA CNS_KIGO INTO GW_KINSOKU.

* 会社コードの必須チェック
IF P_BUKRS IS INITIAL.
MESSAGE E006 WITH '会社コード'.
ELSE.
* 会社コードの存在チェック
SELECT SINGLE
BUKRS  "会社コード
WAERS  "通貨コード
LAND1  "国コード
INTO (GW_JBANK-BUKRS,GW_JBANK-WAERS,GW_JBANK-LAND1)
FROM T001
WHERE BUKRS = P_BUKRS
.
IF SY-SUBRC <> 0.
MESSAGE E614 WITH '会社コード'.
ENDIF.
ENDIF.

* 仕入先コードの存在チェック
IF S_LIFNR IS NOT INITIAL.
CONCATENATE '%' P_ZWELS '%' INTO LW_ZWELS.
SELECT COUNT(*)
FROM LFB1
WHERE LIFNR IN S_LIFNR
AND BUKRS =  P_BUKRS
AND ZWELS LIKE LW_ZWELS
.
IF SY-SUBRC <> 0.
MESSAGE E614 WITH '仕入先コード'.
ENDIF.
ENDIF.

* 特殊仕訳コードの必須チェック
IF S_UMSKZ IS INITIAL.
MESSAGE E006 WITH '特殊仕訳コード'.
ENDIF.

* 振出日の必須チェック
IF P_WDATE IS INITIAL.
MESSAGE E006 WITH '振出日'.
ENDIF.

* 支払方法の必須チェック
IF P_ZWELS IS INITIAL.
MESSAGE E006 WITH '支払方法'.
ENDIF.

* 企業コードの必須チェック
IF P_KGYCD IS INITIAL.
MESSAGE E006 WITH '企業コード'.
* 企業コードの禁則文字チェック
ELSE.
IF P_KGYCD CN CNS_SUJI.
MESSAGE E400 WITH '企業コードは9桁の数字です'.
ENDIF.
GW_JBANK-KGYCD = P_KGYCD.
ENDIF.

* 企業名の必須チェック
IF P_KGYTX IS INITIAL.
MESSAGE E006 WITH '企業名'.
*企業名の禁則文字チェック
ELSE.
IF P_KGYTX CN GW_KINSOKU.
MESSAGE E400 WITH '企業名に禁則文字が含まれています'.
ENDIF.
GW_JBANK-KGYTX = P_KGYTX.
ENDIF.

* サイトの必須チェック
IF P_SIGHT IS INITIAL.
MESSAGE E006 WITH 'サイト'.
ENDIF.

* 出力フォルダの必須チェック
IF P_FOLDER IS INITIAL.
MESSAGE E006 WITH '出力フォルダ'.
*その他のチェック①
*末尾が\でなければエラー
ELSE.
IF P_FOLDER CP '*\'.
ELSE.
MESSAGE E400 WITH '出力フォルダの末尾に\をつけてください'.
ENDIF.
*その他のチェック②
*フォルダが存在しなければエラー
PERFORM FOLDER_EXIT.  "call function 'TMP_GUI_GET_FILE_EXIST'
ENDIF.

ENDFORM.                    "CHECK_INPUT
*---------------------------------------------------------------------*
*       FORM folder_exit                                              *
*---------------------------------------------------------------------*
*       出力フォルダの存在チェック                                    *
*---------------------------------------------------------------------*
FORM FOLDER_EXIT.
DATA: LW_EXIST(1) TYPE C.

CALL FUNCTION 'TMP_GUI_GET_FILE_EXIST'
EXPORTING
FNAME          = P_FOLDER
IMPORTING
EXIST          = LW_EXIST
EXCEPTIONS
FILEINFO_ERROR = 1
OTHERS         = 2.

IF SY-SUBRC = 0.
*DUMMY
ENDIF.
IF LW_EXIST <> 'X' OR P_FOLDER = '\'.
MESSAGE E400 WITH '出力フォルダが存在しません'.
ENDIF.
ENDFORM.                    "FOLDER_EXIT
*---------------------------------------------------------------------*
*       FORM f4_help                                                  *
*---------------------------------------------------------------------*
*       出力フォルダの取得                                            *
*---------------------------------------------------------------------*
FORM F4_HELP.

CALL FUNCTION 'TMP_GUI_BROWSE_FOR_FOLDER '
EXPORTING
WINDOW_TITLE    = CNS_WTITLE
INITIAL_FOLDER  = CNS_IFOLDER
IMPORTING
SELECTED_FOLDER = P_FOLDER
EXCEPTIONS
CNTL_ERROR      = 1
OTHERS          = 2.
IF SY-SUBRC = 0.
*DUMMY
ENDIF.

ENDFORM.                    "F4_HELP
*---------------------------------------------------------------------*
*       FORM get_data                                                 *
*---------------------------------------------------------------------*
*       データの取得                                                  *
*---------------------------------------------------------------------*
FORM GET_DATA  USING    I_JBANK  TYPE T_JBANK
CHANGING O_SHIIRE TYPE TI_SHIIRE
O_MEISAI TYPE TI_MEISAI.

DATA: LT_SHIIRE  TYPE TABLE OF T_SHIIRE,
LT_FURIDEN TYPE TI_FURIDEN,
LW_ZWELS   TYPE LFB1-ZWELS
.
FIELD-SYMBOLS: <F_MEISAI>   TYPE T_MEISAI,
<F_FURIDEN>  TYPE T_FURIDEN
.

*対象仕入先コード＆銀行情報の取得
CLEAR: LT_SHIIRE,O_SHIIRE.
CONCATENATE '%' P_ZWELS '%' INTO LW_ZWELS.

SELECT
LFB1~LIFNR "仕入先コード
LFB1~BUKRS "会社コード
LFB1~ZWELS "支払方法
LFBK~BANKS "銀行国コード
LFBK~BANKL "銀行コード
LFBK~BKONT "預金種別
LFBK~BANKN "口座番号
LFBK~KOINH "口座名義人名
LFA1~SORTL "仕入先名
INTO TABLE LT_SHIIRE
FROM LFB1 AS LFB1 INNER JOIN LFA1 AS LFA1
ON LFB1~LIFNR = LFA1~LIFNR
LEFT OUTER JOIN LFBK AS LFBK
ON LFB1~LIFNR = LFBK~LIFNR
WHERE LFB1~LIFNR IN S_LIFNR
AND  LFB1~BUKRS =  I_JBANK-BUKRS
AND  LFB1~ZWELS LIKE LW_ZWELS
.
*仕入先コードの重複行を削除
SORT   LT_SHIIRE BY LIFNR.
DELETE ADJACENT DUPLICATES FROM LT_SHIIRE COMPARING LIFNR.
*対象仕入先コードが1件も存在しない場合エラー
IF LINES( LT_SHIIRE ) IS INITIAL.
MESSAGE S400 WITH '対象の仕入先コードが存在しません'.
STOP.
ENDIF.
*仕入先内部テーブルを設定
O_SHIIRE[] = LT_SHIIRE[].
FREE LT_SHIIRE.

*対象仕入先明細（電子記録債務）の取得
SELECT
ZFBDT "決済日
BUKRS "会社コード
LIFNR "仕入先コード
WRBTR "伝票通貨額
GJAHR "会計年度
BELNR "会計伝票番号
BUZEI "明細番号
WAERS "通貨コード
SHKZG "貸借
INTO TABLE O_MEISAI
FROM BSIK
FOR ALL ENTRIES IN O_SHIIRE
WHERE BSIK~BUKRS  = O_SHIIRE-BUKRS    "会社コード
AND BSIK~LIFNR  = O_SHIIRE-LIFNR    "仕入先コード
AND BSIK~UMSKZ IN S_UMSKZ           "特殊仕訳コード
AND BSIK~ZFBDT IN S_ZFBDT           "支払基準日
.

*電子記録債務明細が1件も存在しない場合エラー
IF SY-SUBRC <> 0.
MESSAGE S400 WITH '対象の仕入先明細(電子記録債務明細)が存在しません'.
STOP.
ENDIF.

SORT O_MEISAI BY BUKRS BELNR GJAHR BUZEI.

*該当振出日の伝票の取得
SELECT BUKRS
BELNR
GJAHR
BUZEI
WDATE
INTO TABLE LT_FURIDEN
FROM BSED
FOR ALL ENTRIES IN O_MEISAI
WHERE BUKRS = O_MEISAI-BUKRS
AND BELNR = O_MEISAI-BELNR
AND GJAHR = O_MEISAI-GJAHR
AND BUZEI = O_MEISAI-BUZEI
AND WDATE = P_WDATE.

*電子記録債務明細が1件も存在しない場合エラー
IF SY-SUBRC <> 0.
MESSAGE S400 WITH '対象の仕入先明細(電子記録債務明細)が存在しません'.
STOP.
ENDIF.

*対象仕入先明細候補から該当振出日のものを選択
LOOP AT O_MEISAI ASSIGNING <F_MEISAI>.
*
READ TABLE LT_FURIDEN ASSIGNING <F_FURIDEN>
WITH TABLE KEY
BUKRS = <F_MEISAI>-BUKRS "会社コード
BELNR = <F_MEISAI>-BELNR "会計伝票番号
GJAHR = <F_MEISAI>-GJAHR "会計年度
BUZEI = <F_MEISAI>-BUZEI "明細番号
.
IF SY-SUBRC IS INITIAL.
<F_MEISAI>-WDATE = <F_FURIDEN>-WDATE.
ENDIF.
ENDLOOP.

FREE LT_FURIDEN.

*振出日が初期値は対象外とする
DELETE O_MEISAI WHERE WDATE IS INITIAL.
*電子記録債務明細が1件も存在しない場合エラー
IF O_MEISAI IS INITIAL.
MESSAGE S400 WITH '対象の仕入先明細(電子記録債務明細)が存在しません'.
STOP.
ENDIF.

*最大出力件数のチェック
IF P_OCNT IS INITIAL.
* 入力無しの場合、チェック不要
ELSEIF  P_OCNT < LINES( O_MEISAI ).
MESSAGE S400 DISPLAY LIKE 'E'
WITH '対象の仕入先明細(電子記録債務明細)が最大出力件数を超えています'.
STOP.
ENDIF.
ENDFORM.                    "GET_DATA

*---------------------------------------------------------------------*
*       FORM CHECK_MEISAI                                             *
*---------------------------------------------------------------------*
*       電子記録債務明細のチェック                                    *
*---------------------------------------------------------------------*
FORM CHECK_MEISAI    USING I_KINSOKU   TYPE C
I_JBANK     TYPE T_JBANK
I_SHIIRE    TYPE TI_SHIIRE
I_MEISAI    TYPE TI_MEISAI
CHANGING O_MSAI_OUT  TYPE TI_MSAI_OUT
O_ERRLOG    TYPE TI_ERRLOG
O_ERRFLG    TYPE C
.
DATA:LW_MEISAI   TYPE T_MEISAI,
LW_MSAI_OUT TYPE T_MSAI_OUT,
LW_ERRLOG   TYPE T_ERRLOG,
LW_STCHECK  TYPE I,
LW_ZLSCH    TYPE T042E-ZLSCH,
LW_VONBT    TYPE T042E-VONBT,
LW_KAGEN    TYPE C LENGTH 7,
LW_OBJECT   TYPE THEAD-TDOBJECT,
LW_LANG     TYPE THEAD-TDSPRAS,
LW_NAME     TYPE THEAD-TDNAME,
LT_LINES    TYPE TABLE OF TLINE,
LW_LINES    TYPE TLINE,
LW_CURR     TYPE BAPICURR-BAPICURR
*       LW_KINSOKU  TYPE C LENGTH 84
.

FIELD-SYMBOLS: <F_SHIIRE>   TYPE T_SHIIRE
.
CONSTANTS:
CNS_ID     TYPE CHAR04 VALUE 'ZF04',
CNS_OBJECT TYPE CHAR04 VALUE 'LFB1',
CNS_LANG   TYPE CHAR02 VALUE 'JA'
.

CLEAR: O_ERRFLG, O_MSAI_OUT, O_ERRLOG.
**禁則変数(英数字、カナ)
*  CONCATENATE CNS_SUJI CNS_EIJI CNS_KANA INTO LW_KINSOKU.
*支払最低額取得
LW_ZLSCH = P_ZWELS.
SELECT SINGLE
VONBT  "支払の最低額
INTO LW_VONBT
FROM T042E
WHERE ZBUKR = P_BUKRS
AND ZLSCH = LW_ZLSCH
.
IF SY-SUBRC <> 0.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払最低額が取得できません'.
STOP.
ENDIF.

LOOP AT I_MEISAI INTO LW_MEISAI.
CLEAR LW_MSAI_OUT.
*　仕入先情報を取得
READ TABLE I_SHIIRE ASSIGNING <F_SHIIRE>
WITH TABLE KEY
LIFNR = LW_MEISAI-LIFNR "仕入先コード
.
IF SY-SUBRC <> 0.
MESSAGE S400 DISPLAY LIKE 'E' WITH '仕入先、債務明細間で矛盾しています'.
STOP.
ENDIF.

*   銀行マスタから銀行名、銀行支店名を取得
PERFORM GET_BNKA  USING    <F_SHIIRE>-BANKS  "銀行国コード
<F_SHIIRE>-BANKL  "銀行コード
CHANGING LW_MSAI_OUT-BANKA "銀行名
LW_MSAI_OUT-BRNCH "銀行支店名
.
IF SY-SUBRC <> 0.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M25.
"仕入先銀行情報が取得できません
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*   相手取引利用者番号取得 ※出力レイアウト「でんさい」が選択された時だけ
IF P_DSAI = ABAP_TRUE.
CLEAR LT_LINES.
LW_OBJECT = CNS_OBJECT.
LW_LANG   = CNS_LANG.
CONCATENATE LW_MEISAI-LIFNR LW_MEISAI-BUKRS INTO LW_NAME.
CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                      = CNS_ID
LANGUAGE                = LW_LANG
NAME                    = LW_NAME
OBJECT                  = LW_OBJECT
TABLES
LINES                   = LT_LINES
EXCEPTIONS
ID                      = 1
LANGUAGE                = 2
NAME                    = 3
NOT_FOUND               = 4
OBJECT                  = 5
REFERENCE_CHECK         = 6
WRONG_ACCESS_TO_ARCHIVE = 7
OTHERS                  = 8.
IF SY-SUBRC = 0.
*       相手利用者番号が取得
READ TABLE LT_LINES INTO LW_LINES INDEX 1.
IF SY-SUBRC <> 0.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M26.
"相手利用者番号が取得できません
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.
IF LW_LINES-TDLINE+0(9) CN I_KINSOKU.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M27.
"相手取引利用者番号に禁則文字が使用されています
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.
ELSE.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M26.
"相手利用者番号が取得できません
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.
ENDIF.

*相手銀行名の禁則文字チェック
IF LW_MSAI_OUT-BANKA CN I_KINSOKU.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M28.
"相手銀行名に禁則文字が使用されています
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*相手銀行支店名の禁則文字チェック
IF LW_MSAI_OUT-BRNCH CN I_KINSOKU.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M29.
"相手銀行支店名に禁則文字が使用されています
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*相手銀行口座名義人の禁則文字チェック
IF <F_SHIIRE>-KOINH CN I_KINSOKU.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M30.
"相手銀行口座名義人に禁則文字が使用されています
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

**仕入先コードの禁則文字チェック
*    IF <F_SHIIRE>-LIFNR CN LW_KINSOKU.
*      O_ERRFLG = 'X'.
*      LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
*      LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
*      LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
*      LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
*      LW_ERRLOG-ERRTX = TEXT-M18.
*      "仕入先コードに禁則文字が使用されています
*      APPEND LW_ERRLOG TO O_ERRLOG.
*    ENDIF.

*サイトチェック
LW_STCHECK = ( LW_MEISAI-ZFBDT - LW_MEISAI-WDATE ) .
IF  LW_STCHECK > P_SIGHT.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
CONCATENATE TEXT-M15 P_SIGHT TEXT-M16 INTO LW_ERRLOG-ERRTX.
"サイトが(入力日数)を超えています
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.


*金額の下限チェック
IF  LW_MEISAI-WRBTR < LW_VONBT.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
WRITE LW_VONBT TO LW_KAGEN  CURRENCY I_JBANK-WAERS.
CONCATENATE TEXT-M21 LW_KAGEN TEXT-M22 INTO LW_ERRLOG-ERRTX.
"金額が(設定金額)円未満です
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*金額の通貨コードチェック
IF  LW_MEISAI-WAERS <> I_JBANK-WAERS.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
CONCATENATE TEXT-M19 I_JBANK-WAERS TEXT-M20 INTO LW_ERRLOG-ERRTX.
"通貨コードが(設定通貨)以外です
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*金額のマイナスチェック
IF  LW_MEISAI-SHKZG = CNS_DC.
O_ERRFLG = 'X'.
LW_ERRLOG-BUKRS = LW_MEISAI-BUKRS.
LW_ERRLOG-LIFNR = LW_MEISAI-LIFNR.
LW_ERRLOG-GJAHR = LW_MEISAI-GJAHR.
LW_ERRLOG-BELNR = LW_MEISAI-BELNR.
LW_ERRLOG-ERRTX = TEXT-M23.
"金額がマイナスです
APPEND LW_ERRLOG TO O_ERRLOG.
ENDIF.

*   外部形式に変換
CALL FUNCTION 'BAPI_CURRENCY_CONV_TO_EXTERNAL'
EXPORTING
CURRENCY        = I_JBANK-WAERS
AMOUNT_INTERNAL = LW_MEISAI-WRBTR
IMPORTING
AMOUNT_EXTERNAL = LW_CURR.
LW_MSAI_OUT-ZFBDT = LW_MEISAI-ZFBDT.       "決済日
LW_MSAI_OUT-LIFNR = LW_MEISAI-LIFNR.       "仕入先コード
LW_MSAI_OUT-WDATE = LW_MEISAI-WDATE.       "振出日
LW_MSAI_OUT-BELNR = LW_MEISAI-BELNR.       "伝票番号
LW_MSAI_OUT-WRBTR = LW_CURR.               "伝票通貨額
LW_MSAI_OUT-BANKL = <F_SHIIRE>-BANKL.      "仕入先銀行コード
LW_MSAI_OUT-BKONT = <F_SHIIRE>-BKONT.      "仕入先預金種別
LW_MSAI_OUT-BANKN = <F_SHIIRE>-BANKN.      "仕入先口座番号
LW_MSAI_OUT-KOINH = <F_SHIIRE>-KOINH.      "仕入先口座名義人名
LW_MSAI_OUT-SORTL = <F_SHIIRE>-SORTL.      "仕入先名
LW_MSAI_OUT-RIYNO = LW_LINES-TDLINE+0(9).  "利用者番号
APPEND LW_MSAI_OUT TO O_MSAI_OUT.
ENDLOOP.
ENDFORM.                    "CHECK_MEISAI

*---------------------------------------------------------------------*
*       FORM output_file                                              *
*---------------------------------------------------------------------*
*       ファイル出力                                                  *
*---------------------------------------------------------------------*
FORM OUTPUT_FILE.

*エラーログの件数がゼロ件のときのみ処理を行う
IF GW_ERRFLG <> 'X'.
*  決済日>仕入先コードにて内部テーブルをソート
SORT GT_MSAI_OUT BY ZFBDT LIFNR.
IF P_DTE = ABAP_TRUE.  "「電手」指定
*     ファイル編集
PERFORM EDIT_DTE_F USING    GW_JBANK
GT_MSAI_OUT
CHANGING GT_DTE_F
.
*     ファイルダウンロード
PERFORM FILE_DOWNLOAD USING GT_DTE_F
CNS_DTE
.
ELSE.                  "「でんさい」指定
*     ファイル編集
PERFORM EDIT_DSAI_F USING    GW_JBANK
GT_MSAI_OUT
CHANGING GT_DSAI_F
.
*     ファイルダウンロード
PERFORM FILE_DOWNLOAD USING GT_DSAI_F
CNS_DSAI
.
ENDIF.

ENDIF.
ENDFORM.                    "OUTPUT_FILE

*---------------------------------------------------------------------*
*       FORM EDIT_DTE_F                                               *
*---------------------------------------------------------------------*
*       「電手」ファイル編集                                          *
*---------------------------------------------------------------------*
*      -->I_JBANK     text
*      -->I_MSAI_OUT  text
*      <--O_DTE_F     text
*---------------------------------------------------------------------*
FORM EDIT_DTE_F   USING    I_JBANK    TYPE T_JBANK
I_MSAI_OUT TYPE TI_MSAI_OUT
CHANGING O_DTE_F    TYPE TI_DTE_F
.
DATA:LW_MSAI_OUT TYPE T_MSAI_OUT,
LW_MSAI_NEW TYPE T_MSAI_OUT,
LW_FILE     TYPE T_DTE_F,
LW_CNT(6)  TYPE N,
LW_SUM(12) TYPE N
.

CONSTANTS:
*  ヘッダレコード
LCNS_HEAD01(1)  TYPE C VALUE '1',             "データ区分
LCNS_HEAD02(2)  TYPE C VALUE '35',            "種別コード
* 2013/12/13 MOD START
*    LCNS_HEAD03(1)  TYPE C VALUE '1',             "コード区分
LCNS_HEAD03(1)  TYPE C VALUE '0',             "コード区分
* 2013/12/13 MOD END
*  データレコード
LCNS_DATA01(1)  TYPE C VALUE '2',             "データ区分
*  トレーラレコード
LCNS_TRAIL01(1) TYPE C VALUE '8',             "データ区分
*  エンドレコード
LCNS_FOOT01(1)  TYPE C VALUE '9'              "データ区分
.

CLEAR: LW_CNT,LW_SUM
.
LOOP AT I_MSAI_OUT INTO LW_MSAI_OUT.
LW_MSAI_NEW = LW_MSAI_OUT.
*①決済日ごとにヘッダレコードを作成
AT NEW ZFBDT.
CLEAR: LW_FILE.
LW_FILE-FIELD+0(1)   = LCNS_HEAD01.            "データ区分
LW_FILE-FIELD+1(2)   = LCNS_HEAD02.            "種別コード
LW_FILE-FIELD+3(1)   = LCNS_HEAD03.            "コード区分
CONCATENATE I_JBANK-BANKL+4(3)
I_JBANK-BANKN+0(7)
INTO LW_FILE-FIELD+4(10).          "データ伝送用コード
LW_FILE-FIELD+14(40) = I_JBANK-KGYTX.         "支払企業名
*     LW_FILE-FIELD+4(54)                            "DUMMY 4桁
LW_FILE-FIELD+58(4)  = I_JBANK-BANKL+0(4).    "支払企業口座の銀行コード
LW_FILE-FIELD+62(15) = I_JBANK-BANKA+0(15).   "支払企業口座の銀行名
LW_FILE-FIELD+77(3)  = I_JBANK-BANKL+4(3).    "支払企業口座の支店コード
LW_FILE-FIELD+80(15) = I_JBANK-BRNCH+0(15).   "支払企業口座の支店名
LW_FILE-FIELD+95(1)  = I_JBANK-BKONT+1(1).    "支払企業口座の預金種目
LW_FILE-FIELD+96(7)  = I_JBANK-BANKN+0(7).    "支払企業口座の口座番号
LW_FILE-FIELD+103(6) = LW_MSAI_NEW-WDATE+2(6). "発生日
LW_FILE-FIELD+109(6) = LW_MSAI_NEW-ZFBDT+2(6). "支払期日
*     LW_FILE-FIELD+115(3)                            "DUMMY 3桁
LW_FILE-FIELD+118(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DTE_F.
ENDAT.

*②同決済日のデータレコードを作成
CLEAR: LW_FILE.
LW_FILE-FIELD+0(1)    = LCNS_DATA01.                "データ区分
*    LW_FILE-FIELD+1(4)    = LW_MSAI_OUT-BANKL+0(4).     "債権者口座の銀行コード
*    LW_FILE-FIELD+5(15)   = LW_MSAI_OUT-BANKA.          "債権者口座の銀行名
*    LW_FILE-FIELD+20(3)   = LW_MSAI_OUT-BANKL+4(3).     "債権者口座の支店コード
*    LW_FILE-FIELD+23(15)  = LW_MSAI_OUT-BRNCH.          "債権者口座の支店名
*   LW_FILE-FIELD+38(4)                                 "記録の回数制限
*    LW_FILE-FIELD+42(1)   = LW_MSAI_OUT-BKONT.          "債権者口座の預金種目
*    LW_FILE-FIELD+43(7)   = LW_MSAI_OUT-BANKN.          "債権者口座の口座番号
LW_FILE-FIELD+50(30)  = LW_MSAI_OUT-KOINH.          "債権者名
LW_FILE-FIELD+80(10)  = LW_MSAI_OUT-WRBTR.          "債権金額
*   LW_FILE-FIELD+90(1)                                 "DUMMY 1桁
LW_FILE-FIELD+91(10)  = LW_MSAI_OUT-BELNR.          "管理番号
LW_FILE-FIELD+101(10) = LW_MSAI_OUT-LIFNR.          "仕入先コード
*   LW_FILE-FIELD+111(6)                                "支払部署コード 6桁
*   LW_FILE-FIELD+117(1)                                "DUMMY 1桁
LW_FILE-FIELD+118(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DTE_F.

*合計件数取得
LW_CNT = LW_CNT + 1.

*合計金額取得
LW_SUM = LW_SUM + LW_MSAI_OUT-WRBTR.

*③決済日ごとにトレイルレコードを作成
AT END OF ZFBDT.
CLEAR:LW_FILE.
LW_FILE-FIELD+0(1)  = LCNS_TRAIL01.                 "データ区分
LW_FILE-FIELD+1(6)  = LW_CNT.                       "合計件数
LW_FILE-FIELD+7(12) = LW_SUM.                       "合計金額
*     LW_FILE-FIELD+19(99)                                "DUMMY 99桁
LW_FILE-FIELD+118(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DTE_F.
CLEAR: LW_CNT,LW_SUM.
ENDAT.

*④ファイルの最後にエンドレコードを追加
AT LAST.
CLEAR:LW_FILE.
LW_FILE-FIELD+0(1)   = LCNS_FOOT01.                 "データ区分
*     LW_FILE-FIELD+1(117)                                "DUMMY 99桁
LW_FILE-FIELD+118(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DTE_F.
ENDAT.
ENDLOOP.

ENDFORM.                    "EDIT_DTE_F
*---------------------------------------------------------------------*
*       FORM file_download                                            *
*---------------------------------------------------------------------*
*       ファイルダウンロード                                          *
*---------------------------------------------------------------------*
FORM FILE_DOWNLOAD USING I_FILE TYPE STANDARD TABLE
I_FNAM TYPE CHAR6
.

DATA: LW_ODATE(8) TYPE C,
LW_OTIME(6) TYPE C,
LW_PNAME TYPE STRING.
* 2013/12/13 ADD START
DATA  L_CODEPAGE TYPE ABAP_ENCODING.
L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( CNS_SJIS ).
* 2013/12/13 ADD END

LW_ODATE = SY-DATUM.
LW_OTIME = SY-UZEIT.
CONCATENATE P_FOLDER CNS_FIL P_HBKID CNS_A I_FNAM CNS_A LW_ODATE LW_OTIME CNS_FTYPE
INTO LW_PNAME.

CALL FUNCTION 'GUI_DOWNLOAD'
EXPORTING
FILENAME            = LW_PNAME
FILETYPE            = 'ASC'     "テキスト形式
WRITE_LF            = ABAP_FALSE "汎用モジュールでの改行無
* 2013/12/13 ADD START
CODEPAGE            = L_CODEPAGE
* 2013/12/13 ADD END
TABLES
DATA_TAB            = I_FILE
EXCEPTIONS
*            FILE_OPEN_ERROR     = 1
FILE_WRITE_ERROR    = 2
*            INVALID_TABLE_WIDTH = 3
INVALID_TYPE        = 5
NO_BATCH            = 6
UNKNOWN_ERROR       = 7
OTHERS              = 8.

IF SY-SUBRC <> 0.
MESSAGE S400 WITH 'ダウンロードに失敗しました。'.
STOP.
ENDIF.

ENDFORM.                    "FILE_DOWNLOAD
*---------------------------------------------------------------------*
*       FORM output_log                                               *
*---------------------------------------------------------------------*
*       ログ出力                                                      *
*---------------------------------------------------------------------*
FORM OUTPUT_LOG.

*ログヘッダ編集
PERFORM EDIT_LOGHEAD.

*スプール登録
PERFORM SET_SPOOLE.

*ログ一覧出力
PERFORM WRITE_LOG.

ENDFORM.                    "OUTPUT_LOG

*---------------------------------------------------------------------*
*       FORM edit_loghead                                             *
*---------------------------------------------------------------------*
*       ログヘッダ編集                                                *
*---------------------------------------------------------------------*
FORM EDIT_LOGHEAD.

DATA LW_LAY TYPE CHAR08.

IF P_DTE = ABAP_TRUE.
LW_LAY = TEXT-R01.
ELSE.
LW_LAY = TEXT-R02.
ENDIF.

*エラーログの件数がゼロ件（正常終了）
IF GW_ERRFLG <> 'X'.
*ログヘッダ作成
GW_LOGHEAD-MNGN01 = TEXT-M01.
GW_LOGHEAD-MNGN02 = TEXT-M02.
CONCATENATE TEXT-M31 P_HBKID INTO GW_LOGHEAD-MNGN03.
CONCATENATE TEXT-M32 LW_LAY  INTO GW_LOGHEAD-MNGN04.
GW_LOGHEAD-KMKM01 = TEXT-M03.
GW_LOGHEAD-KMKM02 = TEXT-M04.
GW_LOGHEAD-KMKM03 = TEXT-M05.
GW_LOGHEAD-KMKM04 = TEXT-M06.
GW_LOGHEAD-KMKM05 = TEXT-M07.

*エラーログの件数が1件以上（以上終了）
ELSE.
*ログヘッダ部分
GW_LOGHEAD-MNGN01 = TEXT-M08.
GW_LOGHEAD-MNGN02 = TEXT-M09.
CONCATENATE TEXT-M31 P_HBKID INTO GW_LOGHEAD-MNGN03.
CONCATENATE TEXT-M32 LW_LAY  INTO GW_LOGHEAD-MNGN04.
GW_LOGHEAD-KMKM01 = TEXT-M10.
GW_LOGHEAD-KMKM02 = TEXT-M11.
GW_LOGHEAD-KMKM03 = TEXT-M12.
GW_LOGHEAD-KMKM04 = TEXT-M13.
GW_LOGHEAD-KMKM05 = TEXT-M14.
ENDIF.

ENDFORM.                    "EDIT_LOGHEAD
*---------------------------------------------------------------------*
*       FORM set_spoole                                               *
*---------------------------------------------------------------------*
*       スプール登録                                                  *
*---------------------------------------------------------------------*
FORM SET_SPOOLE.

DATA: LF_PRIPAR     TYPE PRI_PARAMS,
LF_ARCPAR     TYPE ARC_PARAMS,
LF_VALID      TYPE C LENGTH 1                       "#EC NEEDED
.

*ユーザ設定取得
SELECT SINGLE * FROM USR01 WHERE BNAME = SY-UNAME.

* プリンタ設定
LF_PRIPAR-PDEST = USR01-SPLD.
* 2013/12/13 MOD START
*  CALL FUNCTION 'GET_PRINT_PARAMETERS'
*         EXPORTING
*              IN_ARCHIVE_PARAMETERS  = LF_ARCPAR
*              IN_PARAMETERS          = LF_PRIPAR
*              LAYOUT                 = 'X_90_120'
*              LINE_COUNT             = 90
*              LINE_SIZE              = 120
*              NO_DIALOG              = 'X'
*              DESTINATION            = LF_PRIPAR-PDEST
*              IMMEDIATELY            = ''
*              NEW_LIST_ID            = 'X'
*              RELEASE                = USR01-SPDA
*         IMPORTING
*              OUT_ARCHIVE_PARAMETERS = LF_ARCPAR
*              OUT_PARAMETERS         = LF_PRIPAR
*              VALID                  = LF_VALID
*         EXCEPTIONS
*              ARCHIVE_INFO_NOT_FOUND = 1
*              INVALID_PRINT_PARAMS   = 2
*              INVALID_ARCHIVE_PARAMS = 3
*              OTHERS                 = 4.
*  IF SY-SUBRC = 0.
**    DUMMY
*  ENDIF.
*
*  NEW-PAGE PRINT ON NEW-SECTION PARAMETERS LF_PRIPAR NO DIALOG.
LF_PRIPAR-PAART = 'X_90_120'.
CALL FUNCTION 'SET_PRINT_PARAMETERS'
EXPORTING
DESTINATION = LF_PRIPAR-pdest
IMMEDIATELY = ''
NEW_LIST_ID = 'X'
RELEASE     = USR01-SPDA.
NEW-PAGE PRINT ON LAYOUT LF_PRIPAR-PAART NO DIALOG.

* 2013/12/13 MOD END

*スプールへのログ出力
PERFORM WRITE_LOG.

*画面印刷への切替
NEW-PAGE PRINT OFF.

ENDFORM.                    "SET_SPOOLE

*---------------------------------------------------------------------*
*       FORM write_head                                               *
*---------------------------------------------------------------------*
*       ログヘッダ出力                                                *
*---------------------------------------------------------------------*
FORM WRITE_HEAD.
*ログヘッダ出力
WRITE: /003 GW_LOGHEAD-MNGN01.
WRITE: /003 GW_LOGHEAD-MNGN02.
WRITE: /003 GW_LOGHEAD-MNGN03.
WRITE: /003 GW_LOGHEAD-MNGN04.
WRITE: /.
IF GW_ERRFLG = ''.
WRITE: /003 GW_LOGHEAD-KMKM01,
015 GW_LOGHEAD-KMKM02,
027 GW_LOGHEAD-KMKM03,
039 GW_LOGHEAD-KMKM04,
076 GW_LOGHEAD-KMKM05.
ELSE.
WRITE: /003 GW_LOGHEAD-KMKM01,
009 GW_LOGHEAD-KMKM02,
019 GW_LOGHEAD-KMKM03,
031 GW_LOGHEAD-KMKM04,
043 GW_LOGHEAD-KMKM05.
ENDIF.
WRITE: / SY-ULINE NO-GAP.

ENDFORM.                    "WRITE_HEAD
*---------------------------------------------------------------------*
*       FORM write_log                                                *
*---------------------------------------------------------------------*
*       ログ一覧出力                                                  *
*---------------------------------------------------------------------*
FORM WRITE_LOG.
DATA: LW_OKLOG     TYPE T_MSAI_OUT,
LW_ERRLOG    TYPE T_ERRLOG,
LW_WRBTR     TYPE P LENGTH 10,
LW_SUM_WRBTR TYPE NUMC10.

*ログデータ部分出力
*正常終了時
IF GW_ERRFLG <> 'X'.
LOOP AT GT_MSAI_OUT INTO LW_OKLOG.
LW_WRBTR = LW_OKLOG-WRBTR.
WRITE: /003 LW_OKLOG-WDATE,
015 LW_OKLOG-ZFBDT,
027 LW_OKLOG-LIFNR,
039 LW_OKLOG-SORTL,
061 LW_WRBTR  NO-ZERO.
*合計金額の表示
LW_SUM_WRBTR = LW_SUM_WRBTR + LW_OKLOG-WRBTR.
AT LAST.
FORMAT COLOR 3 ON INTENSIFIED.
LW_WRBTR = LW_SUM_WRBTR.
WRITE:  SY-ULINE NO-GAP,
/052
TEXT-M24,
061 LW_WRBTR NO-ZERO.
FORMAT COLOR OFF.
ENDAT.
ENDLOOP.
ELSE.
*異常終了時
LOOP AT GT_ERRLOG INTO LW_ERRLOG.
WRITE: /003 LW_ERRLOG-BUKRS,
011 LW_ERRLOG-GJAHR,
019 LW_ERRLOG-BELNR,
031 LW_ERRLOG-LIFNR,
043 LW_ERRLOG-ERRTX.
ENDLOOP.
ENDIF.

ENDFORM.                    "WRITE_LOG
*&---------------------------------------------------------------------*
*&      Form  GET_JBANK
*&---------------------------------------------------------------------*
*       支払企業銀行情報取得
*----------------------------------------------------------------------*
*      <--O_JBANK  構造_支払企業銀行情報
*----------------------------------------------------------------------*
FORM GET_JBANK  USING    I_KINSOKU TYPE C
CHANGING O_JBANK   TYPE T_JBANK.


* 取引銀行から銀行コード、支店コードを取得
SELECT SINGLE
BANKS  "銀行国コード
BANKL  "銀行コード
INTO (O_JBANK-BANKS,O_JBANK-BANKL)
FROM T012
WHERE BUKRS = P_BUKRS AND
HBKID = P_HBKID
.
IF SY-SUBRC <> 0.
MESSAGE S614 DISPLAY LIKE 'E' WITH '取引銀行（支払企業）'.
STOP.
ENDIF.
*支払企業銀行コードの禁則文字チェック
IF O_JBANK-BANKL+0(7) CN CNS_SUJI.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払企業銀行コードに禁則文字が含まれています'.
STOP.
ENDIF.

* 銀行マスタから銀行名、銀行支店名を取得
PERFORM GET_BNKA  USING    O_JBANK-BANKS  "銀行国コード
O_JBANK-BANKL  "銀行コード
CHANGING O_JBANK-BANKA  "銀行名
O_JBANK-BRNCH  "銀行支店名
.
IF SY-SUBRC <> 0.
MESSAGE S614 DISPLAY LIKE 'E' WITH '銀行マスタ（支払企業）'.
STOP.
ENDIF.

*支払企業銀行名の禁則文字チェック
IF O_JBANK-BANKA CN I_KINSOKU.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払企業銀行名に禁則文字が含まれています'.
STOP.
ENDIF.

*支払企業銀行支店名の禁則文字チェック
IF O_JBANK-BRNCH CN I_KINSOKU.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払企業銀行支店名に禁則文字が含まれています'.
STOP.
ENDIF.

* 勘定 ID (支払処理）から口座 ID 詳細を取得
SELECT SINGLE
HKTID  "口座 ID 詳細
INTO (O_JBANK-HKTID)
FROM T042I
WHERE  ZBUKR = P_BUKRS AND     "支払会社コード
HBKID = P_HBKID AND     "取引銀行のショートキー
ZLSCH = P_ZWELS AND     "支払方法
WAERS = O_JBANK-WAERS   "通貨コード
.
IF SY-SUBRC <> 0.
MESSAGE S400 DISPLAY LIKE 'E' WITH '取引銀行IDと支払方法の組合せが不正です'.
STOP.
ENDIF.

* 取引銀行口座から預金種別、口座番号を取得
SELECT SINGLE
BKONT  "預金種別
BANKN  "口座番号
INTO (O_JBANK-BKONT,O_JBANK-BANKN)
FROM T012K
WHERE  BUKRS = P_BUKRS AND        "支払会社コード
HBKID = P_HBKID AND        "取引銀行のショートキー
HKTID = O_JBANK-HKTID      "口座 ID 詳細
.
IF SY-SUBRC <> 0.
MESSAGE S400 DISPLAY LIKE 'E' WITH '取引銀行口座（支払企業）が存在しません'.
STOP.
ENDIF.

*支払企業銀行預金種別の禁則文字チェック
IF O_JBANK-BKONT CN CNS_SUJI.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払企業銀行預金種別に禁則文字が含まれています'.
STOP.
ENDIF.

*支払企業銀行口座番号の禁則文字チェック
IF O_JBANK-BANKN+0(7) CN CNS_SUJI.
MESSAGE S400 DISPLAY LIKE 'E' WITH '支払企業銀行口座番号に禁則文字が含まれています'.
STOP.
ENDIF.

ENDFORM.                    " GET_JBANK
*&---------------------------------------------------------------------*
*&      Form  GET_BNKA
*&---------------------------------------------------------------------*
*       銀行マスタから銀行名、銀行支店名を取得
*----------------------------------------------------------------------*
*      -->I_BANKS  銀行国コード
*      -->I_BANKL  銀行コード
*      <--O_BANKA  銀行名
*      <--O_BRNCH  銀行支店名
*----------------------------------------------------------------------*
FORM GET_BNKA  USING    I_BANKS TYPE BNKA-BANKS
I_BANKL TYPE BNKA-BANKL
CHANGING O_BANKA TYPE T_MSAI_OUT-BANKA
O_BRNCH TYPE T_MSAI_OUT-BRNCH
.

* 銀行マスタから銀行名、銀行支店名を取得
SELECT SINGLE
BANKA  "銀行名
BRNCH  "銀行支店名
INTO (O_BANKA,O_BRNCH)
FROM BNKA
WHERE BANKS = I_BANKS AND
BANKL = I_BANKL
.

ENDFORM.                    " GET_BNKA
*&---------------------------------------------------------------------*
*&      Form  EDIT_DSAI_F
*&---------------------------------------------------------------------*
*       「でんさい」ファイル編集                                          *
*----------------------------------------------------------------------*
*      -->I_JBANK  text
*      -->I_MSAI_OUT  text
*      <--O_DSAI_F  text
*----------------------------------------------------------------------*
FORM EDIT_DSAI_F  USING    I_JBANK    TYPE T_JBANK
I_MSAI_OUT TYPE TI_MSAI_OUT
CHANGING O_DSAI_F   TYPE TI_DSAI_F
.

DATA:LW_MSAI_OUT TYPE T_MSAI_OUT,
LW_FILE     TYPE T_DSAI_F,
LW_CNT(6)   TYPE N,
LW_SUM(12)  TYPE N
.

CONSTANTS:
*  ヘッダレコード
LCNS_HEAD01(1)  TYPE C VALUE '1',             "データ区分
LCNS_HEAD02(2)  TYPE C VALUE '11',            "種別コード
* 2013/12/13 MOD START
*    LCNS_HEAD03(1)  TYPE C VALUE '1',             "コード区分
LCNS_HEAD03(1)  TYPE C VALUE '0',             "コード区分
* 2013/12/13 MOD END
*  データレコード
LCNS_DATA01(1)  TYPE C VALUE '2',             "データ区分
*  トレーラレコード
LCNS_TRAIL01(1) TYPE C VALUE '8',             "データ区分
*  エンドレコード
LCNS_FOOT01(1)  TYPE C VALUE '9'              "データ区分
.
* ヘッダレコード作成
CLEAR: LW_FILE,LW_MSAI_OUT.
READ TABLE I_MSAI_OUT INTO LW_MSAI_OUT INDEX 1.
LW_FILE-FIELD+0(1)   = LCNS_HEAD01.            "データ区分
LW_FILE-FIELD+1(2)   = LCNS_HEAD02.            "種別コード
LW_FILE-FIELD+3(1)   = LCNS_HEAD03.            "コード区分
LW_FILE-FIELD+4(8)   = LW_MSAI_OUT-WDATE.      "発生日
LW_FILE-FIELD+12(9)  = I_JBANK-KGYCD.          "請求者_利用者番号
LW_FILE-FIELD+21(30) = I_JBANK-KGYTX+0(30).    "支払企業名
LW_FILE-FIELD+51(4)  = I_JBANK-BANKL+0(4).     "取引銀行番号
LW_FILE-FIELD+55(15) = I_JBANK-BANKA+0(15).    "支払企業口座の銀行名
LW_FILE-FIELD+70(3)  = I_JBANK-BANKL+4(3).     "取引支店番号
LW_FILE-FIELD+73(15) = I_JBANK-BRNCH+0(15).    "取引支店名
LW_FILE-FIELD+88(1)  = I_JBANK-BKONT+1(1).     "預金種目
LW_FILE-FIELD+89(7)  = I_JBANK-BANKN+0(7).     "口座番号
* LW_FILE-FIELD+96(154)                          "DUMMY 154桁
LW_FILE-FIELD+250(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DSAI_F.

* データレコードを作成
LOOP AT I_MSAI_OUT INTO LW_MSAI_OUT.
CLEAR: LW_FILE.
LW_FILE-FIELD+0(1)    = LCNS_DATA01.                "データ区分
LW_FILE-FIELD+1(9)    = LW_MSAI_OUT-RIYNO.          "取引相手_利用者番号
LW_FILE-FIELD+10(4)   = LW_MSAI_OUT-BANKL+0(4).     "取引相手_取引銀行番
LW_FILE-FIELD+14(15)  = LW_MSAI_OUT-BANKA.          "取引相手_取引銀行名
LW_FILE-FIELD+29(3)   = LW_MSAI_OUT-BANKL+4(3).     "取引相手_取引支店番
LW_FILE-FIELD+32(15)  = LW_MSAI_OUT-BRNCH.          "取引相手_取引支店名
LW_FILE-FIELD+47(1)   = LW_MSAI_OUT-BKONT.          "取引相手_預金種目
LW_FILE-FIELD+48(7)   = LW_MSAI_OUT-BANKN.          "取引相手_口座番号
LW_FILE-FIELD+55(10)  = LW_MSAI_OUT-WRBTR.          "債権金額
LW_FILE-FIELD+65(8)   = LW_MSAI_OUT-ZFBDT.          "支払期日
*   LW_FILE-FIELD+73(1)                                 "譲渡制限有無フラグ
*   LW_FILE-FIELD+74(20)                                "記録番号
*   LW_FILE-FIELD+94(1)                                 "記録番号
LW_FILE-FIELD+95(40)  = LW_MSAI_OUT-BELNR.          "依頼人Ref.
*   LW_FILE-FIELD+135(115)                              "DUMMY 115桁
LW_FILE-FIELD+250(2)  = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DSAI_F.
*合計件数取得
LW_CNT = LW_CNT + 1.
*合計金額取得
LW_SUM = LW_SUM + LW_MSAI_OUT-WRBTR.
ENDLOOP.

*トレイラレコードを作成
CLEAR:LW_FILE.
LW_FILE-FIELD+0(1)  = LCNS_TRAIL01.                 "データ区分
LW_FILE-FIELD+1(6)  = LW_CNT.                       "合計件数
LW_FILE-FIELD+7(12) = LW_SUM.                       "合計金額
* LW_FILE-FIELD+19(231)                               "DUMMY 231桁
LW_FILE-FIELD+250(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DSAI_F.

*エンドレコードを作成
CLEAR:LW_FILE.
LW_FILE-FIELD+0(1)   = LCNS_FOOT01.                 "データ区分
* LW_FILE-FIELD+1(249)                                "DUMMY 249桁
LW_FILE-FIELD+250(2) = CL_ABAP_CHAR_UTILITIES=>CR_LF.
APPEND LW_FILE TO O_DSAI_F.

ENDFORM.                    " EDIT_DSAI_F
