*&---------------------------------------------------------------------*
*&  プログラムＩＤ ： ZA1FYAL100RP001
*&  プログラム名称 ： 費用/決済系データコンバージョン
*&  処　理　概　要 ： ファイルをアップロードし、汎用フォーマットへ
*&　　　　　　　　　　変換し、ローカルファイルを出力する。
*&  作　  成  　者 ： MIS
*&  作　  成  　日 ： 2011.03.23
*&---------------------------------------------------------------------*
*& 【変更履歴】
*&  更新日     管理番号     更新者               更新内容
*& ----------  --------  ---------------  -----------------------------*
*& 2011/03/15  DMWXXXX   MIS              新規作成
*&---------------------------------------------------------------------*
REPORT ZA1FYAL100RP001 NO STANDARD PAGE HEADING
LINE-SIZE 120
LINE-COUNT 65
MESSAGE-ID YLMS0201.
*----------------------------------------------------------------------*
* Types 定 義
*----------------------------------------------------------------------*
TYPES :
* 入力ファイルレイアウト(未払金データ)
BEGIN OF A1F_TY_UPLOAD1,
ITEM001(3)   TYPE C,               " 番号
ITEM002(4)   TYPE C,               " 部門
ITEM003(10)  TYPE C,               " 転記日
ITEM004(10)  TYPE C,               " 科目コード
ITEM005(40)  TYPE C,               " 勘定科目名称
ITEM006(10)  TYPE C,               " 未払金コード
ITEM007(40)  TYPE C,               " 支払先名
ITEM008(50)  TYPE C,               " 摘要：内容等
DUMMY(50) TYPE C,                  " ダミー項目
ITEM009(14)  TYPE C,               " 請求金額（税込）
ITEM010(2)   TYPE C,               " 税区分
ITEM013(10)  TYPE C,               " 支払基準日
DUMMY2(50) TYPE C,                 " ダミー項目
*   編集用:ファイルアップロード時点の行番号
INDEX        TYPE SYST-INDEX,
END   OF A1F_TY_UPLOAD1,
* 入力ファイルレイアウト(仕訳データ)
BEGIN OF A1F_TY_UPLOAD2,
TANI(3)      TYPE C,               " 番号（伝票単位）
BUDAT(10)    TYPE C,               " 転記日付
WAERS(5)     TYPE C,               " 通貨ｺｰﾄﾞ
KURSF(12)    TYPE C,               " 通貨レート
BSCHL_S(2)   TYPE C,               " (借方)転記ｷｰ
SAKNR_S(10)  TYPE C,               " (借方)勘定科目
TXT50_S(50)  TYPE C,               " (借方)科目名称
KOSTL_S(10)  TYPE C,               " (借方)負担部門
DMBTR_S(14)  TYPE C,               " (借方)金額
WRBTR_S(14)  TYPE C,               " (借方)国内通貨額
MWSKZ_S(2)   TYPE C,               " (借方)税ｺｰﾄﾞ
ZFBDT_S(10)  TYPE C,               " (借方)支払基準日
ZUONR_S(18)  TYPE C,               " (借方)ｿｰﾄｷｰ
SGTXT_S(50)  TYPE C,               " (借方)明細ﾃｷｽﾄ
BSCHL_H(2)   TYPE C,               " (貸方)転記ｷｰ
SAKNR_H(10)  TYPE C,               " (貸方)勘定科目
TXT50_H(50)  TYPE C,               " (貸方)科目名称
KOSTL_H(10)  TYPE C,               " (貸方)負担部門
DMBTR_H(14)  TYPE C,               " (貸方)金額
WRBTR_H(14)  TYPE C,               " (貸方)国内通貨額
MWSKZ_H(2)   TYPE C,               " (貸方)税ｺｰﾄﾞ
ZFBDT_H(10)  TYPE C,               " (貸方)支払基準日
ZUONR_H(18)  TYPE C,               " (貸方)ｿｰﾄｷｰ
SGTXT_H(50)  TYPE C,               " (貸方)明細ﾃｷｽﾄ
*   編集用:ファイルアップロード時点の行番号
INDEX        TYPE SYST-INDEX,
END   OF A1F_TY_UPLOAD2,
* 出力ファイルレイアウト（汎用フォーマット）
BEGIN OF A1F_TY_DOWNLOAD,
TANI(3)      TYPE C,               " 伝票単位項目
BLART        TYPE BKPF-BLART,      " 伝票タイプ
BUKRS        TYPE T001-BUKRS,      " 会社コード
BUDAT        TYPE D,               " 会計転記日付
CPUDT        TYPE D,               " 会計伝票日付
WAERS        TYPE BKPF-WAERS,      " 通貨
RATE(14)     TYPE C,               " 通貨レート
XBLNR        TYPE BKPF-XBLNR,      " 参照伝票番号
BKTXT        TYPE BKPF-BKTXT,      " 伝票テキスト
XMWST        TYPE BKPF-XMWST,      " 税自動計算フラグ
BUZEI        TYPE BSEG-BUZEI,      " 伝票明細番号
SHKZG        TYPE BSEG-SHKZG,      " 貸借フラグ
KOART        TYPE BSEG-KOART,      " 勘定タイプ
UMSKZ        TYPE BSEG-UMSKZ,      " 特殊仕訳コード
SAKNR        TYPE BSEG-SAKNR,      " 勘定科目
MWSKZ        TYPE BSEG-MWSKZ,      " 税コード
BEWAR        TYPE BSEG-BEWAR,      " 取引タイプ
ANLN1        TYPE BSEG-ANLN1,      " 資産番号
ANLN2        TYPE BSEG-ANLN2,      " 資産補助番号
WRBTR(14)    TYPE C,               " 金額
DMBTR(14)    TYPE C,               " 国内通貨額
KOSTL        TYPE BSEG-KOSTL,      " 原価センタ
PRCTR        TYPE BSEG-PRCTR,      " 利益センタ
PROJK(8)     TYPE C,               " WBS
AUFNR        TYPE BSEG-AUFNR,      " 指図書
SGTXT        TYPE BSEG-SGTXT,      " 明細テキスト
ZUONR        TYPE BSEG-ZUONR,      " ソートキー
XREF1        TYPE BSEG-XREF1,      " 参照キー１
XREF2        TYPE BSEG-XREF2,      " 参照キー２
XREF3        TYPE BSEG-XREF3,      " 参照キー３
ZTERM        TYPE BSEG-ZTERM,      " 支払条件
ZLSCH        TYPE BSEG-ZLSCH,      " 支払方法
ZFBDT        TYPE D,               " 支払基準日
ANFAE        TYPE D,               " 支払期日
BZDAT        TYPE D,               " 資産評価日
BSCHL(2)     TYPE C,               " 転記ｷｰ
END   OF A1F_TY_DOWNLOAD,
* 処理結果リスト
BEGIN OF A1F_TY_LIST,
INDEX     TYPE I,
TEXT(100) TYPE C,
END   OF A1F_TY_LIST,
BEGIN OF A1F_TY_POS,
TANI(3)   TYPE C,                  " 伝票単位項目
BUZEI     TYPE BSEG-BUZEI,         " 伝票明細番号
END   OF A1F_TY_POS.

*----------------------------------------------------------------------*
* Data 定 義
*----------------------------------------------------------------------*
DATA :
* ＜内部テーブル＞
A1F_TG_UPLOAD1  TYPE STANDARD TABLE OF A1F_TY_UPLOAD1,
A1F_TG_UPLOAD2  TYPE STANDARD TABLE OF A1F_TY_UPLOAD2,
A1F_TG_DOWNLOAD TYPE STANDARD TABLE OF A1F_TY_DOWNLOAD,
A1F_TG_LIST     TYPE STANDARD TABLE OF A1F_TY_LIST,
A1F_TG_POS      TYPE STANDARD TABLE OF A1F_TY_POS,
* ＜　変　　数　＞
A1F_WG_DATUM    TYPE SYST-DATUM,      " 処理開始日付
A1F_WG_UZEIT    TYPE SYST-UZEIT,      " 処理終了日付
A1F_WG_WAERS    TYPE T001-WAERS,      " 通貨コード
A1F_WG_LTEXT    TYPE T003T-LTEXT,     " 伝票タイプテキスト
* ＜カウント変数＞
BEGIN OF A1F_CG,
FILE          TYPE I,               " ファイル件数
PROC          TYPE I,               " 処理件数
OK            TYPE I,               " 成功件数
NG            TYPE I,               " 失敗件数
END   OF A1F_CG,
* ＜フラグ 変 数＞
A1F_FG_END      TYPE C.               " 終了判定

*----------------------------------------------------------------------*
* Constants  定 義
*----------------------------------------------------------------------*
CONSTANTS :
A1F_CNS_ON(1)     TYPE C              VALUE 'X',
A1F_CNS_ZERO(1)   TYPE C              VALUE '0',
A1F_CNS_MARK1(1)  TYPE C              VALUE '/',
A1F_CNS_MARK2(1)  TYPE C              VALUE ',',
A1F_CNS_MARK3(1)  TYPE C              VALUE '"',
A1F_CNS_BLART     TYPE T003-BLART     VALUE 'KR',
A1F_CNS_BUKRS     TYPE T001-BUKRS     VALUE 'C001',
A1F_CNS_XMWST     TYPE BKPF-XMWST     VALUE 'X',
A1F_CNS_SHKZG_H   TYPE BSEG-SHKZG     VALUE 'H',
A1F_CNS_SHKZG_S   TYPE BSEG-SHKZG     VALUE 'S',
A1F_CNS_BUZEI     TYPE BSEG-BUZEI     VALUE '001',
A1F_CNS_BUZEI2    TYPE BSEG-BUZEI     VALUE '002',
A1F_CNS_MSGNO_001 TYPE SYST-MSGNO     VALUE '001',
A1F_CNS_MSGNO_002 TYPE SYST-MSGNO     VALUE '002',
A1F_CNS_MSGNO_003 TYPE SYST-MSGNO     VALUE '003',
A1F_CNS_MSGNO_004 TYPE SYST-MSGNO     VALUE '004',
A1F_CNS_MSGNO_005 TYPE SYST-MSGNO     VALUE '005',
A1F_CNS_MSGNO_006 TYPE SYST-MSGNO     VALUE '006',
A1F_CNS_CHAR      TYPE DD01V-DATATYPE VALUE 'CHAR',
A1F_CNS_JPY       TYPE BKPF-WAERS     VALUE 'JPY',
A1F_CNS_GOU(2)    TYPE C              VALUE '合'.

*----------------------------------------------------------------------*
* Parameters 定 義
*----------------------------------------------------------------------*
* 処理種別
SELECTION-SCREEN BEGIN OF BLOCK BLOCK1 WITH FRAME TITLE TEXT-001.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS P_PROC1 TYPE C RADIOBUTTON GROUP RA1.
SELECTION-SCREEN COMMENT 10(34) TEXT-P01
FOR FIELD P_PROC1.
SELECTION-SCREEN END   OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS P_PROC2 TYPE C RADIOBUTTON GROUP RA1.
SELECTION-SCREEN COMMENT 10(34) TEXT-P02
FOR FIELD P_PROC2.
SELECTION-SCREEN END   OF LINE.
SELECTION-SCREEN END   OF BLOCK BLOCK1.
* 格納先
SELECTION-SCREEN BEGIN OF BLOCK BLOCK2 WITH FRAME TITLE TEXT-002.
PARAMETERS :
P_INFL(255) TYPE C LOWER CASE      " 入力ファイル名
OBLIGATORY,
P_OTFL(255) TYPE C LOWER CASE      " 出力ファイル名
OBLIGATORY.
SELECTION-SCREEN END   OF BLOCK BLOCK2.

*----------------------------------------------------------------------*
* 初期処理
*----------------------------------------------------------------------*
INITIALIZATION.
PERFORM A1F_CLEAR_PROC.    " 初期化処理
PERFORM A1F_GET_CNSDATA.   " 補足情報取得

*----------------------------------------------------------------------*
* 検索ヘルプ画面
*----------------------------------------------------------------------*
* <入力ファイル名>
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_INFL.
PERFORM A1F_OPEN_DIALOG  CHANGING P_INFL.

* <出力ファイル名>
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_OTFL.
PERFORM A1F_SAVE_DIALOG  CHANGING P_OTFL.

*----------------------------------------------------------------------*
* 主処理
*----------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM A1F_INIT_PROC.      " 初期処理
PERFORM A1F_MAIN_PROC.      " 主 処 理

*----------------------------------------------------------------------*
* 後処理
*----------------------------------------------------------------------*
END-OF-SELECTION.
PERFORM A1F_END_PROC.      " 終了処理
*&---------------------------------------------------------------------*
*&      Form  A1F_CLEAR_PROC
*&---------------------------------------------------------------------*
*       初期化処理
*----------------------------------------------------------------------*
FORM A1F_CLEAR_PROC.

CLEAR :
*   ＜内部テーブル＞
A1F_TG_UPLOAD1,
A1F_TG_UPLOAD2,
A1F_TG_DOWNLOAD,
A1F_TG_LIST,
A1F_TG_POS,
*   ＜　変　　数　＞
A1F_WG_DATUM,      " 処理開始日付
A1F_WG_UZEIT,      " 処理終了日付
A1F_WG_WAERS,      " 通貨コード
A1F_WG_LTEXT,      " 伝票タイプテキスト
*   ＜カウント変数＞
A1F_CG,
*   ＜フラグ 変 数＞
A1F_FG_END.        " 終了判定

ENDFORM.                    " A1F_CLEAR_PROC
*&---------------------------------------------------------------------*
*&      Form  A1F_GET_CNSDATA
*&---------------------------------------------------------------------*
*       補足情報取得
*----------------------------------------------------------------------*
FORM A1F_GET_CNSDATA.

* (T001)データ検索
SELECT SINGLE WAERS
FROM T001
INTO A1F_WG_WAERS
WHERE BUKRS = A1F_CNS_BUKRS.

IF SY-SUBRC <> 0.
CLEAR A1F_WG_WAERS.
ENDIF.

* (T003T)データ検索
SELECT SINGLE LTEXT
FROM T003T
INTO A1F_WG_LTEXT
WHERE SPRAS = SY-LANGU
AND BLART = A1F_CNS_BLART.

IF SY-SUBRC <> 0.
CLEAR A1F_WG_LTEXT.
ENDIF.

ENDFORM.                    " A1F_GET_CNSDATA
*&---------------------------------------------------------------------*
*&      Form  A1F_OPEN_DIALOG
*&---------------------------------------------------------------------*
*       ファイルオープンダイアログ処理
*----------------------------------------------------------------------*
*      --> FC_FILENAME  選択画面「入力ファイル名」
*----------------------------------------------------------------------*
FORM A1F_OPEN_DIALOG
CHANGING VALUE(FC_FILENAME) TYPE C.
* <<< Local Data >>>
DATA :
LTD_IT_FILE TYPE STANDARD TABLE OF FILE_TABLE,
LTH_IT_FILE LIKE FILE_TABLE ,
LW_FILENAME TYPE STRING,
LFLG_RC     TYPE I.
**
CLEAR :
FC_FILENAME.

MOVE :
FC_FILENAME TO LW_FILENAME.

* Methodコール
CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
EXPORTING
INITIAL_DIRECTORY       = LW_FILENAME
CHANGING
FILE_TABLE              = LTD_IT_FILE
RC                      = LFLG_RC
EXCEPTIONS
FILE_OPEN_DIALOG_FAILED = 1
CNTL_ERROR              = 2
ERROR_NO_GUI            = 3.

IF LFLG_RC = 1.
*   ユーザ指定のファイルを設定
READ TABLE LTD_IT_FILE INTO LTH_IT_FILE INDEX 1.
MOVE :
LTH_IT_FILE-FILENAME TO FC_FILENAME.
ELSE.
CLEAR FC_FILENAME.
ENDIF.

ENDFORM.                    " A1F_OPEN_DIALOG
*&---------------------------------------------------------------------*
*&      Form  A1F_SAVE_DIALOG
*&---------------------------------------------------------------------*
*       ファイルセーブダイアログ処理
*----------------------------------------------------------------------*
*      --> FC_FILENAME  選択画面「出力ファイル名」
*----------------------------------------------------------------------*
FORM A1F_SAVE_DIALOG
CHANGING VALUE(FC_FILENAME) TYPE C.

* <<< Local Data >>>
DATA :
LW_FILENAME TYPE STRING,
LW_PATH     TYPE STRING,
LW_FULLPATH TYPE STRING,
LW_FILTER   TYPE STRING,
LFLG_RC     TYPE I.
**
CLEAR :
FC_FILENAME.

MOVE :
FC_FILENAME TO LW_FILENAME,
TEXT-003    TO LW_FILTER.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_SAVE_DIALOG
EXPORTING
FILE_FILTER       = LW_FILTER
INITIAL_DIRECTORY = LW_FILENAME
CHANGING
FILENAME          = LW_FILENAME
PATH              = LW_PATH
FULLPATH          = LW_FULLPATH
USER_ACTION       = LFLG_RC
EXCEPTIONS
CNTL_ERROR        = 1
ERROR_NO_GUI      = 2
OTHERS            = 3.

IF LFLG_RC = 0.
MOVE :
LW_FULLPATH TO FC_FILENAME.
ELSE.
CLEAR : FC_FILENAME.
ENDIF.

ENDFORM.                    " A1F_SAVE_DIALOG
*&---------------------------------------------------------------------*
*&      Form  A1F_INIT_PROC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM A1F_INIT_PROC.

**
* 処理開始日付／時間を退避
MOVE :
SY-DATUM TO A1F_WG_DATUM,      " 日付
SY-UZEIT TO A1F_WG_UZEIT.      " 時刻

IF P_PROC1 = A1F_CNS_ON.
PERFORM A1F_PROC_UPLOAD1.    " 未払金データ取込
ENDIF.

IF P_PROC2 = A1F_CNS_ON.
PERFORM A1F_PROC_UPLOAD2.    " 仕訳データ取込
ENDIF.

ENDFORM.                    " A1F_INIT_PROC
*&---------------------------------------------------------------------*
*&      Form  A1F_PROC_UPLOAD1
*&---------------------------------------------------------------------*
*       未払金データ取込
*----------------------------------------------------------------------*
FORM A1F_PROC_UPLOAD1.

* <<< Local Data >>>
DATA :
A1F_TL_UPLOAD TYPE STANDARD TABLE OF A1F_TY_UPLOAD1,
A1F_WL_UPLOAD TYPE A1F_TY_UPLOAD1.

**

* File Upload 処理（未払金データ取込）
PERFORM A1F_GET_FILE CHANGING A1F_TL_UPLOAD
A1F_FG_END.

CHECK A1F_FG_END = SPACE.

DO 5 TIMES.
DELETE A1F_TL_UPLOAD INDEX 1.
ENDDO.

* File件数チェック
DESCRIBE TABLE A1F_TL_UPLOAD LINES A1F_CG-FILE.

IF A1F_CG-FILE = 0.
MOVE A1F_CNS_ON TO A1F_FG_END.
*   対象データは存在しません
MESSAGE S004.
EXIT.
ENDIF.

LOOP AT A1F_TL_UPLOAD INTO A1F_WL_UPLOAD.
A1F_WL_UPLOAD-INDEX = SY-TABIX.

CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
INPUT  = A1F_WL_UPLOAD-ITEM001
IMPORTING
OUTPUT = A1F_WL_UPLOAD-ITEM001.

APPEND A1F_WL_UPLOAD TO A1F_TG_UPLOAD1.
ENDLOOP.

* 処理対象データ削除(請求金額＝『0』or『空白』データ）
DELETE A1F_TG_UPLOAD1 WHERE ITEM009 = A1F_CNS_ZERO.
DELETE A1F_TG_UPLOAD1 WHERE ITEM009 IS INITIAL.
DELETE A1F_TG_UPLOAD1 WHERE ITEM001(2) = A1F_CNS_GOU.

* File件数チェック
DESCRIBE TABLE A1F_TG_UPLOAD1 LINES A1F_CG-PROC.

IF A1F_CG-PROC = 0.
MOVE A1F_CNS_ON TO A1F_FG_END.
*   対象データは存在しません
MESSAGE S004.
EXIT.
ENDIF.

ENDFORM.                    " A1F_PROC_UPLOAD1
*&---------------------------------------------------------------------*
*&      Form  A1F_PROC_UPLOAD2
*&---------------------------------------------------------------------*
*       仕訳データ取込
*----------------------------------------------------------------------*
FORM A1F_PROC_UPLOAD2.
* <<< Local Data >>>
DATA :
A1F_TL_UPLOAD TYPE STANDARD TABLE OF A1F_TY_UPLOAD2,
A1F_WL_UPLOAD TYPE A1F_TY_UPLOAD2,
A1F_CL_INDEX  TYPE SYST-INDEX.

* File Upload 処理（仕訳データ取込）
PERFORM A1F_GET_FILE CHANGING A1F_TL_UPLOAD
A1F_FG_END.

CHECK A1F_FG_END = SPACE.

DO 2 TIMES.
DELETE A1F_TL_UPLOAD INDEX 1.
ENDDO.

* File件数チェック
DESCRIBE TABLE A1F_TL_UPLOAD LINES A1F_CG-FILE.

IF A1F_CG-FILE = 0.
MOVE A1F_CNS_ON TO A1F_FG_END.
*   対象データは存在しません
MESSAGE S004.
EXIT.
ENDIF.

LOOP AT A1F_TL_UPLOAD INTO A1F_WL_UPLOAD.
A1F_CL_INDEX = A1F_CL_INDEX + 1.
A1F_WL_UPLOAD-INDEX = A1F_CL_INDEX.
CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
INPUT  = A1F_WL_UPLOAD-TANI
IMPORTING
OUTPUT = A1F_WL_UPLOAD-TANI.

APPEND A1F_WL_UPLOAD TO A1F_TG_UPLOAD2.
ENDLOOP.

* 処理対象データ削除(請求金額＝『0』or『空白』データ）
DELETE A1F_TG_UPLOAD2 WHERE   DMBTR_S =  A1F_CNS_ZERO
AND ( DMBTR_H =  A1F_CNS_ZERO OR
DMBTR_H IS INITIAL ).

DELETE A1F_TG_UPLOAD2 WHERE   DMBTR_S IS INITIAL
AND ( DMBTR_H =  A1F_CNS_ZERO OR
DMBTR_H IS INITIAL ).

DELETE A1F_TG_UPLOAD2 WHERE   TANI IS INITIAL.

* File件数チェック
DESCRIBE TABLE A1F_TG_UPLOAD2 LINES A1F_CG-PROC.

IF A1F_CG-PROC = 0.
MOVE A1F_CNS_ON TO A1F_FG_END.
*   対象データは存在しません
MESSAGE S004.
EXIT.
ENDIF.

ENDFORM.                    " A1F_PROC_UPLOAD2
*&---------------------------------------------------------------------*
*&      Form  A1F_GET_FILE
*&---------------------------------------------------------------------*
*       File Upload 処理
*----------------------------------------------------------------------*
FORM A1F_GET_FILE
CHANGING       FC_T_FILE  TYPE ANY TABLE
VALUE(FC_FG_END) TYPE C.

* <<< Local Data >>>
DATA :
LW_FILENAME TYPE STRING.

**
MOVE :
P_INFL TO LW_FILENAME.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>GUI_UPLOAD
EXPORTING
FILENAME                = LW_FILENAME
FILETYPE                = 'ASC'
HAS_FIELD_SEPARATOR     = A1F_CNS_ON
CHANGING
DATA_TAB                = FC_T_FILE
EXCEPTIONS
FILE_OPEN_ERROR         = 1
FILE_READ_ERROR         = 2
NO_BATCH                = 3
GUI_REFUSE_FILETRANSFER = 4
INVALID_TYPE            = 5
NO_AUTHORITY            = 6
UNKNOWN_ERROR           = 7
BAD_DATA_FORMAT         = 8
HEADER_NOT_ALLOWED      = 9
SEPARATOR_NOT_ALLOWED   = 10
HEADER_TOO_LONG         = 11
UNKNOWN_DP_ERROR        = 12
ACCESS_DENIED           = 13
DP_OUT_OF_MEMORY        = 14
DISK_FULL               = 15
DP_TIMEOUT              = 16
OTHERS                  = 17.

IF SY-SUBRC <> 0.
MOVE A1F_CNS_ON TO FC_FG_END.
*   処理に失敗しました （ファイルアップロード）
MESSAGE S003 WITH TEXT-M04.
ENDIF.

ENDFORM.                    " A1F_GET_FILE
*&---------------------------------------------------------------------*
*&      Form  A1F_MAIN_PROC
*&---------------------------------------------------------------------*
*       主 処 理
*----------------------------------------------------------------------*
FORM A1F_MAIN_PROC.

CHECK A1F_FG_END <> A1F_CNS_ON.

IF P_PROC1 = A1F_CNS_ON.
PERFORM A1F_MAIN_PROC1.      " 未払金データ取込
ENDIF.

IF P_PROC2 = A1F_CNS_ON.
PERFORM A1F_MAIN_PROC2.      " 仕訳データ取込
ENDIF.

ENDFORM.                    " A1F_MAIN_PROC
*&---------------------------------------------------------------------*
*&      Form  A1F_MAIN_PROC1
*&---------------------------------------------------------------------*
*       未払金データ取込
*----------------------------------------------------------------------*
FORM A1F_MAIN_PROC1.
* <<< Local Data >>>
DATA :
A1F_WL_UPLOAD    TYPE A1F_TY_UPLOAD1,
A1F_WL_UPLOAD2   TYPE A1F_TY_UPLOAD1,
A1F_WL_SAVE      TYPE A1F_TY_UPLOAD1,
A1F_WL_DOWNLOAD  TYPE A1F_TY_DOWNLOAD,
A1F_WL_POS       TYPE A1F_TY_POS,
A1F_WL_INDEX     TYPE SYST-INDEX,
A1F_WL_SAKNR     TYPE SKA1-SAKNR,
A1F_WL_BUDAT     TYPE D,
A1F_WL_WRBTR(14) TYPE C,
A1F_FL_NEW       TYPE C,
A1F_FL_ERR       TYPE C.

**
SORT A1F_TG_UPLOAD1 BY ITEM001 ASCENDING
INDEX   ASCENDING.

LOOP AT A1F_TG_UPLOAD1 INTO A1F_WL_UPLOAD.
CLEAR :
A1F_WL_DOWNLOAD,
A1F_WL_SAKNR,
A1F_FL_ERR,
A1F_FL_NEW,
A1F_WL_POS.

AT NEW ITEM001.
MOVE A1F_CNS_ON TO A1F_FL_NEW.
ENDAT.

*   項目「転記日」日付チェック
PERFORM A1F_CHECK_DATE USING    A1F_WL_UPLOAD-ITEM003
CHANGING A1F_WL_DOWNLOAD-BUDAT
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.

IF A1F_FL_NEW EQ A1F_CNS_ON.
CLEAR : A1F_WL_BUDAT.
MOVE :
A1F_WL_DOWNLOAD-BUDAT TO A1F_WL_BUDAT,
A1F_WL_UPLOAD-ITEM006 TO A1F_WL_SAKNR.
ENDIF.

IF A1F_WL_DOWNLOAD-BUDAT NE A1F_WL_BUDAT.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_005.
CONTINUE.
ENDIF.

IF A1F_WL_UPLOAD-ITEM006 NE A1F_WL_SAKNR.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_006.
CONTINUE.
ENDIF.

IF NOT A1F_WL_UPLOAD-ITEM013 IS INITIAL.
*     項目「支払基準日」日付チェック
PERFORM A1F_CHECK_DATE USING    A1F_WL_UPLOAD-ITEM013
CHANGING A1F_WL_DOWNLOAD-ZFBDT
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.
ENDIF.

*   項目「請求金額」数値チェック
PERFORM A1F_CHECK_NUMC USING    A1F_WL_UPLOAD-ITEM009
A1F_WL_UPLOAD-ITEM009  "ダミー
CHANGING A1F_WL_DOWNLOAD-WRBTR
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_002.
CONTINUE.
ENDIF.

A1F_WL_WRBTR = A1F_WL_WRBTR + A1F_WL_DOWNLOAD-WRBTR.

READ TABLE A1F_TG_POS
INTO A1F_WL_POS
WITH KEY TANI = A1F_WL_UPLOAD-ITEM001.

IF SY-SUBRC = 0.
A1F_WL_INDEX = SY-TABIX.
A1F_WL_DOWNLOAD-BUZEI = A1F_WL_POS-BUZEI + A1F_CNS_BUZEI.
ELSE.
A1F_WL_POS-TANI       = A1F_WL_UPLOAD-ITEM001.
A1F_WL_POS-BUZEI      = A1F_CNS_BUZEI.
A1F_WL_DOWNLOAD-BUZEI = A1F_CNS_BUZEI.
APPEND A1F_WL_POS TO A1F_TG_POS.
A1F_WL_INDEX = SY-TABIX.
ENDIF.

*   出力項目編集
PERFORM A1F_EDIT_ITEM USING    SPACE
A1F_WL_UPLOAD
CHANGING A1F_WL_DOWNLOAD.

A1F_WL_POS-BUZEI = A1F_WL_DOWNLOAD-BUZEI + A1F_CNS_BUZEI.
A1F_WL_DOWNLOAD-BUZEI = A1F_WL_POS-BUZEI.
MODIFY A1F_TG_POS FROM A1F_WL_POS INDEX A1F_WL_INDEX.

MOVE A1F_WL_UPLOAD TO A1F_WL_SAVE.

AT END OF ITEM001.
PERFORM A1F_CHECK_NUMC USING    A1F_WL_WRBTR
A1F_WL_WRBTR "ダミー
CHANGING A1F_WL_DOWNLOAD-WRBTR
A1F_FL_ERR.
*     出力項目編集
PERFORM A1F_EDIT_ITEM USING    A1F_CNS_ON
A1F_WL_SAVE
CHANGING A1F_WL_DOWNLOAD.
CLEAR : A1F_WL_WRBTR.
ENDAT.

ENDLOOP.

IF A1F_CG-NG <> 0.
REFRESH : A1F_TG_DOWNLOAD.
ENDIF.

ENDFORM.                    " A1F_MAIN_PROC1
*&---------------------------------------------------------------------*
*&      Form  A1F_MAIN_PROC2
*&---------------------------------------------------------------------*
*       仕訳データ取込
*----------------------------------------------------------------------*
FORM A1F_MAIN_PROC2.

* <<< Local Data >>>
DATA :
A1F_TL_DOWN        TYPE TABLE OF A1F_TY_DOWNLOAD,
A1F_WL_UPLOAD      TYPE A1F_TY_UPLOAD2,
A1F_WL_DOWNLOAD    TYPE A1F_TY_DOWNLOAD,
A1F_WL_DOWNLOAD_S  TYPE A1F_TY_DOWNLOAD,
A1F_WL_DOWNLOAD_H  TYPE A1F_TY_DOWNLOAD,
A1F_WL_POS         TYPE A1F_TY_POS,
A1F_WL_INDEX       TYPE SYST-INDEX,
A1F_WL_INDEX1      TYPE SYST-INDEX,
A1F_WL_SAKNR_S     TYPE SKA1-SAKNR,
A1F_WL_SAKNR_H     TYPE SKA1-SAKNR,
A1F_WL_DMBTR(16)   TYPE P,
A1F_WL_DMBTR_S(16) TYPE P,
A1F_WL_DMBTR_H(16) TYPE P,
A1F_WL_WRBTR_S(16) TYPE P,
A1F_WL_WRBTR_H(16) TYPE P,
A1F_WL_BUDAT       TYPE D,
A1F_FL_CHECK_S     TYPE C,
A1F_FL_CHECK_H     TYPE C,
A1F_FL_SUM         TYPE C,
A1F_FL_NEW         TYPE C,
A1F_FL_ERR         TYPE C.

**
CHECK A1F_FG_END <> A1F_CNS_ON.

SORT A1F_TG_UPLOAD2 BY TANI  ASCENDING
BUDAT ASCENDING
INDEX ASCENDING.

LOOP AT A1F_TG_UPLOAD2 INTO A1F_WL_UPLOAD.
CLEAR :
A1F_WL_DOWNLOAD_S,
A1F_WL_DOWNLOAD_H,
A1F_FL_ERR,
A1F_FL_NEW,
A1F_WL_POS.

AT NEW TANI.
REFRESH: A1F_TL_DOWN.
MOVE A1F_CNS_ON TO A1F_FL_NEW.
ENDAT.

*   項目「国内通貨額」入力チェック
PERFORM A1F_CHECK_DMBTR USING   A1F_WL_UPLOAD-WAERS
A1F_WL_UPLOAD-WRBTR_S
A1F_WL_UPLOAD-WRBTR_H
CHANGING  A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_004.
CONTINUE.
ENDIF.

*   項目「転記日」日付チェック
PERFORM A1F_CHECK_DATE USING    A1F_WL_UPLOAD-BUDAT
CHANGING A1F_WL_DOWNLOAD_S-BUDAT
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.

IF A1F_FL_NEW EQ A1F_CNS_ON.
CLEAR :
A1F_WL_BUDAT,
A1F_WL_SAKNR_S,
A1F_WL_SAKNR_H.

MOVE :
A1F_WL_DOWNLOAD_S-BUDAT TO A1F_WL_BUDAT,
A1F_WL_UPLOAD-SAKNR_S   TO A1F_WL_SAKNR_S,
A1F_WL_UPLOAD-SAKNR_H   TO A1F_WL_SAKNR_H.

*     勘定コードチェック
PERFORM A1F_CHECK_LFA1 USING    A1F_WL_SAKNR_S
CHANGING A1F_FL_CHECK_S.
*     勘定コードチェック
PERFORM A1F_CHECK_LFA1 USING    A1F_WL_SAKNR_H
CHANGING A1F_FL_CHECK_H.
ENDIF.

IF A1F_WL_DOWNLOAD_S-BUDAT NE A1F_WL_BUDAT.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.

IF A1F_FL_CHECK_S EQ A1F_CNS_ON.
IF  A1F_WL_UPLOAD-SAKNR_S NE A1F_WL_SAKNR_S.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_006.
CONTINUE.
ENDIF.
ENDIF.

IF A1F_FL_CHECK_H EQ A1F_CNS_ON.
IF  A1F_WL_UPLOAD-SAKNR_H NE A1F_WL_SAKNR_H.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_006.
CONTINUE.
ENDIF.
ENDIF.

IF NOT A1F_WL_UPLOAD-ZFBDT_S IS INITIAL.
*     項目「支払基準日」日付チェック
PERFORM A1F_CHECK_DATE USING    A1F_WL_UPLOAD-ZFBDT_S
CHANGING A1F_WL_DOWNLOAD_S-ZFBDT
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.
ENDIF.

IF NOT A1F_WL_UPLOAD-ZFBDT_H IS INITIAL.
*     項目「支払基準日」日付チェック
PERFORM A1F_CHECK_DATE USING    A1F_WL_UPLOAD-ZFBDT_H
CHANGING A1F_WL_DOWNLOAD_H-ZFBDT
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_001.
CONTINUE.
ENDIF.
ENDIF.

*   項目「金額」数値チェック
PERFORM A1F_CHECK_NUMC USING    A1F_WL_UPLOAD-DMBTR_S
A1F_WL_UPLOAD-WAERS
CHANGING A1F_WL_DOWNLOAD_S-WRBTR
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_002.
CONTINUE.
ENDIF.

*   項目「金額」数値チェック
PERFORM A1F_CHECK_NUMC USING    A1F_WL_UPLOAD-DMBTR_H
A1F_WL_UPLOAD-WAERS
CHANGING A1F_WL_DOWNLOAD_H-WRBTR
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_002.
CONTINUE.
ENDIF.

*   項目「国内通貨額」数値チェック
PERFORM A1F_CHECK_NUMC USING    A1F_WL_UPLOAD-WRBTR_S
A1F_WL_UPLOAD-WAERS
CHANGING A1F_WL_DOWNLOAD_S-DMBTR
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_002.
CONTINUE.
ENDIF.

*   項目「国内通貨額」数値チェック
PERFORM A1F_CHECK_NUMC USING    A1F_WL_UPLOAD-WRBTR_H
A1F_WL_UPLOAD-WAERS
CHANGING A1F_WL_DOWNLOAD_H-DMBTR
A1F_FL_ERR.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_UPLOAD-INDEX
A1F_CNS_MSGNO_002.
CONTINUE.
ENDIF.

*   金額の貸借一致チェック用変数設定
A1F_WL_DMBTR_S = A1F_WL_DMBTR_S + A1F_WL_DOWNLOAD_S-WRBTR.
A1F_WL_DMBTR_H = A1F_WL_DMBTR_H + A1F_WL_DOWNLOAD_H-WRBTR.

*   国内通貨額の貸借一致チェック用変数設定
A1F_WL_WRBTR_S = A1F_WL_WRBTR_S + A1F_WL_DOWNLOAD_S-DMBTR.
A1F_WL_WRBTR_H = A1F_WL_WRBTR_H + A1F_WL_DOWNLOAD_H-DMBTR.

A1F_WL_INDEX = A1F_WL_UPLOAD-INDEX.

AT END OF BUDAT.

*     金額の貸借一致チェック
IF A1F_WL_DMBTR_S <> A1F_WL_DMBTR_H.
MOVE A1F_CNS_ON TO A1F_FL_ERR.
ENDIF.

*     国内通貨額の貸借一致チェック
IF A1F_WL_WRBTR_S <> A1F_WL_WRBTR_H.
MOVE A1F_CNS_ON TO A1F_FL_ERR.
ENDIF.

IF A1F_FL_ERR = A1F_CNS_ON.
PERFORM A1F_SET_ERROR_LOG USING A1F_WL_INDEX
A1F_CNS_MSGNO_003.
CLEAR: A1F_WL_WRBTR_S,A1F_WL_WRBTR_H.
CONTINUE.

ENDIF.
ENDAT.

READ TABLE A1F_TG_POS
INTO A1F_WL_POS
WITH KEY TANI = A1F_WL_UPLOAD-TANI.

IF SY-SUBRC = 0.
A1F_WL_INDEX = SY-TABIX.
A1F_WL_DOWNLOAD_S-BUZEI = A1F_WL_POS-BUZEI + A1F_CNS_BUZEI.
ELSE.
A1F_WL_POS-TANI         = A1F_WL_UPLOAD-TANI.
A1F_WL_POS-BUZEI        = A1F_CNS_BUZEI.
A1F_WL_DOWNLOAD_S-BUZEI = A1F_CNS_BUZEI.
APPEND A1F_WL_POS TO A1F_TG_POS.
A1F_WL_INDEX = SY-TABIX.
ENDIF.

MOVE :
*     ＜ヘッダ部作成＞
A1F_WL_UPLOAD-TANI      TO A1F_WL_DOWNLOAD_S-TANI,   " 単位
A1F_CNS_BLART           TO A1F_WL_DOWNLOAD_S-BLART,  " 伝票タイプ
A1F_CNS_BUKRS           TO A1F_WL_DOWNLOAD_S-BUKRS,  " 会社コード
A1F_WL_DOWNLOAD_S-BUDAT TO A1F_WL_DOWNLOAD_S-CPUDT,  " 伝票日付
A1F_WL_UPLOAD-WAERS     TO A1F_WL_DOWNLOAD_S-WAERS,  " 通貨
A1F_WG_LTEXT            TO A1F_WL_DOWNLOAD_S-BKTXT,  " 伝票T
A1F_CNS_XMWST           TO A1F_WL_DOWNLOAD_S-XMWST,  " 税フラグ
A1F_WL_UPLOAD-TANI      TO A1F_WL_DOWNLOAD_H-TANI,   " 単位
A1F_CNS_BLART           TO A1F_WL_DOWNLOAD_H-BLART,  " 伝票タイプ
A1F_CNS_BUKRS           TO A1F_WL_DOWNLOAD_H-BUKRS,  " 会社コード
A1F_WL_DOWNLOAD_S-BUDAT TO A1F_WL_DOWNLOAD_H-BUDAT,  " 転記日付
A1F_WL_DOWNLOAD_S-BUDAT TO A1F_WL_DOWNLOAD_H-CPUDT,  " 伝票日付
A1F_WL_UPLOAD-WAERS     TO A1F_WL_DOWNLOAD_H-WAERS,  " 通貨
A1F_WG_LTEXT            TO A1F_WL_DOWNLOAD_H-BKTXT,  " 伝票T
A1F_CNS_XMWST           TO A1F_WL_DOWNLOAD_H-XMWST,  " 税フラグ
*     ＜借方データ作成＞
A1F_CNS_SHKZG_S         TO A1F_WL_DOWNLOAD_S-SHKZG,  " 貸借フラグ
A1F_WL_UPLOAD-SAKNR_S   TO A1F_WL_DOWNLOAD_S-SAKNR,  " 勘定科目
A1F_WL_UPLOAD-MWSKZ_S   TO A1F_WL_DOWNLOAD_S-MWSKZ,  " 税コード
A1F_WL_UPLOAD-KOSTL_S   TO A1F_WL_DOWNLOAD_S-KOSTL,  " 原価センタ
A1F_WL_UPLOAD-SGTXT_S   TO A1F_WL_DOWNLOAD_S-SGTXT,  " 明細T
A1F_WL_UPLOAD-ZUONR_S   TO A1F_WL_DOWNLOAD_S-ZUONR,  " ソートキー
A1F_WL_UPLOAD-BSCHL_S   TO A1F_WL_DOWNLOAD_S-BSCHL.  " 転記キー

A1F_WL_DMBTR = A1F_WL_DOWNLOAD_S-WRBTR.

CLEAR : A1F_WL_DOWNLOAD, A1F_WL_INDEX1.
READ TABLE A1F_TL_DOWN INTO A1F_WL_DOWNLOAD
WITH KEY TANI  = A1F_WL_DOWNLOAD_S-TANI   " 単位
BLART = A1F_WL_DOWNLOAD_S-BLART  " 伝票タイプ
BUKRS = A1F_WL_DOWNLOAD_S-BUKRS  " 会社コード
BUDAT = A1F_WL_DOWNLOAD_S-BUDAT  " 転記日付
CPUDT = A1F_WL_DOWNLOAD_S-CPUDT  " 伝票日付
WAERS = A1F_WL_DOWNLOAD_S-WAERS  " 通貨
BKTXT = A1F_WL_DOWNLOAD_S-BKTXT  " 伝票T
XMWST = A1F_WL_DOWNLOAD_S-XMWST  " 税フラグ
SHKZG = A1F_WL_DOWNLOAD_S-SHKZG  " 貸借フラグ
SAKNR = A1F_WL_DOWNLOAD_S-SAKNR.  " 勘定科目

IF SY-SUBRC <> 0.
IF A1F_WL_DMBTR <> 0.
A1F_WL_POS-BUZEI = A1F_WL_DOWNLOAD_S-BUZEI.
MODIFY A1F_TG_POS FROM A1F_WL_POS INDEX A1F_WL_INDEX.
APPEND A1F_WL_DOWNLOAD_S  TO A1F_TL_DOWN.
A1F_WL_DOWNLOAD_H-BUZEI = A1F_WL_DOWNLOAD_S-BUZEI
+ A1F_CNS_BUZEI.
ENDIF.
ELSE.
A1F_WL_INDEX1 = SY-TABIX.
PERFORM A1F_CHECK_LFA1 USING    A1F_WL_DOWNLOAD-SAKNR
CHANGING A1F_FL_SUM.

IF A1F_FL_SUM EQ A1F_CNS_ON.
A1F_WL_DOWNLOAD_H-BUZEI = A1F_WL_DOWNLOAD_S-BUZEI.
A1F_WL_DOWNLOAD-WRBTR = A1F_WL_DOWNLOAD-WRBTR
+ A1F_WL_DOWNLOAD_S-WRBTR.
CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
INPUT  = A1F_WL_DOWNLOAD-WRBTR
IMPORTING
OUTPUT = A1F_WL_DOWNLOAD-WRBTR.

MODIFY A1F_TL_DOWN FROM A1F_WL_DOWNLOAD INDEX A1F_WL_INDEX1.
ELSE.
A1F_WL_POS-BUZEI = A1F_WL_DOWNLOAD_S-BUZEI.
MODIFY A1F_TG_POS FROM A1F_WL_POS INDEX A1F_WL_INDEX.
APPEND A1F_WL_DOWNLOAD_S  TO A1F_TL_DOWN.
A1F_WL_DOWNLOAD_H-BUZEI = A1F_WL_DOWNLOAD_S-BUZEI
+ A1F_CNS_BUZEI.
ENDIF.
ENDIF.


*   ＜貸方データ作成＞
MOVE :
A1F_CNS_SHKZG_H         TO A1F_WL_DOWNLOAD_H-SHKZG,  " 貸借フラグ
A1F_WL_UPLOAD-SAKNR_H   TO A1F_WL_DOWNLOAD_H-SAKNR,  " 勘定科目
A1F_WL_UPLOAD-MWSKZ_H   TO A1F_WL_DOWNLOAD_H-MWSKZ,  " 税コード
A1F_WL_UPLOAD-KOSTL_H   TO A1F_WL_DOWNLOAD_H-KOSTL,  " 原価センタ
A1F_WL_UPLOAD-SGTXT_H   TO A1F_WL_DOWNLOAD_H-SGTXT,  " 明細T
A1F_WL_UPLOAD-ZUONR_H   TO A1F_WL_DOWNLOAD_H-ZUONR,  " ソートキー
A1F_WL_UPLOAD-BSCHL_H   TO A1F_WL_DOWNLOAD_H-BSCHL.  " 転記キー

A1F_WL_DMBTR = A1F_WL_DOWNLOAD_H-WRBTR.

CLEAR : A1F_WL_DOWNLOAD, A1F_WL_INDEX1.
READ TABLE A1F_TL_DOWN INTO A1F_WL_DOWNLOAD
WITH KEY TANI  = A1F_WL_DOWNLOAD_H-TANI   " 単位
BLART = A1F_WL_DOWNLOAD_H-BLART  " 伝票タイプ
BUKRS = A1F_WL_DOWNLOAD_H-BUKRS  " 会社コード
BUDAT = A1F_WL_DOWNLOAD_H-BUDAT  " 転記日付
CPUDT = A1F_WL_DOWNLOAD_H-CPUDT  " 伝票日付
WAERS = A1F_WL_DOWNLOAD_H-WAERS  " 通貨
BKTXT = A1F_WL_DOWNLOAD_H-BKTXT  " 伝票T
XMWST = A1F_WL_DOWNLOAD_H-XMWST  " 税フラグ
SHKZG = A1F_WL_DOWNLOAD_H-SHKZG  " 貸借フラグ
SAKNR = A1F_WL_DOWNLOAD_H-SAKNR.  " 勘定科目

IF SY-SUBRC <> 0.
IF A1F_WL_DMBTR <> 0.
APPEND A1F_WL_DOWNLOAD_H  TO A1F_TL_DOWN.
A1F_WL_POS-BUZEI = A1F_WL_DOWNLOAD_H-BUZEI.
MODIFY A1F_TG_POS FROM A1F_WL_POS INDEX A1F_WL_INDEX.

ELSE.
CONTINUE.
ENDIF.
ELSE.
A1F_WL_INDEX1 = SY-TABIX.
PERFORM A1F_CHECK_LFA1 USING    A1F_WL_DOWNLOAD-SAKNR
CHANGING A1F_FL_SUM.

IF A1F_FL_SUM EQ A1F_CNS_ON.
A1F_WL_DOWNLOAD_H-WRBTR = A1F_WL_DOWNLOAD-WRBTR
+ A1F_WL_DOWNLOAD_H-WRBTR.
A1F_WL_DOWNLOAD-WRBTR = A1F_WL_DOWNLOAD_H-WRBTR.

CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
INPUT  = A1F_WL_DOWNLOAD-WRBTR
IMPORTING
OUTPUT = A1F_WL_DOWNLOAD-WRBTR.

MODIFY A1F_TL_DOWN FROM A1F_WL_DOWNLOAD INDEX A1F_WL_INDEX1.
ELSE.
APPEND A1F_WL_DOWNLOAD_H  TO A1F_TL_DOWN.
A1F_WL_POS-BUZEI = A1F_WL_DOWNLOAD_H-BUZEI.
MODIFY A1F_TG_POS FROM A1F_WL_POS INDEX A1F_WL_INDEX.

ENDIF.
ENDIF.

AT END OF TANI.
APPEND LINES OF A1F_TL_DOWN TO A1F_TG_DOWNLOAD.
ENDAT.

ENDLOOP.

IF A1F_CG-NG <> 0.
REFRESH : A1F_TG_DOWNLOAD.
ENDIF.

SORT A1F_TG_LIST BY INDEX ASCENDING.

SORT A1F_TG_DOWNLOAD BY TANI  ASCENDING
BUZEI ASCENDING.

ENDFORM.                    " A1F_MAIN_PROC2
*&---------------------------------------------------------------------*
*&      Form  A1F_CHECK_DATE
*&---------------------------------------------------------------------*
*       日付項目チェック
*----------------------------------------------------------------------*
*      --> FU_INPUT   入力日付
*      <-- FC_OUTPUT　編集後
*      <-- FC_ERR     フラグ変数：エラー判定
*----------------------------------------------------------------------*
FORM A1F_CHECK_DATE
USING    VALUE(FU_INPUT)  TYPE C
CHANGING VALUE(FC_OUTPUT) TYPE D
VALUE(FC_ERR)    TYPE C.

* <<< Local Data >>>
DATA :
LW_YEAR(4)    TYPE N,
LW_MONTH(2)   TYPE C,
LW_MONTH_N(2) TYPE N,
LW_DAY(2)     TYPE C,
LW_DAY_N(2)   TYPE N,
LW_DATUM(8)   TYPE C,
LW_DATE       TYPE SYST-DATUM.

**
* 編集
SPLIT FU_INPUT AT A1F_CNS_MARK1 INTO LW_YEAR LW_MONTH LW_DAY.

IF SY-SUBRC <> 0.
MOVE FU_INPUT TO LW_DATUM.
ELSE.
LW_MONTH_N = LW_MONTH.  " 型変換（Ｎ⇒Ｃ）
LW_DAY_N   = LW_DAY.    " 型変換（Ｎ⇒Ｃ）
CONCATENATE LW_YEAR
LW_MONTH_N
LW_DAY_N
INTO LW_DATUM.
ENDIF.

MOVE LW_DATUM TO LW_DATE.

CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
EXPORTING
DATE                      = LW_DATE
EXCEPTIONS
PLAUSIBILITY_CHECK_FAILED = 1
OTHERS                    = 2.

IF SY-SUBRC <> 0.
MOVE A1F_CNS_ON TO FC_ERR.
ELSE.
MOVE LW_DATE TO FC_OUTPUT.
ENDIF.

ENDFORM.                    " A1F_CHECK_DATE
*&---------------------------------------------------------------------*
*&      Form  A1F_SET_ERROR_LOG
*&---------------------------------------------------------------------*
*       ERRORログ情報設定
*----------------------------------------------------------------------*
*      -->FU_TABIX   ファイル行番号
*      -->FU_INPUT   エラーNo.
*----------------------------------------------------------------------*
FORM A1F_SET_ERROR_LOG
USING VALUE(FU_TABIX) TYPE SYST-TABIX
VALUE(FU_INPUT) TYPE SYST-MSGNO.

* <<< Local Data >>>
DATA :
A1F_WL_LIST LIKE LINE OF A1F_TG_LIST.

**
* 失敗件数カウントＵＰ
A1F_CG-NG = A1F_CG-NG + 1.

MOVE FU_TABIX TO A1F_WL_LIST-INDEX.

CASE FU_INPUT.
WHEN A1F_CNS_MSGNO_001.
MOVE TEXT-M01 TO A1F_WL_LIST-TEXT.
WHEN A1F_CNS_MSGNO_002.
MOVE TEXT-M02 TO A1F_WL_LIST-TEXT.
WHEN A1F_CNS_MSGNO_003.
MOVE TEXT-M03 TO A1F_WL_LIST-TEXT.
WHEN A1F_CNS_MSGNO_004.
MOVE TEXT-M06 TO A1F_WL_LIST-TEXT.
WHEN A1F_CNS_MSGNO_005.
MOVE TEXT-M07 TO A1F_WL_LIST-TEXT.
WHEN A1F_CNS_MSGNO_006.
MOVE TEXT-M08 TO A1F_WL_LIST-TEXT.
WHEN OTHERS.
ENDCASE.

APPEND A1F_WL_LIST TO A1F_TG_LIST.

ENDFORM.                    " A1F_SET_ERROR_LOG
*&---------------------------------------------------------------------*
*&      Form  A1F_CHECK_NUMC
*&---------------------------------------------------------------------*
*       数値チェック
*----------------------------------------------------------------------*
*      --> FU_INPUT   入力項目
*      <-- FC_OUTPUT　編集後
*      <-- FC_ERR     フラグ変数：エラー判定
*----------------------------------------------------------------------*
FORM A1F_CHECK_NUMC
USING    VALUE(FU_INPUT)  TYPE C
VALUE(FU_WAERS)  TYPE C
CHANGING VALUE(FC_OUTPUT) TYPE C
VALUE(FC_ERR)    TYPE C.

* <<< Local Data >>>
DATA :
A1F_WL_WRBTR(14) TYPE C,
A1F_WL_FDPOS     TYPE SYST-FDPOS,
A1F_WL_HTYPE     TYPE DD01V-DATATYPE.

**
MOVE FU_INPUT TO A1F_WL_WRBTR.

A1F_WL_FDPOS = STRLEN( A1F_WL_WRBTR ).

DO A1F_WL_FDPOS TIMES.
REPLACE A1F_CNS_MARK2 WITH SPACE INTO A1F_WL_WRBTR.
REPLACE A1F_CNS_MARK3 WITH SPACE INTO A1F_WL_WRBTR.

IF SY-SUBRC = 0.
CONDENSE A1F_WL_WRBTR NO-GAPS.
ELSE.
EXIT.
ENDIF.
ENDDO.


CALL FUNCTION 'NUMERIC_CHECK'
EXPORTING
STRING_IN  = A1F_WL_WRBTR
IMPORTING
STRING_OUT = FC_OUTPUT
HTYPE      = A1F_WL_HTYPE.

IF A1F_WL_HTYPE = A1F_CNS_CHAR.

MOVE : A1F_CNS_ON TO FC_ERR.

IF P_PROC2 = A1F_CNS_ON.

IF FU_WAERS <> 'JPY'.
CLEAR : FC_ERR.
ENDIF.

ENDIF.

ENDIF.

ENDFORM.                    " A1F_CHECK_NUMC
*&---------------------------------------------------------------------*
*&      Form  A1F_EDIT_ITEM
*&---------------------------------------------------------------------*
*       出力項目編集
*----------------------------------------------------------------------*
*      --> FU_FLG       登録フラグ
*      --> FU_UPLOAD    アップロード項目
*      <-- FC_DOWNLOAD  ダウンロード項目
*----------------------------------------------------------------------*
FORM A1F_EDIT_ITEM
USING    VALUE(FU_FLG)      TYPE C
VALUE(FU_UPLOAD)   TYPE A1F_TY_UPLOAD1
CHANGING VALUE(FC_DOWNLOAD) TYPE A1F_TY_DOWNLOAD.

**
MOVE :
*   ＜UPLOAD項目⇒DOWNLOAD項目＞
FU_UPLOAD-ITEM001 TO FC_DOWNLOAD-TANI,       " 伝票単位項目
FU_UPLOAD-ITEM010 TO FC_DOWNLOAD-MWSKZ,      " 税コード
FU_UPLOAD-ITEM008 TO FC_DOWNLOAD-SGTXT,      " 明細テキスト
FC_DOWNLOAD-BUDAT TO FC_DOWNLOAD-CPUDT,      " 会計伝票日付

*   ＜固定値⇒DOWNLOAD項目＞
A1F_CNS_BLART     TO FC_DOWNLOAD-BLART,      " 伝票タイプ
A1F_CNS_BUKRS     TO FC_DOWNLOAD-BUKRS,      " 会社コード
A1F_CNS_XMWST     TO FC_DOWNLOAD-XMWST,      " 税自動計算フラグ

*   ＜DB取得項目⇒DOWNLOAD項目＞
A1F_WG_LTEXT      TO FC_DOWNLOAD-BKTXT,      " 伝票テキスト
A1F_WG_WAERS      TO FC_DOWNLOAD-WAERS,      " 通貨

*   ＜借方データ作成＞
A1F_CNS_SHKZG_S   TO FC_DOWNLOAD-SHKZG,      " 貸借フラグ
FU_UPLOAD-ITEM004 TO FC_DOWNLOAD-SAKNR,      " 勘定科目
FU_UPLOAD-ITEM002 TO FC_DOWNLOAD-KOSTL.      " 原価センタ

IF FU_FLG = SPACE.
APPEND FC_DOWNLOAD TO A1F_TG_DOWNLOAD.
ENDIF.

*   ＜貸方データ作成＞
MOVE :
A1F_CNS_SHKZG_H   TO FC_DOWNLOAD-SHKZG,      " 貸借フラグ
FU_UPLOAD-ITEM006 TO FC_DOWNLOAD-SAKNR.      " 勘定科目
CLEAR : FC_DOWNLOAD-KOSTL,
: FC_DOWNLOAD-MWSKZ.

IF FU_FLG = A1F_CNS_ON.
APPEND FC_DOWNLOAD TO A1F_TG_DOWNLOAD.
ENDIF.

ENDFORM.                    " A1F_EDIT_ITEM
*&---------------------------------------------------------------------*
*&      Form  A1F_END_PROC
*&---------------------------------------------------------------------*
*       終了処理
*----------------------------------------------------------------------*
FORM A1F_END_PROC.

**
CHECK A1F_FG_END <> A1F_CNS_ON.

IF A1F_CG-NG = 0.
PERFORM A1F_FILE_DOWNLOAD.    " DOWNLOAD処理
ENDIF.

PERFORM A1F_LIST_OUT.

ENDFORM.                    " A1F_END_PROC
*&---------------------------------------------------------------------*
*&      Form  A1F_FILE_DOWNLOAD
*&---------------------------------------------------------------------*
*       DOWNLOAD処理
*----------------------------------------------------------------------*
FORM A1F_FILE_DOWNLOAD.
* <<< Local Data >>>
DATA :
A1F_WL_FILENAME TYPE STRING.

**
MOVE P_OTFL TO A1F_WL_FILENAME.

CALL METHOD CL_GUI_FRONTEND_SERVICES=>GUI_DOWNLOAD
EXPORTING
FILENAME               = A1F_WL_FILENAME
FILETYPE               = 'ASC'
WRITE_FIELD_SEPARATOR   = A1F_CNS_ON
CHANGING
DATA_TAB                = A1F_TG_DOWNLOAD
EXCEPTIONS
FILE_WRITE_ERROR        = 1
NO_BATCH                = 2
GUI_REFUSE_FILETRANSFER = 3
INVALID_TYPE            = 4
NO_AUTHORITY            = 5
UNKNOWN_ERROR           = 6
HEADER_NOT_ALLOWED      = 7
SEPARATOR_NOT_ALLOWED   = 8
FILESIZE_NOT_ALLOWED    = 9
HEADER_TOO_LONG         = 10
DP_ERROR_CREATE         = 11
DP_ERROR_SEND           = 12
DP_ERROR_WRITE          = 13
UNKNOWN_DP_ERROR        = 14
ACCESS_DENIED           = 15
DP_OUT_OF_MEMORY        = 16
DISK_FULL               = 17
DP_TIMEOUT              = 18
FILE_NOT_FOUND          = 19
DATAPROVIDER_EXCEPTION  = 20
CONTROL_FLUSH_ERROR     = 21
OTHERS                  = 22
.
IF SY-SUBRC <> 0.
*   処理に失敗しました （ファイルダウンロード）
MESSAGE S003 WITH TEXT-M05.
ELSE.
*   処理が正常終了しました。
MESSAGE S010.
ENDIF.

ENDFORM.                    " A1F_FILE_DOWNLOAD
*&---------------------------------------------------------------------*
*&      Form  A1F_LIST_OUT
*&---------------------------------------------------------------------*
*       処理結果リスト出力処理
*----------------------------------------------------------------------*
FORM A1F_LIST_OUT.
* <<< Local Data >>>
DATA :
A1F_WL_LIST TYPE A1F_TY_LIST,
A1F_CL_LINE TYPE I.

**
* 成功件数算出
A1F_CG-OK = A1F_CG-PROC - A1F_CG-NG.

PERFORM A1F_OUT_HEAD.

LOOP AT A1F_TG_LIST INTO A1F_WL_LIST.

IF A1F_CL_LINE = 54.
NEW-PAGE.
PERFORM A1F_OUT_HEAD.
ENDIF.

WRITE:/ A1F_WL_LIST-INDEX,
A1F_WL_LIST-TEXT.
A1F_CL_LINE = A1F_CL_LINE + 1.
ENDLOOP.

ENDFORM.                    " A1F_LIST_OUT
*&---------------------------------------------------------------------*
*&      Form  A1F_OUT_HEAD
*&---------------------------------------------------------------------*
*       ヘッダ情報出力
*----------------------------------------------------------------------*
FORM A1F_OUT_HEAD.

**
WRITE:/
TEXT-H01, SY-MANDT,
15  TEXT-H02, SY-REPID,
45  TEXT-H03,
87  TEXT-H04, A1F_WG_DATUM,
105 TEXT-H05, A1F_WG_UZEIT.

SKIP 1.
WRITE:/ TEXT-H06, A1F_CG-FILE, TEXT-H07.
WRITE:/ TEXT-H08, A1F_CG-PROC, TEXT-H07.
WRITE:/ TEXT-H09, A1F_CG-OK, TEXT-H07.
WRITE:/ TEXT-H10, A1F_CG-NG, TEXT-H07.

CHECK A1F_CG-NG <> 0.

SKIP 1.
WRITE:/ TEXT-H11,13(100) TEXT-H12.
ULINE:/0(120).

ENDFORM.                    " A1F_OUT_HEAD
*&---------------------------------------------------------------------*
*&      Form  A1F_CHECK_DMBTR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_A1F_WL_UPLOAD_WAERS  text
*      -->P_A1F_WL_UPLOAD_DMBTR_S  text
*      -->P_A1F_WL_UPLOAD_DMBTR_H  text
*      <--P_A1F_FL_ERR  text
*----------------------------------------------------------------------*
FORM A1F_CHECK_DMBTR USING    P_A1F_WL_UPLOAD_WAERS
P_A1F_WL_UPLOAD_DMBTR_S
P_A1F_WL_UPLOAD_DMBTR_H
CHANGING P_A1F_FL_ERR.

* 通貨がJPYであり国内通貨額に入力がある場合エラー
IF P_A1F_WL_UPLOAD_WAERS = A1F_CNS_JPY
AND NOT P_A1F_WL_UPLOAD_DMBTR_S IS INITIAL.

MOVE A1F_CNS_ON TO P_A1F_FL_ERR.

ENDIF.

* 通貨がJPYであり国内通貨額に入力がある場合エラー
IF P_A1F_WL_UPLOAD_WAERS = A1F_CNS_JPY
AND NOT P_A1F_WL_UPLOAD_DMBTR_H IS INITIAL.

MOVE A1F_CNS_ON TO P_A1F_FL_ERR.

ENDIF.

ENDFORM.                    " A1F_CHECK_DMBTR
*&---------------------------------------------------------------------*
*&      Form  A1F_CHECK_LFA1
*&---------------------------------------------------------------------*
*       勘定コードチェック
*----------------------------------------------------------------------*
*      --> FU_SAKNR　  勘定コード
*      <-- FC_CHECK_S  チェックフラグ
*----------------------------------------------------------------------*
FORM A1F_CHECK_LFA1
USING    VALUE(FU_SAKNR) TYPE SKA1-SAKNR
CHANGING VALUE(FC_CHECK) TYPE C.
* <<< Local Data >>>
DATA :
A1F_WL_LIFNR_S TYPE LFA1-LIFNR,
A1F_WL_LIFNR   TYPE LFA1-LIFNR.
**
CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
INPUT  = FU_SAKNR
IMPORTING
OUTPUT = A1F_WL_LIFNR_S.

* データ検索
SELECT SINGLE LIFNR
FROM LFB1
INTO A1F_WL_LIFNR
WHERE LIFNR EQ A1F_WL_LIFNR_S
AND BUKRS EQ A1F_CNS_BUKRS.

IF SY-SUBRC = 0.
MOVE A1F_CNS_ON TO FC_CHECK.
ELSE.
CLEAR FC_CHECK.
ENDIF.

ENDFORM.                    " A1F_CHECK_LFA1
