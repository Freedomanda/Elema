report  ztest_isid06_002
line-size 340.
*&---------------------------------------------------------------------*
*&      型宣言
*&---------------------------------------------------------------------*
type-pools: abap, synt.
types rslinlmsg_t type standard table of rslinlmsg.
*&---------------------------------------------------------------------*
*&      選択画面
*&---------------------------------------------------------------------*
tables: sscrfields, trdir.
select-options p_obj for trdir-name. "プログラム名
selection-screen skip.
* 出力条件
selection-screen begin of block block with frame title text_001.
parameters:
p_err  as checkbox default 'X', "エラー出力
p_warn as checkbox default 'X', "警告出力
p_info as checkbox default 'X'. "情報出力
selection-screen begin of block block2 with frame.
parameters p_offset as checkbox default ' '. "OFFSET指定のみ出力
selection-screen end of block block2.
selection-screen end of block block.
*&---------------------------------------------------------------------*
*&      型定義
*&---------------------------------------------------------------------*
constants:
c_line_len type i value 6,
begin of c_name,
obj_low type screen-name value 'P_OBJ-LOW',
end of c_name,
begin of c_required,
make_only type screen-required value '2',
end of c_required,
begin of c_ucomm,
extension type syst-ucomm value '%000', "複数選択
end of c_ucomm,
begin of c_subc,
rpt type trdir-subc value '1', "実行可能プログラム
mdl type trdir-subc value 'M', "モジュールプール
fng type trdir-subc value 'F', "汎用グループ
end of c_subc.
*&---------------------------------------------------------------------*
*&      選択画面初期化
*&---------------------------------------------------------------------*
initialization.
%_p_obj_%_app_%-text = 'プログラム名'.
%_p_err_%_app_%-text = 'エラー出力'.
%_p_warn_%_app_%-text = '警告出力'.
%_p_info_%_app_%-text = '情報出力'.
%_p_offset_%_app_%-text = '変数OFFSETと構造への代入のみ出力'.
text_001 = '出力条件'.
perform initialization.
*&---------------------------------------------------------------------*
*&      選択画面イベント
*&---------------------------------------------------------------------*
at selection-screen.
* 複数選択画面に遷移するとき以外は、必須チェックする
if  sscrfields-ucomm <> c_ucomm-extension
and lines( p_obj ) = 0.
message e002(00).
endif.
*&---------------------------------------------------------------------*
*&      START-OF-SELECTION
*&---------------------------------------------------------------------*
start-of-selection.
perform startofselection.
*&---------------------------------------------------------------------*
*&      Form  INITIALIZATION
*&---------------------------------------------------------------------*
form initialization.
* プログラム名を必須マーク付きの任意項目にする
loop at screen.
check screen-name = c_name-obj_low.
screen-required = c_required-make_only.
modify screen.
endloop.
endform.                    "INITIALIZATION
*&---------------------------------------------------------------------*
*&      Form  STARTOFSELECTION
*&---------------------------------------------------------------------*
form startofselection.
data: i_dir    type table of trdir,
l_dir    type ref to trdir,
soc      type table of string,
i_err    type table of rslinlmsg,
i_warn   type table of rslinlmsg,
i_info   type table of rslinlmsg,
w_msg    type ref to rslinlmsg.
* 未使用の変数：必須パラメータのため必要
set extended check off.
data: mess     type string,
lin      type i,
wrd      type string.
set extended check on.
* 一覧取得
select * into table i_dir from trdir
where name in p_obj
and subc in (c_subc-rpt, c_subc-mdl, c_subc-fng).
loop at i_dir reference into l_dir.
* 構文チェック
read report l_dir->name into soc.
check sy-subrc = 0.
if cl_abap_char_utilities=>charsize > 1.
l_dir->uccheck = abap_true.
else.
l_dir->uccheck = abap_false.
endif.
syntax-check for soc message mess line lin word wrd
directory entry l_dir->*
id synt_id-err_msg  table i_err
id synt_id-warn_msg table i_warn
id synt_id-info_msg table i_info.
*  出力するエラーの抽出
if p_err = abap_false.
clear i_err.
endif.
if p_warn = abap_false.
clear i_warn.
endif.
if p_info = abap_false.
clear i_info.
else.
loop at i_info reference into w_msg.
find `リテラル` in w_msg->message.
if sy-subrc <> 0.
check p_offset = abap_true.
find regex `(オフセット|'structure'|'u'|'v')` in w_msg->message.
check sy-subrc <> 0.
endif.
delete i_info index sy-tabix.
endloop.
endif.
*  出力
perform f_write using l_dir->name:
i_err  'E' col_negative,
i_warn 'W' col_total,
i_info 'I' col_positive.
endloop.
endform.                    "STARTOFSELECTION
*&---------------------------------------------------------------------*
*&      Form  f_write
*&---------------------------------------------------------------------*
form f_write using name  type trdir-name
i_tab type rslinlmsg_t
msgty type syst-msgty
color like col_heading.
data w_msg type ref to rslinlmsg.
loop at i_tab reference into w_msg.
write: at /            name           color col_heading,
w_msg->incname color col_heading,
at (c_line_len) w_msg->line    color col_heading,
msgty          color = color,
w_msg->message color = color.
endloop.
endform.                    "f_write
