*&---------------------------------------------------------------------*
*& 品目マスタチェック
*&---------------------------------------------------------------------*
report ztest_isid06_014 line-size 155
line-count 58.
type-pools abap.
*&---------------------------------------------------------------------*
* 選択画面
*&---------------------------------------------------------------------*
tables: mara, knmt.
selection-screen begin of block block4
with frame title text_004.
selection-screen begin of line.
parameters p_mara radiobutton group grp1.
selection-screen comment (20) for field p_mara.
parameters p_knmt radiobutton group grp1.
selection-screen comment (20) for field p_knmt.
parameters p_all  radiobutton group grp1 default 'X'.
selection-screen comment (20) for field p_all.
selection-screen end of line.
selection-screen end of block block4.
selection-screen begin of block block2
with frame title text_002.
select-options p_matnr for mara-matnr. "品目コード
selection-screen begin of block block3
with frame title text_003.
select-options p_kunnr for knmt-kunnr. "得意先コード
select-options p_kdmat for knmt-kdmat. "得意先品目コード
selection-screen end of block block3.
selection-screen end of block block2.
*&---------------------------------------------------------------------*
* 固定値
*&---------------------------------------------------------------------*
constants:
c_001 type c length 23 value 'SJISに変換不可能な品目テキストはありません',
c_002 type c length 2 value '共通',
c_003 type c length 8 value '得意先品目マスタ',
c_004 type c length 6 value 'チェック対象',
c_t01 type c length 5 value '得意先ＣＤ',
c_t02 type c length 9 value '(得意先)品目ＣＤ',
c_t03 type c length 11 value '(得意先)品目テキスト',
c_t04 type c length 9 value '最終更新者(ID)',
c_t05 type c length 8 value '最終更新者(名)',
c_t06 type c length 5 value '最終更新日'.
*&---------------------------------------------------------------------*
* 型定義
*&---------------------------------------------------------------------*
types: begin of t_makt,
kunnr type knmt-kunnr, "得意先
kdmat type knmt-kdmat, "(得意先)品目
postx type knmt-postx, "(得意先)テキスト
ernam type mara-ernam, "登録者
ersda type mara-ersda, "登録日
end of t_makt.
types: begin of t_mara,
matnr type mara-matnr, "品目
ersda type mara-ersda, "登録日
ernam type mara-ernam, "登録者
lvorm type mara-lvorm, "削除フラグ
end of t_mara.
types: begin of t_knmt,
matnr type knmt-matnr, "品目コード
kunnr type knmt-kunnr, "得意先
kdmat type knmt-kdmat, "(得意先)品目
postx type knmt-postx, "(得意先)テキスト
end of t_knmt.
data: g_out_ce type ref to cl_abap_conv_out_ce,
g_view   type ref to cl_abap_view_offlen,
g_makt   type sorted table of t_makt with non-unique key kunnr kdmat.
*&---------------------------------------------------------------------*
* initialization
*&---------------------------------------------------------------------*
initialization.
text_002 = c_002.
text_003 = c_003.
text_004 = c_004.
%_p_all_%_app_%-text = '両方'.
%_p_kdmat_%_app_%-text = '得意先品目コード'.
%_p_knmt_%_app_%-text = '得意先品目マスタ'.
%_p_kunnr_%_app_%-text = '得意先コード'.
%_p_mara_%_app_%-text = '品目マスタ'.
%_p_matnr_%_app_%-text = '品目コード'.

*&---------------------------------------------------------------------*
* start-of-selection
*&---------------------------------------------------------------------*
start-of-selection.
perform init.
if p_mara = abap_true or p_all = abap_true.
perform chk_mara.
endif.
if p_knmt = abap_true or p_all = abap_true.
perform chk_knmt.
endif.
perform out_data.
*&---------------------------------------------------------------------*
* top-of-page
*&---------------------------------------------------------------------*
top-of-page.
write: /(10) c_t01 color col_heading,
(35) c_t02 color col_heading,
(40) c_t03 color col_heading,
(14) c_t04 color col_heading,
(40) c_t05 color col_heading,
(10) c_t06 color col_heading.
uline.
*&---------------------------------------------------------------------*
* 変換engine構築
*&---------------------------------------------------------------------*
form init.
constants sjis type string value `shift_jis`.
data: l_codepage type abap_encoding,
l_makt  type t_makt.
l_codepage = cl_abap_codepage=>sap_codepage( sjis ).
g_out_ce = cl_abap_conv_out_ce=>create( encoding = l_codepage ).
g_view = cl_abap_view_offlen=>create_legacy_view( l_makt ).
endform.                    "init
*&---------------------------------------------------------------------*
* 品目テキスト検索
*&---------------------------------------------------------------------*
form chk_mara.
data: l_mara  type t_mara,
l_makt  type t_makt,
l_matnr type mara-matnr,
l_rts  type abap_bool.
*   品目テキスト検索
select matnr maktx into (l_matnr, l_makt-postx) from makt
where matnr in p_matnr
and spras = sy-langu.
*   SJIS変換チェック
l_makt-kdmat = l_matnr.
perform makt_out using l_makt l_rts.
check l_rts = abap_false.
*   削除品目除外
perform get_mara using l_matnr l_mara.
check l_mara-lvorm = abap_false.
l_makt-ersda = l_mara-ersda.
l_makt-ernam = l_mara-ernam.
insert l_makt into table g_makt.
endselect.
endform.                    "chk_mara
*&---------------------------------------------------------------------*
* 得意先品目テキストの検索
*&---------------------------------------------------------------------*
form chk_knmt.
data: l_makt type t_makt,
l_knmt type t_knmt,
l_mara type t_mara,
l_rts  type abap_bool.
select matnr kunnr kdmat postx into l_knmt from knmt
where kunnr in p_kunnr
and kdmat in p_kdmat
and matnr in p_matnr.
*   SJIS変換チェック
l_makt-kdmat = l_knmt-kdmat.
l_makt-postx = l_knmt-postx.
perform makt_out using l_makt l_rts.
check l_rts = abap_false.
*   削除品目除外
perform get_mara using l_knmt-matnr l_mara.
check l_mara-lvorm = abap_false.
l_makt-kunnr = l_knmt-kunnr.
insert l_makt into table g_makt.
endselect.
endform.                    "chk_knmt
*&---------------------------------------------------------------------*
* 品目テキストチェック
*&---------------------------------------------------------------------*
form makt_out using l_makt type t_makt
l_rts  type abap_bool.
l_rts = abap_true.
try.
g_out_ce->convert_struc( data = l_makt
view = g_view ).
catch cx_dynamic_check.
l_rts = abap_false.
endtry.
endform.                    "makt_out
*&---------------------------------------------------------------------*
* 品目マスタ取得
*&---------------------------------------------------------------------*
form get_mara using pi_matnr type mara-matnr
po_mara  type t_mara.
statics i_mara type hashed table of t_mara with unique key matnr.
data: l_laeda type mara-laeda, "更新日
l_aenam type mara-aenam. "更新者
read table i_mara into po_mara
with table key matnr = pi_matnr.
if sy-subrc = 0.
return.
endif.
select single ersda laeda ernam aenam lvorm
into (po_mara-ersda, l_laeda, po_mara-ernam, l_aenam, po_mara-lvorm)
from mara
where matnr = pi_matnr.
po_mara-matnr = pi_matnr.
if sy-subrc = 0.
if l_laeda > po_mara-ersda.
po_mara-ersda = l_laeda.
po_mara-ernam = l_aenam.
endif.
else.
po_mara-lvorm = abap_true.
endif.
insert po_mara into table i_mara.
endform.                    "get_mara
*&---------------------------------------------------------------------*
* 結果出力
*&---------------------------------------------------------------------*
form out_data.
data: i_userlist type table of bapiusname,
l_userlist type bapiusname,
l_makt     type ref to t_makt.
if lines( g_makt ) = 0.
write / c_001.
return.
endif.
* ユーザ一覧取得
call function 'BAPI_USER_GETLIST'
exporting
with_username = abap_true
tables
userlist      = i_userlist.
sort i_userlist by username.
* 出力
loop at g_makt reference into l_makt.
new-line.
if l_makt->kunnr is not initial.
write l_makt->kunnr under c_t01. "得意先
endif.
write: l_makt->kdmat under c_t02, "(得意先)品目
l_makt->postx under c_t03. "(得意先)テキスト
check l_makt->ernam is not initial.
clear l_userlist-fullname.
read table i_userlist into l_userlist
with key username = l_makt->ernam
binary search.
write: l_makt->ernam under c_t04, "登録者
(40) l_userlist-fullname under c_t05. "姓名
check l_makt->ersda is not initial.
write l_makt->ersda under c_t06. "登録日
endloop.
endform.                    "out_data
