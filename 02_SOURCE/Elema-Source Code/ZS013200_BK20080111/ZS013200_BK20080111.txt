REPORT ZS013200 .

*&---------------------------------------------------------------------*
*&   TYPES定義
*&---------------------------------------------------------------------*
TYPE-POOLS SLIS.
TYPES:
BEGIN OF TYP_DATA_H,                         " 販売価格MAIN
KUNNR     TYPE A901-KUNNR,      "得意先コード
NAME1     TYPE KNA1-NAME1,      "名称 1
ZZZKDMAT  TYPE A901-ZZZKDMAT,   "得意先品目コード
MATNR     TYPE KNMT-MATNR,      "品目コード
MAKTX     TYPE MAKT-MAKTX,      "品目テキスト
KSTBM     TYPE KONM-KSTBM,      "条件スケール数量
KPEIN     TYPE KONP-KPEIN,      "価格条件単位
VALUE_OLD(13) TYPE C,           "変更前の項目内容
VALUE_NEW(13) TYPE C,           "変更後の項目内容
KONWA     TYPE KONP-KONWA,      "通貨
KMEIN     TYPE KONP-KMEIN,      "数量単位
DATBI     TYPE A901-DATBI,      "条件マスタ有効終了日
DATAB     TYPE A901-DATAB,      "条件マスタ有効開始日
VKORGAU   TYPE A901-VKORGAU,    "販売組織
VTWEG     TYPE A901-VTWEG,      "流通チャネル
TABKEY    TYPE CDPOS-TABKEY,    "変更テーブルレコードキー
OBJECTID  TYPE CDPOS-OBJECTID,  "対象値
CHANGENR  TYPE CDPOS-CHANGENR,  "変更文書番
VKBUR     TYPE KNVV-VKBUR ,    "営業所
END OF TYP_DATA_H,

BEGIN OF TYP_DATA_K,                         " 購買価格MAIN
LIFNR     TYPE A017-LIFNR,      "仕入先勘定コード
NAME1     TYPE LFA1-NAME1,      "名称 1
IDNLF     TYPE EINA-IDNLF,      "仕入先の品目コード
MATNR     TYPE A017-MATNR,      "品目コード
MAKTX     TYPE MAKT-MAKTX,      "品目テキスト
KSTBM     TYPE KONM-KSTBM,      "条件スケール数量
KPEIN     TYPE KONP-KPEIN,      "価格条件単位
VALUE_OLD(13) TYPE C,           "変更前の項目内容
VALUE_NEW(13) TYPE C,           "変更後の項目内容
KONWA     TYPE KONP-KONWA,      "通貨
KMEIN     TYPE KONP-KMEIN,      "数量単位
DATBI     TYPE A017-DATBI,      "条件マスタ有効終了日
DATAB     TYPE A017-DATAB,      "条件マスタ有効開始日
TABKEY    TYPE CDPOS-TABKEY,    "変更テーブルレコードキー
OBJECTID  TYPE CDPOS-OBJECTID,  "対象値
CHANGENR  TYPE CDPOS-CHANGENR,  "変更文書番号
END OF TYP_DATA_K.


*&---------------------------------------------------------------------*
*&   DATA定義（内部テーブル、テーブルヘッダ）
*&---------------------------------------------------------------------*
DATA:    IT_CDHDR     TYPE STANDARD TABLE OF CDHDR,     " 伝票ヘッダ変更
IT_CDPOS     TYPE STANDARD TABLE OF CDPOS,     " 伝票明細変更
IF_CDPOS     TYPE CDPOS,
IT_DATA_H    TYPE STANDARD TABLE OF TYP_DATA_H," 販売価格一覧
IF_DATA_H    TYPE TYP_DATA_H,
IT_DATA_K    TYPE STANDARD TABLE OF TYP_DATA_K," 購買価格一覧
IF_DATA_K    TYPE TYP_DATA_K,
GT_ALV_H     TYPE TABLE OF ZSLIST_V22 WITH HEADER LINE,
GF_ALV_H     TYPE ZSLIST_V22,
GT_ALV_K     TYPE TABLE OF ZSLIST_V23 WITH HEADER LINE,
GF_ALV_K     TYPE ZSLIST_V23.

*&---------------------------------------------------------------------*
*&   DATA定義（グローバル変数）
*&---------------------------------------------------------------------*
DATA:    G_VKBUR        TYPE TVKBT-VKBUR,         "営業所
G_BEZEI        TYPE TVKBT-BEZEI,         "営業所名
G_NAME1        TYPE T001W-NAME1,         "プラント名
GF_LAYOUT      TYPE SLIS_LAYOUT_ALV,     "レイアウト構造
GF_VARIANT     TYPE DISVARIANT,          "バリアント
GF_PRINT       TYPE SLIS_PRINT_ALV,
GT_EVENT       TYPE SLIS_T_EVENT,
GF_EVENT       TYPE SLIS_ALV_EVENT,
G_TOP_OF_PAGE  TYPE SLIS_FORMNAME VALUE 'TOP_OF_PAGE',
G_REPID        TYPE SY-REPID,
L_COUNT        TYPE I .

*&---------------------------------------------------------------------*
*&   画面定義
*&---------------------------------------------------------------------*
PARAMETERS:     PR_WERKS TYPE TVKWZ-WERKS OBLIGATORY.     " プラント
SELECT-OPTIONS: PR_DATE  FOR SY-DATUM DEFAULT SY-DATUM.   " 登録日付

PARAMETERS: PR_KB  RADIOBUTTON GROUP RAD1,                " 購買価格一覧
PR_HB  RADIOBUTTON GROUP RAD1.                " 販売価格一覧

*&---------------------------------------------------------------------*
*&   START-OF-SELECTION
*&---------------------------------------------------------------------*
START-OF-SELECTION.

G_REPID = SY-REPID.
G_VKBUR = PR_WERKS.
SHIFT G_VKBUR BY 1 PLACES LEFT.
G_VKBUR+3(1) = '0'.                           " 営業所

* 変更文書の取得
PERFORM FRM_GET_CDHDR.

* 変更文書明細の取得
PERFORM FRM_GET_CDPOS.

* ラジオボタン「販売価格マスタ登録・更新一覧表」がONの場合
IF PR_HB = 'X'.
CLEAR: GT_ALV_H[].
* 販売価格一覧対象データの取得
PERFORM FRM_GET_DATAH.

LOOP AT IT_DATA_H
INTO IF_DATA_H.
CLEAR: GF_ALV_H.
* 得意先名の取得
PERFORM FRM_GET_KNA1 USING IF_DATA_H-KUNNR
IF_DATA_H-NAME1.
* 営業所の取得
Perform FRM_GET__KNVV Using IF_DATA_H-KUNNR
IF_DATA_H-VKBUR .


* 品目コードの取得
PERFORM FRM_GET_KNMT USING IF_DATA_H.
* 品目名の取得
PERFORM FRM_GET_MAKT USING IF_DATA_H-MATNR
IF_DATA_H-MAKTX.
* 通貨・単位・価格条件単位の取得
PERFORM FRM_GET_KONP USING IF_DATA_H-OBJECTID
IF_DATA_H-KONWA
IF_DATA_H-KMEIN
IF_DATA_H-KPEIN.
* スケール数量の取得
PERFORM FRM_GET_KONM  USING IF_DATA_H-TABKEY
IF_DATA_H-OBJECTID
IF_DATA_H-CHANGENR
IF_DATA_H-KSTBM.
* 受注単価（旧）の取得
PERFORM FRM_GET_VALUE_OLD USING IF_DATA_H-TABKEY
IF_DATA_H-OBJECTID
IF_DATA_H-CHANGENR
IF_DATA_H-VALUE_OLD.
WRITE IF_DATA_H-VALUE_OLD
TO IF_DATA_H-VALUE_OLD RIGHT-JUSTIFIED.
MOVE-CORRESPONDING IF_DATA_H TO GF_ALV_H.
If G_VKBUR EQ IF_DATA_H-VKBUR .
APPEND GF_ALV_H TO GT_ALV_H.
ENDIF .
ENDLOOP.
* 営業所名の取得
PERFORM FRM_GET_TVKBT USING G_VKBUR
G_BEZEI.
DESCRIBE TABLE GT_ALV_H LINES L_COUNT.
IF L_COUNT = 0.
MESSAGE S614(Z1) WITH TEXT-M01.
STOP.
ENDIF.


*--- 「販売価格マスタ登録・更新一覧表」出力
PERFORM FRM_WRITE_DATAH.
ENDIF.
* ラジオボタン「購買価格マスタ登録・更新一覧表」がONの場合
IF PR_KB = 'X'.
CLEAR: GT_ALV_K[].
* 購買価格一覧対象データの取得
PERFORM FRM_GET_DATAK.

LOOP AT IT_DATA_K
INTO IF_DATA_K.
CLEAR: GF_ALV_K.
* 仕入先名の取得
PERFORM FRM_GET_LFA1 USING IF_DATA_K-LIFNR
IF_DATA_K-NAME1.
* 仕入先品目コードの取得
PERFORM FRM_GET_EINA USING IF_DATA_K-LIFNR
IF_DATA_K-MATNR
IF_DATA_K-IDNLF.
* 品目名の取得
PERFORM FRM_GET_MAKT USING IF_DATA_K-MATNR
IF_DATA_K-MAKTX.
* 通貨・単位・価格条件単位の取得
PERFORM FRM_GET_KONP USING IF_DATA_K-OBJECTID
IF_DATA_K-KONWA
IF_DATA_K-KMEIN
IF_DATA_K-KPEIN.
* スケール数量の取得
PERFORM FRM_GET_KONM USING IF_DATA_K-TABKEY
IF_DATA_K-OBJECTID
IF_DATA_K-CHANGENR
IF_DATA_K-KSTBM.
* 購買単価（旧）の取得
PERFORM FRM_GET_VALUE_OLD USING IF_DATA_K-TABKEY
IF_DATA_K-OBJECTID
IF_DATA_K-CHANGENR
IF_DATA_K-VALUE_OLD.
WRITE IF_DATA_K-VALUE_OLD
TO IF_DATA_K-VALUE_OLD RIGHT-JUSTIFIED.
MOVE-CORRESPONDING IF_DATA_K TO GF_ALV_K.
APPEND GF_ALV_K TO GT_ALV_K.
ENDLOOP.
* プラント名の取得
PERFORM FRM_GET_T001W USING PR_WERKS
G_NAME1.
*--- 「購買価格マスタ登録・更新一覧表」出力
PERFORM FRM_WRITE_DATAK.
ENDIF.


*&---------------------------------------------------------------------*
*&      Form  FRM_GET_CDHDR
*&---------------------------------------------------------------------*
*       変更文書の取得
*----------------------------------------------------------------------*
FORM FRM_GET_CDHDR.

SELECT OBJECTID                                " 対象値
CHANGENR                                " 変更文書番号
INTO CORRESPONDING FIELDS OF TABLE IT_CDHDR
FROM CDHDR
WHERE OBJECTCLAS = 'COND_A'
AND UDATE IN PR_DATE.

ENDFORM.                    " FRM_GET_CDHDR
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_CDPOS
*&---------------------------------------------------------------------*
*       変更文書明細の取得
*----------------------------------------------------------------------*
FORM FRM_GET_CDPOS.

IF NOT IT_CDHDR IS INITIAL.
SELECT VALUE_NEW                           " 変更後の項目内容
VALUE_OLD                           " 変更前の項目内容
TABKEY                      " 変更テーブルレコードキー
OBJECTID                            " 対象値
CHNGIND                             " 変更タイプ
CHANGENR                            " 変更文書番号
FNAME
INTO CORRESPONDING FIELDS OF TABLE IT_CDPOS
FROM CDPOS
FOR ALL ENTRIES IN IT_CDHDR
WHERE OBJECTCLAS = 'COND_A'
AND ( TABNAME = 'KONM'
OR TABNAME = 'KONPAE' )                   " テーブル名
AND ( FNAME = 'KBETR'
OR FNAME = 'KSTBM'
OR FNAME = 'KEY' )
AND ( CHNGIND = 'U'
OR CHNGIND = 'E'
OR CHNGIND = 'I' )                        " 変更タイプ
AND OBJECTID = IT_CDHDR-OBJECTID                " 対象値
AND CHANGENR = IT_CDHDR-CHANGENR.               " 変更文書番号
ENDIF.

ENDFORM.                    " FRM_GET_CDPOS
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_DATAH
*&---------------------------------------------------------------------*
*      販売価格一覧対象データの取得
*----------------------------------------------------------------------*
FORM FRM_GET_DATAH.

DATA: L_LINES TYPE I.

LOOP AT IT_CDPOS
INTO IF_CDPOS
WHERE CHNGIND = 'U'
AND FNAME = 'KBETR'.

CLEAR: IF_DATA_H.
SELECT SINGLE
A~KUNNR                                    "得意先コード
A~ZZZKDMAT                                 "得意先品目コード
A~DATBI                                    "条件マスタ有効終了日
A~DATAB                                    "条件マスタ有効開始日
A~VKORGAU                                  "販売組織	
A~VTWEG                                    "流通チャネル
INTO CORRESPONDING FIELDS OF IF_DATA_H
FROM A901 AS A INNER JOIN TVKWZ AS B
ON A~VKORGAU = B~VKORG
AND A~VTWEG   = B~VTWEG
WHERE A~KNUMH = IF_CDPOS-OBJECTID
AND B~WERKS = PR_WERKS.
IF SY-SUBRC = 0.
IF_DATA_H-VALUE_NEW = IF_CDPOS-VALUE_NEW+241(13).    "変更後内容
IF_DATA_H-VALUE_OLD = IF_CDPOS-VALUE_OLD+241(13).    "変更前内容
IF_DATA_H-TABKEY   = IF_CDPOS-TABKEY.    "変更テーブルレコードキー
IF_DATA_H-OBJECTID = IF_CDPOS-OBJECTID.  "対象値
IF_DATA_H-CHANGENR = IF_CDPOS-CHANGENR.  "変更文書番号
WRITE IF_DATA_H-VALUE_NEW
TO IF_DATA_H-VALUE_NEW RIGHT-JUSTIFIED.
WRITE IF_DATA_H-VALUE_OLD
TO IF_DATA_H-VALUE_OLD RIGHT-JUSTIFIED.
APPEND IF_DATA_H TO IT_DATA_H.
ENDIF.
ENDLOOP.

DESCRIBE TABLE IT_DATA_H LINES L_LINES.
IF L_LINES = 0.
MESSAGE S614(Z1) WITH TEXT-M01.
STOP.
ENDIF.

ENDFORM.                    " FRM_GET_DATAH
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_KNA1
*&---------------------------------------------------------------------*
*       得意先名の取得
*----------------------------------------------------------------------*
*      -->P_KUNNR 得意先コード
*      <--P_NAME  得意先名
*----------------------------------------------------------------------*
FORM FRM_GET_KNA1 USING    P_KUNNR
P_NAME.
SELECT SINGLE
NAME1
INTO P_NAME
FROM KNA1
WHERE KUNNR = P_KUNNR.

ENDFORM.                    " FRM_GET_KNA1
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_KNMT
*&---------------------------------------------------------------------*
*       品目コードの取得
*----------------------------------------------------------------------*
FORM FRM_GET_KNMT USING    P_IF_DATA_H TYPE TYP_DATA_H.

SELECT SINGLE
MATNR
INTO P_IF_DATA_H-MATNR
FROM KNMT
WHERE VKORG = P_IF_DATA_H-VKORGAU      "販売組織
AND VTWEG = P_IF_DATA_H-VTWEG        "流通チャネル
AND KUNNR = P_IF_DATA_H-KUNNR        "得意先コード
AND KDMAT = P_IF_DATA_H-ZZZKDMAT.    "得意先品目コード

ENDFORM.                    " FRM_GET_KNMT
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_MAKT
*&---------------------------------------------------------------------*
*       品目名の取得
*----------------------------------------------------------------------*
*      -->P_MATNR  品目コード
*      <--P_MAKTX 品目名
*----------------------------------------------------------------------*
FORM FRM_GET_MAKT USING    P_MATNR
P_MAKTX.
SELECT SINGLE
MAKTX
INTO P_MAKTX
FROM MAKT
WHERE MATNR = P_MATNR.

ENDFORM.                    " FRM_GET_MAKT
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_KONM
*&---------------------------------------------------------------------*
*       スケール数量の取得
*----------------------------------------------------------------------*
*      -->P_TABKEY     変更テーブルレコードキー
*      -->P_OBJECTID   対象値
*      -->P_CHANGENR   変更文書番号
*      <--P_KSTBM     スケール数量
*----------------------------------------------------------------------*
FORM FRM_GET_KONM USING     P_TABKEY
P_OBJECTID
P_CHANGENR
P_KSTBM.
.

DATA: LF_CDPOS TYPE CDPOS,
L_TMP TYPE I.

READ TABLE IT_CDPOS INTO LF_CDPOS
WITH KEY TABKEY   = P_TABKEY
OBJECTID = P_OBJECTID
CHNGIND  = 'U'
FNAME    = 'KSTBM'
CHANGENR = P_CHANGENR.
IF SY-SUBRC = 0.
L_TMP = LF_CDPOS-VALUE_NEW+241(13).
P_KSTBM = L_TMP / 1000.
ENDIF.

ENDFORM.                    " FRM_GET_KONM
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_KONP
*&---------------------------------------------------------------------*
*       通貨・単位の取得
*----------------------------------------------------------------------*
*      -->P_OBJECTID  条件レコード番号
*      <--P_KONWA     通貨
*      <--P_KMEIN     数量単位
*      <--P_KPEIN     価格条件単位
*----------------------------------------------------------------------*
FORM FRM_GET_KONP USING    P_OBJECTID
P_KONWA
P_KMEIN
P_KPEIN.

SELECT SINGLE
KONWA           "通貨
KMEIN           "数量単位
KPEIN           "価格条件単位
INTO (P_KONWA, P_KMEIN, P_KPEIN)
FROM KONP
WHERE KNUMH = P_OBJECTID.

ENDFORM.                    " FRM_GET_KONP
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_KONP1
*&---------------------------------------------------------------------*
*       スケールを使用しない場合の条件金額/百分率の取得
*----------------------------------------------------------------------*
*      -->P_OBJECTID  条件レコード番号
*      <--P_KBETR     条件金額
*----------------------------------------------------------------------*
FORM FRM_GET_KONP1 USING    P_OBJECTID
P_KBETR.

DATA: L_KBETR TYPE KONP-KBETR,
L_KONWA TYPE KONP-KONWA,
L_IDOC_AMOUNT(255) TYPE C.

SELECT SINGLE
KBETR           "条件金額
KONWA           "通貨
INTO (L_KBETR, L_KONWA)
FROM KONP
WHERE KNUMH = P_OBJECTID.

*--- 汎用モジュール金額変換モジュール
CALL FUNCTION 'CURRENCY_AMOUNT_SAP_TO_IDOC'
EXPORTING
CURRENCY    = L_KONWA
SAP_AMOUNT  = L_KBETR
IMPORTING
IDOC_AMOUNT = L_IDOC_AMOUNT
EXCEPTIONS
OTHERS      = 1.
IF SY-SUBRC <> 1.
P_KBETR = L_IDOC_AMOUNT.
ENDIF.

ENDFORM.                    " FRM_GET_KONP1
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_VALUE_OLD
*&---------------------------------------------------------------------*
*       受注単価（旧）の取得
*----------------------------------------------------------------------*
*      -->P_TABKEY     変更テーブルレコードキー
*      -->P_OBJECTID   対象値
*      -->P_CHANGENR   変更文書番号
*      <--P_VALUE_OLD  受注単価（旧）
*----------------------------------------------------------------------*
FORM FRM_GET_VALUE_OLD USING    P_TABKEY
P_OBJECTID
P_CHANGENR
P_VALUE_OLD.

DATA: LF_CDPOS TYPE CDPOS.

READ TABLE IT_CDPOS INTO LF_CDPOS
WITH KEY TABKEY   = P_TABKEY
OBJECTID = P_OBJECTID
CHNGIND  = 'E'
FNAME    = 'KBETR'
CHANGENR = P_CHANGENR.
IF SY-SUBRC = 0.
P_VALUE_OLD = LF_CDPOS-VALUE_OLD+241(13).
ENDIF.

ENDFORM.                    " FRM_GET_VALUE_OLD
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_DATAK
*&---------------------------------------------------------------------*
*       購買価格一覧対象データの取得
*----------------------------------------------------------------------*
FORM FRM_GET_DATAK.

DATA: L_LINES TYPE I.

LOOP AT IT_CDPOS
INTO IF_CDPOS.
IF ( IF_CDPOS-CHNGIND = 'U'
AND IF_CDPOS-FNAME = 'KBETR' )
OR ( IF_CDPOS-CHNGIND = 'I'
AND IF_CDPOS-FNAME = 'KEY' ).

CLEAR: IF_DATA_H.
SELECT SINGLE
LIFNR                                    "仕入先勘定コード
MATNR                                    "品目コード
DATBI                                    "条件マスタ有効終了日
DATAB                                    "条件マスタ有効開始日
INTO CORRESPONDING FIELDS OF IF_DATA_K
FROM A017
WHERE KNUMH = IF_CDPOS-OBJECTID
AND WERKS = PR_WERKS.
IF SY-SUBRC = 0.
IF IF_CDPOS-CHNGIND = 'U'.
IF_DATA_K-VALUE_NEW = IF_CDPOS-VALUE_NEW+241(13).  "変更後内容
IF_DATA_K-VALUE_OLD = IF_CDPOS-VALUE_OLD+241(13).  "変更前内容
ELSE.
* スケールを使用しない場合の条件金額/百分率の取得
PERFORM FRM_GET_KONP1 USING IF_CDPOS-OBJECTID
IF_DATA_K-VALUE_NEW.
IF_DATA_K-VALUE_OLD = '0'.
ENDIF.
IF_DATA_K-TABKEY   = IF_CDPOS-TABKEY.  "変更テーブルレコードキー
IF_DATA_K-OBJECTID = IF_CDPOS-OBJECTID.  "対象値
IF_DATA_K-CHANGENR = IF_CDPOS-CHANGENR.  "変更文書番号
WRITE IF_DATA_K-VALUE_NEW
TO IF_DATA_K-VALUE_NEW RIGHT-JUSTIFIED.
WRITE IF_DATA_K-VALUE_OLD
TO IF_DATA_K-VALUE_OLD RIGHT-JUSTIFIED.
APPEND IF_DATA_K TO IT_DATA_K.
ENDIF.
ENDIF.
ENDLOOP.

DESCRIBE TABLE IT_DATA_K LINES L_LINES.
IF L_LINES = 0.
MESSAGE S614(Z1) WITH TEXT-M01.
STOP.
ENDIF.

ENDFORM.                    " FRM_GET_DATAK
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_LFA1
*&---------------------------------------------------------------------*
*       仕入先名の取得
*----------------------------------------------------------------------*
*      -->P_LIFNR  仕入先コード
*      <--P_NAME1  仕入先名
*----------------------------------------------------------------------*
FORM FRM_GET_LFA1 USING    P_LIFNR
P_NAME1.

SELECT SINGLE
NAME1
INTO P_NAME1
FROM LFA1
WHERE LIFNR = P_LIFNR.

ENDFORM.                    " FRM_GET_LFA1
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_EINA
*&---------------------------------------------------------------------*
*       仕入先品目コードの取得
*----------------------------------------------------------------------*
*      -->P_LIFNR  仕入先コード
*      -->P_MATNR  品目コード
*      <--P_IDNLF  仕入先品目コード
*----------------------------------------------------------------------*
FORM FRM_GET_EINA USING    P_LIFNR
P_MATNR
P_IDNLF.

SELECT SINGLE
IDNLF
INTO P_IDNLF
FROM EINA
WHERE LIFNR = P_LIFNR
AND MATNR = P_MATNR.

ENDFORM.                    " FRM_GET_EINA
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_TVKBT
*&---------------------------------------------------------------------*
*       営業所名の取得
*----------------------------------------------------------------------*
*      -->P_VKBUR  営業所
*      -->P_BEZEI  営業所名
*----------------------------------------------------------------------*
FORM FRM_GET_TVKBT USING    P_VKBUR
P_BEZEI.

SELECT SINGLE
BEZEI
INTO P_BEZEI
FROM TVKBT
WHERE SPRAS = 'JA'                  "言語キー
AND VKBUR = P_VKBUR.              "営業所

ENDFORM.                    " FRM_GET_TVKBT
*&---------------------------------------------------------------------*
*&      Form  FRM_GET_T001W
*&---------------------------------------------------------------------*
*       プラント名の取得
*----------------------------------------------------------------------*
*      -->P_WERKS  プラント
*      <--P_NAME1  プラント名
*----------------------------------------------------------------------*
FORM FRM_GET_T001W USING    P_WERKS
P_NAME1.

SELECT SINGLE
NAME1
INTO P_NAME1
FROM T001W
WHERE WERKS = P_WERKS .              "プラント

ENDFORM.                    " FRM_GET_T001W
*&---------------------------------------------------------------------*
*&      Form  FRM_WRITE_DATAH
*&---------------------------------------------------------------------*
FORM FRM_WRITE_DATAH.

*--- イベント取得処理
PERFORM FRM_EVENT_GET.
GF_VARIANT-VARIANT = '/ZS013200_SD'.
*--- リストの出力
CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM             = G_REPID         "レポートID
I_CALLBACK_TOP_OF_PAGE         = G_TOP_OF_PAGE   "TOP_OF_PAGE
I_STRUCTURE_NAME               = 'ZSLIST_V22'    "構造
IS_LAYOUT                      = GF_LAYOUT       "レイアウト設定
I_SAVE                         = 'A'             "レイアウト保存
IS_VARIANT                     = GF_VARIANT
IT_EVENTS                      = GT_EVENT[]      "イベント設定
IS_PRINT                       = GF_PRINT        "印刷設定
TABLES
T_OUTTAB                       = GT_ALV_H   " 帳票出力用内部テーブル
EXCEPTIONS
PROGRAM_ERROR                  = 1
OTHERS                         = 2
.
IF SY-SUBRC <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.

ENDFORM.                    " FRM_WRITE_DATAH
*&---------------------------------------------------------------------*
*&      Form  FRM_TOP_OF_PAGE
*&---------------------------------------------------------------------*
FORM FRM_TOP_OF_PAGE.

IF PR_HB = 'X'.
WRITE:/60 TEXT-006.
WRITE:/ TEXT-001, SY-DATUM.
WRITE:/ TEXT-002, G_VKBUR, 30 TEXT-003, G_BEZEI.
ELSE.
WRITE:/60 TEXT-007.
WRITE:/ TEXT-001, SY-DATUM.
WRITE:/ TEXT-004, PR_WERKS, 30 TEXT-005, G_NAME1.
ENDIF.

ENDFORM.                    " FRM_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*&      Form  FRM_EVENT_GET
*&---------------------------------------------------------------------*
*      イベント取得処理
*----------------------------------------------------------------------*
FORM FRM_EVENT_GET.

*--- イベント取得
CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
EXPORTING
I_LIST_TYPE     = 0
IMPORTING
ET_EVENTS       = GT_EVENT
EXCEPTIONS
LIST_TYPE_WRONG = 1
OTHERS          = 2.
IF SY-SUBRC <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.

*--- 使用イベントに実行するサブルーチン名を設定
LOOP AT GT_EVENT INTO GF_EVENT.
CASE GF_EVENT-NAME.
WHEN 'TOP_OF_PAGE'.
GF_EVENT-FORM = 'FRM_TOP_OF_PAGE'.
MODIFY GT_EVENT FROM GF_EVENT.
ENDCASE.
ENDLOOP.

ENDFORM.                    " FRM_EVENT_GET
*&---------------------------------------------------------------------*
*&      Form  FRM_WRITE_DATAK
*&---------------------------------------------------------------------*
*       「購買価格マスタ登録・更新一覧表」出力
*----------------------------------------------------------------------*
FORM FRM_WRITE_DATAK.

*--- イベント取得処理
PERFORM FRM_EVENT_GET.
GF_VARIANT-VARIANT = '/ZS013200_MM'.
*--- リストの出力
CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM             = G_REPID         "レポートID
I_CALLBACK_TOP_OF_PAGE         = G_TOP_OF_PAGE   "TOP_OF_PAGE
I_STRUCTURE_NAME               = 'ZSLIST_V23'    "構造
IS_LAYOUT                      = GF_LAYOUT       "レイアウト設定
I_SAVE                         = 'A'             "レイアウト保存
IS_VARIANT                     = GF_VARIANT
IT_EVENTS                      = GT_EVENT[]      "イベント設定
IS_PRINT                       = GF_PRINT        "印刷設定
TABLES
T_OUTTAB                       = GT_ALV_K   " 帳票出力用内部テーブル
EXCEPTIONS
PROGRAM_ERROR                  = 1
OTHERS                         = 2
.
IF SY-SUBRC <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.

ENDFORM.                    " FRM_WRITE_DATAK
*&---------------------------------------------------------------------*
*&      Form  FRM_GET__KNVV
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_IF_DATA_H_KUNNR  text
*      -->P_IF_DATA_H_VKBUR  text
*----------------------------------------------------------------------*
FORM FRM_GET__KNVV USING    P_KUNNR
P_VKBUR.
Select VKBUR
From KNVV
Up To 1 Rows
Into P_VKBUR
Where KUNNR EQ P_KUNNR .
Exit .
EndSelect .

ENDFORM.                    " FRM_GET__KNVV
