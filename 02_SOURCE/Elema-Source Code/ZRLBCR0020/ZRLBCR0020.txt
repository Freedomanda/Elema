*& ISID Co.,Ltd. 2008 All rights.reserved.
*&---------------------------------------------------------------------*
*& プログラム：ZRLBCR0020
*& 作成者    ：ISID T.Ofuji
*& 作成日付  ：2008/04/30
*& 機能ID    ：
*&---------------------------------------------------------------------*
*& 処理概要&
*&   先行処理（ZRLBCR0010）で記録したバッチインプット処理を実行する
*&   ・バッチインプットデータのアップロード
*&   ・バッチインプット実行
*&   ・バッチインプットデータ編集用のサンプルデータダウンロード
*&   ・バッチインプットデータの一覧表示（ZRLBCR0030のメイン画面へ推移）
*&   ・アップロードデータしたデータの削除
*&---------------------------------------------------------------------*
*& 更新履歴&
*& No.   更新日付    更新者    更新内容
*& 新規  2008/04/30  T.Ofuji
*& #001  2012/10/01  ISID144   レイアウト日本語化、
*&                             バックグラウンド実行機能追加、他
*&---------------------------------------------------------------------*
REPORT ZRLBCR0020 NO STANDARD PAGE HEADING
LINE-SIZE 292
MESSAGE-ID ZRLBC00.

*----------------------------------------------------------------------*
*                            TABLES                                    *
*----------------------------------------------------------------------*
TABLES : ZTRLBCT001,
ZTRLBCT002,
ZTRLBCT003,
ZTRLBCT004,
ZTRLBCT005,
ZTRLBCT006,
ZTRLBCT008.

*----------------------------------------------------------------------*
*                             DATA                                     *
*----------------------------------------------------------------------*
DATA:
GDT_BIDSUM    TYPE STANDARD TABLE OF ZTRLBCT006,
GDT_BIREC     TYPE STANDARD TABLE OF ZTRLBCT007,
GDT_PCFILE    TYPE STANDARD TABLE OF ZTRLBCT009,
GDT_HEADER    TYPE STANDARD TABLE OF ZTRLBCT009,
* GDT_HEADER    TYPE                   ZTRLBCT009,

GDT_BIDAT1    TYPE STANDARD TABLE OF ZTRLBCT001,
GDT_BIDAT2    TYPE STANDARD TABLE OF ZTRLBCT002,
GDT_BIDCTL    TYPE STANDARD TABLE OF ZTRLBCT003,
GDT_FILETABLE TYPE FILETABLE,
*
GDS_BIDSUM    TYPE ZTRLBCT006,
GDS_BIREC     TYPE ZTRLBCT007,
GDS_PCFILE    TYPE ZTRLBCT009,
GDS_HEADER    TYPE ZTRLBCT009,
GDS_BIDAT1    TYPE ZTRLBCT001,
GDS_BIDAT2    TYPE ZTRLBCT002,
GDS_BIDCTL    TYPE ZTRLBCT003.

DATA:
WA_CTR_WAIT   LIKE ZTRLBCT006-CTR_WAIT,
WA_CTLNO      LIKE ZTRLBCT006-CTLNO,
WA_HIDE_CTLNO LIKE ZTRLBCT006-CTLNO,
WA_CBOX(1)    TYPE C,
WA_RC         TYPE I,
WA_USERACTION TYPE I,
WA_ROWS       TYPE I,
WA_FILENAME   TYPE STRING,
WA_UCOMM      TYPE SY-UCOMM.                     "#001 ADD 20121001


DATA: SW_INTENSIFIED TYPE I VALUE 1.
DATA:
BEGIN OF D9100,
RECID     LIKE ZTRLBCT006-RECID,
CTLNO     LIKE ZTRLBCT006-CTLNO,
RECTX     LIKE ZTRLBCT006-RECTX,
END OF D9100.

*----------------------------------------------------------------------*
*                             CONSTANTS                                *
*----------------------------------------------------------------------*
CONSTANTS:
GCF_ON(01)            TYPE C VALUE 'X',
GCF_TYPE(03)          TYPE C VALUE 'ASC',
GCF_REFRESH(07)       TYPE C VALUE 'REFRESH',
GCF_BACK(04)          TYPE C VALUE 'BACK',
GCF_END(03)           TYPE C VALUE 'END',
GCF_UPLOAD(06)        TYPE C VALUE 'UPLOAD',
GCF_DOWNLOAD(08)      TYPE C VALUE 'DOWNLOAD',
GCF_DOWNLOADAL(10)    TYPE C VALUE 'DOWNLOADAL',
GCF_DELETE(06)        TYPE C VALUE 'DELETE',
GCF_DETAIL(06)        TYPE C VALUE 'DETAIL',
GCF_EXECUTE(07)       TYPE C VALUE 'EXECUTE',
GCF_CANCEL(06)        TYPE C VALUE 'CANCEL',
GCF_BATCHJOB(08)      TYPE C VALUE 'BATCHJOB'.

*&---------------------------------------------------------------------*
*                         SELECT-OPTIONS                               *
*&---------------------------------------------------------------------*
SELECT-OPTIONS:
S_RECID    FOR ZTRLBCT006-RECID.

*&---------------------------------------------------------------------*
*                         FIELD-SYMBOLS                                *
*&---------------------------------------------------------------------*
FIELD-SYMBOLS: <FS_FNAM>, <FS_FVAL>.
INCLUDE <ICON>.
RANGES R_RECID FOR ZTRLBCT006-RECID.

*&---------------------------------------------------------------------*
*                         START-OF-SELECTION                           *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

DATA:
L_STATUS_TEXT(6)   TYPE C.

*
LOOP AT S_RECID.
MOVE-CORRESPONDING S_RECID TO R_RECID.
APPEND R_RECID.
ENDLOOP.

* バッチインプットデータ処理合計取得
SELECT * INTO  TABLE GDT_BIDSUM
FROM  ZTRLBCT006
WHERE RECID IN S_RECID.

* 一覧表示
SORT GDT_BIDSUM BY CTLNO.
FORMAT COLOR COL_NORMAL.
LOOP AT GDT_BIDSUM INTO GDS_BIDSUM.

IF SW_INTENSIFIED > 0.
FORMAT INTENSIFIED ON.
ELSE.
FORMAT INTENSIFIED OFF.
ENDIF.
SW_INTENSIFIED = SW_INTENSIFIED * ( -1 ).

WRITE : /001 SY-VLINE NO-GAP,
002 WA_CBOX AS CHECKBOX,
004 SY-VLINE NO-GAP.
IF GDS_BIDSUM-CTR_ERROR > 0.
L_STATUS_TEXT = TEXT-001.
WRITE : 005 ICON_RED_LIGHT AS ICON.
ELSE.
IF GDS_BIDSUM-CTR_WAIT = 0.
L_STATUS_TEXT = TEXT-002.
WRITE : 005 ICON_GREEN_LIGHT AS ICON.
ELSE.
L_STATUS_TEXT = TEXT-003.
WRITE : 005 ICON_YELLOW_LIGHT AS ICON.
ENDIF.
ENDIF.
WRITE :  015 SY-VLINE NO-GAP,
020 GDS_BIDSUM-CTLNO NO-GAP,
030 SY-VLINE NO-GAP,
031 GDS_BIDSUM-RECID NO-GAP,
043 SY-VLINE NO-GAP,
044 GDS_BIDSUM-RECTX,
084 SY-VLINE NO-GAP,
085 GDS_BIDSUM-CTR_WAIT NO-GAP,
095 SY-VLINE NO-GAP,
096 GDS_BIDSUM-CTR_SUCCESS NO-GAP,
106 SY-VLINE NO-GAP,
107 GDS_BIDSUM-CTR_ERROR NO-GAP,
117 SY-VLINE NO-GAP,
118 GDS_BIDSUM-UNAME NO-GAP,
130 SY-VLINE NO-GAP,
131 GDS_BIDSUM-DATUM NO-GAP,
141 SY-VLINE NO-GAP,
142 GDS_BIDSUM-TIMES NO-GAP,
150 SY-VLINE NO-GAP,
151 GDS_BIDSUM-MANDT NO-GAP,          "#001 ADD 20121001
163 SY-VLINE NO-GAP,                  "#001 ADD 20121001
164 GDS_BIDSUM-FILENAME NO-GAP,       "#001 ADD 20121001
292 SY-VLINE NO-GAP.                  "#001 ADD 20121001
WA_HIDE_CTLNO = GDS_BIDSUM-CTLNO.
HIDE: WA_HIDE_CTLNO.
ENDLOOP.

SET PF-STATUS '1000'.
*
*&---------------------------------------------------------------------*
*                         END-OF-SELECTION                             *
*&---------------------------------------------------------------------*
END-OF-SELECTION.

ULINE.

*&---------------------------------------------------------------------*
*                            TOP-OF-PAGE                               *
*&---------------------------------------------------------------------*
TOP-OF-PAGE.

FORMAT COLOR COL_HEADING.
ULINE.
WRITE : /001 SY-VLINE NO-GAP,
002 '' NO-GAP,
004 SY-VLINE NO-GAP,
*           005 'STA' NO-GAP,                      "#001 DEL 20121001
005 TEXT-012 NO-GAP,                    "#001 ADD 2012101
015 SY-VLINE NO-GAP,
016 TEXT-001 NO-GAP,
030 SY-VLINE NO-GAP,
031 TEXT-002 NO-GAP,
043 SY-VLINE NO-GAP,
044 TEXT-003 NO-GAP,
084 SY-VLINE NO-GAP,
085 TEXT-004 NO-GAP,
095 SY-VLINE NO-GAP,
096 TEXT-005 NO-GAP,
106 SY-VLINE NO-GAP,
107 TEXT-006 NO-GAP,
117 SY-VLINE NO-GAP,
118 TEXT-007 NO-GAP,
130 SY-VLINE NO-GAP,
131 TEXT-008 NO-GAP,
141 SY-VLINE NO-GAP,
142 TEXT-009 NO-GAP,
150 SY-VLINE NO-GAP,
151 TEXT-010 NO-GAP,                  "#001 ADD 20121001
163 SY-VLINE NO-GAP,                  "#001 ADD 20121001
164 TEXT-011 NO-GAP,                  "#001 ADD 20121001
292 SY-VLINE NO-GAP.                  "#001 ADD 20121001
ULINE.

*&---------------------------------------------------------------------*
*                        AT LINE-SELECTION                             *
*&---------------------------------------------------------------------*
AT LINE-SELECTION.

*&---------------------------------------------------------------------*
*                         AT USER-COMMAND                              *
*&---------------------------------------------------------------------*
AT USER-COMMAND.

WA_UCOMM = SY-UCOMM.                             "#001 ADD 20121001

CASE SY-UCOMM.
WHEN GCF_REFRESH.
SUBMIT ZRLBCR0020 WITH S_RECID IN R_RECID.
WHEN GCF_BACK.
LEAVE PROGRAM.
WHEN GCF_END.
LEAVE PROGRAM.
WHEN GCF_UPLOAD.                               "アップロード
PERFORM SUB_UPLOAD.
WHEN GCF_DOWNLOAD.                             "ダウンロード(ERR)
PERFORM SUB_DOWNLOAD.
WHEN GCF_DOWNLOADAL.                           "ダウンロード(ALL)
PERFORM SUB_DOWNLOAD_ALL.
WHEN GCF_DELETE.                               "削除
PERFORM SUB_DELETE_BIDATA.
WHEN GCF_DETAIL.                               "詳細
SUBMIT ZRLBCR0030 WITH P_CTLNO EQ WA_HIDE_CTLNO
AND RETURN.
WHEN GCF_EXECUTE                               "処理実行
OR GCF_BATCHJOB.
PERFORM SUB_EXECUTE.

ENDCASE.
SET USER-COMMAND GCF_REFRESH.

*&---------------------------------------------------------------------*
*&      Form  SUB_UPLOAD
*&---------------------------------------------------------------------*
*       バッチインプットデータのアップロード
*----------------------------------------------------------------------*
FORM SUB_UPLOAD.

DATA: L_CANCEL(1)   TYPE C.
DATA: L_INDEX  TYPE I.
DATA: L_OVERVIEW    LIKE ZTRLBCT001-FVAL.

CONSTANTS:
LCF_CTLNOKBN      TYPE ZERLBC016 VALUE 'BI-CTL',
LCF_IGT(01)       TYPE C VALUE '#',
LCF_KNM(01)       TYPE C VALUE ',',
LCF_OKCODE(10)    TYPE C VALUE 'BDC_OKCODE',
LCF_SUBSCR(10)    TYPE C VALUE 'BDC_SUBSCR',
LCF_CURSOR(10)    TYPE C VALUE 'BDC_CURSOR'.

REFRESH: GDT_PCFILE.

* ローカルファイル選択
CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
************************************************ #001 DEL 20121001 STA*
*    EXPORTING
*      DEFAULT_EXTENSION = 'DAT'
*      FILE_FILTER       = 'TAB Seperated Files (*.DAT)|*.DAT|'
************************************************ #001 DEL 20121001 END *
CHANGING
FILE_TABLE        = GDT_FILETABLE
RC                = WA_RC
USER_ACTION       = WA_USERACTION.

* ファイル選択でキャンセルの場合
IF WA_USERACTION <> 0.                           "#001 ADD 20121001
RETURN.                                        "#001 ADD 20121001
ENDIF.                                           "#001 ADD 20121001

* アップロード処理
DESCRIBE TABLE GDT_FILETABLE LINES WA_ROWS.
IF SY-SUBRC = 0.
READ TABLE GDT_FILETABLE INTO WA_FILENAME INDEX WA_ROWS.
CALL FUNCTION 'GUI_UPLOAD'
EXPORTING
FILENAME            = WA_FILENAME
*        FILETYPE            = GCF_TYPE             "ASC
HAS_FIELD_SEPARATOR = GCF_ON
TABLES
DATA_TAB            = GDT_PCFILE.
ENDIF.

IF SY-SUBRC <> 0.
IF L_CANCEL = ' '.
*     アップロード中にエラーが発生しました
MESSAGE E401.
ELSE.
*     アップロードがキャンセルされました。
MESSAGE E402.
ENDIF.
ENDIF.

** アップロードファイルデータチェック
* アップロードファイル一行目読込
READ TABLE GDT_PCFILE INTO GDS_PCFILE INDEX 1.

IF SY-SUBRC = 0.
D9100-RECID = GDS_PCFILE-FIELD001.             "記録ID
D9100-RECTX = GDS_PCFILE-FIELD002.             "記録IDテキスト

*   記録IDの存在チェック
SELECT SINGLE * FROM ZTRLBCT004
WHERE RECID = D9100-RECID.
IF SY-SUBRC = 0.
ELSE.
*     アップロードデータの１行目に制御レコードが見つかりません。
MESSAGE E403.
ENDIF.
ELSE.
*   アップロードデータが空です。
MESSAGE E404.
ENDIF.

* アップロードファイル二行目読込
READ TABLE GDT_PCFILE INTO GDS_PCFILE INDEX 2.

IF SY-SUBRC = 0.
*   項目IDの行退避
MOVE GDS_PCFILE TO GDS_HEADER.                 "
ELSE.
*   アップロードデータの２行目に制御レコードが見つかりません。
MESSAGE E405.
ENDIF.

* アドオン用コントロール番号管理 更新／登録
SELECT SINGLE * FROM ZTRLBCT008
WHERE CTLNOKBN = LCF_CTLNOKBN.
IF SY-SUBRC = 0.
D9100-CTLNO = ZTRLBCT008-CTLNO + 1.            "コントロールＮＯ
UPDATE ZTRLBCT008 SET CTLNO = D9100-CTLNO
WHERE CTLNOKBN = LCF_CTLNOKBN.
ELSE.
CLEAR ZTRLBCT008.
ZTRLBCT008-CTLNOKBN = LCF_CTLNOKBN.
D9100-CTLNO = ZTRLBCT008-CTLNO + 1.            "コントロールＮＯ
ZTRLBCT008-CTLNO    = D9100-CTLNO.
INSERT ZTRLBCT008.
ENDIF.
COMMIT WORK.

* バッチインプットデータ（概要）（詳細）登録
WA_CTR_WAIT = 0.
CLEAR ZTRLBCT001.
CLEAR ZTRLBCT002.
ZTRLBCT001-CTLNO = D9100-CTLNO.                  "コントロールＮＯ
ZTRLBCT002-CTLNO = D9100-CTLNO.

* アップロードファイル四行目から編集開始
LOOP AT GDT_PCFILE INTO GDS_PCFILE FROM 4.
WA_CTR_WAIT = WA_CTR_WAIT + 1.

L_INDEX = 0.
ZTRLBCT001-DATASEQ = ZTRLBCT001-DATASEQ + 1.   "バッチインプットデータ番号
ZTRLBCT002-DATASEQ = ZTRLBCT001-DATASEQ.
ZTRLBCT002-SEQNO = 0.
CLEAR: L_OVERVIEW.
DO.
IF L_INDEX > 100.
EXIT.
ENDIF.
L_INDEX = L_INDEX + 1.
*     ASSIGN COMPONENT L_INDEX OF STRUCTURE GDT_HEADER TO <FS_FNAM>.
*     ASSIGN COMPONENT L_INDEX OF STRUCTURE GDT_PCFILE TO <FS_FVAL>.

*     アップロードファイル二行目の項目IDを一項目づつセット
ASSIGN COMPONENT L_INDEX OF STRUCTURE GDS_HEADER TO <FS_FNAM>.
*     アップロードファイル四行目からの項目値を一項目づつセット
ASSIGN COMPONENT L_INDEX OF STRUCTURE GDS_PCFILE TO <FS_FVAL>.

IF <FS_FNAM> <> ' '.
ZTRLBCT002-SEQNO   = ZTRLBCT002-SEQNO + 1."SEQ番号
ZTRLBCT002-FNAM    = <FS_FNAM>.           "項目ID
ZTRLBCT002-FVAL    = <FS_FVAL>.           "項目値

IF ZTRLBCT002-FVAL(1) = LCF_IGT.           "#
SHIFT ZTRLBCT002-FVAL.
ENDIF.
*       項目値をカンマ刻みに繋げて編集
CONCATENATE L_OVERVIEW LCF_KNM ZTRLBCT002-FVAL
INTO L_OVERVIEW.
*       バッチインプットデータ（詳細）登録
INSERT ZTRLBCT002.
ENDIF.
ENDDO.

ZTRLBCT001-STATUS  = ' '.                      "ステータス
ZTRLBCT001-FVAL    = L_OVERVIEW+1(130).        "項目値
*   バッチインプットデータ（概要）
INSERT ZTRLBCT001.
ENDLOOP.

* バッチインプットデータ処理合計 登録
CLEAR ZTRLBCT006.
ZTRLBCT006-CTLNO = D9100-CTLNO.                  "コントロールＮＯ
ZTRLBCT006-RECID = D9100-RECID.                  "記録ID
ZTRLBCT006-TCODE = ZTRLBCT004-TCODE.             "トランザクション
ZTRLBCT006-RECTX = D9100-RECTX.                  "記録テキスト
ZTRLBCT006-UNAME = SY-UNAME.                     "登録者
ZTRLBCT006-DATUM = SY-DATUM.                     "登録日付
ZTRLBCT006-TIMES = SY-UZEIT.                     "登録時刻
ZTRLBCT006-CTR_WAIT = WA_CTR_WAIT.               "バッチインプット待ち件数
ZTRLBCT006-FILENAME = WA_FILENAME.               "#001 ADD 20121001
INSERT ZTRLBCT006.
COMMIT WORK.

* バッチインプットデータ制御 登録
* バッチインプット記録ＩＤトランザクション記録結果 取得
SELECT * FROM ZTRLBCT007
INTO TABLE GDT_BIREC
WHERE RECID = D9100-RECID.               "記録ID
* 編集処理
LOOP AT GDT_BIREC INTO GDS_BIREC.
CLEAR ZTRLBCT003.
MOVE-CORRESPONDING GDS_BIREC TO ZTRLBCT003.
ZTRLBCT003-CTLNO = D9100-CTLNO.                "コントロールＮＯ
*   データ部編集／登録
IF GDS_BIREC-DYNBEGIN = ' ' AND
GDS_BIREC-FNAM <> LCF_OKCODE AND
GDS_BIREC-FNAM <> LCF_SUBSCR AND
GDS_BIREC-FNAM <> LCF_CURSOR.
*     バッチインプットデータ（詳細） 取得
SELECT SINGLE * FROM ZTRLBCT002
WHERE CTLNO = D9100-CTLNO     "コントロールＮＯ
AND DATASEQ = 1             "バッチインプットデータ番号
AND FNAM = GDS_BIREC-FNAM.  "項目名
IF SY-SUBRC = 0.
INSERT ZTRLBCT003.
ENDIF.
ELSE.
*     制御部登録
INSERT ZTRLBCT003.
ENDIF.
ENDLOOP.
COMMIT WORK.
*
SET USER-COMMAND GCF_REFRESH.

ENDFORM.                    " SUB_UPLOAD

*&---------------------------------------------------------------------*
*&      処理実行
*&---------------------------------------------------------------------*
FORM SUB_EXECUTE.
DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

*   バッチインプット実行画面表示
IF WA_CBOX = GCF_ON.
*     バッチインプットデータ処理合計
*      WA_CTLNO = SY-LISEL+9(10).                  "#001 DEL 20121001
WA_CTLNO = SY-LISEL+19(10).                  "#001 ADD 20121001
SELECT SINGLE * FROM ZTRLBCT006 WHERE CTLNO = WA_CTLNO.

IF SY-SUBRC = 0.
*       実行済みチェック
IF ZTRLBCT006-CTR_WAIT = 0
AND ZTRLBCT006-CTR_ERROR = 0.
*         既に処理済です。実行は出来ません。
MESSAGE E407.
ELSE.
*         次画面表示
D9100-RECID = ZTRLBCT006-RECID.          "記録ID
D9100-RECTX = ZTRLBCT006-RECTX.          "記録IDテキスト
D9100-CTLNO = ZTRLBCT006-CTLNO.          "コントロールＮＯ
CALL SCREEN 9100 STARTING AT 5 5.
ENDIF.
ENDIF.
ENDIF.
CLEAR WA_CBOX.
ENDDO.

ENDFORM.                    " SUB_EXECUTE

*&---------------------------------------------------------------------*
*&      データ削除
*&---------------------------------------------------------------------*
FORM SUB_DELETE_BIDATA.
DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

*   選択データの削除処理
IF WA_CBOX = 'X'.
*      WA_CTLNO = SY-LISEL+9(10).                  "#001 DEL 20121001
WA_CTLNO = SY-LISEL+19(10).                  "#001 ADD 20121001
DELETE FROM ZTRLBCT006                       "バッチインプットデータ処理合計
WHERE CTLNO = WA_CTLNO.
DELETE FROM ZTRLBCT003                       "バッチインプットデータ制御
WHERE CTLNO = WA_CTLNO.
DELETE FROM ZTRLBCT001                       "バッチインプットデータ（概要）
WHERE CTLNO = WA_CTLNO.
DELETE FROM ZTRLBCT002                       "バッチインプットデータ（詳細）
WHERE CTLNO = WA_CTLNO.
DELETE FROM ZTRLBCT005                       "バッチインプットデータ処理ログ
WHERE CTLNO = WA_CTLNO.
*     コントロール№ & を削除致しました。
MESSAGE S408 WITH WA_CTLNO.
ENDIF.

CLEAR WA_CBOX.
ENDDO.

ENDFORM.                    " SUB_DELETE_BIDATA

*&---------------------------------------------------------------------*
*&      Module  STATUS_9100  OUTPUT
*&---------------------------------------------------------------------*
MODULE STATUS_9100 OUTPUT.
SET PF-STATUS '9100'.
ENDMODULE.                 " STATUS_9100  OUTPUT

*&---------------------------------------------------------------------*
*&      Dynproボタン処理
*&---------------------------------------------------------------------*
MODULE USER_COMMAND_9100 INPUT.

CASE SY-UCOMM.
WHEN GCF_CANCEL.                               "中止
LEAVE TO SCREEN 0.
WHEN GCF_EXECUTE.                              "処理実行
PERFORM SUB_BI_EXECUTE.
LEAVE TO SCREEN 0.
*    WHEN GCF_BATCHJOB.                             "バックグランド
*      PERFORM SUB_SUBMIT_BIJOB.
*      LEAVE TO SCREEN 0.
ENDCASE.

SET USER-COMMAND GCF_REFRESH.

ENDMODULE.                 " USER_COMMAND_9100  INPUT

*&---------------------------------------------------------------------*
*&      バックグランド実行
*&---------------------------------------------------------------------*
FORM SUB_SUBMIT_BIJOB.

DATA: L_JOBNAME   LIKE TBTCJOB-JOBNAME.
DATA: L_JOBCOUNT  LIKE TBTCJOB-JOBCOUNT.

CONSTANTS:
LCF_JOB(06)     TYPE C VALUE 'BIJOB-',
LCF_HFN(01)     TYPE C VALUE '-'.

* ジョブ名編集
CONCATENATE LCF_JOB D9100-RECID LCF_HFN WA_CTLNO INTO L_JOBNAME.
CONDENSE L_JOBNAME NO-GAPS.

* ジョブ開始
CALL FUNCTION 'JOB_OPEN'
EXPORTING
JOBNAME  = L_JOBNAME
IMPORTING
JOBCOUNT = L_JOBCOUNT.

*********************************************** #001 MOD 20121001 STA *
*  SUBMIT ZRLBCR0050 WITH P_CTLNO = WA_CTLNO
*                         WITH P_DATSEQ = '000000'
*         USER SY-UNAME VIA JOB L_JOBNAME NUMBER L_JOBCOUNT
*         AND RETURN.
* バックグランドで処理実行
SUBMIT ZRLBCR0021 WITH P_CTLNO = WA_CTLNO        "コントロールＮＯ
WITH P_RECID = D9100-RECID     "記録ID
USER SY-UNAME VIA JOB L_JOBNAME NUMBER L_JOBCOUNT
AND RETURN.
*********************************************** #001 MOD 20121001 END *

* ジョブ終了
CALL FUNCTION 'JOB_CLOSE'
EXPORTING
JOBCOUNT  = L_JOBCOUNT
JOBNAME   = L_JOBNAME
STRTIMMED = GCF_ON.

* バッチインプットジョブ & を起動しました。
MESSAGE S410 WITH L_JOBNAME.

ENDFORM.                    " SUB_SUBMIT_BIJOB

*&---------------------------------------------------------------------*
*&      エラー分のダウンロード処理
*&---------------------------------------------------------------------*
FORM SUB_DOWNLOAD .

DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

*   選択データのダウンロード処理
IF WA_CBOX = GCF_ON.
*      WA_CTLNO = SY-LISEL+9(10).                  "#001 DEL 20121001
WA_CTLNO = SY-LISEL+19(10).                  "#001 ADD 20121001
CALL FUNCTION 'ZRL_BC_BIDATA_DOWNLOAD'
EXPORTING
CONTROLNO = WA_CTLNO.                    "コントロールＮＯ
*         ERROR_ONLY       = 'X'.
ENDIF.
CLEAR WA_CBOX.
ENDDO.

ENDFORM.                    " SUB_DOWNLOAD
*&---------------------------------------------------------------------*
*&      全件分のダウンロード処理
*&---------------------------------------------------------------------*
FORM SUB_DOWNLOAD_ALL .

DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

*   選択データのダウンロード処理
IF WA_CBOX = GCF_ON.
*      WA_CTLNO = SY-LISEL+9(10).                  "#001 DEL 20121001
WA_CTLNO = SY-LISEL+19(10).                  "#001 ADD 20121001
CALL FUNCTION 'ZRL_BC_BIDATA_DOWNLOAD'
EXPORTING
CONTROLNO  = WA_CTLNO                    "コントロールＮＯ
ERROR_ONLY = ' '.
ENDIF.
CLEAR WA_CBOX.
ENDDO.

ENDFORM.                    " SUB_DOWNLOAD_ALL
*&---------------------------------------------------------------------*
*&      処理実行
*&---------------------------------------------------------------------*
FORM SUB_BI_EXECUTE .

* オンライン実行
IF WA_UCOMM = GCF_EXECUTE.                       "#001 ADD 20121001
CALL FUNCTION 'ZRL_BC_BI_EXECUTE'
*    IN BACKGROUND TASK
EXPORTING
CTLNO         = WA_CTLNO.                  "コントロールＮＯ

COMMIT WORK.
*  MESSAGE S411.                                   "#001 DEL 20121001
* バッチインプットを開始しました。
MESSAGE S412.                                  "#001 ADD 20121001
ELSE.                                            "#001 ADD 20121001
*   バックグランド実行
PERFORM SUB_SUBMIT_BIJOB.                      "#001 ADD 20121001
ENDIF.

ENDFORM.                    " SUB_BI_EXECUTE
*Text symbol text・
*001:Control No.
*002:Rec ID
*003:Rec Det
*004:Untre.
*005:Finish
*006:Error
*007:Crea.
*008:Cre. YMD
*009:Cre. T
*010:Client
*011:Upload File
*012:Status
*Selection text・
*S_RECID:D       .
