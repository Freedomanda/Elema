*&---------------------------------------------------------------------*
*& 石取りゲーム
*&---------------------------------------------------------------------*
report  ztest_isid06_000.
tables sscrfields.
selection-screen comment /1(80): ttl1, ttl2, ttl3.
selection-screen uline.
selection-screen comment /1(80): dtl1, dtl2, dtl3, dtl4, dtl5.
selection-screen uline.
selection-screen begin of tabbed block mytab for 3 lines.
selection-screen end of block mytab.
* 画面100
selection-screen begin of screen 0200 as subscreen.
parameters p_p type i.
selection-screen end of screen 0200.
* 画面200
selection-screen begin of screen 0100 as subscreen.
selection-screen begin of line.
selection-screen comment (10) t0.
selection-screen position pos_low.
parameters p_tbn0 radiobutton group grp1 default 'X'.
selection-screen comment (10) t1 for field p_tbn0.
parameters p_tbn1 radiobutton group grp1.
selection-screen comment (10) t2 for field p_tbn1.
selection-screen end of line.
selection-screen begin of line.
selection-screen comment (10) t00.
selection-screen position pos_low.
parameters p_21 radiobutton group grp2 default 'X'.
selection-screen comment (10) t21 for field p_21.
parameters p_31 radiobutton group grp2.
selection-screen comment (10) t31 for field p_31.
parameters p_41 radiobutton group grp2.
selection-screen comment (10) t41 for field p_41.
selection-screen end of line.
selection-screen end of screen 0100.

data: g_t     type i, "石の数
g_n     type i, "残り
g_max   type i, "取れる数
my_turn type i. "手番
data random type ref to cl_abap_random.
* 初期画面
initialization.
%_P_P_%_APP_%-TEXT = '何個とりますか？'.
mytab-prog = sy-repid.
mytab-dynnr = '0100'.
t0 = '手番'.
t1 = '先手'.
t2 = '後手'.
t00 = '石の数'.
t21 = '２１個'.
t31 = '３１個'.
t41 = '４１個'.
ttl1 = '石を交互に取っていき、最後の石を取ると勝ちです。'.
ttl2 = '最初は何個でも取れますが、全部は取れません。'.
ttl3 = '次からは、相手の取った数の２倍まで取れます。'.
clear: dtl1, dtl2, dtl3, dtl4, dtl5.
* 入力チェック(画面100のみ)
at selection-screen on p_p.
if g_n = 0.
submit (sy-repid) via selection-screen.
elseif p_p not between 1 and g_max.
message e001(00) with '1〜' g_max '個まで取れます'.
endif.
* 判定
at selection-screen.
if sy-dynnr = mytab-dynnr.
if mytab-dynnr = '0100'.
perform scr100.
else.
perform scr200.
endif.
clear sscrfields-ucomm.
endif.
* 画面100
form scr100.
mytab-dynnr = '0200'.
if p_21 = abap_on.
g_t = 21.
elseif p_31 = abap_on.
g_t = 31.
else.
g_t = 41.
endif.
g_n   = g_t.     "残り
g_max = g_t - 1. "取れる数
do g_t times.
concatenate dtl3 '●' into dtl3.
enddo.
if p_tbn0 = abap_on.
my_turn = 0.     "0:先手
else.
my_turn = 1.     "1:後手
perform scr200.
endif.
endform.                    "scr200
* 画面200
form scr200.
data: l_i type i, "ループカウンタ
l_n type i, "フィボナッチ数列
l_x type i. "取る数
if my_turn = 0.
l_x = p_p.
perform xxx using l_x.
if g_n = 0.
message 'あなたの勝ちです' type 'I'.
exit.
endif.
endif.
l_x = g_n.
do 9 times.
l_i = 10 - sy-index.
l_n = ( ( ( 1 + sqrt( 5 ) ) / 2 ) ** l_i ) / sqrt( 5 ).
if l_x = l_n. exit. endif.
check l_x > l_n.
l_x = l_x - l_n.
enddo.
if l_x > g_max.
if random is not bound.
l_x = sy-uzeit.
random = cl_abap_random=>create( l_x ).
endif.
if g_max > 3.
g_max = 3.
endif.
l_x = random->intinrange( low = 1 high = g_max ).
endif.
perform xxx using l_x.
if g_n = 0.
message 'あなたの負けです' type 'I'.
exit.
endif.
endform.                    "scr100
* 画面更新
form xxx using l_x type i.
data: len type i,
l_z type c length 3.
do l_x times.
if my_turn = 0.
concatenate dtl1 '#' into dtl1.
concatenate dtl2 '#' into dtl2.
concatenate dtl4 '↓' into dtl4.
concatenate dtl5 '●' into dtl5.
else.
concatenate dtl1 '●' into dtl1.
concatenate dtl2 '↑' into dtl2.
concatenate dtl4 '#' into dtl4.
concatenate dtl5 '#' into dtl5.
endif.
enddo.
clear dtl3.
len = strlen( dtl5 ).
do len times.
concatenate dtl3 '○' into dtl3.
enddo.
g_n = g_n - l_x. "残りの数
write g_n to l_z left-justified.
do g_n times.
concatenate dtl3 '●' into dtl3.
enddo.
concatenate dtl3 '  残り＝' l_z '個' into dtl3.
* 変数更新
g_max = l_x * 2. "取れる数
if g_max > g_n.
g_max = g_n.
endif.
my_turn = 1 - my_turn. "手番
endform.                    "xxx
