*&---------------------------------------------------------------------*
*&  REPORT ZN011900                                                    *
*&         売掛金年齢表（売上）                                        *
*&---------------------------------------------------------------------*
*&  機能     : 指定された期間の売掛金年齢表をALV表示する
*&
*&  作成日   : 2012/02/10
*&  作成者   : H.KOMIYAMA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/02/28 H.KOMIYAMA(SOLFIS)
*&  変更内容 : 消込伝票タイプ複数化（仕様変更）
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/03/27 K.KAJISA(SOLFIS)
*&  変更内容 : インデックス対応
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/04/16 K.FURUYA(SOLFIS)
*&  変更内容 : 売上伝票タイプ複数化（仕様変更）
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/06/12 H.YAMASHITA(SOLFIS)
*&  変更内容 : 選択画面に締日（入力値以前）を追加（仕様変更）
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/07/19 H.YAMASHITA(SOLFIS)
*&  変更内容 : 滞留日の境界値 90 を 120 に変更する。
*&             出力項目に照合締日を出力する（仕様変更）
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/08/24 K.FURUYA(SOLFIS)
*&  変更内容 : 営業グループ二重表示不具合対応
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/09/04 K.FURUYA(SOLFIS)
*&  変更内容 : 営業所、営業グループをマスタ表示（仕様変更）
*&---------------------------------------------------------------------*
*&  変更履歴 : 2013/01/28 K.FURUYA(GSL)
*&  変更内容 : 支払条件が決定できない場合の条件入金日の対応
*&---------------------------------------------------------------------*

REPORT ZN011900
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.

*----------------------------------------------------------------------*
*   参照先
*----------------------------------------------------------------------*
TYPE-POOLS: SSCR,
SLIS.
TABLES: BSID,
YN120.

*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
*-- 営業所名称取得用
TYPES: BEGIN OF T_TVKBT,
VKBUR TYPE TVKBT-VKBUR,            " 営業所
BEZEI TYPE TVKBT-BEZEI,            " 内容説明
END   OF T_TVKBT,
*-- 営業グループ名称取得用
BEGIN OF T_TVGRT,
VKGRP TYPE TVGRT-VKGRP,            " 営業グループ
BEZEI TYPE TVGRT-BEZEI,            " 内容説明
END   OF T_TVGRT,
*-- 得意先名称取得用
BEGIN  OF T_KNXX,
KUNNR TYPE KNA1-KUNNR,             " 得意先コード(請求先)
NAME1 TYPE KNA1-NAME1,             " 名称１
VKGRP TYPE KNVV-VKGRP,             " 営業GRP  "2012/09/04 ADD
VKBUR TYPE KNVV-VKBUR,             " 営業所   "2012/09/04 ADD
END    OF T_KNXX,
*-- 未消込伝票取得用
BEGIN OF T_BSXX,
KUNNR   TYPE BSID-KUNNR,           " 得意先コード
GJAHR   TYPE BSID-GJAHR,           " 会計年度
BELNR   TYPE BSID-BELNR,           " 会計伝票番号
BUZEI   TYPE BSID-BUZEI,           " 会計伝票内の明細番号
BUDAT   TYPE BSID-BUDAT,           " 伝票の転記日付
WAERS   TYPE BSID-WAERS,           " 通貨コード
BLART   TYPE BSID-BLART,           " 伝票タイプ
SHKZG   TYPE BSID-SHKZG,           " 貸借フラグ
WRBTR   TYPE BSID-WRBTR,           " 伝票通貨額
ZUONR   TYPE BSID-ZUONR,           " ソートキー
SGTXT   TYPE BSID-SGTXT,           " 明細テキスト
ZFBDT   TYPE BSID-ZFBDT,           " 期日計算の支払基準日
ZTERM   TYPE BSID-ZTERM,           " 支払条件キー
MANDT   TYPE BSAD-MANDT,           " クライアント(BSADフラグ)
GJAHR2  TYPE YN120-YNGJAHR,        " 自社データ取得用会計年度
BELNR2  TYPE YN120-BELNR,          " 自社データ取得用伝票番号
BUZEI2  TYPE YN120-BUZEI,          " 自社データ取得用伝票明細
END   OF T_BSXX,
*-- 自社データ取得用
BEGIN OF T_YN120,
YNGJAHR TYPE YN120-YNGJAHR,        " 伝票会計年度
BELNR   TYPE YN120-BELNR,          " 伝票番号
BUZEI   TYPE YN120-BUZEI,          " 伝票明細番号
VRFCTON TYPE YN120-VRFCTON,        " 得意先コード(支払人)
BUKRS   TYPE YN120-BUKRS,          " 会社コード
ZFBDT   TYPE YN120-ZFBDT,          " 計上締日
DVSON   TYPE YN120-DVSON,          " 営業所
CSTS    TYPE YN120-CSTS,           " 照合ステータス
COMMT   TYPE YN120-COMMT,          " コメント
LIST2   TYPE YN120-LIST2,          " 得意先発注番号
LIST3   TYPE YN120-LIST3,          " 受注伝票
LIST4   TYPE YN120-LIST4,          " 営業グループ
LDATE1  TYPE YN120-LDATE1,         " 請求日
*** 2012/07/19 INSERT START *** 照合締日追加
CZFBDT  TYPE YN120-CZFBDT,         " 照合締日
*** 2012/07/19 INSERT END ***
GJAHR   TYPE YN120-GJAHR,          " 伝票会計年度
SLPDOC  TYPE YN120-SLPDOC,         " 伝票番号
DTLDOC  TYPE YN120-DTLDOC,         " 伝票明細番号
END OF   T_YN120,
*-- データ出力用
BEGIN OF T_OUTPUT,
DVSON   TYPE YN120-DVSON,          " 営業所
KUNNR   TYPE BSID-KUNNR,           " 得意先（支払人）
NAME1   TYPE KNA1-NAME1,           " 名称
LIST2   TYPE YN120-LIST2,          " 得意先発注番号
WAERS   TYPE BSID-WAERS,           " 通貨
BUDAT   TYPE BSID-BUDAT,           " 転記日付
ZFBDT   TYPE BSID-ZFBDT,           " 締日
PAYDT   TYPE DATUM,                " 条件入金日
TERM00  TYPE BSID-WRBTR,           " 未滞留
TERM30  TYPE BSID-WRBTR,           " 1 〜 30 日
TERM60  TYPE BSID-WRBTR,           " 31 〜 60 日
*** 2012/07/19 MOD START *** 境界値 90 ⇒ 120
*         TERM90  TYPE BSID-WRBTR,           " 61 〜 90 日
*         TERM99  TYPE BSID-WRBTR,           " 121日超
TERM120 TYPE BSID-WRBTR,           " 61 〜 120 日
TERM999 TYPE BSID-WRBTR,           " 121日超
*** 2012/07/19 MOD END ***
TOTAL   TYPE BSID-WRBTR,           " 合計
BEZEI1  TYPE TVKBT-BEZEI,          " 営業所名
LIST4   TYPE YN120-LIST4,          " 営業G
LIST3   TYPE YN120-LIST3,          " 受注番号
RETENT1 TYPE I,                    " 滞留（日）
RETENT2 TYPE I,                    " 転記日滞留（日）
STATUS  TYPE CHAR20,               " ステータス
COMMT   TYPE YN120-COMMT,          " コメント
BLART   TYPE BSID-BLART,           " 伝票タイプ
BELNR   TYPE BSID-BELNR,           " 会計伝票
BUZEI   TYPE BSID-BUZEI,           " 明細
GJAHR   TYPE BSID-GJAHR,           " 会計年度
BEZEI2  TYPE TVGRT-BEZEI,          " 営業グループ名
*** 2012/07/19 INSERT START *** 照合締日追加
CZFBDT  TYPE YN120-CZFBDT,         " 照合締日
*** 2012/07/19 INSERT END ***
END   OF T_OUTPUT.

*----------------------------------------------------------------------*
*   構造定義
*----------------------------------------------------------------------*
DATA: W_TVKBT  TYPE T_TVKBT,
W_TVGRT  TYPE T_TVGRT,
W_KNXX   TYPE T_KNXX,
W_BSXX   TYPE T_BSXX,
W_YN120  TYPE T_YN120,
W_OUTPUT TYPE T_OUTPUT.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*
DATA: I_TVKBT  TYPE HASHED   TABLE OF T_TVKBT
WITH UNIQUE KEY VKBUR,
I_TVGRT  TYPE HASHED   TABLE OF T_TVGRT
WITH UNIQUE KEY VKGRP,
I_KNXX   TYPE HASHED   TABLE OF T_KNXX
WITH UNIQUE KEY KUNNR,
I_BSXX   TYPE STANDARD TABLE OF T_BSXX,
I_YN120  TYPE SORTED TABLE OF T_YN120
WITH NON-UNIQUE KEY YNGJAHR BELNR BUZEI VRFCTON,
I_OUTPUT TYPE STANDARD TABLE OF T_OUTPUT.

*----------------------------------------------------------------------*
*   ALV出力用定義
*----------------------------------------------------------------------*
*-- ALV出力用
DATA: W_LAYOUT      TYPE SLIS_LAYOUT_ALV,     " ALV出力レイアウト
I_FIELDCAT    TYPE SLIS_T_FIELDCAT_ALV, " ALV出力項目設定
W_REPID       TYPE SY-REPID,            " ALV出力用：プログラムID
W_EDISVARIANT TYPE DISVARIANT,          " ALV出力バリアント
I_EVENT       TYPE SLIS_T_EVENT,        " イベント
W_EVENT       LIKE LINE OF I_EVENT,
I_HEAD        TYPE SLIS_T_LISTHEADER,   " ヘッダ
W_HEAD        LIKE LINE OF I_HEAD,
W_PRINT       TYPE SLIS_PRINT_ALV,
W_SETTINGS    TYPE LVC_S_GLAY.

*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_ON         TYPE CHAR1 VALUE 'X',        " フラグON
C_SAVE       TYPE CHAR1 VALUE 'A',        " SAVE機能ON
C_TYP1       TYPE CHAR1 VALUE 'H',        " ヘッダ書式(H)
C_TYP2       TYPE CHAR1 VALUE 'S',        " ヘッダ書式(S)
C_STATUS     TYPE CHAR6 VALUE '入金済'.   " 照合ステータステキスト

*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
*-- データ選択
SELECTION-SCREEN: BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-S01.
*-- 会社コード
PARAMETERS:     P_BUKRS TYPE T001-BUKRS OBLIGATORY MEMORY ID BUK,
*-- 年齢基準日
P_BUDAT TYPE BSID-BUDAT OBLIGATORY,
*** 2012/06/12 INSERT START ***
*-- 締日          入力値以前を対象とする。
P_ZFBDT TYPE BSID-ZFBDT.
*** 2012/06/12 INSERT END ***
*-- 得意先コード（支払人）
SELECT-OPTIONS: S_KUNNR FOR  BSID-KUNNR,
*-- 通貨コード
S_WAERS FOR  BSID-WAERS NO INTERVALS,
*-- 営業所
S_DVSON FOR  YN120-DVSON.
SELECTION-SCREEN: END   OF BLOCK BL01.

*-- 処理オプション
SELECTION-SCREEN: BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-S02.
*-- ALVレイアウト
PARAMETERS: P_LAYOUT TYPE SLIS_VARI.
SELECTION-SCREEN:END   OF BLOCK BL02.

*-- システム項目
SELECTION-SCREEN: BEGIN OF BLOCK BL03 WITH FRAME TITLE TEXT-S03.
*-- 売掛金勘定コード
SELECT-OPTIONS: S_SAKNR  FOR  BSID-SAKNR OBLIGATORY.

*-- 売上売掛金伝票タイプ
*** 2012/04/16 MOD START ***
*  PARAMETERS:     P_BLART1 TYPE BSID-BLART OBLIGATORY.
SELECT-OPTIONS: S_BLART1  FOR  BSID-BLART OBLIGATORY.
*** 2012/04/16 MOD END   ***

*-- 部分照合伝票タイプ
PARAMETERS:     P_BLART2 TYPE BSID-BLART OBLIGATORY.

*-- 消込伝票タイプ
*** 2012/02/28 MOD START ***
*                  P_BLART3 TYPE BSID-BLART OBLIGATORY.
SELECT-OPTIONS: S_BLART3  FOR  BSID-BLART OBLIGATORY.
*** 2012/02/28 MOD END   ***

SELECTION-SCREEN: END   OF BLOCK BL03.

************************************************************************
INITIALIZATION.
************************************************************************
*---選択画面の複数入力ボタン制御
PERFORM F_BUTTON_CONTROL.

************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_LAYOUT.
************************************************************************
*-- ALVバリアント取得
PERFORM F_CALL_ALV_VARIANT CHANGING P_LAYOUT.

************************************************************************
AT SELECTION-SCREEN.
************************************************************************
*-- チェック処理
PERFORM F_CHECK_PARAMETER.

************************************************************************
START-OF-SELECTION.
************************************************************************
*-- 初期処理
PERFORM INT_SEC.
*-- メイン処理
PERFORM MAIN_SEC.
*-- 終了処理
PERFORM END_SEC.

************************************************************************
END-OF-SELECTION.
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  F_BUTTON_CONTROL
*&---------------------------------------------------------------------*
*       選択画面の複数ボタン制御
*----------------------------------------------------------------------*
FORM F_BUTTON_CONTROL.

DATA:LW_RES        TYPE SSCR_RESTRICT,
LW_OPT_LIST   TYPE SSCR_OPT_LIST,
LW_ASS        TYPE SSCR_ASS.

*-- 単一指定のみ可能
LW_OPT_LIST-NAME       = 'JUST_EQ'.
LW_OPT_LIST-OPTIONS-EQ = 'X'.
APPEND LW_OPT_LIST TO LW_RES-OPT_LIST_TAB.

LW_ASS-KIND    = 'S'.
LW_ASS-SG_MAIN = 'I'.
LW_ASS-OP_MAIN = 'JUST_EQ'.

*-- 通貨コード
LW_ASS-NAME    = 'S_WAERS'.
APPEND LW_ASS TO LW_RES-ASS_TAB.

*-- 実行
CALL FUNCTION 'SELECT_OPTIONS_RESTRICT'
EXPORTING
RESTRICTION = LW_RES.

ENDFORM.                    " F_BUTTON_CONTROL
*&-------------------------------------------------------------------*
*&      Form  F_CALL_ALV_VARIANT
*&-------------------------------------------------------------------*
*       ALVレイアウト用F4ヘルプ
*--------------------------------------------------------------------*
*      <--AO_VARI    ALVレイアウト
*--------------------------------------------------------------------*
FORM F_CALL_ALV_VARIANT CHANGING VALUE(AO_VARI) TYPE ANY.

DATA: LW_DISVARIANT TYPE DISVARIANT.

CLEAR: LW_DISVARIANT,
AO_VARI.

LW_DISVARIANT-REPORT   = SY-REPID.
LW_DISVARIANT-USERNAME = SY-UNAME.

CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
EXPORTING
IS_VARIANT         = LW_DISVARIANT
I_SAVE             = C_SAVE
I_DISPLAY_VIA_GRID = C_ON
IMPORTING
ES_VARIANT         = LW_DISVARIANT
EXCEPTIONS
NOT_FOUND          = 1
PROGRAM_ERROR      = 2
OTHERS             = 3.

IF SY-SUBRC = 0.
AO_VARI = LW_DISVARIANT-VARIANT.
ENDIF.

ENDFORM.                    " F_CALL_ALV_VARIANT
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       チェック処理
*----------------------------------------------------------------------*
FORM F_CHECK_PARAMETER.

*-- 会社コードの存在チェック
PERFORM F_CHECK_P_BUKRS.

ENDFORM.                    " F_CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_P_BUKRS
*&---------------------------------------------------------------------*
*       会社コードの存在チェック
*----------------------------------------------------------------------*
FORM F_CHECK_P_BUKRS.

DATA: L_BUKRS TYPE T001-BUKRS.

SELECT SINGLE BUKRS             " 会社コード
FROM T001
INTO L_BUKRS
WHERE BUKRS = P_BUKRS.         " 会社コード

IF SY-SUBRC <> 0.
*-- 対象データがありません
MESSAGE E762.
ENDIF.

ENDFORM.                    " F_CHECK_P_BUKRS
*&---------------------------------------------------------------------*
*&      Form  INT_SEC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INT_SEC.

CLEAR: W_TVKBT,
W_TVGRT,
W_KNXX,
W_BSXX,
W_YN120,
W_OUTPUT,
W_LAYOUT,
W_REPID,
W_EDISVARIANT,
W_EVENT,
W_HEAD,
W_PRINT,
W_SETTINGS.

REFRESH: I_TVKBT,
I_TVGRT,
I_KNXX,
I_BSXX,
I_YN120,
I_OUTPUT,
I_FIELDCAT,
I_EVENT,
I_HEAD.

ENDFORM.                    " INT_SEC
*&---------------------------------------------------------------------*
*&      Form  MAIN_SEC
*&---------------------------------------------------------------------*
*      メイン処理
*----------------------------------------------------------------------*
FORM MAIN_SEC.

*-- 付属データ取得
PERFORM F_GET_ADJUNCT_DATA.

*-- 未消込伝票取得
PERFORM F_GET_NOT_SCRUB_DATA.

*-- 自社データ取得
PERFORM F_GET_YN120_DATA.

*-- 出力データ編集
PERFORM F_EDIT_OUTPUT_DATA.

ENDFORM.                    " MAIN_SEC
*&---------------------------------------------------------------------*
*&      Form  F_GET_ADJUNCT_DATA
*&---------------------------------------------------------------------*
*       付属データ取得
*----------------------------------------------------------------------*
FORM F_GET_ADJUNCT_DATA.

*-- 営業所名称の一括取得
PERFORM F_SELECT_TVKBT.

*-- 営業グループ名称の一括取得
PERFORM F_SELECT_TVGRT.

*-- 得意先名称の一括取得
PERFORM F_SELECT_KNA1.

ENDFORM.                    " F_GET_ADJUNCT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_TVKBT
*&---------------------------------------------------------------------*
*       営業所名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_TVKBT.

REFRESH: I_TVKBT[].

SELECT VKBUR                              " 営業所
BEZEI                              " 内容説明
FROM TVKBT
INTO CORRESPONDING FIELDS OF TABLE I_TVKBT
WHERE SPRAS = SY-LANGU.                  " 言語

ENDFORM.                    " F_SELECT_TVKBT
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_TVGRT
*&---------------------------------------------------------------------*
*       営業グループ名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_TVGRT.

REFRESH: I_TVGRT[].

SELECT VKGRP                              " 営業グループ
BEZEI                              " 名称 1
FROM TVGRT
INTO CORRESPONDING FIELDS OF TABLE I_TVGRT
WHERE SPRAS = SY-LANGU.                  " 言語

ENDFORM.                    " F_SELECT_TVGRT
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_KNA1
*&---------------------------------------------------------------------*
*       得意先名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_KNA1.

REFRESH: I_KNXX[].

*2012/09/04 MOD START
*  SELECT KUNNR                             " 得意先コード
*         NAME1                             " 名称１
*    FROM KNA1
*    INTO CORRESPONDING FIELDS OF TABLE I_KNXX
*   WHERE KUNNR IN S_KUNNR.

SELECT KNA1~KUNNR                             " 得意先コード
KNA1~NAME1                             " 名称１
KNVV~VKGRP                             " 営業所
KNVV~VKBUR                             " 営業グループ
FROM KNA1
INNER JOIN KNVV
ON KNA1~KUNNR = KNVV~KUNNR
INTO CORRESPONDING FIELDS OF TABLE I_KNXX
WHERE KNA1~KUNNR IN S_KUNNR
AND KNVV~VKBUR IN S_DVSON.
*2012/09/04 MOD DEL



ENDFORM.                    " F_SELECT_KNA1
*&---------------------------------------------------------------------*
*&      Form  F_GET_NOT_SCRUB_DATA
*&---------------------------------------------------------------------*
*       未消込伝票データ取得
*----------------------------------------------------------------------*
FORM F_GET_NOT_SCRUB_DATA.

*-- BSIDより未消込伝票取得
PERFORM F_SELECT_BSID.

*-- BSADより未消込伝票取得
PERFORM F_SELECT_BSAD.

IF I_BSXX[] IS INITIAL.
*-- 対象データがありません
MESSAGE S762.
LEAVE LIST-PROCESSING.
ENDIF.

*-- 自社データ取得用項目の作成
PERFORM F_EDIT_I_BSXX.

ENDFORM.                    " F_GET_NOT_SCRUB_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_BSID
*&---------------------------------------------------------------------*
*       BSIDより未消込伝票取得
*----------------------------------------------------------------------*
FORM F_SELECT_BSID.

REFRESH: I_BSXX[].

SELECT KUNNR                              " 得意先コード
GJAHR                              " 会計年度
BELNR                              " 会計伝票番号
BUZEI                              " 会計伝票内の明細番号
BUDAT                              " 伝票の転記日付
WAERS                              " 通貨コード
BLART                              " 伝票タイプ
SHKZG                              " 貸借フラグ
WRBTR                              " 伝票通貨額
ZUONR                              " ソートキー
SGTXT                              " 明細テキスト
ZFBDT                              " 期日計算の支払基準日
ZTERM                              " 支払条件キー
FROM BSID
INTO CORRESPONDING FIELDS OF TABLE I_BSXX
WHERE BUKRS =  P_BUKRS                   " 会社コード
AND KUNNR IN S_KUNNR                   " 得意先コード
AND UMSKZ =  SPACE                     " 特殊仕訳コード
AND BUDAT <= P_BUDAT                   " 転記日付
AND WAERS IN S_WAERS                   " 通貨コード
AND SAKNR IN S_SAKNR.                  " G/L 勘定コード

ENDFORM.                    " F_SELECT_BSID
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_BSAD
*&---------------------------------------------------------------------*
*       BSADより未消込伝票取得
*----------------------------------------------------------------------*
FORM F_SELECT_BSAD.

SELECT KUNNR                              " 得意先コード
GJAHR                              " 会計年度
BELNR                              " 会計伝票番号
BUZEI                              " 会計伝票内の明細番号
BUDAT                              " 伝票の転記日付
WAERS                              " 通貨コード
BLART                              " 伝票タイプ
SHKZG                              " 貸借フラグ
WRBTR                              " 伝票通貨額
ZUONR                              " ソートキー
SGTXT                              " 明細テキスト
ZFBDT                              " 期日計算の支払基準日
ZTERM                              " 支払条件キー
MANDT                              " クライアント(BSADフラグ)
FROM BSAD
APPENDING CORRESPONDING FIELDS OF TABLE I_BSXX
WHERE BUKRS =  P_BUKRS                   " 会社コード
AND KUNNR IN S_KUNNR                   " 得意先コード
AND UMSKZ =  SPACE                     " 特殊仕訳コード
AND AUGDT >  P_BUDAT                   " 決済日付
AND BUDAT <= P_BUDAT                   " 転記日付
AND WAERS IN S_WAERS                   " 通貨コード
AND SAKNR IN S_SAKNR.                  " G/L 勘定コード

ENDFORM.                    " F_SELECT_BSAD
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_I_BSxx
*&---------------------------------------------------------------------*
*       自社データ取得用項目の作成
*----------------------------------------------------------------------*
FORM F_EDIT_I_BSXX.

DATA:L_KNXX TYPE T_KNXX. "2012/09/04 ADD

LOOP AT I_BSXX INTO W_BSXX.
*2012/09/04 ADD START
*営業所が該当しないレコードは削除
READ TABLE I_KNXX INTO L_KNXX
WITH TABLE KEY KUNNR = W_BSXX-KUNNR.
IF SY-SUBRC <> 0.
DELETE I_BSXX.
CONTINUE.
ENDIF.
*2012/09/04 ADD END

*** 2012/02/28 MOD START ***
*    CASE W_BSxx-BLART.
*      WHEN P_BLART1                         " 売上売掛金伝票タイプ
*        OR P_BLART2.                        " 部分照合伝票タイプ
**-- 伝票情報をそのまま設定
*        W_BSxx-GJAHR2 = W_BSxx-GJAHR.       " 会計年度
*        W_BSxx-BELNR2 = W_BSxx-BELNR.       " 会計伝票番号
*        W_BSxx-BUZEI2 = W_BSxx-BUZEI.       " 会計伝票内の明細番号
*      WHEN P_BLART3.
**-- 消込時に発生した伝票を元伝票の情報に置き換える
*        W_BSxx-GJAHR2 = W_BSxx-ZUONR+0(4).  " 会計年度
*        W_BSxx-BELNR2 = W_BSxx-ZUONR+4(10). " 会計伝票番号
*        W_BSxx-BUZEI2 = W_BSxx-ZUONR+14(3). " 会計伝票内の明細番号
*      WHEN OTHERS.
**-- それ以外は初期値（取得対象外）
*        CONTINUE.
*    ENDCASE.

*** 2012/04/16 MOD START ***
*    IF     W_BSXX-BLART = P_BLART1            " 売上売掛金伝票タイプ
IF     W_BSXX-BLART IN S_BLART1            " 売上売掛金伝票タイプ
*** 2012/04/16 MOD END   ***
OR     W_BSXX-BLART = P_BLART2.           " 部分照合伝票タイプ
*-- 伝票情報をそのまま設定
W_BSXX-GJAHR2 = W_BSXX-GJAHR.           " 会計年度
W_BSXX-BELNR2 = W_BSXX-BELNR.           " 会計伝票番号
W_BSXX-BUZEI2 = W_BSXX-BUZEI.           " 会計伝票内の明細番号
ELSEIF W_BSXX-BLART IN S_BLART3.
*-- 消込時に発生した伝票を元伝票の情報に置き換える
W_BSXX-GJAHR2 = W_BSXX-ZUONR+0(4).      " 会計年度
W_BSXX-BELNR2 = W_BSXX-ZUONR+4(10).     " 会計伝票番号
W_BSXX-BUZEI2 = W_BSXX-ZUONR+14(3).     " 会計伝票内の明細番号
ELSE.
*-- それ以外は初期値（取得対象外）
CONTINUE.
ENDIF.
*** 2012/02/28 MOD END   ***
MODIFY I_BSXX FROM W_BSXX.
ENDLOOP.

ENDFORM.                    " F_EDIT_I_BSxx
*&---------------------------------------------------------------------*
*&      Form  F_GET_YN120_DATA
*&---------------------------------------------------------------------*
*       自社データ取得
*----------------------------------------------------------------------*
FORM F_GET_YN120_DATA.

REFRESH: I_YN120[].
CHECK NOT I_BSXX[] IS INITIAL.

SELECT YNGJAHR                            " 伝票会計年度
BELNR                              " 伝票番号
BUZEI                              " 伝票明細番号
VRFCTON                            " 得意先コード(支払人)
BUKRS                              " 会社コード
ZFBDT                              " 計上締日
DVSON                              " 営業所
CSTS                               " 照合ステータス
COMMT                              " コメント
LIST2                              " 得意先発注番号
LIST3                              " 受注伝票
LIST4                              " 営業グループ
LDATE1                             " 請求日
*** 2012/07/19 INSERT START *** 照合締日追加
CZFBDT                             " 照合締日
*** 2012/07/19 INSERT END ***
GJAHR                              " 伝票会計年度
SLPDOC                             " 伝票番号
DTLDOC                             " 伝票明細番号
FROM YN120
INTO CORRESPONDING FIELDS OF TABLE I_YN120
FOR ALL ENTRIES IN I_BSXX
* 2012/03/27 MOD インデックス対応 START
*   WHERE VRFCTON =  I_BSXX-KUNNR            " 得意先コード(支払人)
*     AND BUKRS   =  P_BUKRS                 " 会社コード
*     AND DVSON   IN S_DVSON                 " 営業所
*     AND YNGJAHR =  I_BSXX-GJAHR2           " 会計年度
*     AND BELNR   =  I_BSXX-BELNR2           " 伝票番号
*     AND BUZEI   =  I_BSXX-BUZEI2.          " 明細番号
WHERE BUKRS   =  P_BUKRS                 " 会社コード
AND YNGJAHR =  I_BSXX-GJAHR2           " 会計年度
AND BELNR   =  I_BSXX-BELNR2           " 伝票番号
AND BUZEI   =  I_BSXX-BUZEI2           " 明細番号
AND VRFCTON =  I_BSXX-KUNNR.            " 得意先コード(支払人)
*     AND DVSON   IN S_DVSON                " 営業所 "2012/09/04 DEL

* 2012/03/27 MOD インデックス対応 END

ENDFORM.                    " F_GET_YN120_DATA
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_OUTPUT_DATA
*&---------------------------------------------------------------------*
*       出力データ編集
*----------------------------------------------------------------------*
FORM F_EDIT_OUTPUT_DATA.

SORT I_BSXX[].

LOOP AT I_BSXX INTO W_BSXX.
CLEAR: W_OUTPUT.
*-- 内部テーブルデータ読込み
PERFORM F_READ_TABLE_DATA USING W_BSXX       " 対象データ
CHANGING W_KNXX       " 得意先マスタデータ
W_YN120      " 自社データ
W_TVKBT      " 営業所名称
W_TVGRT.     " 営業Ｇ名称

*2012/09/04 DEL START
**-- 画面：営業所によるデータ絞込み
*    IF NOT W_YN120-DVSON IN S_DVSON.
*      CONTINUE.
*    ENDIF.
**** 2012/06/12 INSERT START ***
**-- 画面：締日によるデータ絞込み
*    IF P_ZFBDT NE '00000000'.
*      IF W_YN120-ZFBDT > P_ZFBDT.
*        CONTINUE.
*      ENDIF.
*    ENDIF.
**** 2012/06/12 INSERT END ***
*2012/09/04 DEL END


*-- 金額変換(+/-)
IF W_BSXX-SHKZG = 'H'.                       " 貸方
W_BSXX-WRBTR = W_BSXX-WRBTR * -1.
ENDIF.

*-- 営業所
*2012/09/04 MOD START
*    W_OUTPUT-DVSON = W_YN120-DVSON.
W_OUTPUT-DVSON = W_KNXX-VKBUR.
*2012/09/04 MOD END

*-- 得意先(支払人)
W_OUTPUT-KUNNR = W_BSXX-KUNNR.
*-- 名称
W_OUTPUT-NAME1 = W_KNXX-NAME1.
*-- 得意先発注番号
W_OUTPUT-LIST2 = W_YN120-LIST2.
*-- 通貨
W_OUTPUT-WAERS = W_BSXX-WAERS.
*-- 転記日付
IF W_BSXX-BLART =  P_BLART2                  " 部分照合伝票タイプ
*** 2012/02/28 MOD START ***
*    OR W_BSxx-BLART = P_BLART3.                 " 消込伝票タイプ
OR W_BSXX-BLART IN S_BLART3                 " 消込伝票タイプ
*** 2012/02/28 MOD END   ***
AND W_YN120-LDATE1 <> '00000000'.            "2012/09/04 ADD
W_OUTPUT-BUDAT = W_YN120-LDATE1.           " 請求日
ELSE.
W_OUTPUT-BUDAT = W_BSXX-BUDAT.             " 転記日
ENDIF.

*2012/09/04 DEL START
*-- 締日
*    W_OUTPUT-ZFBDT = W_YN120-ZFBDT.
*2012/09/04 DEL END

*** 2012/04/16 MOD START ***
*    IF W_BSXX-BLART = P_BLART1                   " 売上売掛金伝票タイプ
IF W_BSXX-BLART IN S_BLART1                   " 売上売掛金伝票タイプ
*** 2012/04/16 MOD END   ***
OR W_BSXX-BLART = P_BLART2                   " 部分照合伝票タイプ
*** 2012/02/28 MOD START ***
*    OR W_BSxx-BLART = P_BLART3.                  " 消込伝票タイプ
OR W_BSXX-BLART IN S_BLART3.                 " 消込伝票タイプ
*** 2012/02/28 MOD END   ***
*-- 条件入金日、締日
PERFORM F_CALL_YK_ZFBDT_GET_BY_ZTERM USING W_BSXX-ZTERM
W_OUTPUT-BUDAT
CHANGING W_OUTPUT-PAYDT
W_OUTPUT-ZFBDT."2012/09/04 ADD
*2012/09/04 ADD START
IF W_YN120-ZFBDT <> '00000000'.
W_OUTPUT-ZFBDT = W_YN120-ZFBDT.
ENDIF.
IF P_ZFBDT NE '00000000'.
IF W_OUTPUT-ZFBDT > P_ZFBDT.
CONTINUE.
ENDIF.
ENDIF.
*2012/09/04 ADD END
*-- 滞留（日）
IF NOT W_OUTPUT-PAYDT IS INITIAL.
W_OUTPUT-RETENT1 = P_BUDAT - W_OUTPUT-PAYDT.
IF W_OUTPUT-RETENT1 <= 0.
W_OUTPUT-RETENT1 = 0.
ENDIF.
ENDIF.
*-- 転記日滞留（日）
IF NOT W_OUTPUT-BUDAT IS INITIAL.
W_OUTPUT-RETENT2 = P_BUDAT - W_OUTPUT-BUDAT.
IF W_OUTPUT-RETENT2 <= 0.
W_OUTPUT-RETENT2 = 0.
ENDIF.
ENDIF.
ENDIF.
IF W_OUTPUT-RETENT1 = 0.            " 滞留(日) = 0
*-- 未滞留
W_OUTPUT-TERM00 = W_BSXX-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >  0        " 滞留(日) = 1~30
AND W_OUTPUT-RETENT1 <= 30.
*-- 1 〜 30 日
W_OUTPUT-TERM30 = W_BSXX-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >  30       " 滞留(日) = 31~60
AND W_OUTPUT-RETENT1 <= 60.
*-- 31 〜 60 日
W_OUTPUT-TERM60 = W_BSXX-WRBTR.
*** 2012/07/19 MOD START *** 境界値 90 ⇒ 120
*    ELSEIF W_OUTPUT-RETENT1 >  60       " 滞留(日) = 61~90
*       AND W_OUTPUT-RETENT1 <= 90.
**-- 61 〜 90 日
*      W_OUTPUT-TERM90 = W_BSXX-WRBTR.
*    ELSE.                              " 滞留(日) = 91~
**-- 91日超
*      W_OUTPUT-TERM99 = W_BSXX-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >   60       " 滞留(日) = 61~120
AND W_OUTPUT-RETENT1 <= 120.
*-- 61 〜 120 日
W_OUTPUT-TERM120 = W_BSXX-WRBTR.
ELSE.                                " 滞留(日) = 121~
*-- 121日超
W_OUTPUT-TERM999 = W_BSXX-WRBTR.
*** 2012/07/19 MOD END ***
ENDIF.
*-- 合計
W_OUTPUT-TOTAL  = W_BSXX-WRBTR.
*-- 営業所名
W_OUTPUT-BEZEI1 = W_TVKBT-BEZEI.
*-- 営業G
*2012/09/04 MOD START
*    W_OUTPUT-LIST4  = W_YN120-LIST4.
W_OUTPUT-LIST4  = W_KNXX-VKGRP.
*2012/09/04 MOD END

*-- 受注番号
W_OUTPUT-LIST3  = W_YN120-LIST3.
*-- ステータス
*** 2012/04/16 MOD START ***
*    IF W_BSXX-BLART =  P_BLART1        " 伝票ﾀｲﾌﾟ = 画面：売上伝票ﾀｲﾌﾟ
IF W_BSXX-BLART IN  S_BLART1        " 伝票ﾀｲﾌﾟ = 画面：売上伝票ﾀｲﾌﾟ
*** 2012/04/16 MOD END   ***
OR W_BSXX-BLART =  P_BLART2        " 伝票ﾀｲﾌﾟ = 画面：部照伝票ﾀｲﾌﾟ
*** 2012/02/28 MOD START ***
*    OR W_BSxx-BLART = P_BLART3.    " 伝票ﾀｲﾌﾟ = 画面：消込伝票ﾀｲﾌﾟ
OR W_BSXX-BLART IN S_BLART3.    " 伝票ﾀｲﾌﾟ = 画面：消込伝票ﾀｲﾌﾟ
*** 2012/02/28 MOD END   ***
IF NOT W_BSXX-MANDT IS INITIAL.  " データ取得元 = [BSAD]
W_OUTPUT-STATUS = C_STATUS.
ELSE.                            " データ取得元 = [BSID]
IF W_YN120-CSTS = 7
OR W_YN120-CSTS = 8.
W_OUTPUT-STATUS = TEXT-M01.
ELSE.
W_OUTPUT-STATUS = TEXT-M02.
ENDIF.
ENDIF.
ENDIF.
*-- コメント
W_OUTPUT-COMMT  = W_YN120-COMMT.
*-- 伝票タイプ
W_OUTPUT-BLART  = W_BSXX-BLART.
*-- 会計伝票
W_OUTPUT-BELNR  = W_BSXX-BELNR.
*-- 明細
W_OUTPUT-BUZEI  = W_BSXX-BUZEI.
*-- 会計年度
W_OUTPUT-GJAHR  = W_BSXX-GJAHR.
*-- 営業グループ名
W_OUTPUT-BEZEI2 = W_TVGRT-BEZEI.
*** 2012/07/19 INSERT START *** 照合締日追加
W_OUTPUT-CZFBDT = W_YN120-CZFBDT.
*** 2012/07/19 INSERT END ***
APPEND W_OUTPUT TO I_OUTPUT.
ENDLOOP.

IF I_OUTPUT[] IS INITIAL.
*-- 対象データがありません
MESSAGE S762.
LEAVE LIST-PROCESSING.
ENDIF.

SORT I_OUTPUT[].

FREE: I_BSXX,
I_KNXX,
I_YN120,
I_TVKBT,
I_TVGRT.

ENDFORM.                    " F_EDIT_OUTPUT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_READ_TABLE_DATA
*&---------------------------------------------------------------------*
*       内部テーブルデータ読込み
*----------------------------------------------------------------------*
*      -->AI_BSxx   対象データ
*      <--AO_KNxx   得意先マスタデータ
*      <--AO_YN120  自社データ
*      <--AO_TVKBT  営業所名称
*      <--AO_TVGRT  営業Ｇ名称
*----------------------------------------------------------------------*
FORM F_READ_TABLE_DATA USING VALUE(AI_BSXX)   TYPE T_BSXX
CHANGING VALUE(AO_KNXX)   TYPE T_KNXX
VALUE(AO_YN120)  TYPE T_YN120
VALUE(AO_TVKBT)  TYPE T_TVKBT
VALUE(AO_TVGRT)  TYPE T_TVGRT.

CLEAR: AO_KNXX,
AO_YN120,
AO_TVKBT,
AO_TVGRT.

*-- 得意先名称読込み
READ TABLE I_KNXX INTO AO_KNXX
WITH TABLE KEY KUNNR = AI_BSXX-KUNNR.
*-- 自社データ読込み
READ TABLE I_YN120 INTO AO_YN120
WITH KEY YNGJAHR = AI_BSXX-GJAHR2 " 会計年度
BELNR   = AI_BSXX-BELNR2 " 伝票番号
BUZEI   = AI_BSXX-BUZEI2 " 明細番号
VRFCTON = AI_BSXX-KUNNR " 得意先(支払人)
BINARY SEARCH.

*2012/09/04 MOD START
*  IF SY-SUBRC = 0.
**-- 営業所名称読込み
*    READ TABLE I_TVKBT INTO AO_TVKBT
**                       WITH TABLE KEY VKBUR = AO_YN120-DVSON. " 営業所
**-- 営業Ｇ名称読込み
*    READ TABLE I_TVGRT INTO AO_TVGRT
*                      WITH TABLE KEY VKGRP = AO_YN120-LIST4+0(3).
*  ENDIF.

*-- 営業所名称読込み
READ TABLE I_TVKBT INTO AO_TVKBT
WITH TABLE KEY VKBUR = AO_KNXX-VKBUR.
*-- 営業Ｇ名称読込み
READ TABLE I_TVGRT INTO AO_TVGRT
WITH TABLE KEY VKGRP = AO_KNXX-VKGRP.
*2012/09/04 MOD END

ENDFORM.                    " F_READ_TABLE_DATA
*&---------------------------------------------------------------------*
*&      Form  F_CALL_YK_ZFBDT_GET_BY_ZTERM
*&---------------------------------------------------------------------*
*       条件入金日の取得
*----------------------------------------------------------------------*
*      -->AI_ZTERM   支払条件キー
*      -->AI_ZFBDT   期日計算の支払基準日
*      <--AO_ZFBDT   条件入金日
*----------------------------------------------------------------------*
FORM F_CALL_YK_ZFBDT_GET_BY_ZTERM USING VALUE(AI_ZTERM) TYPE ANY
VALUE(AI_ZFBDT) TYPE ANY
*2012/09/04 MOD START
*                               CHANGING VALUE(AO_ZFBDT) TYPE ANY.
CHANGING VALUE(AO_ZFBDT_S) TYPE ANY
VALUE(AO_ZFBDT) TYPE ANY.
*2012/09/04 MOD END

CALL FUNCTION 'YK_ZFBDT_GET_BY_ZTERM'
EXPORTING
I_ZTERM              = AI_ZTERM
I_DAY                = AI_ZFBDT
IMPORTING
*2012/09/04 MOD START
*      E_SIHARAIBI          = AO_ZFBDT
E_ZFBDT              = AO_ZFBDT
E_SIHARAIBI          = AO_ZFBDT_S
*2012/09/04 MOD END
EXCEPTIONS
OTHERS               = 0.

*2013/01/28 ADD START 支払条件がない場合は条件入金日'99991231'
IF ( AI_ZTERM = SPACE )
OR ( AI_ZTERM = '0001' ).
AO_ZFBDT   = '99991231'.
AO_ZFBDT_S = '99991231'.
ENDIF.
*2013/01/28 ADD END

ENDFORM.                    " F_CALL_YK_ZFBDT_GET_BY_ZTERM
*&---------------------------------------------------------------------*
*&      Form  END_SEC
*&---------------------------------------------------------------------*
*       終了処理
*----------------------------------------------------------------------*
FORM END_SEC.

*-- ALVリスト出力
PERFORM F_OUTPUT_ALV.

ENDFORM.                    " END_SEC
*&---------------------------------------------------------------------*
*&      Form  F_OUTPUT_ALV
*&---------------------------------------------------------------------*
*       ALVリスト出力
*----------------------------------------------------------------------*
FORM F_OUTPUT_ALV.

*-- 項目属性編集
PERFORM F_SET_ALV_FIELDCAT.

*-- その他編集
PERFORM F_SET_ALV_LAYOUT.

*-- ALV一覧出力
PERFORM F_CALL_ALV.

ENDFORM.                    " F_OUTPUT_ALV
*&---------------------------------------------------------------------*
*&      Form  F_SET_ALV_FIELDCAT
*&---------------------------------------------------------------------*
*       項目属性編集
*----------------------------------------------------------------------*
FORM F_SET_ALV_FIELDCAT.

*** 2012/02/29 ALV INSERT START ***
DATA:L_COL      TYPE I.               "カラム位置

* カラム位置の初期化
L_COL = 0.

* 項目名を編集する
PERFORM SET_FIELDCAT USING 'DVSON'          "項目ID
TEXT-F01         "項目テキスト(短)
TEXT-F01         "項目テキスト(中)
TEXT-F01         "項目テキスト(長)
4                "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
4                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'KUNNR'          "項目ID
TEXT-F02         "項目テキスト(短)
TEXT-F02         "項目テキスト(中)
TEXT-F02         "項目テキスト(長)
10               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
10               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'NAME1'          "項目ID
TEXT-F03         "項目テキスト(短)
TEXT-F03         "項目テキスト(中)
TEXT-F03         "項目テキスト(長)
35               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
35               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'LIST2'          "項目ID
TEXT-F04         "項目テキスト(短)
TEXT-F04         "項目テキスト(中)
TEXT-F04         "項目テキスト(長)
40               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
40               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'WAERS'          "項目ID
TEXT-F05         "項目テキスト(短)
TEXT-F05         "項目テキスト(中)
TEXT-F05         "項目テキスト(長)
5                "出力データ長
'CUKY'           "データ型
SPACE            "数値データ型
5                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BUDAT'          "項目ID
TEXT-F06         "項目テキスト(短)
TEXT-F06         "項目テキスト(中)
TEXT-F06         "項目テキスト(長)
10               "出力データ長
'DATS'           "データ型
SPACE            "数値データ型
8                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'ZFBDT'          "項目ID
TEXT-F07         "項目テキスト(短)
TEXT-F07         "項目テキスト(中)
TEXT-F07         "項目テキスト(長)
10               "出力データ長
'DATS'           "データ型
SPACE            "数値データ型
8                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'PAYDT'          "項目ID
TEXT-F08         "項目テキスト(短)
TEXT-F08         "項目テキスト(中)
TEXT-F08         "項目テキスト(長)
10               "出力データ長
'DATS'           "データ型
SPACE            "数値データ型
8                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'TERM00'         "項目ID
TEXT-F09         "項目テキスト(短)
TEXT-F09         "項目テキスト(中)
TEXT-F09         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'TERM30'         "項目ID
TEXT-F10         "項目テキスト(短)
TEXT-F10         "項目テキスト(中)
TEXT-F10         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'TERM60'         "項目ID
TEXT-F11         "項目テキスト(短)
TEXT-F11         "項目テキスト(中)
TEXT-F11         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
*** 2012/07/19 MOD START *** 境界値 90 ⇒ 120
*  PERFORM SET_FIELDCAT USING 'TERM90'         "項目ID
PERFORM SET_FIELDCAT USING 'TERM120'         "項目ID
*** 2012/07/19 MOD END ***
TEXT-F12         "項目テキスト(短)
TEXT-F12         "項目テキスト(中)
TEXT-F12         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
*** 2012/07/19 MOD START *** 境界値 90 ⇒ 120
*  PERFORM SET_FIELDCAT USING 'TERM99'         "項目ID
PERFORM SET_FIELDCAT USING 'TERM999'         "項目ID
*** 2012/07/19 MOD END ***
TEXT-F13         "項目テキスト(短)
TEXT-F13         "項目テキスト(中)
TEXT-F13         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'TOTAL'          "項目ID
TEXT-F14         "項目テキスト(短)
TEXT-F14         "項目テキスト(中)
TEXT-F14         "項目テキスト(長)
16               "出力データ長
'CURR'           "データ型
'P'              "数値データ型
13               "数値データ長
'WAERS'          "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BEZEI1'         "項目ID
TEXT-F15         "項目テキスト(短)
TEXT-F15         "項目テキスト(中)
TEXT-F15         "項目テキスト(長)
20               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
20               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'LIST4'          "項目ID
TEXT-F16         "項目テキスト(短)
TEXT-F16         "項目テキスト(中)
TEXT-F16         "項目テキスト(長)
40               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
40               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'LIST3'          "項目ID
TEXT-F17         "項目テキスト(短)
TEXT-F17         "項目テキスト(中)
TEXT-F17         "項目テキスト(長)
40               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
40               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'RETENT1'        "項目ID
TEXT-F18         "項目テキスト(短)
TEXT-F18         "項目テキスト(中)
TEXT-F18         "項目テキスト(長)
11               "出力データ長
'INT4'           "データ型
SPACE            "数値データ型
10               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'RETENT2'        "項目ID
TEXT-F19         "項目テキスト(短)
TEXT-F19         "項目テキスト(中)
TEXT-F19         "項目テキスト(長)
11               "出力データ長
'INT4'           "データ型
SPACE            "数値データ型
10               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'STATUS'         "項目ID
TEXT-F20         "項目テキスト(短)
TEXT-F20         "項目テキスト(中)
TEXT-F20         "項目テキスト(長)
20               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
20               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'COMMT'          "項目ID
TEXT-F21         "項目テキスト(短)
TEXT-F21         "項目テキスト(中)
TEXT-F21         "項目テキスト(長)
50               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
50               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BLART'          "項目ID
TEXT-F22         "項目テキスト(短)
TEXT-F22         "項目テキスト(中)
TEXT-F22         "項目テキスト(長)
2                "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
2                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BELNR'          "項目ID
TEXT-F23         "項目テキスト(短)
TEXT-F23         "項目テキスト(中)
TEXT-F23         "項目テキスト(長)
10               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
10               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BUZEI'          "項目ID
TEXT-F24         "項目テキスト(短)
TEXT-F24         "項目テキスト(中)
TEXT-F24         "項目テキスト(長)
3                "出力データ長
'NUMC'           "データ型
SPACE            "数値データ型
3                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'GJAHR'          "項目ID
TEXT-F25         "項目テキスト(短)
TEXT-F25         "項目テキスト(中)
TEXT-F25         "項目テキスト(長)
4                "出力データ長
'NUMC'           "データ型
SPACE            "数値データ型
4                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
PERFORM SET_FIELDCAT USING 'BEZEI2'         "項目ID
TEXT-F26         "項目テキスト(短)
TEXT-F26         "項目テキスト(中)
TEXT-F26         "項目テキスト(長)
20               "出力データ長
'CHAR'           "データ型
SPACE            "数値データ型
20               "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
*** 2012/08/24 DEL START "営業グループ名の二重表示不具合修正
*  PERFORM SET_FIELDCAT USING 'BEZEI2'         "項目ID
*                             TEXT-F26         "項目テキスト(短)
*                             TEXT-F26         "項目テキスト(中)
*                             TEXT-F26         "項目テキスト(長)
*                             20               "出力データ長
*                             'CHAR'           "データ型
*                             SPACE            "数値データ型
*                             20               "数値データ長
*                             SPACE            "通貨参照項目
*                    CHANGING L_COL.
*** 2012/08/24 DEL END
*** 2012/07/19 INSERT START ***
PERFORM SET_FIELDCAT USING 'CZFBDT'         "項目ID
TEXT-F27         "項目テキスト(短)
TEXT-F27         "項目テキスト(中)
TEXT-F27         "項目テキスト(長)
10               "出力データ長
'DATS'           "データ型
SPACE            "数値データ型
8                "数値データ長
SPACE            "通貨参照項目
CHANGING L_COL.
*** 2012/07/19 INSERT END ***
*** 2012/02/29 ALV INSERT END ***
ENDFORM.                    " F_SET_ALV_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       FIELD CATALOGの編集
*----------------------------------------------------------------------*
*      -->P_FIELDNAME     項目ID　　　　　　　　　　　　　　　　　必須　
*      -->P_SELLDTEXT_S   項目テキスト(短)　　　　　　　　　　　　必須
*      -->P_SELLDTEXT_S   項目テキスト(中)　　　　　　　　　　　　必須
*      -->P_SELLDTEXT_S   項目テキスト(長)　　　　　　　　　　　　必須
*      -->P_OUTPUTLEN     出力データ長(カンマ、小数点、符号含む)　必須
*      -->P_DATATYPE      データ型    　　　　　　　　　　　　　　必須
*      -->P_INTTYPE       数値データ型 　　　　　　　　　　　 金額のみ
*      -->P_INTLEN        数値データ長                            必須
*      -->P_CFIELD        金額項目の場合参照する通貨項目名    金額のみ
*      -->P_COL　　　　　 カラム位置　　　　　　　　　　　　　　　必須
*----------------------------------------------------------------------*
FORM SET_FIELDCAT USING P_FIELDNAME   TYPE SLIS_FIELDCAT_ALV-FIELDNAME
P_SELTEXT_S   TYPE ANY
P_SELTEXT_M   TYPE ANY
P_SELTEXT_L   TYPE ANY
P_OUTPUTLEN   TYPE SLIS_FIELDCAT_ALV-OUTPUTLEN
P_DATATYPE    TYPE SLIS_FIELDCAT_ALV-DATATYPE
P_INTTYPE     TYPE SLIS_FIELDCAT_ALV-INTTYPE
P_INTLEN      TYPE SLIS_FIELDCAT_ALV-INTLEN
P_CFIELDNAME  TYPE SLIS_FIELDCAT_ALV-CFIELDNAME
CHANGING P_COL         TYPE I.

DATA:W_FIELDCAT TYPE SLIS_FIELDCAT_ALV.


* カラム位置設定
P_COL = P_COL + 1.

* 見出し設定
W_FIELDCAT-COL_POS    = P_COL.             "カラム位置
W_FIELDCAT-FIELDNAME  = P_FIELDNAME.       "項目ID
W_FIELDCAT-SELTEXT_S  = P_SELTEXT_S.       "項目テキスト(短)
W_FIELDCAT-SELTEXT_M  = P_SELTEXT_M.       "項目テキスト(中)
W_FIELDCAT-SELTEXT_L  = P_SELTEXT_L.       "項目テキスト(長)
W_FIELDCAT-OUTPUTLEN  = P_OUTPUTLEN.       "出力データ長
W_FIELDCAT-DATATYPE   = P_DATATYPE.        "データ型
W_FIELDCAT-INTTYPE    = P_INTTYPE.         "数値のデータ型
W_FIELDCAT-INTLEN     = P_INTLEN.          "データ長
W_FIELDCAT-CFIELDNAME = P_CFIELDNAME.      "参照通貨


APPEND W_FIELDCAT TO I_FIELDCAT.

ENDFORM.                    " SET_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  F_SET_ALV_LAYOUT
*&---------------------------------------------------------------------*
*       その他編集
*----------------------------------------------------------------------*
FORM F_SET_ALV_LAYOUT.

*-- プログラムID取得
W_REPID = SY-REPID.

*-- レイアウト設定
W_LAYOUT-COLWIDTH_OPTIMIZE    = ''.  "ALV コントロール: 列幅の最適化
W_LAYOUT-DETAIL_INITIAL_LINES = C_ON.
W_LAYOUT-ZEBRA                = C_ON.
W_LAYOUT-GROUP_CHANGE_EDIT    = C_ON.

*-- バリアント
IF NOT P_LAYOUT IS INITIAL.
W_EDISVARIANT-REPORT  = W_REPID.
W_EDISVARIANT-VARIANT = P_LAYOUT.
ENDIF.

*-- ALV印刷時用 設定項目
W_PRINT-NO_PRINT_LISTINFOS = C_ON.

*-- 印刷プレビュー設定
W_SETTINGS-NO_COLWOPT = C_ON.  "自動最適化禁止

*-- 処理イベントの取得
CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
EXPORTING
I_LIST_TYPE           = 0
IMPORTING
ET_EVENTS             = I_EVENT.

*-- イベント時にコールされるFORM名を設定
CLEAR: W_EVENT.
READ TABLE I_EVENT INTO W_EVENT WITH KEY NAME = 'TOP_OF_PAGE'.
IF SY-SUBRC = 0.
W_EVENT-FORM = 'HEADER_WRITE'.
MODIFY I_EVENT INDEX SY-TABIX FROM W_EVENT.
ENDIF.

ENDFORM.                    " F_SET_ALV_LAYOUT
*&---------------------------------------------------------------------*
*&      Form  F_CALL_ALV
*&---------------------------------------------------------------------*
*       ALV一覧出力
*----------------------------------------------------------------------*
FORM F_CALL_ALV.

CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM      = W_REPID     "プログラム名
I_GRID_SETTINGS         = W_SETTINGS  "グリッド設定
IS_LAYOUT               = W_LAYOUT    "レイアウト定義
IS_PRINT                = W_PRINT
IT_FIELDCAT             = I_FIELDCAT  "構造定義
I_DEFAULT               = C_ON
I_SAVE                  = C_SAVE
IS_VARIANT              = W_EDISVARIANT
IT_EVENTS               = I_EVENT
TABLES
T_OUTTAB                = I_OUTPUT     "出力データ
EXCEPTIONS
PROGRAM_ERROR           = 1
OTHERS                  = 2.

IF SY-SUBRC <> 0.
*-- ALV出力エラー
*** 2012/02/29 MOD START ***
*    MESSAGE I789 WITH TEXT-E01.
*    STOP.
MESSAGE S000 WITH 'システムエラーが発生しました'.
LEAVE LIST-PROCESSING.
*** 2012/02/29 MOD END ***
ENDIF.

ENDFORM.                    " F_CALL_ALV
*&---------------------------------------------------------------------*
*&      Form  HEADER_WRITE
*&---------------------------------------------------------------------*
*       ALVヘッダ編集
*----------------------------------------------------------------------*
FORM HEADER_WRITE.

DATA:LW_WORK     TYPE CHAR100,
LW_PAGNO(4) TYPE C.

*-- タイトル
PERFORM SET_HEADER USING C_TYP1
TEXT-H01
SPACE.

*-- ページ
CLEAR: LW_WORK,
LW_PAGNO.
LW_PAGNO = SY-PAGNO.
CONDENSE LW_PAGNO.
CONCATENATE TEXT-H02
LW_PAGNO
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*-- 処理日付
CLEAR: LW_WORK.
WRITE: SY-DATUM TO LW_WORK.
CONCATENATE TEXT-H03
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*-- 処理時刻
CLEAR: LW_WORK.
WRITE: SY-UZEIT TO LW_WORK.
CONCATENATE TEXT-H04
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*--- ユーザID
CLEAR: LW_WORK.
CONCATENATE TEXT-H05
SY-UNAME
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*--- 年齢基準日
CLEAR: LW_WORK.
WRITE: P_BUDAT TO LW_WORK.
CONCATENATE TEXT-H06
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
LW_WORK
TEXT-H07.

* ヘッダ出力
CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
EXPORTING
IT_LIST_COMMENTARY       = I_HEAD.

REFRESH: I_HEAD.

ENDFORM.                    " HEADER_WRITE
*&---------------------------------------------------------------------*
*&      Form  SET_HEADER
*&---------------------------------------------------------------------*
*       ヘッダ編集
*----------------------------------------------------------------------*
*      -->L_TYP    タイプ
*      -->L_INFO   編集内容2
*      -->L_KEY    編集内容1
*----------------------------------------------------------------------*
FORM SET_HEADER  USING     L_TYP
L_INFO
L_KEY.
CLEAR: W_HEAD.
W_HEAD-TYP  = L_TYP.
W_HEAD-KEY  = L_KEY.
W_HEAD-INFO = L_INFO.
APPEND W_HEAD TO I_HEAD.

ENDFORM.                    " SET_HEADER
