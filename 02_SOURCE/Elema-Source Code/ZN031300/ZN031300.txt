*&---------------------------------------------------------------------*
*&  REPORT ZN03130
*&         【移行】入金予定データ（売上）【セットアップ】
*&---------------------------------------------------------------------*
*&  機能     : BSIDから、ZN005,YN110,YN120を作成
*&
*&  作成日   : 2012/04/02
*&  作成者   : K.KAJISA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 :
*&  変更内容 :
*&---------------------------------------------------------------------*
REPORT ZN031300
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.
*----------------------------------------------------------------------*
*   テーブル
*----------------------------------------------------------------------*
TABLES:YK310,ZN005,YN110,YN120,BSID.
*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*

* 得意先締日
TYPES:BEGIN OF TYP_KNVV,
KUNNR TYPE KNVV-KUNNR,
VKORG TYPE KNVV-VKORG,
VTWEG TYPE KNVV-VTWEG,
SPART TYPE KNVV-SPART,
ZTERM TYPE KNVV-ZTERM,
VKBUR TYPE KNVV-VKBUR,"営業所
ZFAEL TYPE T052-ZFAEL,
END OF TYP_KNVV.

TYPES:BEGIN OF TYP_ZN005_SEQ,
BUKRS  TYPE ZN005-BUKRS,
KUNNR  TYPE ZN005-KUNNR,
CZFBDT TYPE ZN005-CZFBDT,
SEQ    TYPE ZN005-SEQ,
END OF TYP_ZN005_SEQ.

* BSID
TYPES:BEGIN OF TYP_BSID,
*       キー項目
BUKRS TYPE BSID-BUKRS,"会社コード
KUNNR TYPE BSID-KUNNR,"得意先コード
UMSKS TYPE BSID-UMSKS,"
UMSKZ TYPE BSID-UMSKZ,"
AUGDT TYPE BSID-AUGDT,"
AUGBL TYPE BSID-AUGBL,"
ZUONR TYPE BSID-ZUONR,"
GJAHR TYPE BSID-GJAHR,"
BELNR TYPE BSID-BELNR,"会計伝票番号
BUZEI TYPE BSID-BUZEI,"会計伝票内の明細番号
BUDAT TYPE BSID-BUDAT,"転記日
WAERS TYPE BSID-WAERS,"通貨コード
WRBTR TYPE BSID-WRBTR,"伝票通貨額
SHKZG TYPE BSID-SHKZG,"貸借フラグ
SGTXT TYPE BSID-SGTXT,"明細テキスト
ZFBDT TYPE BSID-ZFBDT,"支払基準日
END OF TYP_BSID.
DATA:T_BSID TYPE STANDARD TABLE OF TYP_BSID,
W_BSID TYPE TYP_BSID.

TYPES:BEGIN OF TYP_BSID_O.
INCLUDE STRUCTURE W_BSID.
*営業所(KNVV)
TYPES:VKBUR TYPE KNVV-VKBUR, "営業所
END OF TYP_BSID_O.

DATA:T_BSID_O TYPE STANDARD TABLE OF TYP_BSID_O,
W_BSID_O TYPE TYP_BSID_O.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*

DATA:T_YN120 TYPE STANDARD TABLE OF YN120,
W_YN120 TYPE YN120.

DATA:T_YN110 TYPE STANDARD TABLE OF YN110,
W_YN110 TYPE YN110.

DATA:T_ZN005 TYPE STANDARD TABLE OF ZN005,
W_ZN005 TYPE ZN005.

DATA:T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART,
W_KNVV TYPE TYP_KNVV.

* エラーデータ
TYPES:BEGIN OF TYP_ERR,
BELNR    TYPE BSID-BELNR, "伝票番号
BUZEI    TYPE BSID-BUZEI, "明細番号
MESSAGE  TYPE CHAR255,    "エラー内容
END   OF TYP_ERR.

DATA:T_ERR TYPE STANDARD TABLE OF TYP_ERR,
W_ERR TYPE TYP_ERR.

* SEQ最大
DATA:T_MAX_SEQ TYPE SORTED TABLE OF TYP_ZN005_SEQ
WITH UNIQUE KEY BUKRS KUNNR CZFBDT SEQ,
W_MAX_SEQ TYPE TYP_ZN005_SEQ .


*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_IKO   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(10) TEXT-003 FOR FIELD P_R_IKO.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-003.
** 移行のみ
*SELECTION-SCREEN:BEGIN OF LINE,
*                 POSITION 3.
*PARAMETERS:P_R_UP   RADIOBUTTON GROUP R02.
*SELECTION-SCREEN:COMMENT 6(14) TEXT-007 FOR FIELD P_R_UP.
*SELECTION-SCREEN:END   OF LINE.
** 移行とダウンロード
*SELECTION-SCREEN:BEGIN OF LINE,
*                 POSITION 3.
*PARAMETERS:P_R_DUP   RADIOBUTTON GROUP R02.
*SELECTION-SCREEN:COMMENT 6(24) TEXT-008 FOR FIELD P_R_DUP.
*SELECTION-SCREEN:END   OF LINE.
** ダウンロードのみ
*SELECTION-SCREEN:BEGIN OF LINE,
*                 POSITION 3.
*PARAMETERS:P_R_DOWN   RADIOBUTTON GROUP R02.
*SELECTION-SCREEN:COMMENT 6(18) TEXT-009 FOR FIELD P_R_DOWN.
*SELECTION-SCREEN:END   OF LINE.
*
* 会計管理: 得意先の二次索引（BSID）
SELECTION-SCREEN:BEGIN OF BLOCK BL6 WITH FRAME TITLE TEXT-017.
*-- 締日(BL5)
SELECTION-SCREEN:BEGIN OF BLOCK BL5 WITH FRAME TITLE TEXT-010.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_31 RADIOBUTTON GROUP GR01 DEFAULT 'X'.
SELECTION-SCREEN:COMMENT 3(8) TEXT-011 FOR FIELD R_31 .
PARAMETERS: R_05 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-012 FOR FIELD R_05 .
PARAMETERS: R_10 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-013 FOR FIELD R_10 .
SELECTION-SCREEN:END OF LINE.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_15 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 3(8) TEXT-014 FOR FIELD R_15 .
PARAMETERS: R_20 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-015 FOR FIELD R_20 .
PARAMETERS: R_25 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-016 FOR FIELD R_25.
SELECTION-SCREEN:END OF LINE.
SELECTION-SCREEN:END   OF BLOCK BL5.

SELECT-OPTIONS:P_KUN_B  FOR BSID-KUNNR, "得意先コード
P_HKON   FOR BSID-HKONT, "勘定コード
P_BLART  FOR BSID-BLART. "伝票タイプ
SELECTION-SCREEN:END   OF BLOCK BL6.

SELECTION-SCREEN:BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-005.
PARAMETERS:P_INP_CZ   TYPE ZN005-CZFBDT.
*  PARAMETERS:P_INP_PH   TYPE ZN005-PHKONT.
SELECTION-SCREEN:END   OF BLOCK BL02.
SELECTION-SCREEN:END   OF BLOCK BL01.

SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_DEL   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(14) TEXT-004 FOR FIELD P_R_DEL.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL03 WITH FRAME TITLE TEXT-002.
PARAMETERS:P_ZN005 AS CHECKBOX.
SELECT-OPTIONS:P_BUK_Z   FOR ZN005-BUKRS.
SELECT-OPTIONS:P_KUN_Z   FOR ZN005-KUNNR.
SELECT-OPTIONS:P_CZF_Z   FOR ZN005-CZFBDT.
SELECT-OPTIONS:P_SEQ_Z   FOR ZN005-SEQ.
PARAMETERS:P_YN AS CHECKBOX.
SELECT-OPTIONS:P_GJA_1   FOR YN110-GJAHR.
SELECT-OPTIONS:P_SLP_1   FOR YN110-SLPDOC.
SELECT-OPTIONS:P_DTL_1   FOR YN110-DTLDOC.
SELECT-OPTIONS:P_VRF_1   FOR YN110-VRFCTON.
SELECT-OPTIONS:P_BUK_1   FOR YN110-BUKRS.
SELECT-OPTIONS:P_ZFB_1   FOR YN110-ZFBDT.
SELECTION-SCREEN:END   OF BLOCK BL03.
*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_ZFAEL_05   TYPE T052-ZFAEL   VALUE '05',
C_ZFAEL_10   TYPE T052-ZFAEL   VALUE '10',
C_ZFAEL_15   TYPE T052-ZFAEL   VALUE '15',
C_ZFAEL_20   TYPE T052-ZFAEL   VALUE '20',
C_ZFAEL_25   TYPE T052-ZFAEL   VALUE '25',
C_ZFAEL_31   TYPE T052-ZFAEL   VALUE '31',
C_UNMACH     TYPE YK130-KEKKBN VALUE '3',
C_JPY        TYPE YK130-WAERS  VALUE 'JPY',
C_ON         TYPE C            VALUE 'X',
C_GJAHR      TYPE YN120-GJAHR  VALUE '2012',
C_BUKRS      TYPE YK310-BUKRS  VALUE 'C001',
C_INIT_REC   TYPE YN120-DTLDOC VALUE '000001',
C_SEVEN      TYPE YN120-CSTS   VALUE '7',      "照合ステータス
C_VKORG      TYPE KNVV-VKORG   VALUE '1000' ,"販売組織
C_VTWEG      TYPE KNVV-VTWEG   VALUE '10',   "流通チャネル
C_SPART      TYPE KNVV-SPART   VALUE '10',   "製品部門
C_SPRAS      TYPE SPRAS        VALUE 'J'.    "言語キー

*----------------------------------------------------------------------*
*   変数定義
*----------------------------------------------------------------------*
DATA:G_ZFAEL      TYPE T052-ZFAEL,
G_RENNO(8)   TYPE N,
G_ERR(1)     TYPE C.

DATA:G_CNT_BSID    TYPE I,
G_CNT_ZN005   TYPE I,
G_CNT_YN110   TYPE I,
G_CNT_YN120   TYPE I,
G_CNT_ERR     TYPE I.

*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

*----------------------------------------------------------------------*
*  START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM INIT_PROC.
* 移行用データ取得
IF P_R_IKO = C_ON.
PERFORM GET_BSID.
CHECK NOT T_BSID_O[] IS INITIAL.
* 編集
PERFORM EDIT_BSID.
*   登録
PERFORM INSERT_DATA.
*   処理結果出力
PERFORM WRITE_IKO.
ELSE.
*   データ取得
CLEAR:G_ERR.
PERFORM DEL_DATA_GET.
CHECK G_ERR IS INITIAL.
*   削除
PERFORM DEL_DATA.
*   処理結果出力
PERFORM WRITE_DEL.
ENDIF.

*&---------------------------------------------------------------------*
*&      Form  INIT_PROC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INIT_PROC.

REFRESH: T_KNVV,
T_BSID,
T_BSID_O,
T_YN120,
T_YN110,
T_ZN005.

CLEAR:G_ERR,
G_RENNO,
G_CNT_BSID,
G_CNT_ZN005,
G_CNT_YN110,
G_CNT_YN120,
G_CNT_ERR.

PERFORM GET_RENBAN.


ENDFORM.                    " INIT_PROC
*&---------------------------------------------------------------------*
*&      Form  GET_BSID
*&---------------------------------------------------------------------*
*       移行データ取得
*----------------------------------------------------------------------*
FORM GET_BSID.
SELECT BUKRS
KUNNR
UMSKS
UMSKZ
AUGDT
AUGBL
ZUONR
GJAHR
BELNR
BUZEI
BUDAT
WAERS
WRBTR
SHKZG
SGTXT
ZFBDT
INTO TABLE T_BSID
FROM BSID
WHERE BUKRS =  C_BUKRS
AND KUNNR IN P_KUN_B
AND HKONT IN P_HKON
AND BLART IN P_BLART.

IF SY-SUBRC = 0.
PERFORM CHECK_SIMEBI.
ELSE.
MESSAGE S762(YN01).
ENDIF.

ENDFORM.                    " GET_BSID
*&---------------------------------------------------------------------*
*&      Form  CHECK_SIMEBI
*&---------------------------------------------------------------------*
*       締日チェック
*----------------------------------------------------------------------*
FORM CHECK_SIMEBI.
DATA:L_T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART ZTERM,
L_W_KNVV      TYPE TYP_KNVV,
L_ID          TYPE THEAD-TDID,
L_LANGU       TYPE THEAD-TDSPRAS,
L_NAME        TYPE THEAD-TDNAME,
L_OBJECT      TYPE THEAD-TDOBJECT,
LT_LINES      TYPE STANDARD TABLE OF TLINE,
LW_LINES      TYPE TLINE.

* 選択画面から、今回の締日を取得
CLEAR:G_ZFAEL.
IF R_05 = C_ON.
G_ZFAEL = C_ZFAEL_05.
ELSEIF R_10 = C_ON.
G_ZFAEL = C_ZFAEL_10.
ELSEIF R_15 = C_ON.
G_ZFAEL = C_ZFAEL_15.
ELSEIF R_20 = C_ON.
G_ZFAEL = C_ZFAEL_20.
ELSEIF R_25 = C_ON.
G_ZFAEL = C_ZFAEL_25.
ELSEIF R_31 = C_ON.
G_ZFAEL = C_ZFAEL_31.
ENDIF.

* 今回処理対象のうち、得意先の締日が選択条件に合致する得意先を取得
SELECT KNVV~KUNNR
KNVV~VKORG
KNVV~VTWEG
KNVV~SPART
KNVV~ZTERM
KNVV~VKBUR "営業所
T052~ZFAEL
FROM KNVV
INNER JOIN T052
ON T052~ZTERM = KNVV~ZTERM
INTO TABLE T_KNVV
FOR ALL ENTRIES IN T_BSID
WHERE KNVV~KUNNR = T_BSID-KUNNR
AND KNVV~VKORG = C_VKORG
AND KNVV~VTWEG = C_VTWEG
AND KNVV~SPART = C_SPART
AND T052~ZFAEL = G_ZFAEL.

* 処理対象の得意先を、得意先照合対象の得意先だけにする
REFRESH:L_T_KNVV.
L_T_KNVV[] = T_KNVV[].

L_LANGU   = C_SPRAS.
L_ID      = 'YN01'.
L_OBJECT  = 'KNB1'.

CLEAR:L_W_KNVV.
LOOP AT L_T_KNVV INTO L_W_KNVV.
AT END OF KUNNR.
CLEAR:LW_LINES.
REFRESH: LT_LINES.
L_NAME = L_W_KNVV-KUNNR.
CONCATENATE  L_NAME  'C001'  INTO  L_NAME.

CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                            = L_ID
LANGUAGE                      = L_LANGU
NAME                          = L_NAME
OBJECT                        = L_OBJECT
TABLES
LINES                         = LT_LINES
EXCEPTIONS
ID                            = 1
LANGUAGE                      = 2
NAME                          = 3
*       NOT_FOUND                     = 4
OBJECT                        = 5
REFERENCE_CHECK               = 6
WRONG_ACCESS_TO_ARCHIVE       = 7
OTHERS                        = 8.
IF SY-SUBRC = 0.
READ TABLE LT_LINES INTO LW_LINES INDEX 1.
ENDIF.
IF  LW_LINES-TDLINE <> 'X'
AND LW_LINES-TDLINE <> 'Y'
AND LW_LINES-TDLINE <> 'Z'.
DELETE T_KNVV WHERE KUNNR = L_W_KNVV-KUNNR.
ENDIF.
ENDAT.
ENDLOOP.

*-- 取得したBSIDの得意先コードをチェック
LOOP AT T_BSID INTO W_BSID.
CLEAR:W_KNVV.
READ TABLE T_KNVV INTO W_KNVV
WITH TABLE KEY KUNNR = W_BSID-KUNNR
VKORG = C_VKORG
VTWEG = C_VTWEG
SPART = C_SPART.
IF SY-SUBRC = 0.
MOVE-CORRESPONDING W_BSID TO W_BSID_O.
W_BSID_O-VKBUR = W_KNVV-VKBUR.
APPEND W_BSID_O TO T_BSID_O.
G_CNT_BSID = G_CNT_BSID + 1.
ELSE.
DELETE T_BSID  WHERE KUNNR = W_BSID-KUNNR.
ENDIF.
ENDLOOP.

IF T_BSID_O[] IS INITIAL.
MESSAGE S762(YN01).
ENDIF.

FREE:T_BSID,T_KNVV.


ENDFORM.                    " CHECK_SIMEBI
*&---------------------------------------------------------------------*
*&      Form  EDIT_BSID
*&---------------------------------------------------------------------*
*       編集
*----------------------------------------------------------------------*
FORM EDIT_BSID.
TYPES:BEGIN OF L_TYP_CNT,
KUNNR    TYPE YK310-KUNNR,
CNT(5)   TYPE N,
END OF L_TYp_CNT.
DATA:L_W_CNT TYPE L_TYP_CNT.

SORT T_BSID_O BY KUNNR ZFBDT BUKRS GJAHR BELNR BUZEI.
PERFORM GET_ZN005.

CLEAR:W_BSID_O,L_W_CNT.

LOOP AT T_BSID_O INTO W_BSID_O.
CLEAR:W_ZN005,W_YN120.
* エラーチェック
CLEAR:W_ERR.
PERFORM ERR_CHECK.
IF NOT W_ERR IS INITIAL.
CONTINUE.
ENDIF.

*  エラーが無い場合カウントアップ
IF  L_W_CNT-KUNNR = W_BSID_O-KUNNR. "支払人
L_W_CNT-CNT = L_W_CNT-CNT + 1.
ELSE.
CLEAR:L_W_CNT.
L_W_CNT-KUNNR = W_BSID_O-KUNNR.   "支払人
PERFORM GET_MAX_SEQNO.
L_W_CNT-CNT = W_MAX_SEQ-SEQ + 1.
ENDIF.

*  金額符号確認
IF W_BSID_O-SHKZG = 'H'.
W_BSID_O-WRBTR = W_BSID_O-WRBTR * -1.
ENDIF.

*  ZN005 マッピング
PERFORM SET_ZN005 USING L_W_CNT-CNT.
*  YN120 マッピング
PERFORM SET_YN120.
*  YN110 マッピング
PERFORM SET_YN110.


ENDLOOP.
ENDFORM.                    " EDIT_BSID
*&---------------------------------------------------------------------*
*&      Form  GET_ZN005
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM GET_ZN005.
REFRESH:T_MAX_SEQ.

CHECK NOT T_BSID_O IS INITIAL.

SELECT BUKRS KUNNR CZFBDT SEQ
INTO TABLE T_MAX_SEQ
FROM ZN005
FOR ALL ENTRIES IN T_BSID_O
WHERE BUKRS  = T_BSID_O-BUKRS
AND KUNNR  = T_BSID_O-KUNNR
AND CZFBDT = P_INP_CZ.

ENDFORM.                    " GET_ZN005
*&---------------------------------------------------------------------*
*&      Form  ERR_CHECK
*&---------------------------------------------------------------------*
*       エラーチェック
*----------------------------------------------------------------------*
FORM ERR_CHECK.
DATA:L_MESSAGE  TYPE TYP_ERR-MESSAGE,
L_CHK_IN   TYPE YN120-CHKDOC,
L_CHK_OUT  TYPE YN120-CHKDOC.

CLEAR:L_MESSAGE,L_CHK_IN,L_CHK_OUT.

CONCATENATE 'I' W_BSID_O-BELNR  W_BSID_O-BUZEI
INTO L_CHK_IN.

SELECT CHKDOC
INTO L_CHK_OUT
FROM YN120
UP TO 1 ROWS
WHERE GJAHR  = C_GJAHR   "2012
AND CHKDOC = L_CHK_IN.
*   すでに同じキーが存在します
L_MESSAGE = TEXT-018.
ENDSELECT.

IF NOT L_MESSAGE IS INITIAL.
W_ERR-BELNR   = W_BSID_O-BELNR.  "伝票
W_ERR-BUZEI   = W_BSID_O-BUZEI.  "明細
W_ERR-MESSAGE = L_MESSAGE.
APPEND W_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
ENDIF.

ENDFORM.                    " ERR_CHECK
*&---------------------------------------------------------------------*
*&      Form  GET_MAX_SEQNO
*&---------------------------------------------------------------------*
*       ZN005 得意先/締日での最大SEQ取得
*----------------------------------------------------------------------*
*      -->V_KUNNR  得意先
*      -->V_ZFBDT  締日
*----------------------------------------------------------------------*
FORM GET_MAX_SEQNO.
CLEAR:W_MAX_SEQ.
LOOP AT T_MAX_SEQ INTO W_MAX_SEQ
WHERE BUKRS  = W_BSID_O-BUKRS
AND KUNNR  = W_BSID_O-KUNNR "支払人
AND CZFBDT = P_INP_CZ.
ENDLOOP.

ENDFORM.                    " GET_MAX_SEQNO
*&---------------------------------------------------------------------*
*&      Form  SET_ZN005
*&---------------------------------------------------------------------*
*       ZN005 マッピング
*----------------------------------------------------------------------*
*      -->V_CNT  SEQ連番
*----------------------------------------------------------------------*
FORM SET_ZN005 USING    V_CNT.
CLEAR:W_ZN005.
W_ZN005-BUKRS   = W_BSID_O-BUKRS.      "会社コード
W_ZN005-KUNNR   = W_BSID_O-KUNNR.      "得意先コード(支払人)
W_ZN005-CZFBDT  = P_INP_CZ.            "照合締日(選択画面からセット)
W_ZN005-SEQ     = V_CNT.               "SEQ
W_ZN005-IZFBDT  = W_BSID_O-ZFBDT.      "入金予定日
* W_ZN005-IZLSCH  = 初期値.              "支払方法
* W_ZN005-IZTERM  = 初期値.              "サイト
W_ZN005-WAERS   = W_BSID_O-WAERS.      "通貨コード
W_ZN005-IWRBTR  = W_BSID_O-WRBTR.      "入金予定額
* W_ZN005-IKUNNR  = 初期値.              "入金先
* W_ZN005-ILIFNR  = 初期値.              "相殺先
W_ZN005-IMEMO   = W_BSID_O-SGTXT.      "備考
W_ZN005-INSDT   = SY-DATUM.            "登録日
W_ZN005-INSTM   = SY-UZEIT.            "登録時刻
W_ZN005-INSUSR  = SY-UNAME.            "登録ユーザ
* W_ZN005-AUFLG   = 初期値.              "入金消込
* W_ZN005-AUDAT   = 初期値.              "入金消込日
* W_ZN005-AUTIM   = 初期値.              "入金消込時刻
* W_ZN005-AUUSR   = 初期値.              "入金消込ユーザ
* W_ZN005-IBELNER = 初期値.              "決済伝票番号
* W_ZN005-IGJAHR  = 初期値.              "決済会計年度
* W_ZN005-IBUDAT  = 初期値.              "決済転記日
APPEND W_ZN005 TO T_ZN005.
G_CNT_ZN005 = G_CNT_ZN005 + 1.

ENDFORM.                    " SET_ZN005
*&---------------------------------------------------------------------*
*&      Form  SET_YN120
*&---------------------------------------------------------------------*
*       YN120 マッピング
*----------------------------------------------------------------------*
FORM SET_YN120.
G_RENNO = G_RENNO + 1.
CLEAR:W_YN120.
W_YN120-GJAHR     = C_GJAHR.        "伝票会計年度
* W_YN120-SLPDOC    = "I "+YK310の入金予定番号
CONCATENATE 'I0' G_RENNO
INTO W_YN120-SLPDOC.
W_YN120-DTLDOC    = C_INIT_REC.      "伝票明細番号
W_YN120-VRFCTON   = W_BSID_O-KUNNR. "得意先コード(支払人)
W_YN120-BUKRS     = C_BUKRS.         "会社コード
*2012/05/22 MOD START
W_YN120-ZFBDT     = P_INP_CZ.        "計上締日
* PERFORM GET_SIMEBI.
*2012/05/22 MOD END

* W_YN120-PRCTR     = 初期値.          "利益センタ
W_YN120-PERNR     = SY-UNAME.        "担当者
W_YN120-DVSON     = W_BSID_O-VKBUR.  "営業所
W_YN120-TGTFLG    = C_ON.            "照合対象フラグ
W_YN120-CSTS      = C_SEVEN.         "照合ステータス(7)
W_YN120-COMMT     = TEXT-019.        "コメント(振替後入金予定)
* W_YN120-YNGJAHR   = 初期値.          "会計伝票会計年度
* W_YN120-BELNR     = 初期値.          "会計伝票番号
* W_YN120-BUZEI     = 初期値.          "会計伝票明細番号
* W_YN120-KNQUAN    = 初期値.          "数量
* W_YN120-KNUNIT    = 初期値.          "単位
* W_YN120-KNUNTPRC  = 初期値.          "単価
* W_YN120-KNTAXAMT  = 初期値.          "消費税額
* W_YN120-KNITXAMT  = 初期値.          "税抜金額
W_YN120-KNETXAMT  = W_BSID_O-WRBTR.  "税込金額
W_YN120-WAERS     = W_BSID_O-WAERS.  "通貨コード
* W_YN120-CHKDOC    = .  "照合番号
CONCATENATE 'I' W_BSID_O-BELNR W_BSID_O-BUZEI
INTO W_YN120-CHKDOC.

W_YN120-CHKDAT    = SY-DATUM.        "照合日
W_YN120-CHKUSR    = SY-UNAME.        "照合ユーザ
W_YN120-UPDDAT    = SY-DATUM.        "更新日
W_YN120-UPDTIM    = SY-UZEIT.        "更新時刻
W_YN120-UPDUSR    = SY-UNAME.        "更新ユーザ
W_YN120-UPDPRG    = SY-REPID.        "更新プログラム
* W_YN120-CKEY1     = 初期値.          "照合キー1：得意先発注番号
* W_YN120-CKEY2     = 初期値.          "照合キー2：インボイスNo
* W_YN120-CKEY3     = 初期値.          "照合キー3
* W_YN120-CKEY4     = 初期値.          "照合キー4
* W_YN120-CKEY5     = 初期値.          "照合キー5
* W_YN120-CKEY6     = 初期値.          "照合キー6
* W_YN120-CKEY7     = 初期値.          "照合キー7
* W_YN120-CKEY8     = 初期値.          "照合キー8
W_YN120-LIST1     = W_BSID_O-KUNNR. "得意先コード(受注先)
* W_YN120-LIST2     = 初期値.          "得意先発注番号
* W_YN120-LIST3     = 初期値.          "受注伝票
* W_YN120-LIST4     = 初期値.          "営業グループ
* W_YN120-LIST5     = 初期値.          "品目コード
* W_YN120-LIST6     = 初期値.          "品目テキスト
* W_YN120-LIST7     = 初期値.          "得意先品目
* W_YN120-LIST8     = 初期値.          "インボイスNo
* W_YN120-LIST9     = 初期値.          "一覧表示項目9
* W_YN120-LIST10    = 初期値.          "一覧表示項目10
W_YN120-INSDT     = SY-DATUM.        "登録日
W_YN120-LDATE1    = SY-DATUM.        "請求日
W_YN120-CZFBDT    = P_INP_CZ.        "照合締日
* W_YN120-AUTOFLG   = 初期値.          "自動生成明細（部分照合）
* W_YN120-OUTMNTH3  =  初期値.         "月締請求書出力月
* W_YN120-PRINTDT3  = 初期値.          "月締請求書出力日
* W_YN120-DUMMYMNTH = 初期値.          "ダミー月
* W_YN120-DUMMYDT   = 初期値.          "ダミー日付
APPEND W_YN120 TO T_YN120.
G_CNT_YN120 = G_CNT_YN120 + 1.

ENDFORM.                    " SET_YN120
*&---------------------------------------------------------------------*
*&      Form  GET_RENBAN
*&---------------------------------------------------------------------*
*       I0始まりの最大伝票番号を取得
*----------------------------------------------------------------------*
FORM GET_RENBAN.
TYPES:BEGIN OF L_TYP_YN120,
SLPDOC TYPE YN120-SLPDOC,
END OF L_TYP_YN120.
DATA: L_T_YN120 TYPE STANDARD TABLE OF L_TYP_YN120,
L_W_YN120 TYPE L_TYP_YN120.

REFRESH L_T_YN120.

SELECT  SLPDOC
INTO TABLE  L_T_YN120
FROM YN120
WHERE SLPDOC LIKE 'I0%'.

IF L_T_YN120[] IS INITIAL.
G_RENNO = 0.
ELSE.
SORT  L_T_YN120 BY SLPDOC DESCENDING .
READ TABLE  L_T_YN120 INTO L_W_YN120 INDEX 1.
G_RENNO = L_W_YN120-SLPDOC+2(8).
ENDIF.



ENDFORM.                    " GET_RENBAN
*&---------------------------------------------------------------------*
*&      Form  GET_SIMEBI
*&---------------------------------------------------------------------*
*       締日取得
*----------------------------------------------------------------------*
FORM GET_SIMEBI.
CALL FUNCTION 'YK_ZFBDT_GET_BY_KNVV'
EXPORTING
I_CORD               = W_BSID_O-KUNNR
I_VKORG              = C_VKORG
I_VTWEG              = C_VTWEG
I_SPART              = C_SPARt
I_DAY                = W_BSID_O-BUDAT
*     I_ZTERM              = 'Y000'
IMPORTING
E_ZFBDT              = W_YN120-ZFBDT
*     E_ZFBDT_OLD          =
*     E_SIHARAIBI          =
*     E_ZTERM              =
EXCEPTIONS
NO_GET_T052          = 1
NO_GOOD_GETDAY       = 2
NO_GET_ZTERM         = 3
OTHERS               = 4.
IF SY-SUBRC <> 0.
CLEAR:W_YN120-ZFBDT.
ENDIF.

ENDFORM.                    " GET_SIMEBI
*&---------------------------------------------------------------------*
*&      Form  SET_YN110
*&---------------------------------------------------------------------*
*       YN110 マッピング
*----------------------------------------------------------------------*
FORM SET_YN110.
CLEAR:W_YN110.

W_YN110-GJAHR      = C_GJAHR.         "外部会計年度
W_YN110-SLPDOC     = W_YN120-SLPDOC.  "外部番号
W_YN110-DTLDOC     = C_INIT_REC.      "外部明細番号
W_YN110-VRFCTON    = W_BSID_O-KUNNR.  "得意先コード(支払人)
W_YN110-BUKRS      = C_BUKRS.         "会社コード
W_YN110-ZFBDT      = W_YN120-ZFBDT.   "計上締日
* W_YN110-PRCTR      = 初期値.          "利益センタ
W_YN110-PERNR      = SY-UNAME.        "担当者
W_YN110-DVSON      = W_BSID_O-VKBUR.  "営業所
W_YN110-TGTFLG     = C_ON.            "照合対象フラグ
W_YN110-CSTS       = C_SEVEN.         "照合ステータス
W_YN110-COMMT      = TEXT-019.        "コメント(振替後入金予定)
* W_YN110-KNQUAN     = 初期値.          "数量
* W_YN110-KNUNIT     = 初期値.          "単位
* W_YN110-KNUNTPRC   = 初期値.          "単価
* W_YN110-KNTAXAMT   = 初期値.          "消費税額
* W_YN110-KNITXAMT   = 初期値.          "税抜金額
W_YN110-KNETXAMT   = W_BSID_O-WRBTR.  "税込金額
W_YN110-WAERS      = W_BSID_O-WAERS. "通貨コード
W_YN110-CHKDOC     = W_YN120-CHKDOC.  "照合番号
W_YN110-CHKDAT     = SY-DATUM.        "照合日
W_YN110-CHKUSR     = SY-UNAME.        "照合ユーザ
W_YN110-UPDDAT     = SY-DATUM.        "更新日
W_YN110-UPDTIM     = SY-UZEIT.        "更新時刻
W_YN110-UPDUSR     = SY-UNAME.        "更新ユーザ
W_YN110-UPDPRG     = SY-REPID.        "更新プログラム
* W_YN110-DELFLG     = 初期値.          "削除フラグ
* W_YN110-OLDGJAHR   = 初期値.          "元会計年度
* W_YN110-OLDSLPDOC  = 初期値.          "元外部番号
* W_YN110-OLDDTLDOC  = 初期値.          "元外部明細番号
* W_YN110-CKEY1      = 初期値.          "照合キー1：得意先発注番号
* W_YN110-CKEY2      = 初期値.          "照合キー2：インボイスNo
* W_YN110-CKEY3      = 初期値.          "照合キー3
* W_YN110-CKEY4      = 初期値.          "照合キー4
* W_YN110-CKEY5      = 初期値.          "照合キー5
* W_YN110-CKEY6      = 初期値.          "照合キー6
* W_YN110-CKEY7      = 初期値.          "照合キー7
* W_YN110-CKEY8      = 初期値.          "照合キー8
W_YN110-LIST1      = W_BSID_O-KUNNR. "得意先コード(受注先)
* W_YN110-LIST2      = 初期値.          "得意先発注番号
* W_YN110-LIST3      = 初期値.          "受注伝票
* W_YN110-LIST4      = 初期値.          "営業グループ
* W_YN110-LIST5      = 初期値.          "品目コード
* W_YN110-LIST6      = 初期値.          "品目テキスト
* W_YN110-LIST7      = 初期値.          "得意先品目
* W_YN110-LIST8      = 初期値.          "インボイスNo
* W_YN110-LIST9      = 初期値.          "一覧表示項目9
* W_YN110-LIST10     = 初期値.          "一覧表示項目10
W_YN110-INSDT      = SY-DATUM.        "登録日
W_YN110-LDATE1     = SY-DATUM.        "検収日
W_YN110-CZFBDT     = P_INP_CZ.        "照合締日
* W_YN110-AUTOFLG_S  = 初期値.          "自動生成明細（月締請求）
APPEND W_YN110 TO T_YN110.
G_CNT_YN110 = G_CNT_YN110 + 1.

ENDFORM.                    " SET_YN110
*&---------------------------------------------------------------------*
*&      Form  INSERT_DATA
*&---------------------------------------------------------------------*
*       データ登録
*----------------------------------------------------------------------*
FORM INSERT_DATA.

IF NOT T_ZN005[] IS INITIAL.
INSERT ZN005 FROM TABLE T_ZN005
ACCEPTING DUPLICATE KEYS.
IF SY-SUBRC = 0.
*    COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'ZN005'.
ENDIF.
ENDIF.

IF NOT T_YN110[] IS INITIAL.
INSERT YN110 FROM TABLE T_YN110
ACCEPTING DUPLICATE KEYS.
IF SY-SUBRC = 0.
*    COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN110'.
ENDIF.
ENDIF.

IF NOT T_YN120[] IS INITIAL.
INSERT YN120 FROM TABLE T_YN120
ACCEPTING DUPLICATE KEYS.
IF SY-SUBRC = 0.
*    COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.
ENDIF.

COMMIT WORK AND WAIT.

ENDFORM.                    " INSERT_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_IKO
*&---------------------------------------------------------------------*
*      処理結果出力【移行】
*----------------------------------------------------------------------*
FORM WRITE_IKO.
DATA:LW_CNT(10)  TYPE C,
LW_ERR      TYPE TYP_ERR.

WRITE G_CNT_BSID TO LW_CNT.
WRITE: /001 '<データ移行>',
/003 '会計管理: 得意先の二次索引(BSID)抽出件数：',
045 LW_CNT,
055 '件'.
SKIP 1.

WRITE G_CNT_ZN005 TO LW_CNT.
WRITE: /003 '入金予定（売上）(ZN005)登録件数：',
045 LW_CNT,
055 '件'.
WRITE G_CNT_YN110 TO LW_CNT.
WRITE: /003 '外部データ(売上)(YN110)登録件数：',
045 LW_CNT,
055 '件'.
WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)登録件数：',
045 LW_CNT,
055 '件'.
WRITE G_CNT_ERR TO LW_CNT.
WRITE: /003 'エラー件数：',
045 LW_CNT,
055 '件'.

CHECK NOT T_ERR[] IS INITIAL.
SKIP 1.
WRITE: /003 '伝票番号',
014 '明細',
029 'エラー内容'.
ULINE.
LOOP AT T_ERR INTO LW_ERR.
WRITE: /003 LW_ERR-BELNR,
014 LW_ERR-BUZEI,
029 LW_ERR-MESSAGE.
ENDLOOP.

ENDFORM.                    " WRITE_IKO
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA_GET
*&---------------------------------------------------------------------*
*       削除対象のデータ取得
*----------------------------------------------------------------------*
FORM DEL_DATA_GET.
CLEAR:G_CNT_ZN005,G_CNT_YN110,G_CNT_YN120.
REFRESH:T_ZN005,T_YN110,T_YN120.
* ZN005
IF P_ZN005 = C_ON.
SELECT *
INTO TABLE T_ZN005
FROM ZN005
WHERE BUKRS   IN P_BUK_Z
AND KUNNR   IN P_KUN_Z
AND CZFBDT  IN P_CZF_Z
AND SEQ     IN P_SEQ_Z.
ENDIF.

* YN110/YN120
IF P_YN = C_ON.
SELECT *
INTO TABLE T_YN110
FROM YN110
WHERE GJAHR   IN P_GJA_1
AND SLPDOC  IN P_SLP_1
AND DTLDOC  IN P_DTL_1
AND VRFCTON IN P_VRF_1
AND BUKRS   IN P_BUK_1
AND ZFBDT   IN P_ZFB_1.
SELECT *
INTO TABLE T_YN120
FROM YN120
WHERE GJAHR   IN P_GJA_1
AND SLPDOC  IN P_SLP_1
AND DTLDOC  IN P_DTL_1
AND VRFCTON IN P_VRF_1
AND BUKRS   IN P_BUK_1
AND ZFBDT   IN P_ZFB_1.
ENDIF.


DESCRIBE TABLE T_ZN005 LINES G_CNT_ZN005.
DESCRIBE TABLE T_YN110 LINES G_CNT_YN110.
DESCRIBE TABLE T_YN120 LINES G_CNT_YN120.

IF  G_CNT_ZN005 IS INITIAL
AND G_CNT_YN110 IS INITIAL
AND G_CNT_YN120 IS INITIAL.
MESSAGE S762(YN01).
G_ERR = C_ON.
ENDIF.

ENDFORM.                    " DEL_DATA_GET
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA
*&---------------------------------------------------------------------*
*       データ削除
*----------------------------------------------------------------------*
FORM DEL_DATA.
CLEAR:G_ERR.
IF NOT T_ZN005[] IS INITIAL.
DELETE ZN005 FROM TABLE T_ZN005.
IF SY-SUBRC = 0.
*      COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'ZN005'.
ENDIF.
ENDIF.

IF NOT T_YN110[] IS INITIAL.
DELETE YN110 FROM TABLE T_YN110.
IF SY-SUBRC = 0.
*      COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN110'.
ENDIF.
ENDIF.

IF NOT T_YN120[] IS INITIAL.
DELETE YN120 FROM TABLE T_YN120.
IF SY-SUBRC = 0.
*      COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.
ENDIF.

COMMIT WORK AND WAIT.

ENDFORM.                    " DEL_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_DEL
*&---------------------------------------------------------------------*
*       処理結果出力【削除】
*----------------------------------------------------------------------*
FORM WRITE_DEL.
DATA:LW_CNT(10)  TYPE C.
WRITE: /001 '<移行データ削除>'.

WRITE G_CNT_ZN005 TO LW_CNT.
WRITE: /003 '入金予定（売上）(ZN005)削除件数：',
030 LW_CNT,
040 '件'.

WRITE G_CNT_YN110 TO LW_CNT.
WRITE: /003 '外部データ(売上)(YN110)削除件数：',
030 LW_CNT,
040 '件'.

WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)削除件数：',
030 LW_CNT,
040 '件'.
ENDFORM.                    " WRITE_DEL
