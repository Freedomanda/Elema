*& ISID Co.,Ltd. 2008 All rights.reserved.
*&---------------------------------------------------------------------*
*& プログラム：ZRLBCR0040
*& 作成者    ：ISID T.Ofuji
*& 作成日付  ：2008/04/30
*& 機能ID    ：
*&---------------------------------------------------------------------*
*& 処理概要&
*&   先行処理（ZRLBCR0030）又は、直接実行により指定した
*&   バッチインプットデータの実行を行う。
*&---------------------------------------------------------------------*
*& 更新履歴&
*& No.   更新日付    更新者    更新内容
*& 新規  2008/04/30  T.Ofuji
*& #001  2012/10/01  ISID144   レイアウト日本語化
*&---------------------------------------------------------------------*
REPORT ZRLBCR0040 NO STANDARD PAGE HEADING
LINE-SIZE 109
MESSAGE-ID ZRLBC00.

*----------------------------------------------------------------------*
*                            TABLES                                    *
*----------------------------------------------------------------------*
TABLES: TSTCT,
T100,
ZTRLBCT001,
ZTRLBCT002,
ZTRLBCT006,
DD03L,
DD04T.

*----------------------------------------------------------------------*
*                             DATA                                     *
*----------------------------------------------------------------------*
DATA:
GDT_BIDAT2 TYPE STANDARD TABLE OF ZTRLBCT002,
GDT_BIDLOG TYPE STANDARD TABLE OF ZTRLBCT005,
*
GDS_BIDAT2 TYPE ZTRLBCT002,
GDS_BIDLOG TYPE ZTRLBCT005,
*
WA_TABNAME   LIKE DNTAB-TABNAME,
WA_FIELDNAME LIKE DNTAB-FIELDNAME,
WA_SCRTEXT_L LIKE DD04T-SCRTEXT_L,
WA_MSG(480),
WA_TTEXT  LIKE TSTCT-TTEXT,
WA_RECID        TYPE ZTRLBCT006-RECID,           "#001 ADD 20121001
SW_INTENSIFIED TYPE I VALUE 1.

*----------------------------------------------------------------------*
*                             CONSTANTS                                *----------------------------------------------------------------------*
CONSTANTS:
GCF_ON(01)         TYPE C VALUE 'X',
GCF_CLN(01)        TYPE C VALUE ':',
GCF_NO(02)         TYPE C VALUE 'NO',
GCF_OK(02)         TYPE C VALUE 'OK',
GCF_NG(02)         TYPE C VALUE 'NG',
GCF_BACK(04)       TYPE C VALUE 'BACK',
GCF_END(03)        TYPE C VALUE 'END',
GCF_REFRESH(07)    TYPE C VALUE 'REFRESH',
GCF_EXECUTE(07)    TYPE C VALUE 'EXECUTE',
GCF_EXECMODEA(09)  TYPE C VALUE 'EXECMODEA',
GCF_BATCHJOB(08)   TYPE C VALUE 'BATCHJOB'.

*----------------------------------------------------------------------*
*                           PARAMETERS                                 *
*----------------------------------------------------------------------*
PARAMETERS: P_CTLNO    LIKE ZTRLBCT001-CTLNO,
P_DATSEQ   LIKE ZTRLBCT001-DATASEQ.

*----------------------------------------------------------------------*
*                            INCLUDE                                   *
*----------------------------------------------------------------------*
INCLUDE <ICON>.

*----------------------------------------------------------------------*
*                          START-OF-SELECTION                          *
*----------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM SUB_REFRESH.

*----------------------------------------------------------------------*
*                             TOP-OF-PAGE                              *
*----------------------------------------------------------------------*
TOP-OF-PAGE.

PERFORM SUB_HEADER.
*
TOP-OF-PAGE DURING LINE-SELECTION.

PERFORM SUB_HEADER.

*----------------------------------------------------------------------*
*                            AT USER-COMMAND                           *
*----------------------------------------------------------------------*
AT USER-COMMAND.

CASE SY-UCOMM.
WHEN GCF_BACK.
LEAVE TO SCREEN 0.
WHEN GCF_END.
LEAVE PROGRAM.
WHEN GCF_REFRESH.                              "リフレッシュ
SY-LSIND = SY-LSIND - 1.
PERFORM SUB_REFRESH.
WHEN GCF_EXECUTE.                              "処理実行
PERFORM SUB_EXECUTE USING 'N'.
WHEN GCF_EXECMODEA.                            "画面表示実行
PERFORM SUB_EXECUTE USING 'A'.
************************************************ #001 ADD 20121001 STA *
WHEN GCF_BATCHJOB.
PERFORM SUB_SUBMIT_BIJOB.                    "バックグランド実行
************************************************ #001 ADD 20121001 END *
ENDCASE.

*&---------------------------------------------------------------------*
*&      処理実行
*&---------------------------------------------------------------------*
FORM SUB_EXECUTE  USING P_MODE.

CALL FUNCTION 'ZRL_BC_BI_EXECUTE'
EXPORTING
CTLNO   = P_CTLNO                            "コントロールＮＯ
DATASEQ = P_DATSEQ                           "データ番号
MODE    = P_MODE.                            "実行モード

ENDFORM.                    " SUB_EXECUTE

*&---------------------------------------------------------------------*
*&      メイン画面表示
*&---------------------------------------------------------------------*
FORM SUB_REFRESH .

CONSTANTS:
LCF_AST(01)      TYPE C VALUE '*',
LCF_HFN(01)      TYPE C VALUE '-',
LCF_TYP(01)      TYPE C VALUE 'T',
LCF_MSGID(05)    TYPE C VALUE 'MSGID',
LCF_ANP(01)      TYPE C VALUE '&',
LCF_ANP1(02)     TYPE C VALUE '&1',
LCF_ANP2(02)     TYPE C VALUE '&2',
LCF_ANP3(02)     TYPE C VALUE '&3',
LCF_ANP4(02)     TYPE C VALUE '&4'.


* バッチインプットデータ（詳細）
SELECT * FROM ZTRLBCT002
INTO TABLE GDT_BIDAT2
WHERE CTLNO = P_CTLNO                   "コントロールＮＯ
AND DATASEQ = P_DATSEQ.               "データ番号
SORT GDT_BIDAT2 BY CTLNO DATASEQ SEQNO.

* 詳細データ表示
FORMAT COLOR COL_HEADING.
ULINE.
WRITE : /001 SY-VLINE NO-GAP,
002 GCF_NO   NO-GAP,
006 SY-VLINE NO-GAP,
007 TEXT-002 NO-GAP,
027 SY-VLINE NO-GAP,
028 TEXT-003 NO-GAP,
068 SY-VLINE NO-GAP,
069 TEXT-004 NO-GAP,
109 SY-VLINE NO-GAP.
ULINE.

FORMAT COLOR COL_NORMAL.
SW_INTENSIFIED = 1.
LOOP AT GDT_BIDAT2 INTO GDS_BIDAT2.

IF SW_INTENSIFIED > 0.
FORMAT INTENSIFIED ON.
ELSE.
FORMAT INTENSIFIED OFF.
ENDIF.

SW_INTENSIFIED = SW_INTENSIFIED * ( -1 ).
WRITE : /001 SY-VLINE NO-GAP,
002 GDS_BIDAT2-SEQNO NO-GAP,
006 SY-VLINE NO-GAP,
007 GDS_BIDAT2-FNAM NO-GAP,
027 SY-VLINE NO-GAP.

WA_SCRTEXT_L = ''.
SPLIT GDS_BIDAT2-FNAM AT LCF_HFN
INTO WA_TABNAME WA_FIELDNAME.

IF WA_TABNAME(1) = LCF_AST.
WA_TABNAME = WA_TABNAME+1(29).
ENDIF.

*   項目のエレメントテキスト取得
SELECT SINGLE * FROM DD03L
WHERE TABNAME = WA_TABNAME
AND FIELDNAME = WA_FIELDNAME.
IF SY-SUBRC = 0.
SELECT SINGLE * FROM DD04T
WHERE ROLLNAME = DD03L-ROLLNAME
AND DDLANGUAGE = SY-LANGU.

IF SY-SUBRC = 0.
WA_SCRTEXT_L = DD04T-SCRTEXT_L.
ENDIF.

ENDIF.

WRITE :  028 WA_SCRTEXT_L NO-GAP,
068 SY-VLINE NO-GAP,
069 GDS_BIDAT2-FVAL NO-GAP,
109 SY-VLINE NO-GAP.
ENDLOOP.
ULINE.

* バッチインプット結果一覧表示
SKIP 2.
* バッチインプットデータ処理ログ
SELECT * FROM ZTRLBCT005
INTO TABLE GDT_BIDLOG
WHERE CTLNO = P_CTLNO                    "コントロールＮＯ
AND DATASEQ = P_DATSEQ.                "データ番号
*  IF SY-TABIX > 0.                                "#001 DEL 20121001
IF SY-SUBRC = 0.                                 "#001 ADD 20121001
SORT GDT_BIDLOG BY CTLNO DATASEQ LOGSEQ.
FORMAT COLOR COL_HEADING.
ULINE.
WRITE : /001 SY-VLINE  NO-GAP,
002 GCF_NO    NO-GAP,
006 SY-VLINE  NO-GAP,
007 LCF_TYP   NO-GAP,
008 SY-VLINE  NO-GAP,
009 LCF_MSGID NO-GAP,
029 SY-VLINE  NO-GAP,
030 GCF_NO    NO-GAP,
033 SY-VLINE  NO-GAP,
034 TEXT-005  NO-GAP,
109 SY-VLINE  NO-GAP.

ULINE.
FORMAT COLOR COL_NORMAL.
LOOP AT GDT_BIDLOG INTO GDS_BIDLOG.

WRITE : /001 SY-VLINE NO-GAP,
002 GDS_BIDLOG-LOGSEQ NO-GAP,
006 SY-VLINE NO-GAP.

SELECT SINGLE * FROM T100
WHERE SPRSL = SY-LANGU
AND ARBGB = GDS_BIDLOG-MSGID
AND MSGNR = GDS_BIDLOG-MSGNO.

IF SY-SUBRC = 0.
WA_MSG = T100-TEXT.
IF WA_MSG CS LCF_ANP1.
REPLACE LCF_ANP1 WITH GDS_BIDLOG-MSGV1 INTO WA_MSG.
REPLACE LCF_ANP2 WITH GDS_BIDLOG-MSGV2 INTO WA_MSG.
REPLACE LCF_ANP3 WITH GDS_BIDLOG-MSGV3 INTO WA_MSG.
REPLACE LCF_ANP4 WITH GDS_BIDLOG-MSGV4 INTO WA_MSG.
ELSE.
REPLACE LCF_ANP WITH GDS_BIDLOG-MSGV1 INTO WA_MSG.
REPLACE LCF_ANP WITH GDS_BIDLOG-MSGV2 INTO WA_MSG.
REPLACE LCF_ANP WITH GDS_BIDLOG-MSGV3 INTO WA_MSG.
REPLACE LCF_ANP WITH GDS_BIDLOG-MSGV4 INTO WA_MSG.
ENDIF.
CONDENSE WA_MSG.
ENDIF.

WRITE :  007 GDS_BIDLOG-MSGTY NO-GAP,
008 SY-VLINE NO-GAP,
009 GDS_BIDLOG-MSGID NO-GAP,
029 SY-VLINE NO-GAP,
030 GDS_BIDLOG-MSGNO NO-GAP,
033 SY-VLINE NO-GAP,
034 WA_MSG NO-GAP,
109 SY-VLINE NO-GAP.
ENDLOOP.
ULINE.
ENDIF.

ENDFORM.                    " SUB_REFRESH
*&---------------------------------------------------------------------*
*&      ヘッダ編集
*&---------------------------------------------------------------------*
FORM SUB_HEADER .

* バッチインプットデータ処理合計
SELECT SINGLE * FROM  ZTRLBCT006
WHERE CTLNO = P_CTLNO.           "コントロールＮＯ

FORMAT COLOR COL_BACKGROUND.
WRITE : /001 TEXT-006 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-CTLNO.
WRITE : /001 TEXT-007 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-RECID,
032 ZTRLBCT006-RECTX.
WA_RECID = ZTRLBCT006-RECID.                     "#001 ADD 20121001
WA_TTEXT = ''.
* トランザクションコ−ドテキスト取得
SELECT SINGLE * FROM  TSTCT
WHERE SPRSL = SY-LANGU
AND   TCODE = ZTRLBCT006-TCODE.
IF SY-SUBRC = 0.
WA_TTEXT = TSTCT-TTEXT.
ENDIF.

WRITE : /001 TEXT-008 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-TCODE,
032 WA_TTEXT.
WRITE : /001 TEXT-009 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-DATUM,
030 ZTRLBCT006-TIMES.
WRITE :  043 TEXT-010 NO-GAP, 051 GCF_CLN, 053 P_DATSEQ.

* バッチインプットデータ（概要）
SELECT SINGLE * FROM  ZTRLBCT001
WHERE CTLNO = P_CTLNO
AND   DATASEQ = P_DATSEQ.
IF SY-SUBRC = 0.
CASE ZTRLBCT001-STATUS.
WHEN ' '.
WRITE : 060 ICON_YELLOW_LIGHT AS ICON.
SET PF-STATUS '1000'.
WHEN GCF_NG.
WRITE : 060 ICON_RED_LIGHT AS ICON.
SET PF-STATUS '1000'.
WHEN GCF_OK.
WRITE : 060 ICON_GREEN_LIGHT AS ICON.
SET PF-STATUS '2000'.
ENDCASE.
ENDIF.
FORMAT COLOR COL_HEADING.

ENDFORM.                    " SUB_HEADER
*&---------------------------------------------------------------------*
*&      バックグランド実行
*&---------------------------------------------------------------------*
FORM SUB_SUBMIT_BIJOB.

DATA: L_JOBNAME   LIKE TBTCJOB-JOBNAME.
DATA: L_JOBCOUNT  LIKE TBTCJOB-JOBCOUNT.

CONSTANTS:
LCF_JOB(06)     TYPE C VALUE 'BIJOB-',
LCF_HFN(01)     TYPE C VALUE '-'.

* ジョブ名編集
CONCATENATE LCF_JOB WA_RECID LCF_HFN P_CTLNO INTO L_JOBNAME.
CONDENSE L_JOBNAME NO-GAPS.

* ジョブ開始
CALL FUNCTION 'JOB_OPEN'
EXPORTING
JOBNAME  = L_JOBNAME
IMPORTING
JOBCOUNT = L_JOBCOUNT.

* バックグランドで処理実行
SUBMIT ZRLBCR0021 WITH P_CTLNO  = P_CTLNO        "コントロールＮＯ
WITH P_RECID  = WA_RECID       "記録ID
WITH P_DATSEQ = P_DATSEQ       "データ番号
USER SY-UNAME VIA JOB L_JOBNAME NUMBER L_JOBCOUNT
AND RETURN.

* ジョブ終了
CALL FUNCTION 'JOB_CLOSE'
EXPORTING
JOBCOUNT  = L_JOBCOUNT
JOBNAME   = L_JOBNAME
STRTIMMED = GCF_ON.

* バッチインプットジョブ & を起動しました。
MESSAGE S410 WITH L_JOBNAME.

ENDFORM.                    " SUB_SUBMIT_BIJOB
*Text symbol text・
*001:Clint
*002:Item
*003:Text
*004:Value
*005:Message
*006:Control No.
*007:Rec ID
*008:Transaction
*009:CreDate
*010:Data No.
*Selection text・
*P_CTLNO:        Control No.
*P_DATSEQ:        Data No.
