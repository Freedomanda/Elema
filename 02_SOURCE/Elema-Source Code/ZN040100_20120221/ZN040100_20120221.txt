*&---------------------------------------------------------------------*
*&  REPORT ZN040100                                                    *
*&         支払通知書IF(移行期間用)                                    *
*&---------------------------------------------------------------------*
*&  機能     :当月仕入高を出力する速報（検収明細書）と                 *
*&            当月照合額を出力する確報（支払通知書）の                 *
*&            IFファイルを出力する。                                   *
*&                                                                     *
*&  作成日   : 2012/01/18                                              *
*&  作成者   : H.KOMIYAMA(SOLFIS)                                      *
*&  変更履歴 : 2012/01/24 H.YAMASHITA                                  *
*&  変更内容 : 注文番号の加工時に、明細の前０を除去する。              *
*&  変更履歴 :                                                         *
*&  変更内容 :                                                         *
*&---------------------------------------------------------------------*
REPORT ZN040100
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.

*----------------------------------------------------------------------*
*   宣言部
*----------------------------------------------------------------------*
*** 参照テーブル
TABLES: LFB1.

*** 構造
*-- 仕入先コード取得用
DATA: BEGIN OF W_LFM1,
LIFNR LIKE LFM1-LIFNR,              " 仕入先コード
ZTERM LIKE LFM1-ZTERM,              " 支払条件キー
WAERS LIKE LFM1-WAERS,              " 通貨コード
END   OF W_LFM1,
*-- 支払条件取得用
BEGIN OF W_T052,
ZTERM LIKE T052-ZTERM,              " 支払条件キー
ZTAGG LIKE T052-ZTAGG,              " 期限
END   OF W_T052,
*-- 出力対象データ取得用
BEGIN OF W_YK230,
LIFNR    LIKE YK230-LIFNR,          " 仕入先or債権者の勘定コード
WERKST   LIKE YK230-WERKST,         " 取引プラント
EBELN    LIKE YK230-EBELN,          " 購買伝票番号
EBELP    LIKE YK230-EBELP,          " 購買伝票の明細番号
MAKTX    LIKE YK230-MAKTX,          " 品目テキスト
MATNR    LIKE YK230-MATNR,          " 品目コード
BUDAT    LIKE YK230-BUDAT,          " 伝票の転記日付
KNQUAN   LIKE YK230-KNQUAN,         " 数量
KNUNTPRC LIKE YK230-KNUNTPRC,       " 単価
KNITXAMT LIKE YK230-KNITXAMT,       " 税抜金額
KNTAXAMT LIKE YK230-KNTAXAMT,       " 消費税額
KNETXAMT LIKE YK230-KNETXAMT,       " 税込金額
BELNR    LIKE YK230-BELNR,          " 請求書伝票の伝票番号
BUZEI    LIKE YK230-BUZEI,          " 請求書伝票内伝票明細
END   OF W_YK230,
*-- 仕入先マスタ一括取得用
BEGIN OF W_LFX1,
LIFNR LIKE LFB1-LIFNR,              " 仕入先コード(請求先)
PSTLZ LIKE LFA1-PSTLZ,              " 仕入先郵便番号
ADRNR LIKE LFA1-ADRNR,              " 住所
TLFXS LIKE LFB1-TLFXS,              " ファックス番号
END   OF W_LFX1,
*-- アドレス情報一括取得用
BEGIN OF W_ADRC,
ADDRNUMBER LIKE ADRC-ADDRNUMBER,    " アドレス番号
DATE_FROM  LIKE ADRC-DATE_FROM,     " 有効開始日付
NATION     LIKE ADRC-NATION,        " 国際アドレスバージョン ID
NAME2      LIKE ADRC-NAME2,         " 名前 2
CITY1      LIKE ADRC-CITY1,         " 市区町村
STREET     LIKE ADRC-STREET,        " 都道府県名
*** 2012/02/09 INS START ***
REGION     LIKE ADRC-REGION,        " 地域
*** 2012/02/09 INS START ***
BEZEI      LIKE T005U-BEZEI,        " 地域 (都道府県）
END   OF W_ADRC,
*-- プラント一括取得用
BEGIN OF W_T001W,
WERKS      LIKE T001W-WERKS,        " プラント
NAME1      LIKE T001W-NAME1,        " 名称
END   OF W_T001W,
*-- IFファイル編集用
BEGIN OF W_DATA,
YEARS   TYPE STRING,                " 計上年月
TNAME   TYPE STRING,                " 帳票名称
LIFNR   TYPE STRING,                " 仕入先コード
SEQNO   TYPE STRING,                " SEQ
ODATE   TYPE STRING,                " 発行日
NAME2   TYPE STRING,                " 仕入先名
PSTLZ   TYPE STRING,                " 仕入先郵便番号
BEZEI   TYPE STRING,                " 地域
STREE   TYPE STRING,                " 都道府県
CITY1   TYPE STRING,                " 市区町村
TELFX   TYPE STRING,                " ファックス番号
TOTAL   TYPE STRING,                " 総額
WAERS   TYPE STRING,                " 通貨コード
ZLSC1   TYPE STRING,                " 金種1-文言
WRBT1   TYPE STRING,                " 金種1-金額
ZFBD1   TYPE STRING,                " 金種1-期日
ZLSC2   TYPE STRING,                " 金種2-文言
WRBT2   TYPE STRING,                " 金種2-金額
ZFBD2   TYPE STRING,                " 金種2-期日
ZLSC3   TYPE STRING,                " 金種3-文言
WRBT3   TYPE STRING,                " 金種3-金額
ZFBD3   TYPE STRING,                " 金種3-期日
ZLSC4   TYPE STRING,                " 金種4-文言
WRBT4   TYPE STRING,                " 金種4-金額
ZFBD4   TYPE STRING,                " 金種4-期日
DVSON   TYPE STRING,                " プラントコード
NAME1   TYPE STRING,                " プラント名
ITXAM   TYPE STRING,                " 税抜金額
TAXAM   TYPE STRING,                " 消費税額
ETXAM   TYPE STRING,                " 税込金額
LDATE   TYPE STRING,                " 入庫日
LIST2   TYPE STRING,                " 注文番号
LIST5   TYPE STRING,                " 品目コード
LIST6   TYPE STRING,                " 品名
KNUNT   TYPE STRING,                " 単価
KNQUA   TYPE STRING,                " 数量
KNITX   TYPE STRING,                " 税抜金額
KNTAX   TYPE STRING,                " 税額
KNETX   TYPE STRING,                " 税込金額
END   OF W_DATA,
*-- エラーログ出力用
BEGIN OF TBL_ERRLOG OCCURS 0,
LIFNR(10) TYPE C,                   " 得意先コード
ERR(128)  TYPE C,                   " ERR
END OF TBL_ERRLOG.

*** 内部テーブル
DATA: I_LFM1     LIKE STANDARD TABLE OF W_LFM1,
I_T052     LIKE SORTED   TABLE OF W_T052
WITH UNIQUE KEY ZTERM ZTAGG,
I_YK230    LIKE STANDARD TABLE OF W_YK230,
I_LFX1     LIKE HASHED   TABLE OF W_LFX1
WITH UNIQUE KEY LIFNR,
I_ADRC     LIKE SORTED TABLE OF W_ADRC
WITH NON-UNIQUE KEY ADDRNUMBER,
I_T001W    LIKE STANDARD TABLE OF W_T001W,
I_DATA     LIKE STANDARD TABLE OF W_DATA.
*** 2012/02/09 INS START ***
*-- 地域コード: テキスト取得用
DATA: BEGIN OF W_T005U,
BLAND    TYPE T005U-BLAND,          " 地域
BEZEI    TYPE T005U-BEZEI,          " 内容説明
END   OF W_T005U,
I_T005U    LIKE SORTED TABLE OF W_T005U
WITH NON-UNIQUE KEY BLAND.
*** 2012/02/09 INS END   ***

*** グローバル変数
DATA: RC_CODE_LOCK TYPE SY-SUBRC,
G_CNT_UPD    TYPE I,               " 更新件数
G_CNT_OUT    TYPE I,               " 出力件数
G_CNT_ERR    TYPE I,               " エラー件数
G_CNT_SEQ(5) TYPE N,               " SEQカウンタ
G_ZFBDT      TYPE DATUM,           " 照合締日
G_SUM_TOTAL  LIKE YN220-KNETXAMT,  " 総額計算用
G_SUM_TAXAM  LIKE YN220-KNTAXAMT,  " 消費税額計算用
G_SUM_ITXAM  LIKE YN220-KNITXAMT,  " 税抜金額計算用
G_SUM_ETXAM  LIKE YN220-KNETXAMT.  " 税込金額計算用

*** 定数
CONSTANTS: C_FLG_ON(1)     TYPE C VALUE 'X',
C_SOKUHO(10)    TYPE C VALUE '検収明細書',
C_KAKUHO(10)    TYPE C VALUE '支払通知書',
*** 2012/01/24 MOD START ***
C_MARK(1)       TYPE C VALUE 'Y'.           "支払通知発行対象
*** 2012/01/24 MOD END ***

*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
*-- 速報/確報区分
SELECTION-SCREEN BEGIN OF BLOCK BK1 WITH FRAME TITLE TEXT-S01.
SELECTION-SCREEN BEGIN OF LINE.
*-- 速報（検収明細書）
PARAMETERS: R_SOKUHO RADIOBUTTON GROUP RA01 DEFAULT 'X'.
SELECTION-SCREEN COMMENT 04(18) TEXT-S02.
*-- 確報（支払通知書）
PARAMETERS: R_KAKUHO RADIOBUTTON GROUP RA01.
SELECTION-SCREEN COMMENT 26(18) TEXT-S03.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK BK1.

*-- データ選択
SELECTION-SCREEN BEGIN OF BLOCK BK2 WITH FRAME TITLE TEXT-S04.
*-- 会社コード
PARAMETERS: P_BUKRS    LIKE T001-BUKRS OBLIGATORY MEMORY ID BUK,
*-- 購買組織
P_EKORG    LIKE LFM1-EKORG OBLIGATORY MEMORY ID EKO,
*-- 出力年月
P_YEARS(6) TYPE N,
*-- 出力締日
P_CDATE(2) TYPE N          OBLIGATORY.
*-- 仕入先コード(請求先)
SELECT-OPTIONS S_LIFNR FOR LFB1-LIFNR.
SELECTION-SCREEN END OF BLOCK BK2.

*-- 出力オプション
SELECTION-SCREEN BEGIN OF BLOCK BK3 WITH FRAME TITLE TEXT-S05.
*-- ディレクトリ名
PARAMETERS: P_DNAME LIKE RLGRAP-FILENAME OBLIGATORY,
*-- ファイル名
P_FNAME LIKE RLGRAP-FILENAME OBLIGATORY,
*-- 起動用ファイル名
P_SNAME LIKE RLGRAP-FILENAME OBLIGATORY.
SELECTION-SCREEN END OF BLOCK BK3.

SKIP 1.

*-- 取引先機能
PARAMETERS: P_PARVW LIKE WYT3-PARVW OBLIGATORY.

************************************************************************
INITIALIZATION.
************************************************************************

************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_DNAME.
************************************************************************
*-- ディレクトリ名の検索ヘルプ
PERFORM F_F4HELP_P_DNAME.

************************************************************************
AT SELECTION-SCREEN.
************************************************************************
*-- 選択画面処理
PERFORM F_SELECTION_SCREEN_PROC.

************************************************************************
START-OF-SELECTION.
************************************************************************
PERFORM F_INIT_SEC.                       " 初期処理
PERFORM F_MAIN_SEC.                       " メイン処理

************************************************************************
END-OF-SELECTION.
************************************************************************

*&---------------------------------------------------------------------*
*&      Form F_F4HELP_P_DNAME
*&---------------------------------------------------------------------*
*       ディレクトリ名のヘルプ出力
*----------------------------------------------------------------------*
FORM F_F4HELP_P_DNAME.

DATA: L_PATH          LIKE DXFIELDS-LONGPATH,
L_ABEND_FLG     LIKE DXFIELDS-ABENDFLAG,
L_STRIPPED_NAME LIKE RLGRAP-FILENAME.

*-- ファイルパスの取得
CALL FUNCTION 'F4_DXFILENAME_TOPRECURSION'
EXPORTING
I_LOCATION_FLAG = 'A'
I_SERVER        = ''
I_PATH          = ''
FILEMASK        = '*.*'
FILEOPERATION   = 'R'
IMPORTING
O_PATH          = L_PATH
ABEND_FLAG      = L_ABEND_FLG
EXCEPTIONS
OTHERS          = 0.

IF L_ABEND_FLG <> C_FLG_ON.
P_DNAME = L_PATH.
*-- ディレクトリ名とファイル名の分割
CALL FUNCTION 'SO_SPLIT_FILE_AND_PATH'
EXPORTING
FULL_NAME     = P_DNAME
IMPORTING
STRIPPED_NAME = L_STRIPPED_NAME
FILE_PATH     = P_DNAME
EXCEPTIONS
OTHERS        = 0.
ENDIF.

ENDFORM.                    " F_F4HELP_P_DNAME
*&---------------------------------------------------------------------*
*&      Form  F_SELECTION_SCREEN_PROC
*&---------------------------------------------------------------------*
*       選択画面処理
*----------------------------------------------------------------------*
FORM F_SELECTION_SCREEN_PROC.

*-- 会社コードと購買組織の存在チェック
PERFORM F_CHECK_P_BUKRS_AND_P_EKORG.

*-- 出力締日チェック
PERFORM F_CHECK_P_CDATE.

*-- 出力年月初期値設定
IF P_YEARS IS INITIAL.
PERFORM F_SET_P_YEARS.
ENDIF.

*-- 出力年月チェック
PERFORM F_CHECK_P_YEARS.

*-- 照合締日の取得
PERFORM F_GET_ZFBDT.

*-- ファイル出力チェック
PERFORM F_CHECK_OUTPUT_FILE.

ENDFORM.                    " F_SELECTION_SCREEN_PROC
*&---------------------------------------------------------------------
*&      Form  F_CHECK_P_BUKRS_AND_P_EKORG
*&---------------------------------------------------------------------
*       会社コードと購買組織の存在チェック
*----------------------------------------------------------------------
FORM F_CHECK_P_BUKRS_AND_P_EKORG.

DATA: L_EKORG LIKE T024E-EKORG.

SELECT SINGLE EKORG             " 購買組織
FROM T024E
INTO L_EKORG
WHERE EKORG = P_EKORG          " 購買組織
AND BUKRS = P_BUKRS.         " 会社コード

IF SY-SUBRC <> 0.
*-- 対象データがありません
MESSAGE E762.
ENDIF.

ENDFORM.                    " F_CHECK_P_BUKRS_AND_P_EKORG
*&---------------------------------------------------------------------
*&      Form  F_CHECK_P_CDATE
*&---------------------------------------------------------------------
*       出力締日チェック
*----------------------------------------------------------------------
FORM F_CHECK_P_CDATE.

IF NOT (  P_CDATE =  5
OR P_CDATE = 10
OR P_CDATE = 15
OR P_CDATE = 20
OR P_CDATE = 25
OR P_CDATE = 31 ).
*-- 入力された締日が、五十日、末日ではありません
MESSAGE E913.
ENDIF.

ENDFORM.                    " F_CHECK_P_CDATE
*&---------------------------------------------------------------------
*&      Form  F_SET_P_YEARS
*&---------------------------------------------------------------------
*       出力年月初期値設定
*----------------------------------------------------------------------
FORM F_SET_P_YEARS.

DATA: L_ENDDA LIKE PSKEY-ENDDA.

IF P_CDATE =  5
OR P_CDATE = 10
OR P_CDATE = 15
OR P_CDATE = 20
OR P_CDATE = 25.

*-- システム日付の先頭６桁を設定
P_YEARS = SY-DATUM(6).

ELSE.

*-- システム日付の前月を取得する
CALL FUNCTION 'HRWPC_BL_DATES_MONTH_INTERVAL'
EXPORTING
DATUM     = SY-DATUM
MONTH_PST = 1
MONTH_FTR = -1
IMPORTING
ENDDA     = L_ENDDA
EXCEPTIONS
OTHERS    = 0.

*-- システム日付の前月の先頭６桁を設定
P_YEARS = L_ENDDA(6).

ENDIF.

ENDFORM.                    " F_SET_P_YEARS
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_P_YEARS
*&---------------------------------------------------------------------*
*       出力年月チェック
*----------------------------------------------------------------------*
FORM F_CHECK_P_YEARS.

IF P_YEARS+4(2) < 01
OR P_YEARS+4(2) > 12.
*-- 入力された出力年月が不正です。
MESSAGE E914.
ENDIF.

ENDFORM.                    " F_CHECK_P_YEARS
*&---------------------------------------------------------------------*
*&      Form  F_GET_ZFBDT
*&---------------------------------------------------------------------*
*       照合締日の算出
*----------------------------------------------------------------------*
FORM F_GET_ZFBDT.

CLEAR: G_ZFBDT.

IF P_CDATE =  5
OR P_CDATE = 10
OR P_CDATE = 15
OR P_CDATE = 20
OR P_CDATE = 25.

*-- 出力年月(YYYYMM)＋出力締日(DD)
CONCATENATE P_YEARS
P_CDATE
INTO G_ZFBDT.

ELSE.

*-- 出力年月(YYYYMM)＋[01]
CONCATENATE P_YEARS
'01'
INTO G_ZFBDT.

*-- 月末日へ変換
CALL FUNCTION 'LAST_DAY_OF_MONTHS'
EXPORTING
DAY_IN            = G_ZFBDT
IMPORTING
LAST_DAY_OF_MONTH = G_ZFBDT
EXCEPTIONS
OTHERS            = 0.

ENDIF.

ENDFORM.                    " F_GET_ZFBDT
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_OUTPUT_FILE
*&---------------------------------------------------------------------*
*       ファイル出力チェック
*----------------------------------------------------------------------*
FORM F_CHECK_OUTPUT_FILE.

DATA: L_DIRECTORY LIKE BTCH0000-TEXT80,
L_FILENAME  LIKE BTCH0000-TEXT80.

*-- ファイル名の重複チェック
IF P_FNAME = P_SNAME.
*-- 出力ファイル名と起動用ファイル名が重複しています
MESSAGE E922.
ENDIF.

*-- 起動用ファイルの存在チェック
L_DIRECTORY = P_DNAME.                        " ディレクトリ名
L_FILENAME  = P_SNAME.                        " 起動用ファイル名

CALL FUNCTION 'PFL_CHECK_DIRECTORY'
EXPORTING
DIRECTORY                   = L_DIRECTORY
FILNAME                     = L_FILENAME
EXCEPTIONS
PFL_DIR_NOT_EXIST           = 1
PFL_PERMISSION_DENIED       = 2
PFL_CANT_BUILD_DATASET_NAME = 3
PFL_FILE_NOT_EXIST          = 4
OTHERS                      = 5.

IF SY-SUBRC = 0.
*-- 起動用ファイルが存在しています
MESSAGE E923.
ENDIF.

ENDFORM.                    " F_CHECK_OUTPUT_FILE
*&---------------------------------------------------------------------*
*&      Form  F_INIT_SEC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM F_INIT_SEC.

*-- 変数の初期化
CLEAR: W_LFM1,
W_T052,
W_YK230,
W_LFX1,
W_ADRC,
W_T001W,
W_DATA,
RC_CODE_LOCK,
G_CNT_UPD,
G_CNT_OUT,
G_CNT_ERR,
G_CNT_SEQ,
G_SUM_TOTAL,
G_SUM_TAXAM,
G_SUM_ITXAM,
G_SUM_ETXAM.

REFRESH: I_LFM1,
I_T052,
I_YK230,
I_LFX1,
I_ADRC,
I_T001W,
I_DATA.

ENDFORM.                    " F_INIT_SEC
*&---------------------------------------------------------------------*
*&      Form  F_MAIN_SEC
*&---------------------------------------------------------------------*
*       メイン処理
*----------------------------------------------------------------------*
FORM F_MAIN_SEC.

*-- 対象仕入先取得
PERFORM F_GET_TARGET_SUPPLIER.
*-- テーブルロック設定(出力時)
PERFORM F_SET_TABLE_LOCK.
*-- 出力対象データの取得
PERFORM F_GET_OUTPUT_DATA.
*-- 補足情報取得
PERFORM F_GET_SUPPLEMENT_DATA.
*-- ファイル編集処理
PERFORM F_EDIT_FILE_DATA.
*-- ファイル出力
PERFORM F_OUTPUT_FILE.
*-- テーブルロック解除
PERFORM F_RELEASE_TABLE_LOCK.
*-- コミット処理
COMMIT WORK.
*-- 出力ログ表示
PERFORM F_DISPLAY_OUTPUT_LOG.

ENDFORM.                    " F_MAIN_SEC
*&---------------------------------------------------------------------*
*&      Form  F_GET_TARGET_SUPPLIER
*&---------------------------------------------------------------------*
*       対象仕入先取得
*----------------------------------------------------------------------*
FORM F_GET_TARGET_SUPPLIER.

*-- 対象の仕入先コード取得
PERFORM F_SELECT_LFM1.

*-- 購買データの支払条件を一括取得
PERFORM F_SELECT_T052.

*-- 仕入先データ絞込み
PERFORM F_REFINE_LFM1.

ENDFORM.                    " F_GET_TARGET_SUPPLIER
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_LFM1
*&---------------------------------------------------------------------*
*       対象の仕入先コード取得
*----------------------------------------------------------------------*
FORM F_SELECT_LFM1.

REFRESH: I_LFM1.
*-- 仕入先コード取得
SELECT LIFNR                         " 仕入先コード
ZTERM                         " 支払条件キー
WAERS                         " 通貨コード
FROM LFM1
INTO CORRESPONDING FIELDS OF TABLE I_LFM1
WHERE LIFNR IN S_LIFNR              " 仕入先コード
AND EKORG =  P_EKORG.             " 購買組織

ENDFORM.                    " F_SELECT_LFM1
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_T052
*&---------------------------------------------------------------------*
*       購買データの支払条件を一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_T052.

DATA: LI_LFM1 LIKE I_LFM1.

*-- 支払条件キーをユニークにする
LI_LFM1 = I_LFM1.
SORT LI_LFM1 ASCENDING BY ZTERM.          " 仕入先コード
DELETE ADJACENT  DUPLICATES FROM LI_LFM1
COMPARING ZTERM.

CHECK NOT LI_LFM1[] IS INITIAL.

REFRESH: I_T052.
*-- 支払条件取得
SELECT ZTERM                              " 支払条件キー
ZTAGG                              " 期限
FROM T052
INTO CORRESPONDING FIELDS OF TABLE I_T052
FOR ALL ENTRIES IN LI_LFM1
WHERE ZTERM = LI_LFM1-ZTERM.             " 支払条件キー

*-- 期日を若番1つに絞り込む
DELETE ADJACENT  DUPLICATES FROM I_T052
COMPARING ZTERM.                   " 支払条件キー

ENDFORM.                    " F_SELECT_T052
*&---------------------------------------------------------------------*
*&      Form  F_REFINE_LFM1
*&---------------------------------------------------------------------*
*       仕入先データ絞込み
*----------------------------------------------------------------------*
FORM F_REFINE_LFM1.

DATA: L_NAME   TYPE THEAD-TDNAME,
LW_TLINE TYPE TLINE,
LI_TLINE LIKE STANDARD TABLE OF LW_TLINE.

LOOP AT I_LFM1 INTO W_LFM1.

*-- 支払条件の読込み
CLEAR: W_T052.
READ TABLE I_T052 INTO W_T052
WITH KEY ZTERM = W_LFM1-ZTERM   " 支払条件キー
BINARY SEARCH.

*-- 締日<>期日の場合、処理対象外とする
IF W_T052-ZTAGG <> P_CDATE.
DELETE I_LFM1.
CONTINUE.
ENDIF.

*-- READ_TEXT用NAMEの作成 (仕入先コード＋会社コード)
CLEAR: L_NAME.
CONCATENATE W_LFM1-LIFNR                " 仕入先コード
P_BUKRS                     " 会社コード
INTO L_NAME.

*-- 仕入先マスタテキストの読込み
REFRESH: LI_TLINE.
CALL FUNCTION 'READ_TEXT'
EXPORTING
CLIENT   = SY-MANDT
ID       = 'YN01'
LANGUAGE = SY-LANGU
NAME     = L_NAME
OBJECT   = 'LFB1'
TABLES
LINES    = LI_TLINE
EXCEPTIONS
OTHERS   = 0.

*-- 「Y」以外の場合、処理対象外とする
READ TABLE LI_TLINE INTO LW_TLINE
*** 2012/01/24 MOD START ***
*                        WITH KEY TDLINE = 'Y'.
WITH KEY TDLINE = C_MARK.
*** 2012/01/24 MOD END ***
IF SY-SUBRC <> 0.
DELETE I_LFM1.
CONTINUE.
ENDIF.

ENDLOOP.

*-- 該当仕入先が0件の場合、メッセージを出力して終了
IF I_LFM1[] IS INITIAL.
MESSAGE S915 WITH P_CDATE.
LEAVE LIST-PROCESSING.
ENDIF.

FREE: I_T052.

ENDFORM.                    " F_REFINE_LFM1
*&---------------------------------------------------------------------*
*&      Form  F_SET_TABLE_LOCK
*&---------------------------------------------------------------------*
*       テーブルロック設定
*----------------------------------------------------------------------*
FORM F_SET_TABLE_LOCK.

DATA: L_USER TYPE SY-MSGV1.

*-- 仕入れデータ(YK230)ロック
PERFORM F_TABLE_LOCK_YK230.

L_USER = SY-MSGV1.
IF RC_CODE_LOCK <> 0.
*-- エラーメッセージ出力
MESSAGE I756 WITH TEXT-M01 L_USER.
LEAVE LIST-PROCESSING.
ENDIF.

ENDFORM.                    " F_SET_TABLE_LOCK
*&---------------------------------------------------------------------*
*&      Form  TABLE_LOCK_YK230
*&---------------------------------------------------------------------*
*       自社/外部データロック処理
*----------------------------------------------------------------------*
FORM F_TABLE_LOCK_YK230.

CALL FUNCTION 'ENQUEUE_EY_YK230'
EXPORTING
MODE_YK230     = 'E'
MANDT          = SY-MANDT
EXCEPTIONS
FOREIGN_LOCK   = 1
SYSTEM_FAILURE = 2
OTHERS         = 3.

IF SY-SUBRC <> 0.
RC_CODE_LOCK = '1'.
EXIT.
ENDIF.

ENDFORM.                    " F_TABLE_LOCK_YK230
*&---------------------------------------------------------------------*
*&      Form  F_GET_OUTPUT_DATA
*&---------------------------------------------------------------------*
*       出力対象データの取得
*----------------------------------------------------------------------*
FORM F_GET_OUTPUT_DATA.

IF R_SOKUHO = C_FLG_ON.              " 速報（検収明細書）
PERFORM F_SELECT_YK230_S.
ELSE.                                " 確報（支払通知書）
PERFORM F_SELECT_YK230_K.
ENDIF.

*-- 出力仕入先が0件の場合、メッセージを出力して終了
IF I_YK230[] IS INITIAL.
MESSAGE S915 WITH P_CDATE.
LEAVE LIST-PROCESSING.
ENDIF.

ENDFORM.                    " F_GET_OUTPUT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_YK230_S
*&---------------------------------------------------------------------*
*       速報（検収明細書）の出力対象データ取得
*----------------------------------------------------------------------*
FORM F_SELECT_YK230_S.

CHECK NOT I_LFM1[] IS INITIAL.

REFRESH: I_YK230[].

SELECT LIFNR                         " 仕入先または債権者の勘定コード
WERKST                        " 取引プラント
EBELN                         " 購買伝票番号
EBELP                         " 購買伝票の明細番号
MAKTX                         " 品目テキスト
MATNR                         " 品目コード
BUDAT                         " 伝票の転記日付
KNQUAN                        " 数量
KNUNTPRC                      " 単価
KNITXAMT                      " 税抜金額
KNTAXAMT                      " 消費税額
KNETXAMT                      " 税込金額
BELNR                         " 請求書伝票の伝票番号
BUZEI                         " 請求書伝票内伝票明細
FROM YK230
INTO CORRESPONDING FIELDS OF TABLE I_YK230
FOR ALL ENTRIES IN I_LFM1
*** 2012/01/25 MOD START ***
*   where lifnr      = i_lfm1-lifnr     " 仕入先
WHERE KEKKBN NE '5'                 " 処理結果区分　部分照合以外
AND LIFNR      = I_LFM1-LIFNR     " 仕入先
*** 2012/01/25 MOD END ***
AND EKORG      = P_EKORG          " 購買組織
AND BUKRS      = P_BUKRS          " 会社コード
*** 2012/01/25 MOD START ***
*     and zfbdt     <= g_zfbdt          " 締日
AND ZFBDT      = G_ZFBDT           " 締日
*** 2012/01/25 MOD END **
and (  zfbdt2  = '00000000'       " 照合締日
or zfbdt2  = g_zfbdt ).       " 照合締日


ENDFORM.                    " F_SELECT_YK230_S
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_YK230_K
*&---------------------------------------------------------------------*
*       確報（支払通知書）の出力対象データ取得
*----------------------------------------------------------------------*
FORM F_SELECT_YK230_K.

CHECK NOT I_LFM1[] IS INITIAL.

REFRESH: I_YK230[].

SELECT LIFNR                         " 仕入先または債権者の勘定コード
WERKST                        " 取引プラント
EBELN                         " 購買伝票番号
EBELP                         " 購買伝票の明細番号
MAKTX                         " 品目テキスト
MATNR                         " 品目コード
BUDAT                         " 伝票の転記日付
KNQUAN                        " 数量
KNUNTPRC                      " 単価
KNITXAMT                      " 税抜金額
KNTAXAMT                      " 消費税額
KNETXAMT                      " 税込金額
BELNR                         " 請求書伝票の伝票番号
BUZEI                         " 請求書伝票内伝票明細
FROM YK230
INTO CORRESPONDING FIELDS OF TABLE I_YK230
FOR ALL ENTRIES IN I_LFM1
*** 2012/01/25 MOD START ***
*   where lifnr  = i_lfm1-lifnr         " 仕入先
WHERE KEKKBN NE '5'                 " 照合結果区分 部分照合
AND LIFNR  = I_LFM1-LIFNR         " 仕入先
*** 2012/01/25 MOD END ***
AND EKORG  = P_EKORG              " 購買組織
AND BUKRS  = P_BUKRS              " 会社コード
AND ZFBDT2 = G_ZFBDT.             " 照合締日

ENDFORM.                    " F_SELECT_YK230_K
*&---------------------------------------------------------------------*
*&      Form  F_GET_SUPPLEMENT_DATA
*&---------------------------------------------------------------------*
*       補足情報取得
*----------------------------------------------------------------------*
FORM F_GET_SUPPLEMENT_DATA.

*-- 仕入先マスタの一括取得
PERFORM F_SELECT_LFA1_LFB1.

*-- アドレス情報の一括取得
PERFORM F_SELECT_ADRC.

*-- プラントの一括取得
PERFORM F_SELECT_T001W.

ENDFORM.                    " F_GET_SUPPLEMENT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_LFA1_LFB1
*&---------------------------------------------------------------------*
*       仕入先マスタの一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_LFA1_LFB1.

CHECK NOT I_LFM1[] IS INITIAL.

REFRESH: I_LFX1.

SELECT LFB1~LIFNR                    " 仕入先コード
LFA1~PSTLZ                    " 仕入先郵便番号
LFA1~ADRNR                    " 住所
LFB1~TLFXS                    " ファックス番号
FROM LFA1 INNER JOIN LFB1
ON LFA1~LIFNR = LFB1~LIFNR
INTO CORRESPONDING FIELDS OF TABLE I_LFX1
FOR ALL ENTRIES IN I_LFM1
WHERE LFB1~BUKRS = P_BUKRS          " 会社コード
AND LFB1~LIFNR = I_LFM1-LIFNR.    " 仕入先コード

ENDFORM.                    " F_SELECT_LFA1_LFB1
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_ADRC
*&---------------------------------------------------------------------*
*       アドレス情報の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_ADRC.

CHECK NOT I_LFX1[] IS INITIAL.
*** 2012/02/09 MOD START ***
*  REFRESH: I_ADRC.
*  SELECT ADRC~ADDRNUMBER                    " アドレス番号
*         ADRC~DATE_FROM                     " 有効開始日付
*         ADRC~NATION                        " 国際アドレスバージョン ID
*         ADRC~NAME2                         " 名前 2
*         ADRC~CITY1                         " 市区町村
*         ADRC~STREET                        " 都道府県名
*         T005U~BEZEI                        " 地域 (都道府県）
*    FROM ADRC INNER JOIN T005U
*      ON ADRC~REGION = T005U~BLAND
*    INTO CORRESPONDING FIELDS OF TABLE I_ADRC
*         FOR ALL ENTRIES IN I_LFX1
*   WHERE ADDRNUMBER = I_LFX1-ADRNR          " 住所
*     AND SPRAS      = 'JA'                  " 言語
*     AND LAND1      = 'JP'.                 " 国
REFRESH: I_ADRC,
I_T005U.

SELECT ADDRNUMBER                    " アドレス番号
DATE_FROM                     " 有効開始日付
NATION                        " 国際アドレスバージョン ID
NAME2                         " 名前 2
CITY1                         " 市区町村
STREET                        " 都道府県名
REGION                        " 地域
FROM ADRC
INTO CORRESPONDING FIELDS OF TABLE I_ADRC
FOR ALL ENTRIES IN I_LFX1
WHERE ADDRNUMBER = I_LFX1-ADRNR.         " 住所

SELECT BLAND                              " 地域
BEZEI                              " 内容説明
FROM T005U
INTO CORRESPONDING FIELDS OF TABLE I_T005U
FOR ALL ENTRIES IN I_ADRC
WHERE SPRAS = 'JA'                       " 言語
AND LAND1 = 'JP'                       " 国
AND BLAND = I_ADRC-REGION.             " 地域

*-- アドレス情報(ADRC)に地域名称(T005U)を付与
LOOP AT I_ADRC INTO W_ADRC.
CLEAR: W_T005U.
READ TABLE I_T005U INTO W_T005U
WITH KEY BLAND = W_ADRC-REGION      " 地域
BINARY SEARCH.
IF SY-SUBRC = 0.
W_ADRC-BEZEI = W_T005U-BEZEI.         " 内容説明
MODIFY I_ADRC FROM W_ADRC.
ENDIF.
ENDLOOP.
*** 2012/02/09 MOD END   ***

ENDFORM.                    " F_SELECT_ADRC
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_T001W
*&---------------------------------------------------------------------*
*       プラントの一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_T001W.

CHECK NOT I_YK230[] IS INITIAL.

REFRESH: I_T001W.

SELECT WERKS                         " プラント
NAME1                         " 名称
FROM T001W
INTO CORRESPONDING FIELDS OF TABLE I_T001W
FOR ALL ENTRIES IN I_YK230
WHERE WERKS = I_YK230-WERKST.        " プラント

ENDFORM.                    " F_SELECT_T001W
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_FILE_DATA
*&---------------------------------------------------------------------*
*       ファイル編集処理
*----------------------------------------------------------------------*
FORM F_EDIT_FILE_DATA.

DATA: L_FLG_BREAK_LIFNR TYPE XFELD,
L_FLG_BREAK_WERKS TYPE XFELD.

*-- ソート(仕入先コード/プラント/入庫日/注文番号/明細)
SORT I_YK230 ASCENDING BY LIFNR
WERKST
BUDAT
EBELN
EBELP.

CLEAR: G_CNT_SEQ.

LOOP AT I_YK230 INTO W_YK230.

AT NEW LIFNR.
L_FLG_BREAK_LIFNR = C_FLG_ON.
ENDAT.

IF L_FLG_BREAK_LIFNR = C_FLG_ON.
CLEAR: L_FLG_BREAK_LIFNR,
G_CNT_SEQ,
G_SUM_TOTAL.
*-- ヘッダデータ(仕入先)の編集
PERFORM F_EDIT_HEADER_DATA_LIFNR.
ENDIF.

AT NEW WERKST.
L_FLG_BREAK_WERKS = C_FLG_ON.
ENDAT.

IF L_FLG_BREAK_WERKS = C_FLG_ON.
CLEAR: L_FLG_BREAK_WERKS,
G_SUM_TAXAM,
G_SUM_ITXAM,
G_SUM_ETXAM.
*-- ヘッダデータ(プラント)の編集
PERFORM F_EDIT_HEADER_DATA_WERKS.
ENDIF.

*-- 明細データの編集
PERFORM F_EDIT_DETAIL_DATA.

*-- その他個別編集
PERFORM F_EDIT_OTHERS.

APPEND W_DATA TO I_DATA.

AT END OF WERKST.
L_FLG_BREAK_WERKS = C_FLG_ON.
ENDAT.

IF L_FLG_BREAK_WERKS = C_FLG_ON.
CLEAR: L_FLG_BREAK_WERKS.
*-- プラント金額の更新
PERFORM F_MODIFY_WERKS_WRBTR.
ENDIF.

AT END OF LIFNR.
L_FLG_BREAK_LIFNR = C_FLG_ON.
ENDAT.

IF L_FLG_BREAK_LIFNR = C_FLG_ON.
CLEAR: L_FLG_BREAK_LIFNR.
*-- 総額の更新
PERFORM F_MODIFY_TOTAL_WRBTR.
ENDIF.

ENDLOOP.

FREE: I_LFM1[],
I_LFX1[],
I_ADRC[],
I_T001W[].

ENDFORM.                    " F_EDIT_FILE_DATA
*&---------------------------------------------------------------------*
*&      Form  F_CURRENCY_CONV_TO_EXTERNAL
*&---------------------------------------------------------------------*
*       内部⇒外部書式への変換
*----------------------------------------------------------------------*
*      -->AI_WEARS  通貨コード
*      -->AI_WRBTR  内部書式金額
*      <--AO_WRBTR  外部書式金額
*----------------------------------------------------------------------*
FORM F_CURRENCY_CONV_TO_EXTERNAL USING VALUE(AI_WAERS) TYPE ANY
VALUE(AI_WRBTR) TYPE ANY
CHANGING VALUE(AO_WRBTR) TYPE ANY.

DATA: L_WRBTR_E TYPE BAPICURR-BAPICURR,
L_WRBTR_R TYPE P DECIMALS 2.

CALL FUNCTION 'BAPI_CURRENCY_CONV_TO_EXTERNAL'
EXPORTING
CURRENCY        = AI_WAERS
AMOUNT_INTERNAL = AI_WRBTR
IMPORTING
AMOUNT_EXTERNAL = L_WRBTR_E.

*-- 小数２桁に変換
CALL FUNCTION 'ROUND'
EXPORTING
DECIMALS = 2
INPUT    = L_WRBTR_E
SIGN     = 'X'
IMPORTING
OUTPUT   = L_WRBTR_R
EXCEPTIONS
OTHERS   = 0.

AO_WRBTR = L_WRBTR_R.

ENDFORM.                    " F_CURRENCY_CONV_TO_EXTERNAL
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_HEADER_DATA_LIFNR
*&---------------------------------------------------------------------*
*       ヘッダデータ(仕入先)の編集
*----------------------------------------------------------------------*
FORM F_EDIT_HEADER_DATA_LIFNR.

DATA: L_COUNT_LINE TYPE I.
*** 2012/01/24 DEL START ***
*        L_WRBTR      LIKE ZN002-PWRBTR.
*** 2012/01/24 DEL END ***

CLEAR: L_COUNT_LINE,
*** 2012/01/24 DEL START ***
*         L_WRBTR,
*** 2012/01/24 DEL END ***
W_DATA,
W_LFX1,
W_ADRC,
W_LFM1.

*-- 補足情報の読込み
READ TABLE I_LFX1 INTO W_LFX1
WITH TABLE KEY LIFNR = W_YK230-LIFNR. " 仕入先コード
READ TABLE I_ADRC INTO W_ADRC
WITH KEY ADDRNUMBER  = W_LFX1-ADRNR   " 住所
BINARY SEARCH.
READ TABLE I_LFM1 INTO W_LFM1
WITH KEY LIFNR       = W_YK230-LIFNR. " 仕入先コード

*-- 計上年月
CONCATENATE P_YEARS+0(4)
'/'
P_YEARS+4(2)
INTO W_DATA-YEARS.
*-- 帳票名称
IF R_SOKUHO = C_FLG_ON.
W_DATA-TNAME = C_SOKUHO.
ELSE.
W_DATA-TNAME = C_KAKUHO.
ENDIF.
*-- 仕入先コード
W_DATA-LIFNR = W_LFX1-LIFNR.
*-- 発行日
PERFORM F_CONV_EXIT_PDATE_OUTPUT USING SY-DATUM
CHANGING W_DATA-ODATE.
*-- 仕入先名
*  w_data-name2 = w_adrc-name2.
CONCATENATE W_ADRC-NAME2 SPACE INTO W_DATA-NAME2.
*-- 仕入先郵便番号
W_DATA-PSTLZ = W_LFX1-PSTLZ.
*-- 地域
W_DATA-BEZEI = W_ADRC-BEZEI.
*-- 都道府県
W_DATA-STREE = W_ADRC-STREET.
*-- 市区町村
W_DATA-CITY1 = W_ADRC-CITY1.
*-- ファックス番号
W_DATA-TELFX = W_LFX1-TLFXS.
*-- 通貨コード
W_DATA-WAERS = W_LFM1-WAERS.

ENDFORM.                    " F_EDIT_HEADER_DATA_LIFNR
*&---------------------------------------------------------------------*
*&      Form  F_CLOI_PUT_SIGN_IN_FRONT
*&---------------------------------------------------------------------*
*       金額符号の前置換
*----------------------------------------------------------------------*
*      -->AI_WRBTR  符号後付金額
*      <--AO_WRBTR  符号前付金額
*----------------------------------------------------------------------*
FORM F_CLOI_PUT_SIGN_IN_FRONT USING VALUE(AI_WRBTR) TYPE ANY
CHANGING VALUE(AO_WRBTR) TYPE ANY.

CALL FUNCTION 'CLOI_PUT_SIGN_IN_FRONT'
CHANGING
VALUE = AI_WRBTR.

AO_WRBTR = AI_WRBTR.

ENDFORM.                    " F_CLOI_PUT_SIGN_IN_FRONT
*&---------------------------------------------------------------------*
*&      Form  F_CONV_EXIT_PDATE_OUTPUT
*&---------------------------------------------------------------------*
*       日付書式の変換（内部⇒外部）
*----------------------------------------------------------------------*
*      -->AI_DATE  内部書式日付
*      <--AO_DATE  外部書式日付
*----------------------------------------------------------------------*
FORM F_CONV_EXIT_PDATE_OUTPUT USING VALUE(AI_DATE) TYPE ANY
CHANGING VALUE(AO_DATE) TYPE ANY.

DATA: L_DATE(10) TYPE C.

CHECK NOT AI_DATE = SPACE.

CALL FUNCTION 'CONVERSION_EXIT_PDATE_OUTPUT'
EXPORTING
INPUT  = AI_DATE
IMPORTING
OUTPUT = L_DATE.

AO_DATE = L_DATE.

ENDFORM.                    " F_CONV_EXIT_PDATE_OUTPUT
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_HEADER_DATA_WERKS
*&---------------------------------------------------------------------*
*       ヘッダデータ(プラント)の編集
*----------------------------------------------------------------------*
FORM F_EDIT_HEADER_DATA_WERKS.

*-- 補足情報の読込み
READ TABLE I_T001W INTO W_T001W
WITH KEY WERKS = W_YK230-WERKST. " プラントコード
*-- プラントコード
W_DATA-DVSON = W_YK230-WERKST.
*-- プラント名
W_DATA-NAME1 = W_T001W-NAME1.

ENDFORM.                    " F_EDIT_HEADER_DATA_WERKS
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_DETAIL_DATA
*&---------------------------------------------------------------------*
*       明細データの編集
*----------------------------------------------------------------------*
FORM F_EDIT_DETAIL_DATA.

DATA: L_KNUNTPRC TYPE P DECIMALS 4,    " 単価(小数４桁)
L_KNQUAN   TYPE P DECIMALS 2,    " 数量(小数２桁)
*** 2012/01/24 INSERT START ***
L_EBELP    TYPE CHAR4.           " 作業用の発注明細番号の定義

CLEAR L_EBELP.
*** 2012/01/24 INSERT END ***

*-- 入庫日
PERFORM F_CONV_EXIT_PDATE_OUTPUT USING W_YK230-BUDAT
CHANGING W_DATA-LDATE.
*-- 注文番号
*** 2012/01/24 INSERT START ***
*** W_YK230-EBELPの前０を除去する。
CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
EXPORTING
INPUT  = W_YK230-EBELP+0(4)
IMPORTING
OUTPUT = L_EBELP.
*** 2012/01/24 INSERT END ***

CONCATENATE W_YK230-WERKST+1(2)
'-'
W_YK230-EBELN
'-'
*** 2012/01/24 MOD START ***
*              W_YK230-EBELP
L_EBELP
*** 2012/01/24 MOD END ***
INTO W_DATA-LIST2.
*-- 品目コード
W_DATA-LIST5 = W_YK230-MATNR.
*-- 品名
W_DATA-LIST6 = W_YK230-MAKTX.
*-- 単価
W_DATA-KNUNT = L_KNUNTPRC = W_YK230-KNUNTPRC.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-KNUNT
CHANGING W_DATA-KNUNT.
*-- 数量
W_DATA-KNQUA = L_KNQUAN   = W_YK230-KNQUAN.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-KNQUA
CHANGING W_DATA-KNQUA.
*-- 税抜金額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
W_YK230-KNITXAMT
CHANGING W_DATA-KNITX.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-KNITX
CHANGING W_DATA-KNITX.
*-- 税額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
W_YK230-KNTAXAMT
CHANGING W_DATA-KNTAX.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-KNTAX
CHANGING W_DATA-KNTAX.
*-- 税込金額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
W_YK230-KNETXAMT
CHANGING W_DATA-KNETX.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-KNETX
CHANGING W_DATA-KNETX.

ENDFORM.                    " F_EDIT_DETAIL_DATA
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_OTHERS
*&---------------------------------------------------------------------*
*       その他個別編集(金額計算)
*----------------------------------------------------------------------*
FORM F_EDIT_OTHERS.

*-- SEQ(ヘッダ)
G_CNT_SEQ    = G_CNT_SEQ + 1.
W_DATA-SEQNO = G_CNT_SEQ.

*-- 総額計算
G_SUM_TOTAL = G_SUM_TOTAL + W_YK230-KNETXAMT.
*-- 消費税額計算
G_SUM_TAXAM = G_SUM_TAXAM + W_YK230-KNTAXAMT.
*-- 税抜金額計算
G_SUM_ITXAM = G_SUM_ITXAM + W_YK230-KNITXAMT.
*-- 税込金額計算
G_SUM_ETXAM = G_SUM_ETXAM + W_YK230-KNETXAMT.

ENDFORM.                    " F_EDIT_OTHERS
*&---------------------------------------------------------------------*
*&      Form  F_MODIFY_WERKS_WRBTR
*&---------------------------------------------------------------------*
*       プラント金額の更新
*----------------------------------------------------------------------*
FORM F_MODIFY_WERKS_WRBTR.

*-- プラント税抜金額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
G_SUM_ITXAM
CHANGING W_DATA-ITXAM.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-ITXAM
CHANGING W_DATA-ITXAM.

*-- プラント消費税額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
G_SUM_TAXAM
CHANGING W_DATA-TAXAM.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-TAXAM
CHANGING W_DATA-TAXAM.

*-- プラント税込金額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
G_SUM_ETXAM
CHANGING W_DATA-ETXAM.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-ETXAM
CHANGING W_DATA-ETXAM.

MODIFY I_DATA FROM W_DATA
TRANSPORTING TAXAM ITXAM ETXAM
WHERE LIFNR = W_YK230-LIFNR
AND DVSON = W_YK230-WERKST.

ENDFORM.                    " F_MODIFY_WERKS_WRBTR
*&---------------------------------------------------------------------*
*&      Form  F_MODIFY_TOTAL_WRBTR
*&---------------------------------------------------------------------*
*       総額の更新
*----------------------------------------------------------------------*
FORM F_MODIFY_TOTAL_WRBTR.

*-- 総額
PERFORM F_CURRENCY_CONV_TO_EXTERNAL USING W_LFM1-WAERS
G_SUM_TOTAL
CHANGING W_DATA-TOTAL.
PERFORM F_CLOI_PUT_SIGN_IN_FRONT USING W_DATA-TOTAL
CHANGING W_DATA-TOTAL.

MODIFY I_DATA FROM W_DATA TRANSPORTING TOTAL
WHERE LIFNR = W_YK230-LIFNR.

ENDFORM.                    " F_MODIFY_TOTAL_WRBTR
*&---------------------------------------------------------------------*
*&      Form  F_OUTPUT_FILE
*&---------------------------------------------------------------------*
*       ファイル出力
*----------------------------------------------------------------------*
FORM F_OUTPUT_FILE.

*-- IFファイル出力
PERFORM F_OUTPUT_FILE_I.

*-- 起動用ファイル出力
PERFORM F_OUTPUT_FILE_S.

ENDFORM.                    " F_OUTPUT_FILE
*&---------------------------------------------------------------------*
*&      Form  F_OUTPUT_FILE_I
*&---------------------------------------------------------------------*
*       IFファイル出力
*----------------------------------------------------------------------*
FORM F_OUTPUT_FILE_I.

DATA: L_FNAME TYPE RLGRAP-FILENAME,
L_DATA  TYPE STRING.

CONCATENATE P_DNAME
P_FNAME
INTO L_FNAME.

OPEN DATASET L_FNAME FOR OUTPUT IN TEXT MODE.
IF SY-SUBRC <> 0.
ROLLBACK WORK.
*-- ファイルオープンエラー
MESSAGE I766 WITH TEXT-M02.
LEAVE LIST-PROCESSING.
ENDIF.

LOOP AT I_DATA INTO W_DATA.
*-- カンマ区切りファイル出力データ作成
CONCATENATE W_DATA-YEARS                " 計上年月
W_DATA-TNAME                " 帳票名称
W_DATA-LIFNR                " 仕入先コード
W_DATA-SEQNO                " SEQ
W_DATA-ODATE                " 発行日
W_DATA-NAME2                " 仕入先名
W_DATA-PSTLZ                " 仕入先郵便番号
W_DATA-BEZEI                " 地域
W_DATA-STREE                " 都道府県
W_DATA-CITY1                " 市区町村
W_DATA-TELFX                " ファックス番号
W_DATA-TOTAL                " 総額
W_DATA-WAERS                " 通貨コード
W_DATA-ZLSC1                " 金種1-文言
W_DATA-WRBT1                " 金種1-金額
W_DATA-ZFBD1                " 金種1-期日
W_DATA-ZLSC2                " 金種2-文言
W_DATA-WRBT2                " 金種2-金額
W_DATA-ZFBD2                " 金種2-期日
W_DATA-ZLSC3                " 金種3-文言
W_DATA-WRBT3                " 金種3-金額
W_DATA-ZFBD3                " 金種3-期日
W_DATA-ZLSC4                " 金種4-文言
W_DATA-WRBT4                " 金種4-金額
W_DATA-ZFBD4                " 金種4-期日
W_DATA-DVSON                " プラントコード
W_DATA-NAME1                " プラント名
W_DATA-ITXAM                " 税抜金額
W_DATA-TAXAM                " 消費税額
W_DATA-ETXAM                " 税込金額
W_DATA-LDATE                " 入庫日
W_DATA-LIST2                " 注文番号
W_DATA-LIST5                " 品目コード
W_DATA-LIST6                " 品名
W_DATA-KNUNT                " 単価
W_DATA-KNQUA                " 数量
W_DATA-KNITX                " 税抜金額
W_DATA-KNTAX                " 税額
W_DATA-KNETX                " 税込金額
INTO L_DATA
SEPARATED BY '","'.
CONCATENATE '"'
L_DATA
'"'
INTO L_DATA.
TRANSFER L_DATA TO L_FNAME.
IF SY-SUBRC <> 0.
ROLLBACK WORK.
*-- ファイル出力エラー
MESSAGE I789 WITH TEXT-M02.
LEAVE LIST-PROCESSING.
ENDIF.
*-- 出力件数カウント
G_CNT_OUT = G_CNT_OUT + 1.
ENDLOOP.

CLOSE DATASET L_FNAME.
IF SY-SUBRC <> 0.
ROLLBACK WORK.
*-- ファイル出力エラー
MESSAGE I789 WITH TEXT-M02.
LEAVE LIST-PROCESSING.
ENDIF.

FREE: I_DATA.

ENDFORM.                    " F_OUTPUT_FILE_I
*&---------------------------------------------------------------------*
*&      Form  F_OUTPUT_FILE_S
*&---------------------------------------------------------------------*
*       起動用ファイル出力
*----------------------------------------------------------------------*
FORM F_OUTPUT_FILE_S.

DATA: L_FNAME TYPE RLGRAP-FILENAME.

CONCATENATE P_DNAME
P_SNAME
INTO L_FNAME.

OPEN DATASET L_FNAME FOR OUTPUT.
IF SY-SUBRC <> 0.
ROLLBACK WORK.
*-- ファイルオープンエラー
MESSAGE I766 WITH TEXT-M02.
LEAVE LIST-PROCESSING.
ENDIF.

ENDFORM.                    " F_OUTPUT_FILE_S
*&---------------------------------------------------------------------*
*&      Form  F_RELEASE_TABLE_LOCK
*&---------------------------------------------------------------------*
*       テーブルロック解除
*----------------------------------------------------------------------*
FORM F_RELEASE_TABLE_LOCK.

*-- 自社/外部データ（仕入）ロック解除
PERFORM F_TABLE_UNLOCK_YK230.

ENDFORM.                    " F_RELEASE_TABLE_LOCK_O
*&---------------------------------------------------------------------*
*&      Form  F_TABLE_UNLOCK_YK230
*&---------------------------------------------------------------------*
*       自社/外部データ（仕入）テーブルロック解除
*----------------------------------------------------------------------*
FORM F_TABLE_UNLOCK_YK230.

CALL FUNCTION 'DEQUEUE_EY_YK230'
EXPORTING
MODE_YK230 = 'E'
MANDT      = SY-MANDT.

ENDFORM.                    " F_TABLE_UNLOCK_YK230
*&---------------------------------------------------------------------*
*&      Form  F_DISPLAY_OUTPUT_LOG
*&---------------------------------------------------------------------*
*       出力ログ表示
*----------------------------------------------------------------------*
FORM F_DISPLAY_OUTPUT_LOG.

DATA: L_LINE TYPE I.

WRITE: / TEXT-L01.
ULINE.

IF G_CNT_ERR > 0.
*-- 仕入先コード/エラー内容
WRITE: 04(012) TEXT-L02,
26(128) TEXT-L03.
ULINE.
ENDIF.

LOOP AT TBL_ERRLOG.
WRITE:/04(012) TBL_ERRLOG-LIFNR,
26(128) TBL_ERRLOG-ERR.
L_LINE = SY-TABIX MOD 36.
IF L_LINE = 0 AND SY-TABIX >= 36.
NEW-PAGE.
ENDIF.
ENDLOOP.

IF G_CNT_ERR > 0.
SKIP.
SKIP.
ENDIF.
SKIP.

*-- 更新件数:  件
WRITE: /04    TEXT-L04,
29(8) G_CNT_UPD,
TEXT-L05.
*-- 出力件数:  件
WRITE: /04    TEXT-L06,
29(8) G_CNT_OUT,
TEXT-L05.
*-- エラー件数:  件
WRITE: /04    TEXT-L07,
29(8) G_CNT_ERR,
TEXT-L05.

IF G_CNT_ERR > 0.
*-- エラーがあります。
MESSAGE I759.
ENDIF.

ENDFORM.                    " F_DISPLAY_OUTPUT_LOG
