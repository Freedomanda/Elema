REPORT zzkura69 .

* 使用テーブル
TABLES yk510. "一括請求書ヘッダデータ

* グローバル変数
TYPES: BEGIN OF st_in1,
kunnr LIKE yk510-kunnr, "得意先コード
zfbdt LIKE yk510-zfbdt, "締日
cblamt(21) TYPE c,      "当月請求額
END OF st_in1.
DATA i_in1 TYPE STANDARD TABLE OF st_in1 WITH HEADER LINE.
DATA i_yk510 LIKE STANDARD TABLE OF yk510 WITH HEADER LINE.
DATA w_err(1) TYPE c.

* 選択画面
"実行時条件
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
PARAMETERS p_in1 LIKE rlgrap-filename. "入力ファイル名
PARAMETERS p_test AS CHECKBOX.         "テスト実行
PARAMETERS p_err  AS CHECKBOX.         "エラーを除いて登録
SELECTION-SCREEN END   OF BLOCK b1.
"固定値
SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02.
PARAMETERS p_bukrs LIKE yk510-bukrs. "会社コード
PARAMETERS p_vkorg LIKE yk510-vkorg. "販売組織
PARAMETERS p_vtweg LIKE yk510-vtweg. "流通チャネル
PARAMETERS p_spart LIKE yk510-spart. "製品部門
SELECTION-SCREEN END   OF BLOCK b2.

*----------------------------------------------------------------------*
INITIALIZATION.
p_test = 'X'.                         "テスト実行
GET PARAMETER ID 'BUK' FIELD p_bukrs. "会社コード
GET PARAMETER ID 'VKO' FIELD p_vkorg. "販売組織
GET PARAMETER ID 'VTW' FIELD p_vtweg. "流通チャネル
GET PARAMETER ID 'SPA' FIELD p_spart. "製品部門

START-OF-SELECTION.
PERFORM f_read.
PERFORM f_make.
PERFORM f_check.
IF p_test <> 'X' AND w_err <> 'X'.
PERFORM f_insert.
ENDIF.

*---------------------------------------------------------------------*
*       FORM f_read                                                   *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM f_read.
* ローカル変数
DATA w_cnt LIKE sy-dbcnt.
* 入力ファイルのオープン
OPEN DATASET p_in1 FOR INPUT IN TEXT MODE.
IF sy-subrc <> 0.
MESSAGE e401(y1) WITH '入力ファイルがオープンできません' p_in1.
ENDIF.
* 読み込み
DO.
READ DATASET p_in1 INTO i_in1.
IF sy-subrc = 0.
APPEND i_in1.
ELSE.
EXIT.
ENDIF.
ENDDO.
* クローズ
CLOSE DATASET p_in1.
* 結果
DESCRIBE TABLE i_in1 LINES w_cnt.
MESSAGE s401(y1) WITH '入力件数' w_cnt.
ENDFORM.
*---------------------------------------------------------------------*
*       FORM f_make                                                   *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM f_make.
LOOP AT i_in1.
CLEAR i_yk510.
SELECT SINGLE waers
INTO   i_yk510-waers
FROM   knvv
WHERE  kunnr  = i_in1-kunnr
AND    vkorg  = p_vkorg
AND    vtweg  = p_vtweg
AND    spart  = p_spart.
i_yk510-bukrs = p_bukrs.
i_yk510-kunnr = i_in1-kunnr.
i_yk510-vkorg = p_vkorg.
i_yk510-vtweg = p_vtweg.
i_yk510-spart = p_spart.
i_yk510-zfbdt = i_in1-zfbdt.
i_yk510-cblamt = i_in1-cblamt.
i_yk510-insdat = sy-datum.
i_yk510-instim = sy-uzeit.
i_yk510-insusr = sy-uname.
i_yk510-fiflg = 'X'.
APPEND i_yk510.
ENDLOOP.
ENDFORM.
*---------------------------------------------------------------------*
*       FORM f_check                                                  *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM f_check.
LOOP AT i_yk510.
SELECT SINGLE * FROM  yk510
WHERE  bukrs  = i_yk510-bukrs
AND    kunnr  = i_yk510-kunnr
AND    vkorg  = i_yk510-vkorg
AND    vtweg  = i_yk510-vtweg
AND    spart  = i_yk510-spart
AND    zfbdt  = i_yk510-zfbdt
AND    waers  = i_yk510-waers.
IF sy-subrc = 0.
WRITE: / i_yk510-kunnr,
i_yk510-zfbdt,
i_yk510-cblamt CURRENCY i_yk510-waers,
yk510-cblamt CURRENCY yk510-waers.
IF p_err = 'X'. "エラーを除いて登録
DELETE i_yk510.
ELSE.
w_err = 'X'.
ENDIF.
ENDIF.
ENDLOOP.
ENDFORM.
*---------------------------------------------------------------------*
*       FORM f_insert                                                 *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM f_insert.
INSERT yk510 FROM TABLE i_yk510.
MESSAGE s401(y1) WITH '登録件数' sy-dbcnt.
ENDFORM.
