*&---------------------------------------------------------------------*
*&  Include           ZEGSDI1030F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  CHECK_INPUT
*&---------------------------------------------------------------------*
*       入力チェック
*----------------------------------------------------------------------*
FORM CHECK_INPUT .
* プラントコード存在チェック
PERFORM CHECK_WERKS.

* 出荷ポイント存在チェック
PERFORM CHECK_VSTEL.

* プラントの権限チェック
PERFORM CHECK_WERKS_AUTH.

ENDFORM.                    " CHECK_INPUT
*&---------------------------------------------------------------------*
*&      Form  CHECK_WERKS
*&---------------------------------------------------------------------*
*       プラントコード存在チェック
*----------------------------------------------------------------------*
FORM CHECK_WERKS .

TYPES: BEGIN OF LTYP_WERKS,
WERKS TYPE T001W-WERKS,
END OF LTYP_WERKS.

DATA: LTD_WERKS   TYPE STANDARD TABLE OF LTYP_WERKS,
LST_WERKS   TYPE LTYP_WERKS,
LST_WERKS_R LIKE LINE OF RD_WERKS.

SELECT WERKS
INTO TABLE LTD_WERKS
FROM T001W
WHERE WERKS IN S_WERKS.
IF SY-SUBRC <> 0.
SET CURSOR FIELD 'S_WERKS-LOW'.
*   プラント ( &1 ) は存在しません
MESSAGE E004(ZMEGSD01) WITH S_WERKS-LOW.
ENDIF.

REFRESH RD_WERKS.
LOOP AT LTD_WERKS INTO LST_WERKS.
LST_WERKS_R-SIGN = 'I'.
LST_WERKS_R-OPTION = 'EQ'.
LST_WERKS_R-LOW = LST_WERKS-WERKS.
APPEND LST_WERKS_R TO RD_WERKS.
ENDLOOP.

ENDFORM.                    " CHECK_WERKS
*&---------------------------------------------------------------------*
*&      Form  CHECK_VSTEL
*&---------------------------------------------------------------------*
*       出荷ポイント存在チェック
*----------------------------------------------------------------------*
FORM CHECK_VSTEL .

TYPES: BEGIN OF LTYP_VSTEL,
VSTEL TYPE TVST-VSTEL,
END OF LTYP_VSTEL.

DATA: LTD_VSTEL   TYPE STANDARD TABLE OF LTYP_VSTEL,
LST_VSTEL   TYPE LTYP_VSTEL,
LST_VSTEL_R LIKE LINE OF RD_VSTEL.

SELECT VSTEL
INTO TABLE LTD_VSTEL
FROM TVST
WHERE VSTEL IN S_VSTEL.
IF SY-SUBRC <> 0.
SET CURSOR FIELD 'S_VSTEL-LOW'.
*   出荷ポイント ( &1 ) は存在しません
MESSAGE E005(ZMEGSD01) WITH S_VSTEL-LOW.
ENDIF.

REFRESH RD_VSTEL.
LOOP AT LTD_VSTEL INTO LST_VSTEL.
LST_VSTEL_R-SIGN = 'I'.
LST_VSTEL_R-OPTION = 'EQ'.
LST_VSTEL_R-LOW = LST_VSTEL-VSTEL.
APPEND LST_VSTEL_R TO RD_VSTEL.
ENDLOOP.

ENDFORM.                    " CHECK_VSTEL
*&---------------------------------------------------------------------*
*&      Form  CHECK_WERKS_AUTH
*&---------------------------------------------------------------------*
*       プラントの権限チェック
*----------------------------------------------------------------------*
FORM CHECK_WERKS_AUTH .

DATA: LST_WERKS LIKE LINE OF RD_WERKS.

LOOP AT RD_WERKS INTO LST_WERKS.

AUTHORITY-CHECK OBJECT 'M_MSEG_WMB'
ID 'WERKS' FIELD LST_WERKS-LOW
ID 'ACTVT' FIELD '01'.
IF SY-SUBRC <> 0.

SET CURSOR FIELD 'S_WERKS-LOW'.
*     プラントに対する実行権限がありません
MESSAGE E003(ZMEGSD01).

ENDIF.

ENDLOOP.

ENDFORM.                    " CHECK_WERKS_AUTH
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_LAYOUT
*&---------------------------------------------------------------------*
*       選択画面のLAYOUTのヘルプ設定
*----------------------------------------------------------------------*
FORM F4_FOR_LAYOUT .

DATA: LST_VARIANT_I TYPE DISVARIANT,
LW_SAVE(1)    TYPE C,
LW_EXIT(1)    TYPE C.

LW_SAVE = 'A'.                        "SAVE

LST_VARIANT_I-REPORT = SY-REPID.
*  LST_VARIANT_I-HANDLE = '2000'.
*  LST_VARIANT_I-HANDLE = sy-DYNNR.

CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
EXPORTING
IS_VARIANT    = LST_VARIANT_I
I_SAVE        = LW_SAVE
IMPORTING
E_EXIT        = LW_EXIT
ES_VARIANT    = ST_VARIANT_E
EXCEPTIONS
NOT_FOUND     = 1
PROGRAM_ERROR = 2
OTHERS        = 3.

IF SY-SUBRC <> 0.

SET CURSOR FIELD 'P_LAYOUT'.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ELSE.

IF LW_EXIT = SPACE.

P_LAYOUT = ST_VARIANT_E-VARIANT.

ENDIF.

ENDIF.

ENDFORM.                    " F4_FOR_LAYOUT
*&---------------------------------------------------------------------*
*&      Form  MAIN_PROCESS
*&---------------------------------------------------------------------*
*       主処理
*----------------------------------------------------------------------*
FORM MAIN_PROCESS .

* PackingListヘッダデータ取得
PERFORM GET_PKG_HEADER CHANGING TD_HEADER.

* 画面出力
PERFORM DISPLAY_ALV CHANGING TD_HEADER.

ENDFORM.                    " MAIN_PROCESS
*&---------------------------------------------------------------------*
*&      Form  GET_PKG_HEADER
*&---------------------------------------------------------------------*
*       PackingListヘッダデータ取得
*----------------------------------------------------------------------*
*      <--O_TD_HEADER  PackingListヘッダ
*----------------------------------------------------------------------*
FORM GET_PKG_HEADER  CHANGING O_TD_HEADER TYPE TYP_TD_HEADER.

DATA:
LTD_SHIPTO TYPE TYP_TD_KNA1,
LTD_SOLDTO TYPE TYP_TD_KNA1,
LTD_ZTEGSDT001 TYPE TYP_TD_ZTEGSDT001.

* Outbound Delivery」が選択されている場合
IF RB_01 IS NOT INITIAL.

*   Outbound Delivery
PERFORM GET_OUTBOUND CHANGING O_TD_HEADER
LTD_ZTEGSDT001.

* External Data」が選択されている場合
ELSE.

*   External Data
PERFORM GET_EXTERNAL CHANGING O_TD_HEADER
LTD_ZTEGSDT001.

ENDIF.

* テキスト取得
PERFORM GET_TEXT USING O_TD_HEADER
CHANGING LTD_SHIPTO
LTD_SOLDTO.

* データ編集
PERFORM EDIT_DATA USING LTD_SHIPTO
LTD_SOLDTO
LTD_ZTEGSDT001
CHANGING O_TD_HEADER.

ENDFORM.                    " GET_PKG_HEADER
*&---------------------------------------------------------------------*
*&      Form  GET_OUTBOUND
*&---------------------------------------------------------------------*
*       Outbound Delivery
*----------------------------------------------------------------------*
*      <--O_TD_HEADER      PackingListヘッダ
*      <--O_TD_ZTEGSDT001  TradeCommon
*----------------------------------------------------------------------*
FORM GET_OUTBOUND  CHANGING O_TD_HEADER TYPE TYP_TD_HEADER
O_TD_ZTEGSDT001 TYPE TYP_TD_ZTEGSDT001.

* 販売管理伝票: 出荷ヘッダデータ
SELECT  VSTEL                         "出荷ポイント
WADAT                         "出庫予定日付
KUNAG                         "受注先
KUNNR                         "出荷先
VBELN                         "出荷伝票
ERDAT                         "レコード登録日
ERNAM                         "登録者
INTO CORRESPONDING FIELDS OF TABLE O_TD_HEADER
FROM LIKP
WHERE VBELN         IN S_VBELN        "Outbound Delivery
AND WADAT         IN S_WADAT        "Planed GI
AND KUNNR         IN S_KUNNR        "Ship-to party
AND VSTEL         IN RD_VSTEL       "ShippingPt
AND KUNAG         IN S_KUNAG.       "Sold-to party

* データが存在しない場合、メッセージを出力し処理を終了する
IF SY-SUBRC <> 0.

MESSAGE S006(ZMEGSD01) WITH 'LIKP'.
*   対象データが存在しません。(TBL = &1)
LEAVE LIST-PROCESSING.

ENDIF.

SORT O_TD_HEADER BY VBELN ASCENDING
ERDAT ASCENDING.

* TradeCommon
SELECT VBELN                                "出荷伝票
ERDAT                                "レコード登録日
Z_INVOICE_NO                         "INVOICE No
Z_INVOICE_DATE                       "INVOICE Date
Z_STATUS_PACKING                     "Status
Z_CRE_YMD_PKG                        "Create Date(Packing)
Z_CRE_HMS_PKG                        "Create Time(Packing)
Z_CRE_USERID_PKG                     "Create User(Packing)
Z_MOD_YMD_PKG                        "Modify Date(Packing)
Z_MOD_HMS_PKG                        "Modify Time(Packing)
Z_MOD_USERID_PKG                     "Modify User(Packing)
INTO TABLE O_TD_ZTEGSDT001
FROM ZTEGSDT001
FOR ALL ENTRIES IN O_TD_HEADER
WHERE VBELN            = O_TD_HEADER-VBELN    "出荷伝票
AND ERDAT            = O_TD_HEADER-ERDAT    "レコード登録日
AND Z_INVOICE_NO     IN S_INVO           "Invoice No
AND Z_INVOICE_DATE   IN S_INDATE         "Invoice Date
AND Z_SOURCE_TYPE    = '1'               "ステータス（PackingList）
AND Z_STATUS_PACKING IN S_STATUS         "Status
AND Z_CRE_YMD_PKG    IN S_CREYMD         "Create Date
AND Z_CRE_HMS_PKG    IN S_CREHMS         "Create Time
AND Z_CRE_USERID_PKG IN S_CREID          "Create User
AND Z_MOD_YMD_PKG    IN S_MODYMD         "Modify Date
AND Z_MOD_HMS_PKG    IN S_MODHMS         "Modify Time
AND Z_MOD_USERID_PKG IN S_MODID.         "Modify User

SORT O_TD_ZTEGSDT001 BY VBELN ASCENDING
ERDAT ASCENDING.

ENDFORM.                    " GET_OUTBOUND
*&---------------------------------------------------------------------*
*&      Form  GET_EXTERNAL
*&---------------------------------------------------------------------*
*       External Data
*----------------------------------------------------------------------*
*      <--O_TD_HEADER      PackingListヘッダ
*      <--O_TD_ZTEGSDT001  TradeCommon
*----------------------------------------------------------------------*
FORM GET_EXTERNAL  CHANGING O_TD_HEADER TYPE TYP_TD_HEADER
O_TD_ZTEGSDT001 TYPE TYP_TD_ZTEGSDT001.

* Outbound Delivery(External)
SELECT VSTEL          "出荷ポイント
WADAT          "出庫予定日付
KUNNR          "出荷先
KUNAG          "受注先
VBELN          "出荷伝票
ERDAT          "レコード登録日
ERNAM          "登録者
INTO CORRESPONDING FIELDS OF TABLE O_TD_HEADER
FROM ZTEGSDT010
WHERE VBELN         IN S_VBELN        "Outbound Delivery
AND VSTEL         IN RD_VSTEL       "ShippingPt
AND WADAT         IN S_WADAT        "Planed GI
AND KUNNR         IN S_KUNNR        "Ship-to party
AND KUNAG         IN S_KUNAG.       "Sold-to party

* データが存在しない場合、メッセージを出力し処理を終了する
IF SY-SUBRC <> 0.

MESSAGE S006(ZMEGSD01) WITH 'ZTEGSDT010'.
*   対象データが存在しません。(TBL = &1)
LEAVE LIST-PROCESSING.

ENDIF.

SORT O_TD_HEADER BY VBELN ASCENDING
ERDAT ASCENDING.

DELETE ADJACENT DUPLICATES FROM O_TD_HEADER
COMPARING VBELN
ERDAT.

* PackingList（Header）
SELECT VBELN                                "出荷伝票
ERDAT                                "レコード登録日
Z_INVOICE_NO                         "INVOICE No
Z_INVOICE_DATE                       "INVOICE Date
Z_STATUS_PACKING                     "Status
Z_CRE_YMD_PKG                        "Create Date(Packing)
Z_CRE_HMS_PKG                        "Create Time(Packing)
Z_CRE_USERID_PKG                     "Create User(Packing)
Z_MOD_YMD_PKG                        "Modify Date(Packing)
Z_MOD_HMS_PKG                        "Modify Time(Packing)
Z_MOD_USERID_PKG                     "Modify User(Packing)
INTO TABLE O_TD_ZTEGSDT001
FROM ZTEGSDT001
FOR ALL ENTRIES IN O_TD_HEADER
WHERE VBELN            = O_TD_HEADER-VBELN    "出荷伝票
AND ERDAT            = O_TD_HEADER-ERDAT    "レコード登録日
AND Z_INVOICE_NO     IN S_INVO           "Invoice No
AND Z_INVOICE_DATE   IN S_INDATE         "Invoice Date
AND Z_SOURCE_TYPE    = '2'               "ステータス（PackingList）
AND Z_STATUS_PACKING IN S_STATUS         "Status
AND Z_CRE_YMD_PKG    IN S_CREYMD         "Create Date
AND Z_CRE_HMS_PKG    IN S_CREHMS         "Create Time
AND Z_CRE_USERID_PKG IN S_CREID          "Create User
AND Z_MOD_YMD_PKG    IN S_MODYMD         "Modify Date
AND Z_MOD_HMS_PKG    IN S_MODHMS         "Modify Time
AND Z_MOD_USERID_PKG IN S_MODID.         "Modify User

SORT O_TD_ZTEGSDT001 BY VBELN ASCENDING
ERDAT ASCENDING.

ENDFORM.                    " GET_EXTERNAL
*&---------------------------------------------------------------------*
*&      Form  GET_TEXT
*&---------------------------------------------------------------------*
*       テキスト取得
*----------------------------------------------------------------------*
*      <--I_TD_HEADER  PackingListヘッダ
*      <--O_TD_SHIPTO  ITAB:出荷先名称
*      <--O_TD_SOLDTO  ITAB:受注先名称
*----------------------------------------------------------------------*
FORM GET_TEXT USING I_TD_HEADER TYPE TYP_TD_HEADER
CHANGING O_TD_SHIPTO TYPE TYP_TD_KNA1
O_TD_SOLDTO TYPE TYP_TD_KNA1.

DATA:
LTD_HEADER TYPE TYP_TD_HEADER.

LTD_HEADER = I_TD_HEADER.

* 帳票出力項目：Ship-to party Name
SORT LTD_HEADER BY KUNNR ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_HEADER
COMPARING KUNNR.

SELECT KUNNR
NAME1
INTO TABLE O_TD_SHIPTO
FROM KNA1
FOR ALL ENTRIES IN LTD_HEADER
WHERE KUNNR = LTD_HEADER-KUNNR.

SORT O_TD_SHIPTO BY KUNNR ASCENDING.

* 帳票出力項目：Sold-to party Name
LTD_HEADER = I_TD_HEADER.
SORT LTD_HEADER BY KUNAG ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_HEADER
COMPARING KUNAG.

SELECT KUNNR
NAME1
INTO TABLE O_TD_SOLDTO
FROM KNA1
FOR ALL ENTRIES IN LTD_HEADER
WHERE KUNNR = LTD_HEADER-KUNAG.

SORT O_TD_SOLDTO BY KUNNR ASCENDING.

ENDFORM.                    " GET_TEXT
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       データ編集
*----------------------------------------------------------------------*
*      -->I_TD_SHIPTO      ITAB:出荷先名称
*      -->I_TD_SOLDTO      ITAB:受注先名称
*      -->I_TD_ZTEGSDT001  TradeCommon
*      <--O_TD_HEADER      PackingListヘッダ
*----------------------------------------------------------------------*
FORM EDIT_DATA USING I_TD_SHIPTO TYPE TYP_TD_KNA1
I_TD_SOLDTO TYPE TYP_TD_KNA1
I_TD_ZTEGSDT001 TYPE TYP_TD_ZTEGSDT001
CHANGING O_TD_HEADER TYPE TYP_TD_HEADER.

DATA:
LST_ZTEGSDT001 TYPE TYP_ZTEGSDT001,
LST_KNA1       TYPE TYP_KNA1.

FIELD-SYMBOLS:
<LFS_HEADER>   TYPE ZSEGSD0014.

LOOP AT O_TD_HEADER ASSIGNING <LFS_HEADER>.

*   Outbound Delivery」が選択されている場合
IF RB_01 IS NOT INITIAL.

*     Outbound Delivery
<LFS_HEADER>-Z_SOURCE_TYPE = '1'.             "ソースタイプ
*   External Data」が選択されている場合
ELSE.

*     External Data
<LFS_HEADER>-Z_SOURCE_TYPE = '2'.             "ソースタイプ

ENDIF.

READ TABLE I_TD_ZTEGSDT001 INTO LST_ZTEGSDT001
WITH KEY VBELN = <LFS_HEADER>-VBELN    "出荷伝票
ERDAT = <LFS_HEADER>-ERDAT    "レコード登録日
BINARY SEARCH.

IF SY-SUBRC = 0.

<LFS_HEADER>-Z_INVOICE_NO     = LST_ZTEGSDT001-Z_INVOICE_NO.
<LFS_HEADER>-Z_INVOICE_DATE   = LST_ZTEGSDT001-Z_INVOICE_DATE.
<LFS_HEADER>-Z_STATUS_PACKING = LST_ZTEGSDT001-Z_STATUS_PACKING.
<LFS_HEADER>-Z_CRE_YMD        = LST_ZTEGSDT001-Z_CRE_YMD_PKG.
<LFS_HEADER>-Z_CRE_HMS        = LST_ZTEGSDT001-Z_CRE_HMS_PKG.
<LFS_HEADER>-Z_CRE_USERID     = LST_ZTEGSDT001-Z_CRE_USERID_PKG.
<LFS_HEADER>-Z_MOD_YMD        = LST_ZTEGSDT001-Z_MOD_YMD_PKG.
<LFS_HEADER>-Z_MOD_HMS        = LST_ZTEGSDT001-Z_MOD_HMS_PKG.
<LFS_HEADER>-Z_MOD_USERID     = LST_ZTEGSDT001-Z_MOD_USERID_PKG.

ENDIF.

CLEAR:
LST_KNA1.

*   出荷先
READ TABLE I_TD_SHIPTO INTO LST_KNA1
WITH KEY KUNNR = <LFS_HEADER>-KUNNR
BINARY SEARCH.

IF SY-SUBRC = 0.

<LFS_HEADER>-SHIPTONAME = LST_KNA1-NAME1.

ENDIF.

CLEAR:
LST_KNA1.

*   受注先
READ TABLE I_TD_SOLDTO INTO LST_KNA1
WITH KEY KUNNR = <LFS_HEADER>-KUNAG
BINARY SEARCH.

IF SY-SUBRC = 0.

<LFS_HEADER>-SOLDTONAME = LST_KNA1-NAME1.

ENDIF.

ENDLOOP.

* ソート
SORT O_TD_HEADER BY VSTEL ASCENDING      "ShippingPt
WADAT ASCENDING      "Planed GI
KUNNR ASCENDING      "Ship-to party
VBELN ASCENDING.     "Outbound Delivery

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_ALV
*&---------------------------------------------------------------------*
*       画面出力
*----------------------------------------------------------------------*
*      <--O_TD_HEADER  ITAB:ALVデータ
*----------------------------------------------------------------------*
FORM DISPLAY_ALV  CHANGING O_TD_HEADER TYPE TYP_TD_HEADER.

DATA:
LTD_FIELDCAT TYPE LVC_T_FCAT,     "ITAB:FIELDCAT
LST_LAYOUT   TYPE LVC_S_LAYO.     "ST:LAYOUT

FIELD-SYMBOLS:
<LFS_FIELDCAT> TYPE LVC_S_FCAT.

* FIELDCAT属性の設定
CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
EXPORTING
I_STRUCTURE_NAME       = 'ZSEGSD0014'
CHANGING
CT_FIELDCAT            = LTD_FIELDCAT
EXCEPTIONS
INCONSISTENT_INTERFACE = 1
PROGRAM_ERROR          = 2
OTHERS                 = 3.

IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
DISPLAY LIKE 'E'.

LEAVE LIST-PROCESSING.

ENDIF.

READ TABLE LTD_FIELDCAT ASSIGNING <LFS_FIELDCAT> INDEX 1.

IF SY-SUBRC = 0.
<LFS_FIELDCAT>-NO_OUT = 'X'.
ENDIF.

* レイアウト属性の設定
* ストライプ
LST_LAYOUT-ZEBRA = 'X'.

LST_LAYOUT-BOX_FNAME = 'SEL'.

LST_LAYOUT-SEL_MODE  = 'D'.

* ALV コントロール: 列幅の最適化
LST_LAYOUT-CWIDTH_OPT = 'X'.

CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
EXPORTING
I_CALLBACK_PROGRAM       = SY-REPID
I_CALLBACK_PF_STATUS_SET = CNS_PF_STATUS_2000
I_CALLBACK_USER_COMMAND  = CNS_USER_CMD_2000
IS_LAYOUT_LVC            = LST_LAYOUT
IT_FIELDCAT_LVC          = LTD_FIELDCAT
I_SAVE                   = 'A'
IS_VARIANT               = ST_VARIANT_E
TABLES
T_OUTTAB                 = O_TD_HEADER
EXCEPTIONS
PROGRAM_ERROR            = 1
OTHERS                   = 2.

IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
DISPLAY LIKE 'E'.

LEAVE LIST-PROCESSING.

ENDIF.

ENDFORM.                    " DISPLAY_ALV
*&---------------------------------------------------------------------*
*&      Form  SET_STATUS_2000
*&---------------------------------------------------------------------*
*       2000 ALV画面のステータスの設定
*----------------------------------------------------------------------*
FORM SET_STATUS_2000 USING UT_EXTAB TYPE SLIS_T_EXTAB.

REFRESH: UT_EXTAB.
SET TITLEBAR 'TTB_2000'.
SET PF-STATUS 'STT_2000' EXCLUDING UT_EXTAB.

ENDFORM.                    " SET_STATUS_2000
*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND_2000
*&---------------------------------------------------------------------*
*       2000 ALV画面の事件
*----------------------------------------------------------------------*
FORM USER_COMMAND_2000 USING UCOMM    TYPE SY-UCOMM
SELFIELD TYPE SLIS_SELFIELD.

* ボタンイベント処理
CASE UCOMM.

*   「Item」ボタン押下時の処理
WHEN CNS_UCOMM_ITEM.

PERFORM EXCUTE_ITEM.

WHEN OTHERS.

ENDCASE.

ENDFORM.                    " USER_COMMAND_2000
*&---------------------------------------------------------------------*
*&      Form  EXCUTE_ITEM
*&---------------------------------------------------------------------*
*       「Item」ボタン押下時の処理
*----------------------------------------------------------------------*
*      -->I_TD_HEADER  ITAB:ALVデータ
*----------------------------------------------------------------------*
FORM EXCUTE_ITEM  . "USING I_TD_HEADER TYPE TYP_TD_HEADER

* 行選択のチェック処理
PERFORM CHECK_ROW.

* 「PackingList Data Input（Item）」画面への画面遷移
* 選択行の下記パラメータを引き渡し、画面遷移する
"Source Type
"Outbound Delivery
"Created on
"ShippingPt
"Planed GI
"Sold-to party
"Sold-to party Name
"Ship-to party
"Ship-to party Name
PERFORM TRANSFER_ITEM.

ENDFORM.                    " EXCUTE_ITEM
*&---------------------------------------------------------------------*
*&      Form  CHECK_ROW
*&---------------------------------------------------------------------*
*       行選択のチェック処理
*----------------------------------------------------------------------*
FORM CHECK_ROW .

DATA:
LTD_HEADER   TYPE TYP_TD_HEADER.

LTD_HEADER = TD_HEADER.
* 行のチェックが複数選択されている場合はエラーとしメッセージを出力する
DELETE LTD_HEADER WHERE SEL IS INITIAL.

IF LINES( LTD_HEADER ) > 1.

MESSAGE E007(ZMEGSD01).
*   行が複数選択されています。

ENDIF.

LTD_HEADER = TD_HEADER.
* 行が選択されていない場合は、エラーとしメッセージを出力する
DELETE LTD_HEADER WHERE SEL IS NOT INITIAL.

IF SY-SUBRC <> 0.

MESSAGE E008(ZMEGSD01).
*   行が選択されていません。

ENDIF.

ENDFORM.                    " CHECK_ROW
*&---------------------------------------------------------------------*
*&      Form  TRANSFER_ITEM
*&---------------------------------------------------------------------*
*       ALV明細
*----------------------------------------------------------------------*
FORM TRANSFER_ITEM .

DATA:
LST_HEADER  TYPE ZSEGSD0014.

READ TABLE TD_HEADER INTO LST_HEADER
WITH KEY SEL = 'X'.

* PackingListデータ取得
PERFORM GET_PACKINGLIST USING LST_HEADER
CHANGING TD_ITEM
ST_HEADLINE.

TD_ORIGINAL = TD_ITEM.
* 画面出力
PERFORM DISPLAY_ALV_ITEM CHANGING TD_ITEM.

ENDFORM.                    " TRANSFER_ITEM
*&---------------------------------------------------------------------*
*&      Form  GET_PACKINGLIST
*&---------------------------------------------------------------------*
*       PackingListデータ取得
*----------------------------------------------------------------------*
*      -->I_ST_PARAMATER   前画面から引継いだパラメータ
*      <--O_TD_ITEM        PackingList明細
*      <--O_ST_HEADLINE    明細ヘッダ部
*----------------------------------------------------------------------*
FORM GET_PACKINGLIST USING I_ST_PARAMATER TYPE ZSEGSD0014
CHANGING O_TD_ITEM TYPE TYP_TD_ITEM
O_ST_HEADLINE TYPE TYP_HEADLINE.

DATA:
LTD_ZTEGSDT002   TYPE TYP_TD_ZTEGSDT002,
LTD_MAKT         TYPE TYP_TD_MAKT,        "ITAB:品目テキスト
LW_VTEXT         TYPE TVSTT-VTEXT,        "W:出荷ポイント名称
LW_DDTEXT        TYPE DD07T-DDTEXT,       "固定テキスト(短)
LTD_DIMENSION    TYPE TYP_TD_ZTEGSDM009.  "Dimension Text

* Outbound Delivery」が選択されている場合
IF RB_01 IS NOT INITIAL.

*   Outbound Delivery Item
PERFORM GET_OUTBOUNDITEM USING I_ST_PARAMATER
CHANGING O_TD_ITEM
LTD_ZTEGSDT002.

* External Data」が選択されている場合
ELSE.

*   External Data Item
PERFORM GET_EXTERNALITEM USING I_ST_PARAMATER
CHANGING O_TD_ITEM
LTD_ZTEGSDT002.

ENDIF.

* テキスト取得
PERFORM GET_TEXT_ITEM USING I_ST_PARAMATER
O_TD_ITEM
CHANGING LTD_MAKT
LW_VTEXT
LW_DDTEXT.

* 項目「Dimension Text」
PERFORM GET_TEXT_DIMENSION USING LTD_ZTEGSDT002
CHANGING LTD_DIMENSION.

* データ編集
PERFORM EDIT_ITEM USING I_ST_PARAMATER
LTD_MAKT
LW_VTEXT
LW_DDTEXT
LTD_ZTEGSDT002
LTD_DIMENSION
CHANGING O_TD_ITEM
O_ST_HEADLINE.

ENDFORM.                    " GET_PACKINGLIST
*&---------------------------------------------------------------------*
*&      Form  GET_OUTBOUNDITEM
*&---------------------------------------------------------------------*
*       Outbound Delivery Item
*----------------------------------------------------------------------*
*      -->I_ST_PARAMATER   前画面から引継いだパラメータ
*      <--O_TD_ITEM        PackingList明細
*      <--O_ZTEGSDT002     PackingList
*----------------------------------------------------------------------*
FORM GET_OUTBOUNDITEM USING I_ST_PARAMATER TYPE ZSEGSD0014
CHANGING O_TD_ITEM TYPE TYP_TD_ITEM
O_ZTEGSDT002 TYPE TYP_TD_ZTEGSDT002.

* 出荷伝票
SELECT LIKP~VBELN              "帳票出力項目：Outbound Delivery
LIKP~ERDAT              "Created on
LIPS~POSNR              "帳票出力項目：Item
LIPS~MATNR              "帳票出力項目：Material
LIPS~LFIMG              "帳票出力項目：Deliv. Qty
LIPS~VRKME              "帳票出力項目：Unit
INTO CORRESPONDING FIELDS OF TABLE O_TD_ITEM
FROM LIKP
INNER JOIN LIPS
ON LIKP~VBELN = LIPS~VBELN
WHERE LIKP~VBELN = I_ST_PARAMATER-VBELN
AND LIKP~ERDAT = I_ST_PARAMATER-ERDAT.

* データが存在しない場合、メッセージを出力し処理を終了する
IF SY-SUBRC <> 0.

MESSAGE S006(ZMEGSD01) WITH 'LIKP'.
*   対象データが存在しません。(TBL = &1)
LEAVE LIST-PROCESSING.

ENDIF.

SORT O_TD_ITEM BY VBELN ASCENDING
ERDAT ASCENDING.

* PackingList
SELECT VBELN                       "出荷伝票
ERDAT                       "レコード登録日
POSNR                       "出荷伝票明細
Z_PKG_POSNR_SUB             "Packing枝番
Z_MEINS_PACK                "梱包単位
Z_CASENO_FROM               "CASE No(Fro
Z_CASENO_TO                 "CASE No(To)
Z_CASE_COUNT                "CASE Count
Z_CASE_MIX                  "Mix
Z_LFIMG_UNIT                "1梱包あたりの梱包数量
Z_LFIMG                     "梱包数量
Z_VRKME                     "梱包数量単位
Z_NETWEIGHT_UNIT            "1梱包あたりの正味重量
Z_NETWEIGHT                 "正味重量
Z_GEWEI_NW                  "重量単位（正味重量）
Z_GRSWEIGHT_UNIT            "1梱包あたりの総重量
Z_GRSWEIGHT                 "総重量
Z_GEWEI_GW                  "重量単位（総重量）
Z_DIMENSION                 "Dimension
Z_VOLUM                     "容積
Z_VOLEH                     "容積単位
Z_PKG_REMARKS               "Remarks
INTO TABLE O_ZTEGSDT002
FROM ZTEGSDT002
WHERE VBELN = I_ST_PARAMATER-VBELN
AND ERDAT = I_ST_PARAMATER-ERDAT.

SORT O_ZTEGSDT002 BY VBELN ASCENDING
ERDAT ASCENDING
POSNR ASCENDING.

ENDFORM.                    " GET_OUTBOUNDITEM
*&---------------------------------------------------------------------*
*&      Form  GET_EXTERNALITEM
*&---------------------------------------------------------------------*
*       External Data Item
*----------------------------------------------------------------------*
*      -->I_ST_PARAMATER   前画面から引継いだパラメータ
*      <--O_TD_ITEM        PackingList明細
*      <--O_ZTEGSDT002     PackingList
*----------------------------------------------------------------------*
FORM GET_EXTERNALITEM USING I_ST_PARAMATER TYPE ZSEGSD0014
CHANGING O_TD_ITEM TYPE TYP_TD_ITEM
O_ZTEGSDT002 TYPE TYP_TD_ZTEGSDT002.

SELECT VBELN              "帳票出力項目：Outbound Delivery
ERDAT              "Created on
POSNR              "帳票出力項目：Item
MATNR              "帳票出力項目：Material
LFIMG              "帳票出力項目：Deliv. Qty
VRKME              "帳票出力項目：Unit
INTO CORRESPONDING FIELDS OF TABLE O_TD_ITEM
FROM ZTEGSDT010
WHERE VBELN = I_ST_PARAMATER-VBELN
AND ERDAT = I_ST_PARAMATER-ERDAT.

* データが存在しない場合、メッセージを出力し処理を終了する
IF SY-SUBRC <> 0.

MESSAGE S006(ZMEGSD01) WITH 'LIKP'.
*   対象データが存在しません。(TBL = &1)
LEAVE LIST-PROCESSING.

ENDIF.

SORT O_TD_ITEM BY VBELN ASCENDING
ERDAT ASCENDING.

* PackingList
SELECT VBELN                       "出荷伝票
ERDAT                       "レコード登録日
POSNR                       "出荷伝票明細
Z_PKG_POSNR_SUB             "Packing枝番
Z_MEINS_PACK                "梱包単位
Z_CASENO_FROM               "CASE No(Fro
Z_CASENO_TO                 "CASE No(To)
Z_CASE_COUNT                "CASE Count
Z_CASE_MIX                  "Mix
Z_LFIMG_UNIT                "1梱包あたりの梱包数量
Z_LFIMG                     "梱包数量
Z_VRKME                     "梱包数量単位
Z_NETWEIGHT_UNIT            "1梱包あたりの正味重量
Z_NETWEIGHT                 "正味重量
Z_GEWEI_NW                  "重量単位（正味重量）
Z_GRSWEIGHT_UNIT            "1梱包あたりの総重量
Z_GRSWEIGHT                 "総重量
Z_GEWEI_GW                  "重量単位（総重量）
Z_DIMENSION                 "Dimension
Z_VOLUM                     "容積
Z_VOLEH                     "容積単位
Z_PKG_REMARKS               "Remarks
INTO TABLE O_ZTEGSDT002
FROM ZTEGSDT002
WHERE VBELN = I_ST_PARAMATER-VBELN
AND ERDAT = I_ST_PARAMATER-ERDAT.

SORT O_ZTEGSDT002 BY VBELN ASCENDING
ERDAT ASCENDING
POSNR ASCENDING.

ENDFORM.                    " GET_EXTERNALITEM
*&---------------------------------------------------------------------*
*&      Form  GET_TEXT_ITEM
*&---------------------------------------------------------------------*
*       テキスト取得
*----------------------------------------------------------------------*
*      -->I_ST_PARAMATER  前画面から引継いだパラメータ
*      -->I_TD_ITEM       PackingList明細
*      <--O_TD_MAKT       ITAB:品目テキスト
*      <--O_VTEXT         W:出荷ポイント名称
*      <--O_DDTEXT        固定テキスト(短)
*----------------------------------------------------------------------*
FORM GET_TEXT_ITEM USING I_ST_PARAMATER TYPE ZSEGSD0014
I_TD_ITEM TYPE TYP_TD_ITEM
CHANGING O_TD_MAKT TYPE TYP_TD_MAKT
VALUE(O_VTEXT)  TYPE TVSTT-VTEXT
VALUE(O_DDTEXT) TYPE DD07T-DDTEXT.

DATA:
LTD_MATNR  TYPE TYP_TD_ITEM.

* 品目テキスト取得
LTD_MATNR = I_TD_ITEM.
SORT LTD_MATNR BY MATNR ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_MATNR
COMPARING MATNR.

SELECT MATNR                 "品目コード
MAKTX                 "品目テキスト
INTO TABLE O_TD_MAKT
FROM MAKT
FOR ALL ENTRIES IN LTD_MATNR
WHERE MATNR = LTD_MATNR-MATNR
AND SPRAS = SY-LANGU.

SORT O_TD_MAKT BY MATNR ASCENDING.

* 出荷ポイント名称取得
SELECT SINGLE VTEXT          "テキスト
INTO O_VTEXT
FROM TVSTT
WHERE SPRAS = SY-LANGU
AND VSTEL = I_ST_PARAMATER-VSTEL.

* Source Type名称取得
SELECT DDTEXT
INTO O_DDTEXT
FROM DD07T
UP TO 1 ROWS
WHERE DOMNAME    = 'ZDSOURCETYPE'
AND DDLANGUAGE = SY-LANGU
AND VALPOS     = I_ST_PARAMATER-Z_SOURCE_TYPE.
ENDSELECT.

ENDFORM.                    " GET_TEXT_ITEM
*&---------------------------------------------------------------------*
*&      Form  EDIT_ITEM
*&---------------------------------------------------------------------*
*       データ編集
*----------------------------------------------------------------------*
*      -->I_ST_PARAMATER  前画面から引継いだパラメータ
*      -->I_TD_MAKT       品目テキスト
*      -->I_VTEXT         W:出荷ポイント名称
*      -->I_DDTEXT        固定テキスト(短)
*      -->I_TD_ZTEGSDT002 ITAB:出荷明細
*      -->I_TD_DIMENSION  TemplateMaster(Dimension)
*      <--O_TD_ITEM       ITAB:PackingListデータ
*      <--O_ST_HEADLINE   明細ヘッダ部
*----------------------------------------------------------------------*
FORM EDIT_ITEM USING I_ST_PARAMATER  TYPE ZSEGSD0014
I_TD_MAKT       TYPE TYP_TD_MAKT
VALUE(I_VTEXT)        TYPE TVSTT-VTEXT
VALUE(I_DDTEXT)       TYPE DD07T-DDTEXT
I_TD_ZTEGSDT002 TYPE TYP_TD_ZTEGSDT002
I_TD_DIMENSION  TYPE TYP_TD_ZTEGSDM009
CHANGING O_TD_ITEM       TYPE TYP_TD_ITEM
O_ST_HEADLINE   TYPE TYP_HEADLINE.

DATA:
LST_ZTEGSDT002   TYPE TYP_ZTEGSDT002,
LST_MAKT         TYPE TYP_MAKT,
LST_ZTEGSDM009   TYPE TYP_ZTEGSDM009,
LTD_ITEM         TYPE TYP_TD_ITEM,
LTD_MATNR        TYPE TYP_TD_ITEM,
LST_ITEM         TYPE ZSEGSD0015,
LTD_MEINS        TYPE TYP_TD_MEINS,
LST_MEINS        TYPE TYP_MEINS.

LTD_ITEM = TD_ITEM.

SORT LTD_ITEM BY MATNR ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_ITEM
COMPARING MATNR.

* ヘッダ部の編集

* Source Type
O_ST_HEADLINE-Z_SOURCE_TYPE = I_ST_PARAMATER-Z_SOURCE_TYPE.

* Source Type Name
O_ST_HEADLINE-DDTEXT        = I_DDTEXT.

* ShippingPt
O_ST_HEADLINE-VSTEL         = I_ST_PARAMATER-VSTEL.

* ShippingPt Name
O_ST_HEADLINE-VTEXT         = I_VTEXT.

* 変換 Exit ALPHA:  内部 -> 外部  Outbound Delivery
PERFORM CONVERT_ALPHA USING I_ST_PARAMATER-VBELN
CHANGING O_ST_HEADLINE-VBELN.

* Planed GI
WRITE I_ST_PARAMATER-WADAT TO O_ST_HEADLINE-WADAT.

* 変換 Exit ALPHA:  内部 -> 外部 Sold-to party
PERFORM CONVERT_ALPHA USING I_ST_PARAMATER-KUNAG
CHANGING O_ST_HEADLINE-KUNAG.

* Sold-to party Name
O_ST_HEADLINE-SOLDTONAME    = I_ST_PARAMATER-SOLDTONAME.

* 変換 Exit ALPHA:  内部 -> 外部 Ship-to party
PERFORM CONVERT_ALPHA USING I_ST_PARAMATER-KUNNR
CHANGING O_ST_HEADLINE-KUNNR.

* Ship-to party Name
O_ST_HEADLINE-SHIPTONAME    = I_ST_PARAMATER-SHIPTONAME.

* 明細部の編集
CLEAR:
LTD_ITEM.

LTD_ITEM = O_TD_ITEM.

CLEAR:
O_TD_ITEM.

LTD_MATNR = LTD_ITEM.
SORT LTD_MATNR BY MATNR ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_MATNR
COMPARING MATNR.
SELECT MATNR
MEINS
INTO TABLE LTD_MEINS
FROM MARA
FOR ALL ENTRIES IN LTD_MATNR
WHERE MATNR = LTD_MATNR-MATNR.

SORT LTD_MEINS BY MATNR ASCENDING.

LOOP AT LTD_ITEM INTO LST_ITEM.

*   ITAB:品目テキスト
READ TABLE I_TD_MAKT INTO LST_MAKT
WITH KEY MATNR = LST_ITEM-MATNR
BINARY SEARCH.

IF SY-SUBRC = 0.

*     Description
LST_ITEM-MAKTX = LST_MAKT-MAKTX.

ENDIF.

LOOP AT I_TD_ZTEGSDT002 INTO LST_ZTEGSDT002
WHERE VBELN = LST_ITEM-VBELN
AND ERDAT = LST_ITEM-ERDAT
AND POSNR = LST_ITEM-POSNR.

*     Packing枝番
LST_ITEM-Z_PKG_POSNR_SUB = LST_ZTEGSDT002-Z_PKG_POSNR_SUB.
*     梱包単位
LST_ITEM-Z_MEINS_PACK    = LST_ZTEGSDT002-Z_MEINS_PACK.
*     CASE No(From)
LST_ITEM-Z_CASENO_FROM   = LST_ZTEGSDT002-Z_CASENO_FROM.
*     CASE No(To)
LST_ITEM-Z_CASENO_TO     = LST_ZTEGSDT002-Z_CASENO_TO.
*     CASE Count
LST_ITEM-Z_CASE_COUNT    = LST_ZTEGSDT002-Z_CASE_COUNT.
*     Mix
LST_ITEM-Z_CASE_MIX      = LST_ZTEGSDT002-Z_CASE_MIX.
*     1梱包あたりの梱包数量
LST_ITEM-Z_LFIMG_UNIT    = LST_ZTEGSDT002-Z_LFIMG_UNIT.
*     梱包数量
LST_ITEM-Z_LFIMG         = LST_ZTEGSDT002-Z_LFIMG.
*     梱包数量単位
LST_ITEM-Z_VRKME         = LST_ZTEGSDT002-Z_VRKME.
*     1梱包あたりの総重量 Add
LST_ITEM-Z_NETWEIGHT_UNIT = LST_ZTEGSDT002-Z_NETWEIGHT_UNIT.
*     正味重量
LST_ITEM-Z_NETWEIGHT     = LST_ZTEGSDT002-Z_NETWEIGHT.
*     重量単位（正味重量）
LST_ITEM-Z_GEWEI_NW      = LST_ZTEGSDT002-Z_GEWEI_NW.
*     1梱包あたりの総重量 Add
LST_ITEM-Z_GRSWEIGHT_UNIT = LST_ZTEGSDT002-Z_GRSWEIGHT_UNIT.
*     総重量
LST_ITEM-Z_GRSWEIGHT     = LST_ZTEGSDT002-Z_GRSWEIGHT.
*     重量単位（総重量）
LST_ITEM-Z_GEWEI_GW      = LST_ZTEGSDT002-Z_GEWEI_GW.
*     Dimension
LST_ITEM-Z_DIMENSION     = LST_ZTEGSDT002-Z_DIMENSION.

*     TemplateMaster(Dimension)
READ TABLE I_TD_DIMENSION INTO LST_ZTEGSDM009
WITH KEY Z_TEMPLATE_ID = LST_ITEM-Z_DIMENSION
BINARY SEARCH.

IF SY-SUBRC = 0.

*       Dimension Text
LST_ITEM-Z_DIMENSION_TEXT = LST_ZTEGSDM009-Z_DIMENSION_TEXT.

*       MEAS(M3) Per Pack
LST_ITEM-Z_VOLUM_UNIT = LST_ZTEGSDM009-Z_MEAS.

ENDIF.

*     容積
LST_ITEM-Z_VOLUM         = LST_ZTEGSDT002-Z_VOLUM.
*     容積単位
LST_ITEM-Z_VOLEH         = LST_ZTEGSDT002-Z_VOLEH.
*     Remarks
LST_ITEM-Z_PKG_REMARKS   = LST_ZTEGSDT002-Z_PKG_REMARKS.

APPEND LST_ITEM TO O_TD_ITEM.

CLEAR:
LST_ITEM-Z_DIMENSION_TEXT,
LST_ITEM-Z_VOLUM_UNIT.

ENDLOOP.

IF SY-SUBRC <> 0.

*     Packing枝番
LST_ITEM-Z_PKG_POSNR_SUB = 1.

READ TABLE LTD_MEINS INTO LST_MEINS
WITH KEY MATNR = LST_ITEM-MATNR.

IF SY-SUBRC = 0.

LST_ITEM-Z_VRKME = LST_MEINS-MEINS.

ENDIF.

*     Left join
APPEND LST_ITEM TO O_TD_ITEM.

ENDIF.

CLEAR:
LST_ITEM.

ENDLOOP.

SORT O_TD_ITEM BY VBELN           ASCENDING
POSNR           ASCENDING
Z_PKG_POSNR_SUB ASCENDING.

ENDFORM.                    " EDIT_ITEM
*&---------------------------------------------------------------------*
*&      Form  CONVERT_ALPHA
*&---------------------------------------------------------------------*
*       変換 Exit ALPHA:  内部 -> 外部
*----------------------------------------------------------------------*
*      -->I_INNER  内部
*      <--O_OUT    外部
*----------------------------------------------------------------------*
FORM CONVERT_ALPHA USING VALUE(I_INNER) TYPE CHAR10
CHANGING VALUE(O_OUT) TYPE CHAR10.

CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
EXPORTING
INPUT  = I_INNER
IMPORTING
OUTPUT = O_OUT.

ENDFORM.                    " CONVERT_ALPHA
*&---------------------------------------------------------------------*
*&      Class  CL_EVENT_RECEIVER DEFINITION
*&---------------------------------------------------------------------*
*       Define
*----------------------------------------------------------------------*
CLASS CL_EVENT_RECEIVER DEFINITION FINAL.

PUBLIC SECTION.

CLASS-METHODS:

*   画面値が変更される時事件
HANDLE_DATA_CHANGED
FOR EVENT DATA_CHANGED OF CL_GUI_ALV_GRID
IMPORTING ER_DATA_CHANGED,

*   画面値が変更された後事件
HANDLE_DATA_CHANGED_FINISHED
FOR EVENT DATA_CHANGED_FINISHED OF CL_GUI_ALV_GRID,

HANDLE_MODIFY
FOR EVENT DATA_CHANGED_FINISHED OF CL_GUI_ALV_GRID.



ENDCLASS.                    "CL_EVENT_RECEIVER DEFINITION
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_ALV_ITEM
*&---------------------------------------------------------------------*
*       画面出力
*----------------------------------------------------------------------*
*      <--O_TD_ITEM       ALV
*----------------------------------------------------------------------*
FORM DISPLAY_ALV_ITEM CHANGING O_TD_ITEM TYPE TYP_TD_ITEM.

DATA:
LTD_EVENT    TYPE SLIS_T_EVENT,      "Structure for event handling
LST_EVENT    LIKE LINE OF LTD_EVENT,
LTD_FIELDCAT TYPE LVC_T_FCAT,        "ITAB:FIELDCAT
LST_GLAY     TYPE LVC_S_GLAY,        "編集済セル終了時のコールバック
lST_VARIANT_E TYPE DISVARIANT,      "Variant
LST_LAYOUT   TYPE LVC_S_LAYO.        "ST:LAYOUT

CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
EXPORTING
I_LIST_TYPE     = 0
IMPORTING
ET_EVENTS       = LTD_EVENT
EXCEPTIONS
LIST_TYPE_WRONG = 1
OTHERS          = 2.
IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

READ TABLE LTD_EVENT INTO LST_EVENT
WITH KEY NAME = 'CALLER_EXIT'.

IF SY-SUBRC = 0.

LST_EVENT-FORM = 'ENTER_BUTTON'.
MODIFY LTD_EVENT FROM LST_EVENT INDEX SY-TABIX.

ELSE.

LST_EVENT-NAME = 'CALLER_EXIT'.
LST_EVENT-FORM = 'ENTER_BUTTON'.
APPEND LST_EVENT TO LTD_EVENT.

ENDIF.

* FIELDCAT属性の設定
CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
EXPORTING
I_STRUCTURE_NAME       = 'ZSEGSD0015'
CHANGING
CT_FIELDCAT            = LTD_FIELDCAT
EXCEPTIONS
INCONSISTENT_INTERFACE = 1
PROGRAM_ERROR          = 2
OTHERS                 = 3.

IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

* フィールドカタログ編集
PERFORM EDIT_FIELDCAT CHANGING LTD_FIELDCAT.

* レイアウト属性の設定
* ストライプ
LST_LAYOUT-ZEBRA = 'X'.

LST_LAYOUT-BOX_FNAME = 'SEL'.

LST_LAYOUT-SEL_MODE  = 'D'.

* ALV コントロール: 列幅の最適化
LST_LAYOUT-CWIDTH_OPT = 'X'.

* ALV コントロール: 編集済セル終了時のコールバック
LST_GLAY-EDT_CLL_CB = 'X'.

lST_VARIANT_E-REPORT = SY-REPID.

lST_VARIANT_E-HANDLE = '3000'.

CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
EXPORTING
I_CALLBACK_PROGRAM          = SY-REPID
I_CALLBACK_PF_STATUS_SET    = CNS_PF_STATUS_3000
I_CALLBACK_USER_COMMAND     = CNS_USER_CMD_3000
I_CALLBACK_HTML_TOP_OF_PAGE = 'TOP_OF_PAGE'
I_GRID_SETTINGS             = LST_GLAY
IS_LAYOUT_LVC               = LST_LAYOUT
IT_FIELDCAT_LVC             = LTD_FIELDCAT
I_SAVE                      = 'A'
IS_VARIANT                  = lST_VARIANT_E
IT_EVENTS                   = LTD_EVENT
I_HTML_HEIGHT_TOP           = 28
TABLES
T_OUTTAB                    = O_TD_ITEM
EXCEPTIONS
PROGRAM_ERROR               = 1
OTHERS                      = 2.

IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

ENDFORM.                    " DISPLAY_ALV_ITEM
*&---------------------------------------------------------------------*
*&      Form  SET_STATUS_3000
*&---------------------------------------------------------------------*
*       3000 ALV画面のステータスの設定
*----------------------------------------------------------------------*
FORM SET_STATUS_3000 USING UT_EXTAB TYPE SLIS_T_EXTAB.

REFRESH: UT_EXTAB.
SET TITLEBAR 'TTB_3000'.
SET PF-STATUS 'STT_3000' EXCLUDING UT_EXTAB.

ENDFORM.                    " SET_STATUS_3000
*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND_3000
*&---------------------------------------------------------------------*
*       3000 ALV画面の事件
*----------------------------------------------------------------------*
FORM USER_COMMAND_3000 USING UCOMM    TYPE SY-UCOMM
SELFIELD TYPE SLIS_SELFIELD.

DATA:
LST_LAYOUT   TYPE LVC_S_LAYO.     "ST:LAYOUT

OK_CODE = SY-UCOMM.
IF OK_CODE = 'RETURN'
OR OK_CODE = 'EXIT'
OR OK_CODE = 'CANCEL'.

*     Exit判断
PERFORM EXIT_ALV.

ENDIF.

* ボタンイベント処理
CASE OK_CODE.

*   「Add Row」ボタン押下時の処理
WHEN CNS_UCOMM_ADDROW.

PERFORM ADD_ROW.
SELFIELD-REFRESH = 'X'.

*   「Delete Row」ボタン押下時の処理
WHEN CNS_UCOMM_DELETEROW.

PERFORM DELETE_ROW.
SELFIELD-REFRESH = 'X'.

*   「Save」ボタン押下時の処理
WHEN '&DATA_SAVE'.

PERFORM SAVE_ITEM.

WHEN OTHERS.

ENDCASE.
* ストライプ
LST_LAYOUT-ZEBRA = 'X'.
LST_LAYOUT-NO_TOOLBAR = 'X'.

LST_LAYOUT-BOX_FNAME = 'SEL'.

LST_LAYOUT-SEL_MODE  = 'D'.

* ALV コントロール: 列幅の最適化
LST_LAYOUT-CWIDTH_OPT = 'X'.
CALL METHOD REF_GRID->set_frontend_layout
EXPORTING
is_layout = LST_LAYOUT.
CLEAR OK_CODE.

ENDFORM.                    " USER_COMMAND_3000
*&---------------------------------------------------------------------*
*&      Form  TOP_OF_PAGE
*&---------------------------------------------------------------------*
*       明細ヘッダ部
*----------------------------------------------------------------------*
FORM TOP_OF_PAGE USING I_REF_DOCUMENT TYPE REF TO CL_DD_DOCUMENT.
DATA:
LW_POSITION TYPE I,
LW_BUFF     TYPE STRING.

* Source Type:
CLEAR:
LW_POSITION,
LW_BUFF.

CONCATENATE
'<html>'
'<div style="left: 10px;'
'position: absolute;line-height:20px;top: 10px;">'
TEXT-H01
'</div>'
'<div style="left: 146px;'
'position: absolute;line-height:20px;top: 10px;">'
ST_HEADLINE-Z_SOURCE_TYPE
'</div>'
'<div style="left: 262px;'
'position: absolute;line-height:20px;top: 10px;">'
ST_HEADLINE-DDTEXT
'</div>'
'</html>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

* ShippingPt:
CLEAR:
LW_BUFF.

CONCATENATE
'<div style="left: 10px;'
'position: absolute;line-height:20px;top: 30px;">'
TEXT-H02
'</div>'
'<div style="left: 146px;'
'position: absolute;line-height:20px;top: 30px;">'
ST_HEADLINE-VSTEL
'</div>'
'<div style="left: 262px;'
' position: absolute;line-height:20px;top: 30px;">'
ST_HEADLINE-VTEXT
'</div>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

* Outbound Delivery:
CLEAR:
LW_BUFF.

CONCATENATE
'<div style="left: 10px;'
'position: absolute;line-height:20px;top: 50px;">'
TEXT-H03
'</div>'
'<div style="left: 146px;'
'position: absolute;line-height:20px;top: 50px;">'
ST_HEADLINE-VBELN
'</div>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

* Planed GI:
CLEAR:
LW_BUFF.

CONCATENATE
'<div style="left: 10px;'
'position: absolute;line-height:20px;top: 70px;">'
TEXT-H04
'</div>'
'<div style="left: 146px;'
' position: absolute;line-height:20px;top: 70px;">'
ST_HEADLINE-WADAT
'</div>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

* Sold-to party:
CLEAR:
LW_BUFF.

CONCATENATE
'<div style="left: 10px;'
' position: absolute;line-height:20px;top: 90px;">'
TEXT-H05
'</div>'
'<div style="left: 146px;'
'position: absolute;line-height:20px;top: 90px;">'
ST_HEADLINE-KUNAG
'</div>'
'<div style="left: 262px;'
'position: absolute;line-height:20px;top: 90px;">'
ST_HEADLINE-SOLDTONAME
'</div>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

* Ship-to party:
CLEAR:
LW_BUFF.

CONCATENATE
'<div style="left: 10px;'
'position: absolute;line-height:20px;top: 110px;">'
TEXT-H06
'</div>'
'<div style="left: 146px;'
'position: absolute;line-height:20px;top: 110px;">'
ST_HEADLINE-KUNNR
'</div>'
'<div style="left: 262px; '
'position: absolute;line-height:20px;top: 110px;">'
ST_HEADLINE-SHIPTONAME
'</div>'
INTO LW_BUFF.

CALL METHOD I_REF_DOCUMENT->HTML_INSERT
EXPORTING
CONTENTS = LW_BUFF
CHANGING
POSITION = LW_POSITION.

ENDFORM.                    "HTML_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*&      Form  EDIT_FIELDCAT
*&---------------------------------------------------------------------*
*       フィールドカタログ編集
*----------------------------------------------------------------------*
*      <--O_TD_FIELDCAT  ITAB:FIELDCAT
*----------------------------------------------------------------------*
FORM EDIT_FIELDCAT  CHANGING O_TD_FIELDCAT TYPE LVC_T_FCAT.

FIELD-SYMBOLS:
<LFS_FIELDCAT> TYPE LVC_S_FCAT.

LOOP AT O_TD_FIELDCAT ASSIGNING <LFS_FIELDCAT>.

IF <LFS_FIELDCAT>-FIELDNAME = 'SEL'
OR <LFS_FIELDCAT>-FIELDNAME = 'ERDAT'      "Created on
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_VRKME'    "梱包数量単位
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_GEWEI_NW' "重量単位（正味重量）
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_GEWEI_GW' "重量単位（総重量）
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_VOLEH'.   "容積単位

*     表示しない項目
<LFS_FIELDCAT>-NO_OUT = 'X'.

ENDIF.

IF <LFS_FIELDCAT>-FIELDNAME = 'Z_MEINS_PACK'     "Pack Unit
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_CASENO_FROM'    "CASE No(From)
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_CASENO_TO'      "CASE No(To)
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_CASE_MIX'       "Mix
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_LFIMG_UNIT'     "Qty Per Pack
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_NETWEIGHT_UNIT' "N/W(KG) Per Pack
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_GRSWEIGHT_UNIT' "G/W(KG) Per Pack
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_DIMENSION'      "Dimension
OR <LFS_FIELDCAT>-FIELDNAME = 'Z_PKG_REMARKS'.   "Remarks

*     入力可能
<LFS_FIELDCAT>-EDIT = 'X'.

ENDIF.

ENDLOOP.

ENDFORM.                    " EDIT_FIELDCAT
*&---------------------------------------------------------------------*
*&      Class  CL_EVENT_RECEIVER IMPLEMENTATION
*&---------------------------------------------------------------------*
*       Implementation
*----------------------------------------------------------------------*
CLASS CL_EVENT_RECEIVER IMPLEMENTATION.

*&---------------------------------------------------------------------*
*&       Method HANDLE_DATA_CHANGED
*&---------------------------------------------------------------------*
*        画面値が変更される時事件
*----------------------------------------------------------------------*
METHOD HANDLE_DATA_CHANGED.
DATA:
LST_MSG      TYPE LVC_S_MSG1,
LST_MOD      TYPE LVC_S_MODI,
LST_ITEM     TYPE ZSEGSD0015.

CLEAR:
W_ERR_FLG.

CALL METHOD REF_GRID->ACTIVATE_DISPLAY_PROTOCOL( SPACE ).

LOOP AT ER_DATA_CHANGED->MT_MOD_CELLS INTO LST_MOD.

IF ( LST_MOD-FIELDNAME = 'Z_MEINS_PACK'
OR LST_MOD-FIELDNAME = 'Z_CASENO_FROM'
OR LST_MOD-FIELDNAME = 'Z_CASENO_TO'
OR LST_MOD-FIELDNAME = 'Z_LFIMG_UNIT'
OR LST_MOD-FIELDNAME = 'Z_NETWEIGHT_UNIT'
OR LST_MOD-FIELDNAME = 'Z_GRSWEIGHT_UNIT'
OR LST_MOD-FIELDNAME = 'Z_DIMENSION' )
AND LST_MOD-VALUE IS INITIAL.

CALL METHOD ER_DATA_CHANGED->ADD_PROTOCOL_ENTRY
EXPORTING
I_MSGID     = '00'
I_MSGNO     = '055'
I_MSGTY     = 'E'
I_FIELDNAME = LST_MOD-FIELDNAME
I_ROW_ID    = LST_MOD-ROW_ID.

READ TABLE TD_ITEM INTO LST_ITEM INDEX LST_MOD-ROW_ID.

CASE LST_MOD-FIELDNAME.
WHEN 'Z_MEINS_PACK'.

LST_ITEM-Z_MEINS_PACK = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_MEINS_PACK.


WHEN 'Z_CASENO_FROM'.

*           CaseNo Space , Calculate clear
CLEAR:
LST_ITEM-Z_LFIMG,
LST_ITEM-Z_NETWEIGHT,
LST_ITEM-Z_GRSWEIGHT,
LST_ITEM-Z_VOLUM.

LST_ITEM-Z_CASENO_FROM = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_CASENO_FROM
Z_LFIMG
Z_NETWEIGHT
Z_GRSWEIGHT
Z_VOLUM.

WHEN 'Z_CASENO_TO'.

*           CaseNo Space , Calculate clear
CLEAR:
LST_ITEM-Z_LFIMG,
LST_ITEM-Z_NETWEIGHT,
LST_ITEM-Z_GRSWEIGHT,
LST_ITEM-Z_VOLUM.

LST_ITEM-Z_CASENO_TO = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_CASENO_TO
Z_LFIMG
Z_NETWEIGHT
Z_GRSWEIGHT
Z_VOLUM.

WHEN 'Z_LFIMG_UNIT'.

CLEAR:
LST_ITEM-Z_LFIMG.

LST_ITEM-Z_LFIMG_UNIT = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_LFIMG_UNIT
Z_LFIMG.

WHEN 'Z_NETWEIGHT_UNIT'.

CLEAR:
LST_ITEM-Z_NETWEIGHT.

LST_ITEM-Z_NETWEIGHT_UNIT = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_NETWEIGHT_UNIT
Z_NETWEIGHT.

WHEN 'Z_GRSWEIGHT_UNIT'.

CLEAR:
LST_ITEM-Z_GRSWEIGHT.

LST_ITEM-Z_GRSWEIGHT_UNIT = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_GRSWEIGHT_UNIT
Z_GRSWEIGHT.

WHEN 'Z_DIMENSION'.

CLEAR:
LST_ITEM-Z_VOLUM.

LST_ITEM-Z_DIMENSION = LST_MOD-VALUE.
MODIFY TD_ITEM FROM LST_ITEM
INDEX LST_MOD-ROW_ID
TRANSPORTING Z_DIMENSION
Z_VOLUM.

WHEN OTHERS.

ENDCASE.

CONTINUE.

ENDIF.

CASE LST_MOD-FIELDNAME.

*       項目「Dimension」入力後の処理
WHEN 'Z_DIMENSION'.

PERFORM PROCESS_Z_DIMENSION USING LST_MOD-ROW_ID
LST_MOD-VALUE.

WHEN OTHERS.

ENDCASE.
ENDLOOP.

READ TABLE ER_DATA_CHANGED->MT_PROTOCOL INTO LST_MSG INDEX 1.
IF ER_DATA_CHANGED->MT_PROTOCOL IS NOT INITIAL.

MESSAGE ID LST_MSG-MSGID TYPE 'S' NUMBER LST_MSG-MSGNO
WITH LST_MSG-MSGV1
LST_MSG-MSGV2
LST_MSG-MSGV3
LST_MSG-MSGV4
DISPLAY LIKE 'E'.
W_ERR_FLG = 'X'.
RETURN.

ENDIF.

ENDMETHOD.                    "HANDLE_DATA_CHANGED
*&---------------------------------------------------------------------*
*&      Method  HANDLE_DATA_CHANGED_FINISHED
*&---------------------------------------------------------------------*
*&      データを変更された後
*&---------------------------------------------------------------------*
METHOD HANDLE_DATA_CHANGED_FINISHED.

FIELD-SYMBOLS:
<LFS_ITEM>   TYPE ZSEGSD0015.

LOOP AT TD_ITEM ASSIGNING <LFS_ITEM>.

IF <LFS_ITEM>-Z_CASENO_TO IS INITIAL
OR <LFS_ITEM>-Z_CASENO_FROM IS INITIAL.

CONTINUE.

ENDIF.

*     項目「Qty Per Pack」入力後の処理
IF <LFS_ITEM>-Z_CASENO_TO - <LFS_ITEM>-Z_CASENO_FROM >= 0.

*       帳票出力項目：CASE Count
<LFS_ITEM>-Z_CASE_COUNT =  <LFS_ITEM>-Z_CASENO_TO -
<LFS_ITEM>-Z_CASENO_FROM + 1.

TRY .
*           帳票出力項目：Pack. Qty
<LFS_ITEM>-Z_LFIMG = <LFS_ITEM>-Z_LFIMG_UNIT *
( <LFS_ITEM>-Z_CASENO_TO -
<LFS_ITEM>-Z_CASENO_FROM + 1 ).

CATCH CX_SY_ARITHMETIC_OVERFLOW.

<LFS_ITEM>-Z_LFIMG = CNS_LFIMG_MAX.

ENDTRY.

*       項目「N/W(KG) Per Pack」入力後の処理
TRY .

*           帳票出力項目：N/W(KG)
<LFS_ITEM>-Z_NETWEIGHT = <LFS_ITEM>-Z_NETWEIGHT_UNIT *
( <LFS_ITEM>-Z_CASENO_TO -
<LFS_ITEM>-Z_CASENO_FROM + 1 ).

CATCH CX_SY_ARITHMETIC_OVERFLOW.

<LFS_ITEM>-Z_NETWEIGHT = CNS_NETWEIGHT_MAX.

ENDTRY.

*       項目「G/W(KG) Per Pack」入力後の処理
TRY .

*           帳票出力項目：G/W(KG)
<LFS_ITEM>-Z_GRSWEIGHT = <LFS_ITEM>-Z_GRSWEIGHT_UNIT *
( <LFS_ITEM>-Z_CASENO_TO -
<LFS_ITEM>-Z_CASENO_FROM + 1 ).

CATCH CX_SY_ARITHMETIC_OVERFLOW.

<LFS_ITEM>-Z_GRSWEIGHT = CNS_NETWEIGHT_MAX.

ENDTRY.

TRY .

*           帳票出力項目：MEAS(M3)
<LFS_ITEM>-Z_VOLUM = <LFS_ITEM>-Z_VOLUM_UNIT *
( <LFS_ITEM>-Z_CASENO_TO -
<LFS_ITEM>-Z_CASENO_FROM + 1 ).

CATCH CX_SY_ARITHMETIC_OVERFLOW.

<LFS_ITEM>-Z_VOLUM = CNS_NETWEIGHT_MAX.

ENDTRY.

ENDIF.

ENDLOOP.

CALL METHOD CL_GUI_CFW=>FLUSH.

ENDMETHOD.                    "HANDLE_DATA_CHANGED_FINISHED

*&---------------------------------------------------------------------*
*&      Method  HANDLE_MODIFY
*&---------------------------------------------------------------------*
*&      データを変更された後
*&---------------------------------------------------------------------*
METHOD HANDLE_MODIFY.

DATA:
LST_LAYOUT   TYPE LVC_S_LAYO,     "ST:LAYOUT
LW_COL       TYPE LVC_S_COL,
LW_ROW       TYPE LVC_S_ROW,
LTD_MODI     TYPE LVC_T_CELL.

IF SY-UCOMM = CNS_UCOMM_ADDROW
OR SY-UCOMM = CNS_UCOMM_DELETEROW.

RETURN.

ENDIF.

CALL METHOD REF_GRID->GET_CURRENT_CELL
IMPORTING
ES_ROW_ID = LW_ROW
ES_COL_ID = LW_COL.

*   必須入力チェック
IF W_ERR_FLG IS INITIAL.
PERFORM CHECK_MUST_INPUT CHANGING LTD_MODI.
ENDIF.

*    SET USER-COMMAND '&OPT'.

* ストライプ
LST_LAYOUT-ZEBRA = 'X'.
LST_LAYOUT-NO_TOOLBAR = 'X'.

LST_LAYOUT-BOX_FNAME = 'SEL'.

LST_LAYOUT-SEL_MODE  = 'D'.

* ALV コントロール: 列幅の最適化
LST_LAYOUT-CWIDTH_OPT = 'X'.
CALL METHOD REF_GRID->set_frontend_layout
EXPORTING
is_layout = LST_LAYOUT.

CALL METHOD REF_GRID->REFRESH_TABLE_DISPLAY.

CALL METHOD REF_GRID->SET_CURRENT_CELL_VIA_ID
EXPORTING
IS_ROW_ID    = LW_ROW
IS_COLUMN_ID = LW_COL.

IF LTD_MODI IS NOT INITIAL.
CALL METHOD REF_GRID->SET_SELECTED_CELLS
EXPORTING
IT_CELLS = LTD_MODI.

*     必要な入力項目すべてに入力してください
MESSAGE S055(00) DISPLAY LIKE 'E'.

W_ERR_FLG = 'X'.

ENDIF.
IF SY-UCOMM <> '&DATA_SAVE'.
CLEAR W_ERR_FLG.
ENDIF.

ENDMETHOD.                    "HANDLE_MODIFY

ENDCLASS.                    "LCL_EVENT_RECEIVER IMPLEMENTATION
*&---------------------------------------------------------------------*
*&      Form  ENTER_BUTTON
*&---------------------------------------------------------------------*
*       フィールドカタログ編集
*----------------------------------------------------------------------*
*      <--I_ST_GRID
*----------------------------------------------------------------------*
FORM ENTER_BUTTON USING I_ST_GRID TYPE SLIS_DATA_CALLER_EXIT.

DATA:
LREF_RECEIVER TYPE REF TO CL_EVENT_RECEIVER.

CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
IMPORTING
E_GRID = REF_GRID.

CALL METHOD REF_GRID->REGISTER_EDIT_EVENT
EXPORTING
I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_ENTER.

CREATE OBJECT LREF_RECEIVER.

SET HANDLER LREF_RECEIVER->HANDLE_DATA_CHANGED FOR REF_GRID.
SET HANDLER LREF_RECEIVER->HANDLE_DATA_CHANGED_FINISHED FOR REF_GRID.
SET HANDLER LREF_RECEIVER->HANDLE_MODIFY FOR REF_GRID.

*  set USER-COMMAND '&OPT'.

ENDFORM.                    " ENTER_BUTTON
*&---------------------------------------------------------------------*
*&      Form  ADD_ROW
*&---------------------------------------------------------------------*
*       「Add Row」ボタン押下時の処理
*----------------------------------------------------------------------*
FORM ADD_ROW .

DATA:
LTD_ITEM   TYPE TYP_TD_ITEM,
LST_ITEM   TYPE ZSEGSD0015,
LST_ADDROW TYPE ZSEGSD0015,
LW_LFIMG   TYPE ZSEGSD0015-LFIMG.

* 行選択のチェック処理
PERFORM CHECK_SELECTED.

* 行追加処理
LTD_ITEM = TD_ITEM.

CLEAR LST_ITEM.

READ TABLE LTD_ITEM INTO LST_ITEM
WITH KEY SEL = 'X'.

CLEAR LST_ADDROW.
LST_ADDROW-VBELN = LST_ITEM-VBELN.
LST_ADDROW-ERDAT = LST_ITEM-ERDAT.
LST_ADDROW-POSNR = LST_ITEM-POSNR.
LST_ADDROW-MATNR = LST_ITEM-MATNR.
LST_ADDROW-MAKTX = LST_ITEM-MAKTX.
LST_ADDROW-LFIMG = LST_ITEM-LFIMG.
LST_ADDROW-VRKME = LST_ITEM-VRKME.
LST_ADDROW-Z_VRKME     = LST_ITEM-Z_VRKME.
LST_ADDROW-Z_GEWEI_NW  = LST_ITEM-Z_GEWEI_NW.
LST_ADDROW-Z_GEWEI_GW  = LST_ITEM-Z_GEWEI_GW.
LST_ADDROW-Z_VOLEH     = LST_ITEM-Z_VOLEH.

DELETE LTD_ITEM WHERE VBELN <> LST_ITEM-VBELN
OR POSNR <> LST_ITEM-POSNR.

SORT LTD_ITEM BY VBELN DESCENDING
POSNR DESCENDING
Z_PKG_POSNR_SUB DESCENDING.

CLEAR LST_ITEM.

READ TABLE LTD_ITEM INTO LST_ITEM INDEX 1.
LST_ADDROW-Z_PKG_POSNR_SUB = LST_ITEM-Z_PKG_POSNR_SUB + 1.

LOOP AT LTD_ITEM INTO LST_ITEM.
TRY .

LW_LFIMG = LW_LFIMG + LST_ITEM-Z_LFIMG.

CATCH CX_SY_ARITHMETIC_OVERFLOW.

LW_LFIMG = CNS_LFIMG_MAX.

ENDTRY.

ENDLOOP.

LST_ADDROW-Z_LFIMG_UNIT = LST_ITEM-LFIMG - LW_LFIMG.
IF LST_ADDROW-Z_LFIMG_UNIT < 0.

CLEAR LST_ADDROW-Z_LFIMG_UNIT.

ENDIF.

APPEND LST_ADDROW TO TD_ITEM.

SORT TD_ITEM BY VBELN           ASCENDING
POSNR           ASCENDING
Z_PKG_POSNR_SUB ASCENDING.

* 更新された内容をALV一覧に反映する
PERFORM REFRESH_ALV.

ENDFORM.                    " ADD_ROW
*&---------------------------------------------------------------------*
*&      Form  REFRESH_ALV
*&---------------------------------------------------------------------*
*       更新された内容をALV一覧に反映する
*----------------------------------------------------------------------*
FORM REFRESH_ALV .

CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
IMPORTING
E_GRID = REF_GRID.

CALL METHOD REF_GRID->CHECK_CHANGED_DATA.

ENDFORM.                    " REFRESH_ALV
*&---------------------------------------------------------------------*
*&      Form  CHECK_SELECTED
*&---------------------------------------------------------------------*
*       行選択のチェック処理
*----------------------------------------------------------------------*
FORM CHECK_SELECTED .

DATA:
LTD_ITEM   TYPE TYP_TD_ITEM.

LTD_ITEM = TD_ITEM.
* 行のチェックが複数選択されている場合はエラーとしメッセージを出力する
DELETE LTD_ITEM WHERE SEL IS INITIAL.

IF LINES( LTD_ITEM ) > 1.

MESSAGE E007(ZMEGSD01).
*   行が複数選択されています。

ENDIF.

LTD_ITEM = TD_ITEM.
* 行が選択されていない場合は、エラーとしメッセージを出力する
DELETE LTD_ITEM WHERE SEL IS NOT INITIAL.

IF SY-SUBRC <> 0.

MESSAGE E008(ZMEGSD01).
*   行が選択されていません。

ENDIF.

ENDFORM.                    " CHECK_SELECTED
*&---------------------------------------------------------------------*
*&      Form  DELETE_ROW
*&---------------------------------------------------------------------*
*       「Delete Row」ボタン押下時の処理
*----------------------------------------------------------------------*
FORM DELETE_ROW .

DATA:
LTD_ITEM   TYPE TYP_TD_ITEM,
LST_ITEM   TYPE ZSEGSD0015.

* 行選択のチェック処理
PERFORM CHECK_SELECTED.

* 行削除処理
* 選択行のOutbound Delivery/Itemが同一の明細の中で、
* Sub Itemが最大の行の削除を行う
LTD_ITEM = TD_ITEM.
CLEAR LST_ITEM.

READ TABLE LTD_ITEM INTO LST_ITEM
WITH KEY SEL = 'X'.

DELETE LTD_ITEM WHERE VBELN <> LST_ITEM-VBELN
OR POSNR <> LST_ITEM-POSNR.

SORT LTD_ITEM BY VBELN DESCENDING
POSNR DESCENDING
Z_PKG_POSNR_SUB DESCENDING.

IF LINES( LTD_ITEM ) = 1.

RETURN.

ENDIF.

CLEAR LST_ITEM.

READ TABLE LTD_ITEM INTO LST_ITEM INDEX 1.

DELETE TD_ITEM WHERE VBELN = LST_ITEM-VBELN
AND POSNR = LST_ITEM-POSNR
AND Z_PKG_POSNR_SUB = LST_ITEM-Z_PKG_POSNR_SUB.

SORT TD_ITEM BY VBELN           ASCENDING
POSNR           ASCENDING
Z_PKG_POSNR_SUB ASCENDING.

* 更新された内容をALV一覧に反映する
PERFORM REFRESH_ALV.

ENDFORM.                    " DELETE_ROW
*&---------------------------------------------------------------------*
*&      Form  SAVE_ITEM
*&---------------------------------------------------------------------*
*       「Save」ボタン押下時の処理
*----------------------------------------------------------------------*
FORM SAVE_ITEM.

DATA:
LST_HEADER  TYPE ZSEGSD0014,
LW_YMD      TYPE SY-DATUM,
LW_HMS      TYPE SY-UZEIT,
LTD_MODI    TYPE LVC_T_CELL.


LW_YMD = SY-DATUM.
LW_HMS = SY-UZEIT.

* 必須入力チェック
IF W_ERR_FLG IS NOT INITIAL.

CLEAR:
W_ERR_FLG.

RETURN.

ENDIF.

READ TABLE TD_HEADER INTO LST_HEADER
WITH KEY SEL = 'X'.

* Case No
PERFORM CHECK_CASENO CHANGING LTD_MODI.

* 出荷伝票の数量との差異チェック
PERFORM CHECK_QTY CHANGING LTD_MODI.

IF LTD_MODI IS NOT INITIAL.

CALL METHOD REF_GRID->SET_SELECTED_CELLS
EXPORTING
IT_CELLS = LTD_MODI.

RETURN.

ENDIF.

* データ削除（PackingList）
PERFORM SAVE_EDIT_DATA USING LST_HEADER.

* データ保存@（TradeCommon）
PERFORM SAVE_EDIT_ZTEGSDT001 USING LST_HEADER
LW_YMD
LW_HMS.

* データ保存A（PackingList）
PERFORM SAVE_EDIT_ZTEGSDT002 USING LST_HEADER
LW_YMD
LW_HMS.

* テーブルリリース

COMMIT WORK AND WAIT.

PERFORM UNLOCK_TABLE USING TD_ITEM.

ENDFORM.                    " SAVE_ITEM
*&---------------------------------------------------------------------*
*&      Form  CHECK_CASENO
*&---------------------------------------------------------------------*
*       Case No
*----------------------------------------------------------------------*
FORM CHECK_CASENO CHANGING O_TD_MODI TYPE LVC_T_CELL.

DATA:
LST_ITEM   TYPE ZSEGSD0015,
LST_MIX    TYPE ZSEGSD0015,
LTD_ITEM   TYPE TYP_TD_ITEM,
LTD_CASE   TYPE TYP_TD_ITEM,
LW_Z_CASENO_FROM TYPE ZSEGSD0015-Z_CASENO_FROM,
LW_Z_CASENO_TO   TYPE ZSEGSD0015-Z_CASENO_TO,
LW_CASENO        TYPE CHAR1,
LW_Z_MIX_FROM    TYPE ZSEGSD0015-Z_CASENO_FROM,
LW_Z_MIX_TO      TYPE ZSEGSD0015-Z_CASENO_TO,
LW_Z_EMPTY_TO    TYPE ZSEGSD0015-Z_CASENO_TO,
LW_MIX     TYPE CHAR1,
LW_INDEX   TYPE SY-TABIX,
LW_NEW     TYPE SY-TABIX.

LTD_ITEM = TD_ITEM.

MODIFY LTD_ITEM FROM LST_ITEM
TRANSPORTING SEL
WHERE VBELN IS NOT INITIAL.

SORT LTD_ITEM BY VBELN ASCENDING
ERDAT ASCENDING
POSNR ASCENDING.

* 保存前チェック（From/Toチェック）
LOOP AT LTD_ITEM INTO LST_ITEM.

LW_INDEX = LW_INDEX + 1.
LW_NEW = LW_NEW + 1.

*   「Case No(From) > Case No(To)」の場合、
*   メッセージを出力し処理を中断する
IF LST_ITEM-Z_CASENO_FROM > LST_ITEM-Z_CASENO_TO.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

MESSAGE S009(ZMEGSD01) DISPLAY LIKE 'E'.
RETURN.
*     Case No(From)がCase No(To)より大きくなっています。

ENDIF.

AT NEW POSNR.

CLEAR:
LST_MIX.
LTD_CASE = LTD_ITEM.
SORT LTD_CASE BY VBELN ASCENDING
ERDAT ASCENDING
POSNR ASCENDING
Z_CASE_MIX ASCENDING.

*     取得MIXのLine対象
READ TABLE LTD_CASE INTO LST_MIX
WITH KEY VBELN = LST_ITEM-VBELN
ERDAT = LST_ITEM-ERDAT
POSNR = LST_ITEM-POSNR
Z_CASE_MIX = 'X'
BINARY SEARCH.

IF SY-SUBRC = 0.

LW_Z_MIX_FROM = LST_MIX-Z_CASENO_FROM.
LW_Z_MIX_TO   = LST_MIX-Z_CASENO_TO.
LW_MIX = 'X'.   "MIX Flag

ENDIF.

*     First 1 . Binnary Search not use

LTD_CASE = LTD_ITEM.

DELETE LTD_CASE WHERE VBELN <> LST_ITEM-VBELN
OR ERDAT <> LST_ITEM-ERDAT
OR POSNR <> LST_ITEM-POSNR.

IF LTD_CASE IS INITIAL.

CONTINUE.

ELSE.

CLEAR:
LST_MIX.

READ TABLE LTD_CASE INTO LST_MIX INDEX 1.

LW_Z_CASENO_FROM = LST_MIX-Z_CASENO_FROM.
LW_Z_CASENO_TO   = LST_MIX-Z_CASENO_TO.

ENDIF.

ENDAT.

*   保存前チェック（Mixチェック）
IF  LST_ITEM-Z_CASE_MIX IS NOT INITIAL
AND ( LW_Z_MIX_FROM <> LST_ITEM-Z_CASENO_FROM
OR    LW_Z_MIX_TO <> LST_ITEM-Z_CASENO_TO )
AND LW_MIX IS NOT INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

MESSAGE S016(ZMEGSD01) DISPLAY LIKE 'E'.
RETURN.
*       Mix明細のCase Noが一致していません。

ENDIF.

*   保存前チェック（重複チェック）
IF ( LW_Z_CASENO_FROM >= LST_ITEM-Z_CASENO_FROM
OR LW_Z_CASENO_TO >= LST_ITEM-Z_CASENO_FROM )
AND LST_ITEM-Z_CASE_MIX IS INITIAL
AND LW_NEW > 1.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

MESSAGE S010(ZMEGSD01) DISPLAY LIKE 'E'.
RETURN.
*     Case Noが重複しています。

ENDIF.

IF LST_ITEM-Z_CASE_MIX IS NOT INITIAL
AND LW_NEW > 1
AND LW_CASENO IS INITIAL.

*     取得チェック成功のMAXのCaseNo
LTD_CASE = LTD_ITEM.

DELETE LTD_CASE FROM LW_INDEX + 1.

DELETE LTD_CASE WHERE VBELN <> LST_ITEM-VBELN
OR ERDAT <> LST_ITEM-ERDAT
OR POSNR <> LST_ITEM-POSNR.

SORT LTD_CASE BY Z_CASENO_TO DESCENDING.

CLEAR LST_MIX.

READ TABLE LTD_CASE INTO LST_MIX INDEX 1.

LST_ITEM-Z_CASENO_FROM =  LST_MIX-Z_CASENO_FROM.
LST_ITEM-Z_CASENO_TO   =  LST_MIX-Z_CASENO_TO.

ENDIF.

LW_Z_CASENO_FROM = LST_ITEM-Z_CASENO_FROM.
LW_Z_CASENO_TO   = LST_ITEM-Z_CASENO_TO.
LW_CASENO = LST_ITEM-Z_CASE_MIX.

*   保存前チェック（空き番号チェック）
IF LW_NEW = 1.

IF LST_ITEM-Z_CASENO_FROM < 0
OR LST_ITEM-Z_CASENO_TO < 0
OR LST_ITEM-Z_CASENO_TO - LST_ITEM-Z_CASENO_FROM < 0.

*       SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

MESSAGE S011(ZMEGSD01) DISPLAY LIKE 'E'.
RETURN.
*       Case Noに空き番号が存在します。

ENDIF.

LW_Z_EMPTY_TO = LST_ITEM-Z_CASENO_TO.

ENDIF.

IF LW_NEW > 1.

IF LST_ITEM-Z_CASENO_FROM - LW_Z_EMPTY_TO > 1.

*       SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

MESSAGE S011(ZMEGSD01) DISPLAY LIKE 'E'.
RETURN.
*       Case Noに空き番号が存在します。

ENDIF.

LW_Z_EMPTY_TO = LST_ITEM-Z_CASENO_TO.

ENDIF.

AT END OF POSNR.

CLEAR:
LW_MIX,
LW_NEW,
LW_Z_CASENO_FROM,
LW_Z_CASENO_TO,
LW_Z_EMPTY_TO,
LW_Z_MIX_FROM,
LW_Z_MIX_TO,
LW_CASENO.

ENDAT.

ENDLOOP.

ENDFORM.                    " CHECK_CASENO
*&---------------------------------------------------------------------*
*&      Form  SET_CURSOR
*&---------------------------------------------------------------------*
*       SET CURSOR
*----------------------------------------------------------------------*
*      -->I_TABIX  Lines
*      -->I_NAME   TEXT
*----------------------------------------------------------------------*
FORM SET_CURSOR  USING VALUE(I_TABIX) TYPE SY-TABIX
VALUE(I_NAME) TYPE LVC_FNAME
CHANGING O_TD_MODI TYPE LVC_T_CELL.

DATA:
LST_MODI   TYPE LVC_S_CELL.

LST_MODI-ROW_ID-INDEX = I_TABIX.
LST_MODI-COL_ID-FIELDNAME = I_NAME.
APPEND LST_MODI TO O_TD_MODI.

ENDFORM.                    " SET_CURSOR
*&---------------------------------------------------------------------*
*&      Form  CHECK_QTY
*&---------------------------------------------------------------------*
*       出荷伝票の数量との差異チェック
*----------------------------------------------------------------------*
FORM CHECK_QTY CHANGING O_TD_MODI TYPE LVC_T_CELL.

DATA:
LST_ITEM   TYPE ZSEGSD0015,
LST_SUM    TYPE ZSEGSD0015,
LTD_ITEM   TYPE TYP_TD_ITEM,
LW_Z_LFIMG TYPE ZSEGSD0015-Z_LFIMG,
LW_INDEX   TYPE SY-TABIX.

IF O_TD_MODI IS NOT INITIAL.
RETURN.
ENDIF.

LTD_ITEM = TD_ITEM.

SORT LTD_ITEM BY VBELN           ASCENDING
POSNR           ASCENDING.

DELETE ADJACENT DUPLICATES FROM LTD_ITEM
COMPARING VBELN
POSNR.

LOOP AT LTD_ITEM INTO LST_ITEM.

CLEAR:
LW_Z_LFIMG,
LW_INDEX.

LOOP AT TD_ITEM INTO LST_SUM
WHERE VBELN = LST_ITEM-VBELN
AND POSNR = LST_ITEM-POSNR.

IF LW_INDEX IS INITIAL.

LW_INDEX = SY-TABIX.

ENDIF.

LW_Z_LFIMG = LW_Z_LFIMG + LST_SUM-Z_LFIMG.

ENDLOOP.

IF LST_ITEM-LFIMG <> LW_Z_LFIMG.

*       SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_LFIMG_UNIT'
CHANGING O_TD_MODI.

MESSAGE S015(ZMEGSD01) WITH LST_ITEM-POSNR DISPLAY LIKE 'E'.
RETURN.
*     元伝票とPackingListデータの数量が一致しません。（明細：&1）

ENDIF.

ENDLOOP.

ENDFORM.                    " CHECK_QTY

*&---------------------------------------------------------------------*
*&      Form  PROCESS_Z_DIMENSION
*&---------------------------------------------------------------------*
*       項目「Dimension」入力後の処理
*----------------------------------------------------------------------*
*      -->I_ROW
*----------------------------------------------------------------------*
FORM PROCESS_Z_DIMENSION  USING    I_ROW  TYPE INT4
I_VALUE TYPE LVC_S_MODI-VALUE.

DATA:
LW_DIMENSION_TEXT  TYPE ZTEGSDM009-Z_DIMENSION_TEXT,
LW_MEAS TYPE ZTEGSDM009-Z_MEAS.

FIELD-SYMBOLS:
<LFS_ITEM>   TYPE ZSEGSD0015.

READ TABLE TD_ITEM ASSIGNING <LFS_ITEM> INDEX I_ROW.

SELECT SINGLE
Z_DIMENSION_TEXT      "Dimension Text
Z_MEAS                "MEAS(M3)
INTO (LW_DIMENSION_TEXT , LW_MEAS)
FROM ZTEGSDM009
WHERE Z_TEMPLATE_ID = I_VALUE.

* 帳票出力項目：Dimension Text
<LFS_ITEM>-Z_DIMENSION_TEXT = LW_DIMENSION_TEXT.

* 項目「Dimension」入力後（フォーカスロスト）には、
* 項目「MEAS(M3) Per Pack」「MEAS(M3)」に下記を設定する

* 帳票出力項目：MEAS(M3) Per Pack
<LFS_ITEM>-Z_VOLUM_UNIT = LW_MEAS.

ENDFORM.                    " PROCESS_Z_DIMENSION
*&---------------------------------------------------------------------*
*&      Form  SAVE_EDIT_DATA
*&---------------------------------------------------------------------*
*       データ削除（PackingList）
*----------------------------------------------------------------------*
FORM SAVE_EDIT_DATA USING I_ST_HEADER TYPE ZSEGSD0014.

* テーブルロック
PERFORM LOCK_TABLE.

* DELETE処理
PERFORM DELETE_ZTEGSDT002 USING I_ST_HEADER.

ENDFORM.                    " SAVE_EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  LOCK_TABLE
*&---------------------------------------------------------------------*
*       テーブルロック
*----------------------------------------------------------------------*
FORM LOCK_TABLE .

DATA:
LST_ITEM TYPE ZSEGSD0015,
LTD_ITEM TYPE TYP_TD_ITEM.

LOOP AT TD_ITEM INTO LST_ITEM.

*   PackingList
CALL FUNCTION 'ENQUEUE_EZZTEGSDT002'
EXPORTING
MODE_ZTEGSDT002 = 'E'
VBELN           = LST_ITEM-VBELN
ERDAT           = LST_ITEM-ERDAT
POSNR           = LST_ITEM-POSNR
Z_PKG_POSNR_SUB = LST_ITEM-Z_PKG_POSNR_SUB
EXCEPTIONS
FOREIGN_LOCK    = 1
SYSTEM_FAILURE  = 2
OTHERS          = 3.
IF SY-SUBRC <> 0.

*     テーブルリリース
PERFORM UNLOCK_TABLE USING LTD_ITEM.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

*   TradeCommon
CALL FUNCTION 'ENQUEUE_EZZTEGSDT001'
EXPORTING
MODE_ZTEGSDT001 = 'E'
VBELN           = LST_ITEM-VBELN
ERDAT           = LST_ITEM-ERDAT
EXCEPTIONS
FOREIGN_LOCK    = 1
SYSTEM_FAILURE  = 2
OTHERS          = 3.
IF SY-SUBRC <> 0.

APPEND LST_ITEM TO LTD_ITEM.
*     テーブルリリース
PERFORM UNLOCK_TABLE USING LTD_ITEM.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

APPEND LST_ITEM TO LTD_ITEM.

ENDLOOP.

ENDFORM.                    " LOCK_TABLE
*&---------------------------------------------------------------------*
*&      Form  UNLOCK_TABLE
*&---------------------------------------------------------------------*
*       テーブルリリース
*----------------------------------------------------------------------*
*      -->I_TD_ITEM  PackingList明細
*----------------------------------------------------------------------*
FORM UNLOCK_TABLE  USING    I_TD_ITEM TYPE TYP_TD_ITEM.

DATA:
LST_ITEM TYPE ZSEGSD0015.

LOOP AT I_TD_ITEM INTO LST_ITEM.

CALL FUNCTION 'DEQUEUE_EZZTEGSDT002'
EXPORTING
MODE_ZTEGSDT002 = 'E'
VBELN           = LST_ITEM-VBELN
ERDAT           = LST_ITEM-ERDAT
POSNR           = LST_ITEM-POSNR
Z_PKG_POSNR_SUB = LST_ITEM-Z_PKG_POSNR_SUB.

CALL FUNCTION 'DEQUEUE_EZZTEGSDT001'
EXPORTING
MODE_ZTEGSDT001 = 'E'
VBELN           = LST_ITEM-VBELN
ERDAT           = LST_ITEM-ERDAT.

ENDLOOP.

ENDFORM.                    " UNLOCK_TABLE
*&---------------------------------------------------------------------*
*&      Form  DELETE_ZTEGSDT002
*&---------------------------------------------------------------------*
*       DELETE処理
*----------------------------------------------------------------------*
*      -->I_ST_HEADER
*----------------------------------------------------------------------*
FORM DELETE_ZTEGSDT002  USING I_ST_HEADER TYPE ZSEGSD0014.

DATA:
LW_MSG         TYPE SY-MSGV1,
LW_VBELN       TYPE ZTEGSDT002-VBELN.

* 存在の判断
SELECT VBELN
INTO LW_VBELN
FROM ZTEGSDT002
UP TO 1 ROWS
WHERE VBELN = I_ST_HEADER-VBELN
AND ERDAT = I_ST_HEADER-ERDAT.
ENDSELECT.

IF SY-SUBRC <> 0.

RETURN.

ENDIF.

DELETE FROM ZTEGSDT002 WHERE VBELN = I_ST_HEADER-VBELN
AND ERDAT = I_ST_HEADER-ERDAT.

IF SY-SUBRC <> 0.

ROLLBACK WORK.

*   テーブルリリース
PERFORM UNLOCK_TABLE USING TD_ITEM.

CONCATENATE 'ZMEGSD01'
'012'
INTO LW_MSG
SEPARATED BY SPACE.

MESSAGE E012(ZMEGSD01) WITH 'ZTEGSDT002' LW_MSG.
*   データ更新に失敗しました。(TBL = &1 / MSG = &2 )

ENDIF.

ENDFORM.                    " DELETE_ZTEGSDT002
*&---------------------------------------------------------------------*
*&      Form  SAVE_EDIT_ZTEGSDT001
*&---------------------------------------------------------------------*
*       データ保存@（TradeCommon）
*----------------------------------------------------------------------*
*      -->I_TD_ITEM
*      -->I_YMD
*      -->I_HMS
*----------------------------------------------------------------------*
FORM SAVE_EDIT_ZTEGSDT001  USING I_ST_HEADER TYPE ZSEGSD0014
I_YMD TYPE SY-DATUM
I_HMS TYPE SY-UZEIT.

DATA:
LTD_VBELN      TYPE TYP_TD_VBELN,
LTD_ITEM       TYPE TYP_TD_ITEM,
LST_ITEM       TYPE ZSEGSD0015,
LST_INSERT     TYPE ZTEGSDT001,
LW_MSG         TYPE SY-MSGV1.

LTD_ITEM = TD_ITEM.
SORT LTD_ITEM BY VBELN ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_ITEM
COMPARING VBELN.

SELECT VBELN         "出荷伝票
ERDAT         "レコード登録日
INTO TABLE LTD_VBELN
FROM ZTEGSDT001
FOR ALL ENTRIES IN LTD_ITEM
WHERE VBELN = LTD_ITEM-VBELN
AND ERDAT = I_ST_HEADER-ERDAT.

SORT LTD_VBELN BY VBELN ASCENDING.

LST_INSERT-ERDAT            = I_ST_HEADER-ERDAT.
LST_INSERT-Z_STATUS_PACKING = 'X'.
LST_INSERT-Z_CRE_YMD_PKG    = I_YMD.
LST_INSERT-Z_CRE_HMS_PKG    = I_HMS.
LST_INSERT-Z_CRE_USERID_PKG = SY-UNAME.
LST_INSERT-Z_CRE_YMD        = I_YMD.
LST_INSERT-Z_CRE_HMS        = I_HMS.
LST_INSERT-Z_CRE_USERID     = SY-UNAME.

LOOP AT LTD_ITEM INTO LST_ITEM.


READ TABLE LTD_VBELN TRANSPORTING NO FIELDS
WITH KEY VBELN = LST_ITEM-VBELN
BINARY SEARCH.

*   UPDATE時（TradeCommonが存在する場合）
IF SY-SUBRC = 0.

UPDATE ZTEGSDT001
SET Z_STATUS_PACKING = 'X'
Z_MOD_YMD_PKG    = I_YMD
Z_MOD_HMS_PKG    = I_HMS
Z_MOD_USERID_PKG = SY-UNAME
Z_MOD_YMD        = I_YMD
Z_MOD_HMS        = I_HMS
Z_MOD_USERID     = SY-UNAME
WHERE VBELN = LST_ITEM-VBELN
AND ERDAT = I_ST_HEADER-ERDAT.

*   INSERT時（TradeCommonが存在しない場合）
ELSE.

LST_INSERT-VBELN            = LST_ITEM-VBELN.

INSERT ZTEGSDT001 FROM LST_INSERT.

ENDIF.

*   データ登録/更新に失敗した場合、メッセージを出力し処理を終了する

IF SY-SUBRC <> 0.

ROLLBACK WORK.

*     テーブルリリース
PERFORM UNLOCK_TABLE USING TD_ITEM.

CONCATENATE 'ZMEGSD01'
'012'
INTO LW_MSG
SEPARATED BY SPACE.

MESSAGE E012(ZMEGSD01) WITH 'ZTEGSDT001' LW_MSG.
*     データ更新に失敗しました。(TBL = &1 / MSG = &2 )

ENDIF.

ENDLOOP.

ENDFORM.                    " SAVE_EDIT_ZTEGSDT001
*&---------------------------------------------------------------------*
*&      Form  SAVE_EDIT_ZTEGSDT002
*&---------------------------------------------------------------------*
*       データ保存A（PackingList）
*----------------------------------------------------------------------*
*      -->I_ST_HEADER
*      -->I_YMD
*      -->I_HMS
*----------------------------------------------------------------------*
FORM SAVE_EDIT_ZTEGSDT002  USING I_ST_HEADER TYPE ZSEGSD0014
I_YMD TYPE SY-DATUM
I_HMS TYPE SY-UZEIT.

DATA:
LTD_ITEM   TYPE TYP_TD_ITEM,
LST_ITEM   TYPE ZSEGSD0015,
LST_INSERT TYPE ZTEGSDT002,
LTD_INSERT TYPE STANDARD TABLE OF ZTEGSDT002,
LTD_MEINS  TYPE TYP_TD_MEINS,
LST_MEINS  TYPE TYP_MEINS,
LW_MSG     TYPE SY-MSGV1.

LTD_ITEM = TD_ITEM.

SORT LTD_ITEM BY MATNR ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_ITEM
COMPARING MATNR.

SELECT MATNR
MEINS
INTO TABLE LTD_MEINS
FROM MARA
FOR ALL ENTRIES IN LTD_ITEM
WHERE MATNR = LTD_ITEM-MATNR.

SORT LTD_MEINS BY MATNR ASCENDING.

LOOP AT TD_ITEM INTO LST_ITEM.

CLEAR LST_INSERT.

LST_INSERT-VBELN              = LST_ITEM-VBELN.
LST_INSERT-ERDAT              = I_ST_HEADER-ERDAT.
LST_INSERT-POSNR              = LST_ITEM-POSNR.
LST_INSERT-Z_PKG_POSNR_SUB    = LST_ITEM-Z_PKG_POSNR_SUB.
LST_INSERT-MATNR              = LST_ITEM-MATNR.
LST_INSERT-Z_MEINS_PACK       = LST_ITEM-Z_MEINS_PACK.
LST_INSERT-Z_LFIMG_UNIT       = LST_ITEM-Z_LFIMG_UNIT.
LST_INSERT-Z_LFIMG            = LST_ITEM-Z_LFIMG.

READ TABLE LTD_MEINS INTO LST_MEINS
WITH KEY MATNR = LST_ITEM-MATNR
BINARY SEARCH.

LST_INSERT-Z_VRKME            = LST_MEINS-MEINS.

LST_INSERT-Z_CASENO_FROM      = LST_ITEM-Z_CASENO_FROM.
LST_INSERT-Z_CASENO_TO        = LST_ITEM-Z_CASENO_TO.
LST_INSERT-Z_CASE_COUNT       = LST_ITEM-Z_CASE_COUNT.
LST_INSERT-Z_CASE_MIX         = LST_ITEM-Z_CASE_MIX.
LST_INSERT-Z_NETWEIGHT_UNIT   = LST_ITEM-Z_NETWEIGHT_UNIT.
LST_INSERT-Z_NETWEIGHT        = LST_ITEM-Z_NETWEIGHT.
LST_INSERT-Z_GEWEI_NW         = 'KG'.
LST_INSERT-Z_GRSWEIGHT_UNIT   = LST_ITEM-Z_GRSWEIGHT_UNIT.
LST_INSERT-Z_GRSWEIGHT        = LST_ITEM-Z_GRSWEIGHT.
LST_INSERT-Z_GEWEI_GW         = 'KG'.
LST_INSERT-Z_DIMENSION        = LST_ITEM-Z_DIMENSION.
LST_INSERT-Z_VOLUM_UNIT       = LST_ITEM-Z_VOLUM_UNIT.
LST_INSERT-Z_VOLUM            = LST_ITEM-Z_VOLUM.
LST_INSERT-Z_VOLEH            = 'M3'.
LST_INSERT-Z_PKG_REMARKS      = LST_ITEM-Z_PKG_REMARKS.
LST_INSERT-Z_CRE_YMD          = I_YMD.
LST_INSERT-Z_CRE_HMS          = I_HMS.
LST_INSERT-Z_CRE_USERID       = SY-UNAME.

APPEND LST_INSERT TO LTD_INSERT.

CLEAR:
LST_MEINS.

ENDLOOP.

INSERT ZTEGSDT002 FROM TABLE LTD_INSERT.

* データ登録に失敗した場合、メッセージを出力し処理を終了する
IF SY-SUBRC <> 0.

ROLLBACK WORK.

*   テーブルリリース
PERFORM UNLOCK_TABLE USING TD_ITEM.

CONCATENATE 'ZMEGSD01'
'012'
INTO LW_MSG
SEPARATED BY SPACE.

MESSAGE E012(ZMEGSD01) WITH 'ZTEGSDT002' LW_MSG.
*   データ更新に失敗しました。(TBL = &1 / MSG = &2 )

* データ登録に成功した場合、メッセージを出力する
ELSE.

MESSAGE S013(ZMEGSD01).
*   データ登録が正常終了しました。

ENDIF.

ENDFORM.                    " SAVE_EDIT_ZTEGSDT002
*&---------------------------------------------------------------------*
*&      Form  EXIT_ALV
*&---------------------------------------------------------------------*
*       Exit判断
*----------------------------------------------------------------------*
FORM EXIT_ALV .

DATA:
LW_QUESTION TYPE CHAR72,
LW_ANSWER   TYPE CHAR1.

CLEAR:
W_ERR_FLG.

IF TD_ORIGINAL <> TD_ITEM.

*   終了しますか？
MESSAGE E050(ZMEGSD01) INTO LW_QUESTION.

CALL FUNCTION 'POPUP_TO_CONFIRM'
EXPORTING
TITLEBAR              = TEXT-B01
TEXT_QUESTION         = LW_QUESTION
TEXT_BUTTON_1         = TEXT-B02
TEXT_BUTTON_2         = TEXT-B03
DISPLAY_CANCEL_BUTTON = SPACE
IMPORTING
ANSWER                = LW_ANSWER
EXCEPTIONS
TEXT_NOT_FOUND        = 1
OTHERS                = 2.
IF SY-SUBRC <> 0.

MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.

ENDIF.

IF LW_ANSWER = '1'.

LEAVE TO SCREEN 0.

ENDIF.

ELSE.

LEAVE TO SCREEN 0.

ENDIF.

ENDFORM.                    " EXIT_ALV
*&---------------------------------------------------------------------*
*&      Form  GET_TEXT_DIMENSION
*&---------------------------------------------------------------------*
*       項目「Dimension Text」
*----------------------------------------------------------------------*
*      -->I_TD_ZTEGSDT002
*      <--O_TD_DIMENSION
*----------------------------------------------------------------------*
FORM GET_TEXT_DIMENSION USING I_TD_ZTEGSDT002 TYPE TYP_TD_ZTEGSDT002
CHANGING O_TD_DIMENSION TYPE TYP_TD_ZTEGSDM009.

DATA:
LTD_ZTEGSDT002 TYPE TYP_TD_ZTEGSDT002.

LTD_ZTEGSDT002 = I_TD_ZTEGSDT002.
DELETE LTD_ZTEGSDT002 WHERE Z_DIMENSION IS INITIAL.

IF LTD_ZTEGSDT002 IS INITIAL.

RETURN.

ENDIF.

SORT LTD_ZTEGSDT002 BY Z_DIMENSION ASCENDING.
DELETE ADJACENT DUPLICATES FROM LTD_ZTEGSDT002
COMPARING Z_DIMENSION.

SELECT Z_TEMPLATE_ID         "テンプレートID
Z_DIMENSION_TEXT      "Dimension Text
Z_MEAS                "MEAS(M3) Per Pack
INTO TABLE O_TD_DIMENSION
FROM ZTEGSDM009
FOR ALL ENTRIES IN LTD_ZTEGSDT002
WHERE Z_TEMPLATE_ID = LTD_ZTEGSDT002-Z_DIMENSION.

SORT O_TD_DIMENSION BY Z_TEMPLATE_ID ASCENDING.

ENDFORM.                    " GET_TEXT_DIMENSION
*&---------------------------------------------------------------------*
*&      Form  CHECK_MUST_INPUT
*&---------------------------------------------------------------------*
*       必須入力チェック
*----------------------------------------------------------------------*
FORM CHECK_MUST_INPUT CHANGING O_TD_MODI TYPE LVC_T_CELL.

DATA:
LST_ITEM   TYPE ZSEGSD0015,
LW_INDEX   TYPE SY-TABIX.

LOOP AT TD_ITEM INTO LST_ITEM.

LW_INDEX = SY-TABIX.

IF LST_ITEM-Z_MEINS_PACK IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_MEINS_PACK'
CHANGING O_TD_MODI.

ENDIF.

IF LST_ITEM-Z_CASENO_FROM IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_FROM'
CHANGING O_TD_MODI.

ENDIF.

IF LST_ITEM-Z_CASENO_TO IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_CASENO_TO'
CHANGING O_TD_MODI.

ENDIF.

IF LST_ITEM-Z_LFIMG_UNIT IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_LFIMG_UNIT'
CHANGING O_TD_MODI.

ENDIF.

IF LST_ITEM-Z_NETWEIGHT_UNIT IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_NETWEIGHT_UNIT'
CHANGING O_TD_MODI.

ENDIF.
IF LST_ITEM-Z_GRSWEIGHT_UNIT IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_GRSWEIGHT_UNIT'
CHANGING O_TD_MODI.

ENDIF.

IF LST_ITEM-Z_DIMENSION IS INITIAL.

*     SET CURSOR
PERFORM SET_CURSOR USING LW_INDEX
'Z_DIMENSION'
CHANGING O_TD_MODI.

ENDIF.

ENDLOOP.

ENDFORM.                    " CHECK_MUST_INPUT
