*&---------------------------------------------------------------------*
*&  REPORT ZN030600                                                    *
*&         【移行】自社データ（売上）（未検収）【セットアップ】        *
*&---------------------------------------------------------------------*
*&  機能     : YK130をYN120に変換する
*&
*&  作成日   : 2012/03/30
*&  作成者   : K.KAJISA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 : 2012/05/11
*&  変更内容 : 請求伝票アーカイブ分はエラーとしない
*&---------------------------------------------------------------------*
REPORT ZN030600
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.
*----------------------------------------------------------------------*
*   テーブル
*----------------------------------------------------------------------*
TABLES:YK130,YN120.
*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
TYPES:BEGIN OF TYP_YK130.
INCLUDE STRUCTURE YK130.
TYPES:GJAHR    TYPE BSID-GJAHR,
END OF TYP_YK130.

* 得意先締日
TYPES:BEGIN OF TYP_KNVV,
KUNNR TYPE KNVV-KUNNR,
VKORG TYPE KNVV-VKORG,
VTWEG TYPE KNVV-VTWEG,
SPART TYPE KNVV-SPART,
ZTERM TYPE KNVV-ZTERM,
ZFAEL TYPE T052-ZFAEL,
END OF TYP_KNVV.

* ファイルダウンロード
TYPES:BEGIN OF  TYP_DOWNLOAD,
VBELN     TYPE CHAR10,
POSNR     TYPE CHAR6,
FIXFLG    TYPE CHAR1,
KEKKBN    TYPE CHAR1,
KUNNR     TYPE CHAR10,
KUNR2     TYPE CHAR10,
VKORG     TYPE CHAR4,
VTWEG     TYPE CHAR2,
SPART     TYPE CHAR2,
VKBUR     TYPE CHAR4,
VKGRP     TYPE CHAR3,
BUKRS     TYPE CHAR4,
GSBER     TYPE CHAR4,
BSTKD(35) TYPE C,
MAKTX     TYPE CHAR40,
MATNR     TYPE CHAR18,
KDMAT     TYPE CHAR35,
YKYEAR    TYPE CHAR4,
ZFBDT     TYPE CHAR8,
FKDAT     TYPE CHAR8,
KNQUAN    TYPE CHAR15,
KNUNIT    TYPE CHAR3,
KNUNTPRC  TYPE CHAR22,
KNITXAMT  TYPE CHAR21,
KNTAXAMT  TYPE CHAR21,
KNETXAMT  TYPE CHAR21,
WAERS     TYPE CHAR5,
SGTXT     TYPE CHAR50,
ZFBDT2    TYPE CHAR8,
PRCDAT    TYPE CHAR8,
PRCNO     TYPE CHAR10,
INSDAT    TYPE CHAR8,
INSTIM    TYPE CHAR6,
INSUSR    TYPE CHAR12,
UPDDAT    TYPE CHAR8,
UPDTIM    TYPE CHAR6,
UPDUSR    TYPE CHAR12,
RRFLG     TYPE CHAR1,
END  OF TYP_DOWNLOAD,
T_TYP_DOWNLOAD TYPE STANDARD TABLE OF TYP_DOWNLOAD.


* BSID
TYPES:BEGIN OF TYP_BSID,
GJAHR  TYPE BSID-GJAHR, "会計年度
BELNR  TYPE BSID-BELNR, "会計伝票番号
BUKRS  TYPE BSID-BUKRS, "会社コード
KUNNR  TYPE BSID-KUNNR, "得意先コード
BUZEI  TYPE BSID-BUZEI, "会計伝票明細番号
UMSKS  TYPE BSID-UMSKS, "特殊仕訳取引タイプ
UMSKZ  TYPE BSID-UMSKZ, "特殊仕訳コード
AUGDT  TYPE BSID-AUGDT, "決済日付
AUGBL  TYPE BSID-AUGBL, "決済伝票番号
ZUONR  TYPE BSID-ZUONR, "ソートキー
END OF TYP_BSID.

* 請求伝票: 明細データ
TYPES:BEGIN OF TYP_VBRP,
VBELN  TYPE VBRP-VBELN,  "請求伝票
POSNR  TYPE VBRP-POSNR,  "請求明細
AUBEL  TYPE VBRP-AUBEL,  "販売伝票
END OF TYP_VBRP.

* エラーデータ
TYPES:BEGIN OF TYP_ERR,
VBELN    TYPE YK130-VBELN,   "請求書伝票の伝票番号
POSNR    TYPE YK130-POSNR,   "請求書伝票内伝票明細
MESSAGE  TYPE CHAR255,       "エラー内容
END   OF TYP_ERR.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*
DATA:T_YK130 TYPE STANDARD TABLE OF YK130,
W_YK130 TYPE YK130.
*     T_YN120 TYPE T_TYP_YN120,

DATA:T_YK130_O TYPE STANDARD TABLE OF TYP_YK130,
W_YK130_O TYPE TYP_YK130.

DATA:T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART,
W_KNVV TYPE TYP_KNVV.

DATA:T_BSID TYPE SORTED TABLE OF TYP_BSID
WITH UNIQUE KEY  GJAHR BELNR BUKRS KUNNR BUZEI
UMSKS UMSKZ AUGDT AUGBL ZUONR,
W_BSID TYPE TYP_BSID.

DATA:T_VBRP TYPE SORTED TABLE OF TYP_VBRP
WITH UNIQUE KEY VBELN POSNR,
W_VBRP TYPE TYP_VBRP.

DATA:T_YN120 TYPE STANDARD TABLE OF YN120,
W_YN120 TYPE YN120.

DATA:T_ERR TYPE STANDARD TABLE OF TYP_ERR,
W_ERR TYPE TYP_ERR.

*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_IKO   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(10) TEXT-003 FOR FIELD P_R_IKO.
SELECTION-SCREEN:END   OF LINE.

SELECTION-SCREEN:BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-003.
* 移行のみ
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_UP   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(14) TEXT-007 FOR FIELD P_R_UP.
SELECTION-SCREEN:END   OF LINE.
* 移行とダウンロード
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_DUP   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(24) TEXT-008 FOR FIELD P_R_DUP.
SELECTION-SCREEN:END   OF LINE.
* ダウンロードのみ
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 3.
PARAMETERS:P_R_DOWN   RADIOBUTTON GROUP R02.
SELECTION-SCREEN:COMMENT 6(18) TEXT-009 FOR FIELD P_R_DOWN.
SELECTION-SCREEN:END   OF LINE.
* 条件
SELECTION-SCREEN:BEGIN OF BLOCK BL1 WITH FRAME TITLE TEXT-001.
SELECT-OPTIONS:P_VBELN   FOR YK130-VBELN.
SELECT-OPTIONS:P_POSNR   FOR YK130-POSNR.
SELECT-OPTIONS:P_KUNNR   FOR YK130-KUNNR.
SELECTION-SCREEN:BEGIN OF BLOCK BL2 WITH FRAME TITLE TEXT-010.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_31 RADIOBUTTON GROUP GR01 DEFAULT 'X'.
SELECTION-SCREEN:COMMENT 3(8) TEXT-011 FOR FIELD R_31 .
PARAMETERS: R_05 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-012 FOR FIELD R_05 .
PARAMETERS: R_10 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-013 FOR FIELD R_10 .
SELECTION-SCREEN:END OF LINE.
SELECTION-SCREEN:BEGIN OF LINE.
PARAMETERS: R_15 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 3(8) TEXT-014 FOR FIELD R_15 .
PARAMETERS: R_20 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 13(8) TEXT-015 FOR FIELD R_20 .
PARAMETERS: R_25 RADIOBUTTON GROUP GR01.
SELECTION-SCREEN:COMMENT 24(8) TEXT-016 FOR FIELD R_25.
SELECTION-SCREEN:END OF LINE.
SELECT-OPTIONS:P_ZFBDT   FOR YK130-ZFBDT.
SELECTION-SCREEN:END   OF BLOCK BL2.
SELECT-OPTIONS:P_KEKKBN  FOR YK130-KEKKBN.
SELECT-OPTIONS:P_KNET    FOR YK130-KNETXAMT.
PARAMETERS    :P_PASS(255) TYPE C LOWER CASE.
SELECTION-SCREEN:END   OF BLOCK BL1.
SELECTION-SCREEN:END   OF BLOCK BL01.
* 削除
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_DEL   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(14) TEXT-004 FOR FIELD P_R_DEL.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-002.
SELECT-OPTIONS:P_GJAHR   FOR YN120-GJAHR.
SELECT-OPTIONS:P_SLPDOC  FOR YN120-SLPDOC.
SELECT-OPTIONS:P_DTLDOC  FOR YN120-DTLDOC.
SELECT-OPTIONS:P_VRF     FOR YN120-VRFCTON.
SELECT-OPTIONS:P_ZFB     FOR YN120-ZFBDT.
SELECTION-SCREEN:END   OF BLOCK BL02.

*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_ZFAEL_05   TYPE T052-ZFAEL VALUE '05',
C_ZFAEL_10   TYPE T052-ZFAEL VALUE '10',
C_ZFAEL_15   TYPE T052-ZFAEL VALUE '15',
C_ZFAEL_20   TYPE T052-ZFAEL VALUE '20',
C_ZFAEL_25   TYPE T052-ZFAEL VALUE '25',
C_ZFAEL_31   TYPE T052-ZFAEL VALUE '31',
C_UNMACH     TYPE YK130-KEKKBN VALUE '3',
C_JPY        TYPE YK130-WAERS  VALUE 'JPY',
C_ON         TYPE C     VALUE 'X',
C_SPRAS      TYPE SPRAS VALUE 'J'.   "言語キー



*----------------------------------------------------------------------*
*   変数定義
*----------------------------------------------------------------------*
DATA:G_ZFAEL    TYPE T052-ZFAEL,
G_ERR(1)   TYPE C,
G_BIKO(40) TYPE C.

DATA:G_CNT_YK130   TYPE I,
G_CNT_YN120   TYPE I,
G_CNT_ERR     TYPE I.

*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_PASS.
*-- ファイルダウンロード検索ヘルプ
PERFORM FRM_CHK_FILE.


*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.
IF ( P_R_DOWN = C_ON
OR   P_R_DUP  = C_ON )
AND P_PASS IS INITIAL.
MESSAGE E999(YN01) WITH 'ダウンロードファイル名を入力してください'.
ENDIF.


*----------------------------------------------------------------------*
*  START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM INIT_PROC.

*---データ移行処理
IF P_R_IKO = C_ON.
*   移行用データ取得
PERFORM GET_YK130.
IF P_R_DOWN = C_ON
OR P_R_DUP  = C_ON.
*     ファイルダウンロード
CLEAR G_ERR.
PERFORM DOWNLOAD.
CHECK G_ERR IS INITIAL.
ENDIF.
CHECK P_R_DOWN IS INITIAL.  "ダウンロードのみ以外処理続行
*   標準ＤＢデータ取得
PERFORM GET_DB.
*   チェックと編集
PERFORM EDIT_DATA.
*   登録
PERFORM INSERT_DATA.
*   処理結果出力
PERFORM WRITE_IKO.
*---削除
ELSE.
*   データ取得
PERFORM DEL_DATA_GET.
CHECK NOT T_YN120[] IS INITIAL.
*   削除
PERFORM DEL_DATA.
*   処理結果出力
PERFORM WRITE_DEL.
ENDIF.

*&---------------------------------------------------------------------*
*&      Form  FRM_CHK_FILE
*&---------------------------------------------------------------------*
*       ファイルダウンロード検索ヘルプ
*----------------------------------------------------------------------*
FORM FRM_CHK_FILE.
CALL FUNCTION 'WS_FILENAME_GET'
EXPORTING
MASK     = '*.*,ALL Files,*.*. '
MODE     = 'S'
TITLE    = 'ダウンロード'
IMPORTING
FILENAME = P_PASS
EXCEPTIONS
OTHERS   = 5.
IF SY-SUBRC <> 0.
ENDIF.
ENDFORM.                    " FRM_CHK_FILE
*&---------------------------------------------------------------------*
*&      Form  GET_YK130
*&---------------------------------------------------------------------*
*       移行用データ取得
*----------------------------------------------------------------------*
FORM GET_YK130.

CLEAR:T_YK130.
SELECT *
INTO TABLE T_YK130
FROM YK130
WHERE VBELN  IN P_VBELN
AND POSNR  IN P_POSNR
AND KEKKBN IN P_KEKKBN
AND KUNNR  IN P_KUNNR
AND ZFBDT  IN P_ZFBDT
AND KNETXAMT IN P_KNET.

IF SY-SUBRC = 0.
PERFORM CHECK_SIMEBI.
ELSE.
MESSAGE S762(YN01).
ENDIF.

ENDFORM.                    " GET_YK130
*&---------------------------------------------------------------------*
*&      Form  CHECK_SIMEBI
*&---------------------------------------------------------------------*
*       締日チェック
*----------------------------------------------------------------------*
FORM CHECK_SIMEBI.
DATA:L_T_KNVV TYPE SORTED TABLE OF TYP_KNVV
WITH UNIQUE KEY KUNNR VKORG VTWEG SPART ZTERM,
L_W_KNVV      TYPE TYP_KNVV,
L_ID          TYPE THEAD-TDID,
L_LANGU       TYPE THEAD-TDSPRAS,
L_NAME        TYPE THEAD-TDNAME,
L_OBJECT      TYPE THEAD-TDOBJECT,
LT_LINES      TYPE STANDARD TABLE OF TLINE,
LW_LINES      TYPE TLINE.

* 選択画面から、今回の締日を取得
CLEAR:G_ZFAEL.
IF R_05 = C_ON.
G_ZFAEL = C_ZFAEL_05.
ELSEIF R_10 = C_ON.
G_ZFAEL = C_ZFAEL_10.
ELSEIF R_15 = C_ON.
G_ZFAEL = C_ZFAEL_15.
ELSEIF R_20 = C_ON.
G_ZFAEL = C_ZFAEL_20.
ELSEIF R_25 = C_ON.
G_ZFAEL = C_ZFAEL_25.
ELSEIF R_31 = C_ON.
G_ZFAEL = C_ZFAEL_31.
ENDIF.

* 今回処理対象のうち、得意先の締日が選択条件に合致する得意先を取得
SELECT KNVV~KUNNR
KNVV~VKORG
KNVV~VTWEG
KNVV~SPART
KNVV~ZTERM
T052~ZFAEL
FROM KNVV
INNER JOIN T052
ON T052~ZTERM = KNVV~ZTERM
INTO TABLE T_KNVV
FOR ALL ENTRIES IN T_YK130
WHERE KNVV~KUNNR = T_YK130-KUNNR
AND KNVV~VKORG = T_YK130-VKORG
AND KNVV~VTWEG = T_YK130-VTWEG
AND KNVV~SPART = T_YK130-SPART
AND T052~ZFAEL = G_ZFAEL.

* 処理対象の得意先を、得意先照合対象の得意先だけにする
REFRESH:L_T_KNVV.
L_T_KNVV[] = T_KNVV[].

L_LANGU   = C_SPRAS.
L_ID      = 'YN01'.
L_OBJECT  = 'KNB1'.

CLEAR:L_W_KNVV.
LOOP AT L_T_KNVV INTO L_W_KNVV.
AT END OF KUNNR.
CLEAR:LW_LINES.
REFRESH: LT_LINES.
L_NAME = L_W_KNVV-KUNNR.
CONCATENATE  L_NAME  'C001'  INTO  L_NAME.

CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                            = L_ID
LANGUAGE                      = L_LANGU
NAME                          = L_NAME
OBJECT                        = L_OBJECT
TABLES
LINES                         = LT_LINES
EXCEPTIONS
ID                            = 1
LANGUAGE                      = 2
NAME                          = 3
*       NOT_FOUND                     = 4
OBJECT                        = 5
REFERENCE_CHECK               = 6
WRONG_ACCESS_TO_ARCHIVE       = 7
OTHERS                        = 8.
IF SY-SUBRC = 0.
READ TABLE LT_LINES INTO LW_LINES INDEX 1.
ENDIF.
IF LW_LINES-TDLINE <> 'X'.
DELETE T_KNVV WHERE KUNNR = L_W_KNVV-KUNNR.
ENDIF.
ENDAT.
ENDLOOP.

*-- 取得したYK130の得意先コードをチェック
LOOP AT T_YK130 INTO W_YK130.
CLEAR:W_KNVV.
READ TABLE T_KNVV INTO W_KNVV
WITH TABLE KEY KUNNR = W_YK130-KUNNR
VKORG = W_YK130-VKORG
VTWEG = W_YK130-VTWEG
SPART = W_YK130-SPART.
IF SY-SUBRC = 0.
CLEAR:W_YK130_O.
MOVE-CORRESPONDING W_YK130 TO W_YK130_O.
CALL FUNCTION 'FI_PERIOD_DETERMINE'
EXPORTING
I_BUDAT             = W_YK130_O-FKDAT "請求日
I_BUKRS             = 'C001'
IMPORTING
E_GJAHR              = W_YK130_O-GJAHR
EXCEPTIONS
FISCAL_YEAR          = 1
PERIOD               = 2
PERIOD_VERSION       = 3
POSTING_PERIOD       = 4
SPECIAL_PERIOD       = 5
VERSION              = 6
POSTING_DATE         = 7
OTHERS               = 8.
APPEND W_YK130_O TO T_YK130_O.
G_CNT_YK130 = G_CNT_YK130 + 1.
ELSE.
DELETE T_YK130  WHERE KUNNR = W_YK130-KUNNR
AND VKORG = W_YK130-VKORG
AND VTWEG = W_YK130-VTWEG
AND SPART = W_YK130-SPART.
ENDIF.
ENDLOOP.

IF T_YK130_O[] IS INITIAL.
MESSAGE S762(YN01).
ENDIF.

FREE:T_YK130,T_KNVV.

ENDFORM.                    " CHECK_SIMEBI
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD
*&---------------------------------------------------------------------*
*       ファイルダウンロード
*----------------------------------------------------------------------*
FORM DOWNLOAD.
DATA:L_PASS TYPE STRING,
LT_DOWNLOAD TYPE T_TYP_DOWNLOAD,
LW_DOWNLOAD TYPE TYP_DOWNLOAD,
LW_YK130    TYPE YK130.

L_PASS = P_PASS.
CHECK NOT ( T_YK130_O[] IS INITIAL ).

* 変換処理
REFRESH:LT_DOWNLOAD.
LOOP AT T_YK130_O INTO W_YK130_O.
MOVE-CORRESPONDING W_YK130_O TO LW_DOWNLOAD.
*   後ろマイナスの変換
IF W_YK130_O-KNQUAN < 0.       "数量
SHIFT LW_DOWNLOAD-KNQUAN CIRCULAR RIGHT.
ENDIF.
IF W_YK130_O-KNUNTPRC < 0.     "単価
SHIFT LW_DOWNLOAD-KNUNTPRC CIRCULAR RIGHT.
ENDIF.
IF W_YK130_O-KNITXAMT < 0.     "税抜金額
SHIFT LW_DOWNLOAD-KNITXAMT CIRCULAR RIGHT.
ENDIF.
IF W_YK130_O-KNTAXAMT < 0.     "消費税額
SHIFT LW_DOWNLOAD-KNTAXAMT CIRCULAR RIGHT.
ENDIF.
IF W_YK130_O-KNETXAMT < 0.     "税込金額
SHIFT LW_DOWNLOAD-KNETXAMT CIRCULAR RIGHT.
ENDIF.
CONDENSE LW_DOWNLOAD-KNQUAN NO-GAPS.
CONDENSE LW_DOWNLOAD-KNUNTPRC NO-GAPS.
CONDENSE LW_DOWNLOAD-KNITXAMT NO-GAPS.
CONDENSE LW_DOWNLOAD-KNTAXAMT NO-GAPS.
CONDENSE LW_DOWNLOAD-KNETXAMT NO-GAPS.
APPEND LW_DOWNLOAD TO LT_DOWNLOAD.
ENDLOOP.

CALL FUNCTION 'GUI_DOWNLOAD'
EXPORTING
FILENAME                      = L_PASS
FILETYPE                       = 'ASC'
WRITE_FIELD_SEPARATOR         = 'X'
TABLES
DATA_TAB                      = LT_DOWNLOAD
EXCEPTIONS
FILE_WRITE_ERROR              = 1
NO_BATCH                      = 2
GUI_REFUSE_FILETRANSFER       = 3
INVALID_TYPE                  = 4
NO_AUTHORITY                  = 5
UNKNOWN_ERROR                 = 6
HEADER_NOT_ALLOWED            = 7
SEPARATOR_NOT_ALLOWED         = 8
FILESIZE_NOT_ALLOWED          = 9
HEADER_TOO_LONG               = 10
DP_ERROR_CREATE               = 11
DP_ERROR_SEND                 = 12
DP_ERROR_WRITE                = 13
UNKNOWN_DP_ERROR              = 14
ACCESS_DENIED                 = 15
DP_OUT_OF_MEMORY              = 16
DISK_FULL                     = 17
DP_TIMEOUT                    = 18
FILE_NOT_FOUND                = 19
DATAPROVIDER_EXCEPTION        = 20
CONTROL_FLUSH_ERROR           = 21
OTHERS                        = 22
.
IF SY-SUBRC <> 0.
MESSAGE I000 WITH 'ダウンロードに失敗しました'.
G_ERR = C_ON.
ELSE.
MESSAGE S000 WITH 'ダウンロードしました'.
ENDIF.

ENDFORM.                    " DOWNLOAD
*&---------------------------------------------------------------------*
*&      Form  GET_DB
*&---------------------------------------------------------------------*
*       標準ＤＢデータ取得
*----------------------------------------------------------------------*
FORM GET_DB.
CHECK NOT T_YK130_O[] IS INITIAL.

SELECT GJAHR
BELNR
BUKRS
KUNNR
BUZEI
UMSKS
UMSKZ
AUGDT
AUGBL
ZUONR
INTO TABLE T_BSID
FROM BSID
FOR ALL ENTRIES IN T_YK130_O
WHERE KUNNR = T_YK130_O-KUNNR
AND BUKRS = T_YK130_O-BUKRS
AND GJAHR = T_YK130_O-GJAHR
AND BELNR = T_YK130_O-VBELN.

SELECT VBELN
POSNR
AUBEL
FROM VBRP
INTO TABLE T_VBRP
FOR ALL ENTRIES IN  T_YK130_O
WHERE VBELN = T_YK130_O-VBELN
AND POSNR = T_YK130_O-POSNR.

ENDFORM.                    " GET_DB
*&---------------------------------------------------------------------*
*&      Form  INIT_PROC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INIT_PROC.
REFRESH:T_YK130,
T_YK130_O,
T_KNVV,
T_BSID,
T_VBRP,
T_ERR,
T_YN120.

CLEAR:  G_ERR,
G_CNT_YK130,
G_CNT_YN120,
G_CNT_ERR.

ENDFORM.                    " INIT_PROC
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       チェックと編集
*----------------------------------------------------------------------*
FORM EDIT_DATA.
DATA:L_W_YK130 TYPE TYP_YK130.

SORT T_YK130_O  BY VBELN POSNR.
LOOP AT T_YK130_O INTO W_YK130_O.
L_W_YK130 = W_YK130_O.
AT NEW VBELN.
PERFORM GET_BIKO USING W_YK130_O-VBELN.
CLEAR: W_BSID.
LOOP AT T_BSID INTO W_BSID
WHERE GJAHR = L_W_YK130-GJAHR
AND BELNR = L_W_YK130-VBELN
AND KUNNR = L_W_YK130-KUNNR
AND BUKRS = L_W_YK130-BUKRS.
EXIT.
ENDLOOP.
ENDAT.

CLEAR:W_YN120,W_VBRP.
READ TABLE T_VBRP INTO W_VBRP
WITH TABLE KEY VBELN = W_YK130_O-VBELN
POSNR = W_YK130_O-POSNR.
W_YN120-GJAHR     = W_YK130_O-GJAHR.  "伝票会計年度
W_YN120-SLPDOC    = W_YK130_O-VBELN.  "伝票番号
W_YN120-DTLDOC    = W_YK130_O-POSNR.  "伝票明細番号
W_YN120-VRFCTON   = W_YK130_O-KUNNR.  "得意先コード(支払人)
W_YN120-BUKRS     = W_YK130_O-BUKRS.  "会社コード

* エラーチェック
CLEAR:W_ERR.
PERFORM ERR_CHECK.
IF NOT W_ERR IS INITIAL.
CONTINUE.
ENDIF.

W_YN120-ZFBDT     = W_YK130_O-ZFBDT.  "計上締日
*     W_YN120-PRCTR     = 初期値.           "利益センタ
W_YN120-PERNR     = SY-UNAME.         "担当者
W_YN120-DVSON     = W_YK130_O-VKBUR.  "営業所
*     W_YN120-TGTFLG    = 初期値.           "照合対象フラグ
W_YN120-CSTS      = '1'.              "照合ステータス
IF W_YK130_O-KEKKBN = C_UNMACH.         "アンマッチのみ
W_YN120-COMMT     = TEXT-017.         "コメント
ENDIF.
IF NOT W_BSID-BELNR IS INITIAL.
W_YN120-YNGJAHR   = W_BSID-GJAHR.   "会計伝票会計年度
W_YN120-BELNR     = W_BSID-BELNR.   "会計伝票番号
W_YN120-BUZEI     = W_BSID-BUZEI.   "会計伝票明細番号
ENDIF.
W_YN120-KNQUAN    = W_YK130_O-KNQUAN.   "数量
W_YN120-KNUNIT    = W_YK130_O-KNUNIT.   "単位
W_YN120-KNUNTPRC  = W_YK130_O-KNUNTPRC. "単価
W_YN120-KNTAXAMT  = W_YK130_O-KNTAXAMT. "消費税額
W_YN120-KNITXAMT  = W_YK130_O-KNITXAMT. "税抜金額
W_YN120-KNETXAMT  = W_YK130_O-KNETXAMT. "税込金額
W_YN120-WAERS     = W_YK130_O-WAERS.    "通貨コード
*     W_YN120-CHKDOC    = 初期値.           "照合番号
*     W_YN120-CHKDAT    = 初期値.           "照合日
*     W_YN120-CHKUSR    = 初期値.           "照合ユーザ
W_YN120-UPDDAT    = SY-DATUM.         "更新日
W_YN120-UPDTIM    = SY-UZEIT.         "更新時刻
W_YN120-UPDUSR    = SY-UNAME.         "更新ユーザ
W_YN120-UPDPRG    = SY-REPID.         "更新プログラム
*2012/05/22 MOD START
*必ず得意先発注番号を設定
W_YN120-CKEY1     = W_YK130_O-BSTKD. "照合キー1：得意先発注番号

*      IF W_YN120-WAERS  = C_JPY.
*        W_YN120-CKEY1     = W_YK130_O-BSTKD. "照合キー1：得意先発注番号
*      ELSE.
*        W_YN120-CKEY2     = G_BIKO.         "照合キー2：インボイスNo
*      ENDIF.
*2012/05/22 MOD END
*     W_YN120-CKEY3     = 初期値.           "照合キー3
*     W_YN120-CKEY4     = 初期値.           "照合キー4
*     W_YN120-CKEY5     = 初期値.           "照合キー5
*     W_YN120-CKEY6     = 初期値.           "照合キー6
*     W_YN120-CKEY7     = 初期値.           "照合キー7
*     W_YN120-CKEY8     = 初期値.           "照合キー8
W_YN120-LIST1     = W_YK130_O-KUNNR.    "得意先コード(受注先)
W_YN120-LIST2     = W_YK130_O-BSTKD.    "得意先発注番号
W_YN120-LIST3     = W_VBRP-AUBEL.     "受注伝票
W_YN120-LIST4     = W_YK130_O-VKGRP.    "営業グループ
W_YN120-LIST5     = W_YK130_O-MATNR.    "品目コード
W_YN120-LIST6     = W_YK130_O-MAKTX.    "品目テキスト
W_YN120-LIST7     = W_YK130_O-KDMAT.    "得意先品目
W_YN120-LIST8     = G_BIKO.           "インボイスNo
*     W_YN120-LIST9     = 初期値.           "一覧表示項目9
*     W_YN120-LIST10    = 初期値.           "一覧表示項目10
W_YN120-INSDT     = SY-DATUM.         "登録日
W_YN120-LDATE1    = W_YK130_O-FKDAT.    "請求日
*     W_YN120-CZFBDT    = 初期値.           "照合締日
*     W_YN120-AUTOFLG   = 初期値.           "自動生成明細（部分照合）
*     W_YN120-OUTMNTH3  = 初期値.           "月締請求書出力月
*     W_YN120-PRINTDT3  = 初期値.           "月締請求書出力日
*     W_YN120-DUMMYMNTH = 初期値.           "ダミー月
*     W_YN120-DUMMYDT   = 初期値.           "ダミー日付
APPEND W_YN120 TO T_YN120.
G_CNT_YN120 = G_CNT_YN120 + 1.
ENDLOOP.
ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_BIKO
*&---------------------------------------------------------------------*
*       テキスト取得
*----------------------------------------------------------------------*
*      -->P_VBELN  請求伝票番号
*----------------------------------------------------------------------*
FORM GET_BIKO USING P_VBELN.
DATA:L_ID          TYPE THEAD-TDID,
L_LANGU       TYPE THEAD-TDSPRAS,
L_NAME        TYPE THEAD-TDNAME,
L_OBJECT      TYPE THEAD-TDOBJECT,
LT_LINES      TYPE STANDARD TABLE OF TLINE,
LW_LINES      TYPE TLINE.

L_LANGU   = C_SPRAS.
L_ID      = '0001'.
L_OBJECT  = 'VBBK'.
L_NAME    = P_VBELN.

CLEAR: G_BIKO.

REFRESH: LT_LINES.
CALL FUNCTION 'READ_TEXT'
EXPORTING
ID                            = L_ID
LANGUAGE                      = L_LANGU
NAME                          = L_NAME
OBJECT                        = L_OBJECT
TABLES
LINES                         = LT_LINES
EXCEPTIONS
ID                            = 1
LANGUAGE                      = 2
NAME                          = 3
*    NOT_FOUND                     = 4
OBJECT                        = 5
REFERENCE_CHECK               = 6
WRONG_ACCESS_TO_ARCHIVE       = 7
OTHERS                        = 8.
IF SY-SUBRC = 0.
CLEAR:LW_LINES.
READ TABLE LT_LINES INTO LW_LINES INDEX 1.

CALL FUNCTION 'TEXT_SPLIT'
EXPORTING
LENGTH        = 40
TEXT          = LW_LINES-TDLINE
IMPORTING
LINE          = G_BIKO.
ENDIF.

ENDFORM.                    " GET_BIKO
*&---------------------------------------------------------------------*
*&      Form  INSERT_DATA
*&---------------------------------------------------------------------*
*       データ登録
*----------------------------------------------------------------------*
FORM INSERT_DATA.

IF T_YN120[] IS INITIAL.
EXIT.
ENDIF.

INSERT YN120 FROM TABLE T_YN120
ACCEPTING DUPLICATE KEYS.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.


ENDFORM.                    " INSERT_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_IKO
*&---------------------------------------------------------------------*
*      処理結果出力【移行】
*----------------------------------------------------------------------*
FORM WRITE_IKO.
DATA:LW_CNT(10)  TYPE C,
LW_ERR      TYPE TYP_ERR.

WRITE G_CNT_YK130 TO LW_CNT.
WRITE: /001 '<データ移行>',
/003 '売上データ(YK130)抽出件数：',
040 LW_CNT,
050 '件'.
SKIP 1.

WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)登録件数：',
040 LW_CNT,
050 '件'.
WRITE G_CNT_ERR TO LW_CNT.
WRITE: /003 '自社データ(売上)(YN120)エラー件数：',
040 LW_CNT,
050 '件'.
CHECK NOT T_ERR[] IS INITIAL.
SKIP 1.
WRITE: /003 '伝票番号',
014 '明細',
029 'エラー内容'.
ULINE.
LOOP AT T_ERR INTO LW_ERR.
WRITE: /003 LW_ERR-VBELN,
014 LW_ERR-POSNR,
029 LW_ERR-MESSAGE.
ENDLOOP.

ENDFORM.                    " WRITE_IKO
*&---------------------------------------------------------------------*
*&      Form  ERR_CHECK
*&---------------------------------------------------------------------*
*       エラーチェック
*----------------------------------------------------------------------*
FORM ERR_CHECK.
DATA:L_MESSAGE TYPE TYP_ERR-MESSAGE,
L_SLPDOC  TYPE YNSLPDOC.

CLEAR:L_MESSAGE.
* 会計年度取得エラー
IF W_YK130_O-GJAHR IS INITIAL.
*   会計年度が取得できませんでした
L_MESSAGE = TEXT-018.
*2012/05/11 DEL START
** VBRP 取得エラー
*  ELSEIF W_VBRP IS INITIAL.
**   VBRPが取得できませんでした
*    L_MESSAGE = TEXT-019.
*2012/05/11 DEL END
ENDIF.

IF L_MESSAGE IS INITIAL.
CLEAR:L_SLPDOC.
SELECT SINGLE SLPDOC
INTO L_SLPDOC
FROM YN120
WHERE GJAHR   = W_YN120-GJAHR
AND SLPDOC  = W_YN120-SLPDOC
AND DTLDOC  = W_YN120-DTLDOC
AND VRFCTON = W_YN120-VRFCTON
AND BUKRS   = W_YN120-BUKRS.
IF SY-SUBRC = 0.
*      すでに同じキーが存在します
L_MESSAGE = TEXT-020.
ENDIF.
ENDIF.

IF NOT L_MESSAGE IS INITIAL.
W_ERR-VBELN   = W_YK130_O-VBELN.
W_ERR-POSNR   = W_YK130_O-POSNR.
W_ERR-MESSAGE = L_MESSAGE.
APPEND W_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
ENDIF.

ENDFORM.                    " ERR_CHECK
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA_GET
*&---------------------------------------------------------------------*
*       YN120データ取得【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA_GET.

SELECT *
INTO TABLE T_YN120
FROM YN120
WHERE GJAHR   IN P_GJAHR
AND SLPDOC  IN P_SLPDOC
AND DTLDOC  IN P_DTLDOC
AND VRFCTON IN P_VRF
AND ZFBDT   IN P_ZFB.

IF SY-SUBRC = 0.
DESCRIBE TABLE T_YN120 LINES G_CNT_YN120.
ELSE.
MESSAGE S762(YN01).
ENDIF.
ENDFORM.                    " DEL_DATA_GET
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA
*&---------------------------------------------------------------------*
*       YN120削除【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA.
CHECK NOT T_YN120[] IS INITIAL.

DELETE YN120 FROM TABLE T_YN120.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN120'.
ENDIF.


ENDFORM.                    " DEL_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_DEL
*&---------------------------------------------------------------------*
*       処理結果出力【削除】
*----------------------------------------------------------------------*
FORM WRITE_DEL.
DATA:LW_CNT(10)  TYPE C.

WRITE G_CNT_YN120 TO LW_CNT.
WRITE: /001 '<移行データ削除>',
/003 '自社データ(売上)(YN120)削除件数：',
030 LW_CNT,
040 '件'.

ENDFORM.                    " WRITE_DEL
