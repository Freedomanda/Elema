*&---------------------------------------------------------------------*
*&  REPORT ZN030100                                                    *
*&         自社データ（仕入）【移行】                                  *
*&---------------------------------------------------------------------*
*&  機能     : YK230をYN220に変換する
*&
*&  作成日   : 2012/03/16
*&  作成者   : S.TAKIKAWA(SOLFIS)
*&---------------------------------------------------------------------*
*&  変更履歴 :
*&  変更内容 :
*&---------------------------------------------------------------------*
REPORT ZN030100
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.
*----------------------------------------------------------------------*
*   テーブル
*----------------------------------------------------------------------*
TABLES:YK230,YN220.
*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
TYPES:T_TYP_YK230 TYPE STANDARD TABLE OF YK230,
T_TYP_YN220 TYPE STANDARD TABLE OF YN220.
TYPES:BEGIN OF TYP_YK230_SELECT,
BELNR    TYPE RE_BELNR,
BUZEI    TYPE RBLGP,
AWKEY    TYPE AWKEY,
END   OF TYP_YK230_SELECT,
T_TYP_YK230_SELECT TYPE STANDARD TABLE OF TYP_YK230_SELECT.
TYPES:BEGIN OF TYP_BKPF,
BUKRS    TYPE BUKRS,      "会社コード
GJAHR    TYPE GJAHR,      "会計年度
BELNR    TYPE BELNR_D,    "会計伝票番号
AWKEY    TYPE AWKEY,      "オブジェクトキー
LIFNR    TYPE LIFNR,      "仕入先コード(YK230を設定)
END   OF TYP_BKPF,
T_TYP_BKPF TYPE STANDARD TABLE OF TYP_BKPF.
TYPES:BEGIN OF TYP_BSIK,
LIFNR    TYPE LIFNR,      "仕入先コード
BUKRS    TYPE BUKRS,      "会社コード
GJAHR    TYPE GJAHR,      "会計年度
BELNR    TYPE BELNR_D,    "会計伝票番号
BUZEI    TYPE BUZEI,      "会計伝票内の明細番号
END   OF TYP_BSIK,
T_TYP_BSIK TYPE STANDARD TABLE OF TYP_BSIK.
TYPES:BEGIN OF TYP_RSEG,
GJAHR    TYPE GJAHR,      "会計年度
BELNR    TYPE BELNR_D,    "会計伝票番号
EBELP    TYPE EBELP,      "購買伝票の明細番号
XBLNR    TYPE XBLNR,      "参照伝票番号
END   OF TYP_RSEG,
T_TYP_RSEG TYPE STANDARD TABLE OF TYP_RSEG.
TYPES:BEGIN OF TYP_EKKO,
EBELN    TYPE EBELN,      "購買伝票番号
EKGRP    TYPE BKGRP,      "購買グループ
END   OF TYP_EKKO,
T_TYP_EKKO TYPE STANDARD TABLE OF TYP_EKKO.
TYPES:BEGIN OF TYP_ERR,
BELNR    TYPE RE_BELNR,   "請求書伝票の伝票番号
BUZEI    TYPE RBLGP,      "請求書伝票内伝票明細
MESSAGE  TYPE CHAR255,    "エラー内容
END   OF TYP_ERR,
T_TYP_ERR TYPE STANDARD TABLE OF TYP_ERR.
*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*
DATA:T_YK230 TYPE T_TYP_YK230,
T_YN220 TYPE T_TYP_YN220,
T_BKPF  TYPE T_TYP_BKPF,
T_BSIK  TYPE T_TYP_BSIK,
T_RSEG  TYPE T_TYP_RSEG,
T_EKKO  TYPE T_TYP_EKKO,
T_ERR   TYPE T_TYP_ERR.
*----------------------------------------------------------------------*
*   変数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_ON         TYPE C     VALUE 'X'.
*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
DATA:G_CNT_YK230   TYPE I,
G_CNT_YN220   TYPE I,
G_CNT_ERR     TYPE I.
*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_IKO   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(10) TEXT-003 FOR FIELD P_R_IKO.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-001.
SELECT-OPTIONS:P_BELNR   FOR YK230-BELNR.
SELECT-OPTIONS:P_BUZEI   FOR YK230-BUZEI.
SELECT-OPTIONS:P_LIFN2   FOR YK230-LIFN2.
SELECT-OPTIONS:P_ZFBDT   FOR YK230-ZFBDT.
SELECT-OPTIONS:P_KEKKBN  FOR YK230-KEKKBN.
SELECT-OPTIONS:P_KNET    FOR YK230-KNETXAMT.
SELECTION-SCREEN:END   OF BLOCK BL01.

SELECTION-SCREEN:BEGIN OF LINE,
POSITION 1.
PARAMETERS:P_R_DEL   RADIOBUTTON GROUP R01.
SELECTION-SCREEN:COMMENT 3(14) TEXT-004 FOR FIELD P_R_DEL.
SELECTION-SCREEN:END   OF LINE.
SELECTION-SCREEN:BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-002.
SELECT-OPTIONS:P_GJAHR   FOR YN220-GJAHR.
SELECT-OPTIONS:P_SLPDOC  FOR YN220-SLPDOC.
SELECT-OPTIONS:P_DTLDOC  FOR YN220-DTLDOC.
SELECT-OPTIONS:P_VRF     FOR YN220-VRFCTON.
SELECTION-SCREEN:END   OF BLOCK BL02.
*----------------------------------------------------------------------*
*  START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

*---データ移行処理
IF P_R_IKO = C_ON.
*   移行用データ取得
PERFORM GET_YK230.
CHECK NOT T_YK230[] IS INITIAL.
*   標準ＤＢデータ取得
PERFORM GET_DB.
*   チェックと編集
PERFORM EDIT_DATA.
*   登録
PERFORM INSERT_DATA.
*   処理結果出力
PERFORM WRITE_IKO.

*---削除
ELSE.
*   データ取得
PERFORM DEL_DATA_GET.
CHECK NOT T_YN220[] IS INITIAL.
*   削除
PERFORM DEL_DATA.
*   処理結果出力
PERFORM WRITE_DEL.
ENDIF.

*&---------------------------------------------------------------------*
*&      Form  GET_YK230
*&---------------------------------------------------------------------*
*       移行用データ取得
*----------------------------------------------------------------------*
FORM GET_YK230.

CLEAR:T_YK230.
SELECT *
INTO TABLE T_YK230
FROM YK230
WHERE BELNR IN P_BELNR
AND BUZEI IN P_BUZEI
AND KEKKBN IN P_KEKKBN
AND LIFN2  IN P_LIFN2
AND ZFBDT  IN P_ZFBDT
AND KNETXAMT IN P_KNET.

IF SY-SUBRC = 0.
SORT T_YK230.
DESCRIBE TABLE T_YK230 LINES G_CNT_YK230.
ELSE.
MESSAGE S762(YN01).
ENDIF.

ENDFORM.                    " GET_YK230
*&---------------------------------------------------------------------*
*&      Form  GET_DB
*&---------------------------------------------------------------------*
*       標準ＤＢデータ取得
*----------------------------------------------------------------------*
FORM GET_DB.
DATA:LW_YK230        TYPE YK230,
LT_YK230_SELECT TYPE T_TYP_YK230_SELECT,
LW_YK230_SELECT TYPE TYP_YK230_SELECT,
LT_BKPF         TYPE T_TYP_BKPF,
LW_BKPF         TYPE TYP_BKPF,
LW_BELNR        TYPE BELNR_D,
LW_GJAHR        TYPE GJAHR.

* 取得用データ編集
LOOP AT T_YK230 INTO LW_YK230.
*   伝票会計年度算出                        "修正
CALL FUNCTION 'FI_PERIOD_DETERMINE'
EXPORTING
I_BUDAT              = LW_YK230-BUDAT
I_BUKRS              = LW_YK230-BUKRS
IMPORTING
E_GJAHR              = LW_YK230-GJAHR
EXCEPTIONS
FISCAL_YEAR          = 1
PERIOD               = 2
PERIOD_VERSION       = 3
POSTING_PERIOD       = 4
SPECIAL_PERIOD       = 5
VERSION              = 6
POSTING_DATE         = 7
OTHERS               = 8
.
IF SY-SUBRC <> 0.
*      LW_ERR-BELNR = LW_YK230-BELNR.
*      LW_ERR-BUZEI = LW_YK230-BUZEI.
*      LW_ERR-MESSAGE = '会計年度が取得できませんでした'.
*      APPEND LW_ERR TO T_ERR.
*      G_CNT_ERR = G_CNT_ERR + 1.
*      CONTINUE.
ENDIF.

LW_YK230_SELECT-BELNR = LW_YK230-BELNR.
LW_YK230_SELECT-BUZEI = LW_YK230-BUZEI.
LW_YK230_SELECT-AWKEY+0(10) = LW_YK230-BELNR.  "修正
LW_YK230_SELECT-AWKEY+10(4) = LW_YK230-GJAHR.  "修正
APPEND LW_YK230_SELECT TO LT_YK230_SELECT.
ENDLOOP.

*--- BKPF取得
SELECT BUKRS        "会社コード
GJAHR        "会計年度
BELNR        "会計伝票番号
AWKEY        "オブジェクトキー
INTO TABLE LT_BKPF
FROM BKPF
FOR ALL ENTRIES IN LT_YK230_SELECT
WHERE AWTYP ='RMRP'
AND AWKEY = LT_YK230_SELECT-AWKEY.


* 仕入先を編集
IF SY-SUBRC = 0.
SORT T_YK230 BY BELNR GJAHR.     "修正
LOOP AT LT_BKPF INTO LW_BKPF.
LW_BELNR = LW_BKPF-AWKEY+0(10).
LW_GJAHR = LW_BKPF-AWKEY+10(4).
READ TABLE T_YK230 INTO LW_YK230
WITH KEY BELNR  = LW_BELNR
*                    GJAHR  = LW_GJAHR
BINARY SEARCH.
IF SY-SUBRC = 0.
LW_BKPF-LIFNR = LW_YK230-LIFN2.
APPEND LW_BKPF TO T_BKPF.
ENDIF.
ENDLOOP.
SORT T_BKPF BY AWKEY.

*--- BSIK取得
SELECT LIFNR         "仕入先コード
BUKRS         "会社コード
GJAHR         "会計年度
BELNR         "会計伝票番号
BUZEI         "会計伝票内の明細番号
INTO TABLE T_BSIK
FROM BSIK
FOR ALL ENTRIES IN T_BKPF
WHERE LIFNR = T_BKPF-LIFNR
AND BUKRS = T_BKPF-BUKRS
AND GJAHR = T_BKPF-GJAHR
AND BELNR = T_BKPF-BELNR.
IF SY-SUBRC = 0.
SORT T_BSIK.
ENDIF.
ENDIF.

*--- RSEG取得
SELECT GJAHR
BELNR
EBELP
XBLNR
INTO TABLE T_RSEG
FROM RSEG
FOR ALL ENTRIES IN T_YK230
*   WHERE GJAHR = T_YK230-GJAHR        "修正
*     AND BELNR = T_YK230-BELNR        "修正
WHERE BELNR = T_YK230-BELNR        "修正
AND EBELP = T_YK230-EBELP.
IF SY-SUBRC = 0.
SORT T_RSEG.
ENDIF.

*--- EKKO取得
SELECT EBELN
EKGRP
INTO TABLE T_EKKO
FROM EKKO
FOR ALL ENTRIES IN T_YK230
WHERE EBELN = T_YK230-EBELN.
IF SY-SUBRC = 0.
SORT T_EKKO.        "修正
ENDIF.

ENDFORM.                    " GET_DB
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       チェックと編集
*----------------------------------------------------------------------*
FORM EDIT_DATA.
DATA:LW_YK230   TYPE YK230,
LW_YN220   TYPE YN220,
LW_BKPF    TYPE TYP_BKPF,
LW_BSIK    TYPE TYP_BSIK,
LW_RSEG    TYPE TYP_RSEG,
LW_EKKO    TYPE TYP_EKKO,
LW_ERR     TYPE TYP_ERR,
LW_AWKEY   TYPE AWKEY,
LW_DATUM   TYPE SY-DATUM,
LW_UZEIT   TYPE SY-UZEIT,
LW_SLPDOC  TYPE YNSLPDOC.

LW_DATUM = SY-DATUM.
LW_UZEIT = SY-UZEIT.

LOOP AT T_YK230 INTO LW_YK230.
CLEAR:LW_YN220.
*   伝票会計年度算出
CALL FUNCTION 'FI_PERIOD_DETERMINE'
EXPORTING
I_BUDAT              = LW_YK230-BUDAT
I_BUKRS              = LW_YK230-BUKRS
IMPORTING
E_GJAHR              = LW_YN220-GJAHR
EXCEPTIONS
FISCAL_YEAR          = 1
PERIOD               = 2
PERIOD_VERSION       = 3
POSTING_PERIOD       = 4
SPECIAL_PERIOD       = 5
VERSION              = 6
POSTING_DATE         = 7
OTHERS               = 8
.
IF SY-SUBRC <> 0.
LW_ERR-BELNR = LW_YK230-BELNR.
LW_ERR-BUZEI = LW_YK230-BUZEI.
LW_ERR-MESSAGE = '会計年度が取得できませんでした'.
APPEND LW_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
CONTINUE.
ENDIF.

*   伝票番号
LW_YN220-SLPDOC = LW_YK230-BELNR.
*   伝票明細番号
LW_YN220-DTLDOC = LW_YK230-BUZEI.
*   仕入先コード(請求先)
LW_YN220-VRFCTON = LW_YK230-LIFN2.
*   会社コード
LW_YN220-BUKRS = LW_YK230-BUKRS.
*   計上締日
LW_YN220-ZFBDT = LW_YK230-ZFBDT.
*   利益センタ（初期値）
*   担当者
LW_YN220-PERNR = SY-UNAME.
*   プラント
LW_YN220-DVSON = LW_YK230-WERKST.
*   照合対象フラグ(初期値)
*   照合ステータス
LW_YN220-CSTS = '1'.
*   コメント
IF LW_YK230-KEKKBN = '5'.
LW_YN220-COMMT = '部分照合残'.
ENDIF.

*   会計伝票会計年度/会計伝票番号/会計伝票明細番号
*   会計伝票存在チェック
LW_AWKEY+0(10) = LW_YK230-BELNR.    "修正
LW_AWKEY+10(4) = LW_YN220-GJAHR.    "修正
CLEAR:LW_BKPF.
*   BKPFより会計伝票検索
READ TABLE T_BKPF INTO LW_BKPF
WITH KEY AWKEY = LW_AWKEY
BINARY SEARCH.
IF SY-SUBRC = 0.
*     会計伝票番号より、未決済かどうかチェック
READ TABLE T_BSIK INTO LW_BSIK
WITH KEY LIFNR  = LW_YK230-LIFN2
BUKRS  = LW_BKPF-BUKRS
GJAHR  = LW_BKPF-GJAHR
BELNR  = LW_BKPF-BELNR
BINARY SEARCH.
IF SY-SUBRC = 0.
IF LW_YK230-KEKKBN <> '5'.  "修正
LW_YN220-YNGJAHR = LW_BSIK-GJAHR.
LW_YN220-BELNR   = LW_BSIK-BELNR.
LW_YN220-BUZEI   = LW_BSIK-BUZEI.
ENDIF.                      "修正
ENDIF.

*   BKPFが取得できない場合エラー
ELSE.
LW_ERR-BELNR = LW_YK230-BELNR.
LW_ERR-BUZEI = LW_YK230-BUZEI.
LW_ERR-MESSAGE = 'BKPFが取得できませんでした'.
APPEND LW_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
CONTINUE.
ENDIF.

*   数量
LW_YN220-KNQUAN = LW_YK230-KNQUAN.
*   単位
LW_YN220-KNUNIT = LW_YK230-KNUNIT.
*   単価
LW_YN220-KNUNTPRC = LW_YK230-KNUNTPRC.
*   消費税額
IF LW_YK230-KEKKBN = '5'.
LW_YN220-KNTAXAMT = 0.
ELSE.
LW_YN220-KNTAXAMT = LW_YK230-KNTAXAMT.
ENDIF.
*   税抜金額
IF LW_YK230-KEKKBN = '5'.
LW_YN220-KNITXAMT = 0.
ELSE.
LW_YN220-KNITXAMT = LW_YK230-KNITXAMT.
ENDIF.
*   税込金額
LW_YN220-KNETXAMT = LW_YK230-KNETXAMT -
LW_YK230-PIDAMT   -
LW_YK230-PIDTAX.
*   通貨コード
LW_YN220-WAERS    = LW_YK230-WAERS.
*   照合番号(初期値)
*   照合日(初期値)
*   照合ユーザ(初期値)
*   更新日
LW_YN220-UPDDAT = LW_DATUM.
*   更新時刻
LW_YN220-UPDTIM = LW_UZEIT.
*   更新ユーザ
LW_YN220-UPDUSR = SY-UNAME.
*   更新プログラム
LW_YN220-UPDPRG = SY-REPID.
*   照合キー1：発注番号
IF LW_YN220-WAERS = 'JPY'.
LW_YN220-CKEY1 = LW_YK230-EBELN.
ENDIF.
*   照合キー2：インボイスNo
READ TABLE T_RSEG INTO LW_RSEG
WITH KEY
*                 GJAHR = LW_YK230-GJAHR  "修正
BELNR = LW_YK230-BELNR  "修正
EBELP = LW_YK230-EBELP.
IF SY-SUBRC = 0.
IF LW_YN220-WAERS <> 'JPY'.
LW_YN220-CKEY2 = LW_RSEG-XBLNR.
ENDIF.
*   RSEGが取得できない場合はエラー
ELSE.
LW_ERR-BELNR = LW_YK230-BELNR.
LW_ERR-BUZEI = LW_YK230-BUZEI.
LW_ERR-MESSAGE = 'RSEGが取得できませんでした'.
APPEND LW_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
CONTINUE.
ENDIF.
*   照合キー3〜8(初期値)
*   仕入先コード(発注先)
LW_YN220-LIST1 = LW_YK230-LIFNR.
*   発注番号
LW_YN220-LIST2 = LW_YK230-EBELN.
*   発注明細番号
LW_YN220-LIST3 = LW_YK230-EBELP.
*   購買グループ
READ TABLE T_EKKO INTO LW_EKKO
WITH KEY EBELN = LW_YK230-EBELN
BINARY SEARCH.
*   EKKOが取得できない場合はエラー
IF SY-SUBRC = 0.
LW_YN220-LIST4 = LW_EKKO-EKGRP.
ELSE.
LW_ERR-BELNR = LW_YK230-BELNR.
LW_ERR-BUZEI = LW_YK230-BUZEI.
LW_ERR-MESSAGE = 'EKKOが取得できませんでした'.
APPEND LW_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
CONTINUE.
ENDIF.
*   品目コード
LW_YN220-LIST5 = LW_YK230-MATNR.
*   品目テキスト
LW_YN220-LIST6 = LW_YK230-MAKTX.
*   仕入先品目
LW_YN220-LIST7 = LW_YK230-IDNLF.
*   インボイスNo
LW_YN220-LIST8 = LW_RSEG-XBLNR.
*   一覧表示項目9,10(初期値)
*   登録日
LW_YN220-INSDT = LW_DATUM.
*   入庫日
LW_YN220-LDATE1 = LW_YK230-BUDAT.
*   照合締日/自動生成明細/検収出力月・日/支払月・日(初期値)
*   キー重複チェック
SELECT SINGLE SLPDOC
INTO LW_SLPDOC
FROM YN220
WHERE GJAHR   = LW_YN220-GJAHR
AND SLPDOC  = LW_YN220-SLPDOC
AND DTLDOC  = LW_YN220-DTLDOC
AND VRFCTON = LW_YN220-VRFCTON
AND BUKRS   = LW_YN220-BUKRS.
IF SY-SUBRC = 0.
LW_ERR-BELNR = LW_YK230-BELNR.
LW_ERR-BUZEI = LW_YK230-BUZEI.
LW_ERR-MESSAGE = 'すでに同じキーが存在します'.
APPEND LW_ERR TO T_ERR.
G_CNT_ERR = G_CNT_ERR + 1.
CONTINUE.
ENDIF.

APPEND LW_YN220 TO T_YN220.
G_CNT_YN220 = G_CNT_YN220 + 1.
ENDLOOP.

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  INSERT_DATA
*&---------------------------------------------------------------------*
*       データ登録
*----------------------------------------------------------------------*
FORM INSERT_DATA.

IF T_YN220[] IS INITIAL.
EXIT.
ENDIF.

INSERT YN220 FROM TABLE T_YN220.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN220'.
ENDIF.

ENDFORM.                    " INSERT_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_IKO
*&---------------------------------------------------------------------*
*       処理結果出力【移行】
*----------------------------------------------------------------------*
FORM WRITE_IKO.
DATA:LW_CNT(10)  TYPE C,
LW_ERR      TYPE TYP_ERR.

WRITE G_CNT_YK230 TO LW_CNT.
WRITE: /001 '<データ移行>',
/003 '仕入データ(YK230)抽出件数：',
040 LW_CNT,
050 '件'.
SKIP 1.

WRITE G_CNT_YN220 TO LW_CNT.
WRITE: /003 '自社データ(仕入)(YN220)登録件数：',
040 LW_CNT,
050 '件'.
WRITE G_CNT_ERR TO LW_CNT.
WRITE: /003 '自社データ(仕入)(YN220)エラー件数：',
040 LW_CNT,
050 '件'.
CHECK NOT T_ERR[] IS INITIAL.
SKIP 1.
WRITE: /003 '伝票番号',
014 '明細',
029 'エラー内容'.
ULINE.
LOOP AT T_ERR INTO LW_ERR.
WRITE: /003 LW_ERR-BELNR,
014 LW_ERR-BUZEI,
029 LW_ERR-MESSAGE.
ENDLOOP.

ENDFORM.                    " WRITE_IKO
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA_GET
*&---------------------------------------------------------------------*
*       YN220データ取得【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA_GET.

SELECT *
INTO TABLE T_YN220
FROM YN220
WHERE GJAHR   IN P_GJAHR
AND SLPDOC  IN P_SLPDOC
AND DTLDOC  IN P_DTLDOC
AND VRFCTON IN P_VRF.

IF SY-SUBRC = 0.
DESCRIBE TABLE T_YN220 LINES G_CNT_YN220.
ELSE.
MESSAGE S762(YN01).
ENDIF.
ENDFORM.                    " DEL_DATA_GET
*&---------------------------------------------------------------------*
*&      Form  DEL_DATA
*&---------------------------------------------------------------------*
*       YN220削除【削除】
*----------------------------------------------------------------------*
FORM DEL_DATA.

DELETE YN220 FROM TABLE T_YN220.
IF SY-SUBRC = 0.
COMMIT WORK AND WAIT.
ELSE.
ROLLBACK WORK.
MESSAGE E758(YN01) WITH 'YN220'.
ENDIF.

ENDFORM.                    " DEL_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_DEL
*&---------------------------------------------------------------------*
*       処理結果出力【削除】
*----------------------------------------------------------------------*
FORM WRITE_DEL.
DATA:LW_CNT(10)  TYPE C.

WRITE G_CNT_YN220 TO LW_CNT.
WRITE: /001 '<移行データ削除>',
/003 '自社データ(仕入)(YN220)削除件数：',
030 LW_CNT,
040 '件'.

ENDFORM.                    " WRITE_DEL
