************************************************************************
* プログラムID  :ZF021900
* プログラム名  :ファクタリングIF
* 作成日        :2009/06/18
* 作成者        :NDSC(maekawa)
* 変更履歴      :
*
************************************************************************
report ZF021900 message-id z1
line-size   120
line-count  90
no standard page heading.

************************************************************************
*     宣言部
************************************************************************
*----------------------------------------------------------------------*
* TABLES定義
*----------------------------------------------------------------------*
tables:BSIK,BSED,LFA1,LFB1,T001,USR01.

*----------------------------------------------------------------------*
* TYPES定義
*----------------------------------------------------------------------*
*仕入先取得用
types:begin of t_shiire,
bukrs like lfb1-bukrs,    "会社コード
lifnr like lfb1-lifnr,    "仕入先コード
end of t_shiire.

*ファクタリング明細取得用
types:begin of t_fact_in,
zfbdt like bsik-zfbdt,    "決済日
bukrs like bsik-bukrs,    "会社コード
lifnr like bsik-lifnr,    "仕入先コード
wdate like bsed-wdate,    "振出日
wrbtr like bsik-wrbtr,    "金額
gjahr like bsik-gjahr,    "会計年度
belnr like bsik-belnr,    "会計伝票番号
buzei like bsik-buzei,    "会計伝票明細番号
waers like bsik-waers,    "通貨コード
shkzg like bsik-shkzg,    "貸借
end of t_fact_in.

*ファクタリング明細出力用
types:begin of t_fact_out,
zfbdt like bsik-zfbdt,    "決済日
bukrs like bsik-bukrs,    "会社コード
lifnr like bsik-lifnr,    "仕入先コード
wdate like bsed-wdate,    "振出日
wrbtr like bsik-wrbtr,    "金額
gjahr like bsik-gjahr,    "会計年度
belnr like bsik-belnr,    "会計伝票番号
buzei like bsik-buzei,    "会計伝票明細番号
waers like bsik-waers,    "通貨コード
shkzg like bsik-shkzg,    "貸借
kgycd(10) type c,         "企業コード
kgytx(40) type c,         "企業名
sortl like lfa1-sortl,    "仕入先名
z_lifnr(10) type c,       "仕入先コード（ゼロ埋め）
w_wdate(6) type c,        "和暦振出日
w_zfbdt(6) type c,        "和暦決済日
w_wrbtr(10) type n,       "金額（ゼロ埋め）
end of t_fact_out.

*該当振出日の伝票取得用
types:begin of t_furiden,
bukrs like bsik-bukrs,    "会社コード
gjahr like bsik-gjahr,    "会計年度
belnr like bsik-belnr,    "会計伝票番号
buzei like bsik-buzei,    "会計伝票明細番号
wdate like bsed-wdate,    "振出日
end of t_furiden.

*ログヘッダ用
types:begin of t_loghead,
mngn01(50) type c,
mngn02(50) type c,
kmkm01(10) type c,
kmkm02(10) type c,
kmkm03(10) type c,
kmkm04(10) type c,
kmkm05(10) type c,
end of t_loghead.

*エラーログ取得用
types:begin of t_errlog,
bukrs like bsik-bukrs,    "会社コード
lifnr like bsik-lifnr,    "仕入先コード
gjahr like bsik-gjahr,    "会計年度
belnr like bsik-belnr,    "会計伝票番号
errtx(50) type c,         "内容
end of t_errlog.

*出力ファイル項目取得用
types:begin of t_hrecord,
*ヘッダレコード
head01(1) type c,    "データ区分
head02(2) type c,    "種別コード
head03(1) type c,    "コード区分
head04(10) type c,   "支払企業コード
head05(40) type c,   "支払企業名
*              head06(4) type c,    "ダミー
*              head07(4) type c,    "仕向銀行番号
*              head08(15) type c,   "仕向銀行名
*              head09(3) type c,    "仕向支店番号
*              head10(15) type c,   "仕向支店名
*              head11(1) type c,    "預金種目(支払企業)
*              head12(7) type c,    "口座番号(支払企業)
head13(6) type n,    "当座貸越可能日
head14(6) type n,    "支払期日
*              head15(5) type c,    "ダミー
end of t_hrecord.

*データレコード
types:begin of t_drecord,
data01(1) type c,    "データ区分
*              data02(4) type c,    "被仕向銀行番号
*              data03(15) type c,   "仕入先コード
*              data04(3) type c,    "被仕向支店番号
*              data05(15) type c,   "被仕向支店名
*              data06(4) type c,    "ダミー
*              data07(1) type c,    "預金種目
*              data08(7) type c,    "口座番号
data09(30) type c,   "仕入先名
data10(10) type n,   "金額
*              data11(1) type c,    "ダミー
data12(10) type c,   "仕入先コード(2)
*              data13(19) type c,   "ダミー
end of t_drecord.

*トレーラレコード
types:begin of t_trecord,
trail01(1) type c,   "データ区分
trail02(6) type n,   "合計件数
trail03(12) type n,  "合計金額
*              trail04(101) type c, "ダミー
end of t_trecord.

*エンドレコード
types:begin of t_erecord,
foot01(1) type c,    "データ区分
*              foot02(119) type c,  "ダミー
end of t_erecord.

*ダウンロードファイル
types:begin of t_file,
field(122) type c,
end of t_file.

*----------------------------------------------------------------------*
* 内部テーブル定義
*----------------------------------------------------------------------*
data:
*仕入先内部テーブル
gt_shiire type table of t_shiire,

*ファクタリング明細(抽出前)内部テーブル
gt_fact_raw type table of t_fact_in,
*ファクタリング明細(編集用)内部テーブル
gt_fact_in type table of t_fact_in,
*ファクタリング明細(出力用)内部テーブル
gt_fact_out type table of t_fact_out,
*該当振出日伝票内部テーブル
gt_furiden type sorted table of t_furiden
with unique key bukrs gjahr belnr buzei,

*エラーログ内部テーブル
gt_errlog type table of t_errlog,

*ヘッダレコード内部テーブル
gt_hrecord type table of t_hrecord,
*データレコード内部テーブル
gt_drecord type table of t_drecord,
*トレイルレコード内部テーブル
gt_trecord type table of t_trecord,
*エンドレコード内部テーブル
gt_erecord type table of t_erecord,
*出力ファイル内部テーブル
gt_file type table of t_file.

*----------------------------------------------------------------------*
* ワークエリア定義
*----------------------------------------------------------------------*
data:
*禁則文字チェック用
gw_kinsoku(130) type c,

*支払方法取得用
gw_zwels like lfb1-zwels,

*エラー存在チェック用
gw_errflg(1) type c,

*出力ログヘッダ情報保持用
gw_loghead type t_loghead.

*----------------------------------------------------------------------*
*CONSTANTS定義
*----------------------------------------------------------------------*
constants:
*F4検索ヘルプ
cns_wtitle(40)      type c value '出力先のフォルダを選択してください',
cns_ifolder(50)      type c value '', "選択フォルダの初期値

*企業コード・禁則文字チェック
cns_suji(10) type c value '0123456789',                    "半角数字
cns_eiji(52) type c value
'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ',"半角英字
cns_kana(56) type c value
'ｱｲｳｴｵｶｷｸｹｺｻｼｽｾｿﾀﾁﾂﾃﾄﾅﾆﾇﾈﾉﾊﾋﾌﾍﾎﾏﾐﾑﾒﾓﾔﾕﾖﾗﾘﾙﾚﾛﾜｦﾝｯｬｭｮｧｨｩｪｫｰ',
"半角カナ
cns_kigo(10) type c value 'ﾞﾟ()-\,./ ',              "半角記号, SPACE

*ファクタリング明細の金額下限
cns_kagen type i value 1000,          "千円

*ファクタリング明細の対応通貨
cns_tsuka(3) type c value 'JPY' ,   "日本円

*ファクタリング明細の貸借チェック用
cns_dc(1) type c value 'S',      "貸方

*出力ファイル固定値
*ヘッダレコード
cns_head01(1) type c value '1',                  "データ区分
cns_head02(2) type c value '24',                 "種別コード
cns_head03(1) type c value '1',                  "コード区分
*データレコード
cns_data01(1) type c value '2',                  "データ区分
*トレーラレコード
cns_trail01(1) type c value '8',                 "データ区分
*エンドレコード
cns_foot01(1) type c value '9',                  "データ区分
*改行コード記述用
cns_flg(2)   type c value '%%',
cns_crlf(2)  type x value '0D0A',                "CR/LF

*ファイルダウンロード
cns_fname(17) type c value 'fb_for_factoring_',  "ファイル名（頭）
cns_ftype(4)  type c value '.txt'.               "ファイル形式

************************************************************************
* 選択画面定義
************************************************************************
*共通選択
selection-screen begin of block bl1 with frame title text-s01.
select-options: s_bukrs for bsik-bukrs." obligatory.   "会社コード
select-options: s_lifnr for bsik-lifnr.              "仕入先コード
selection-screen end of block bl1.

*仕入先明細選択
selection-screen begin of block bl2 with frame title text-s02.
select-options: s_umskz for bsik-umskz." obligatory.   "特殊仕訳コード
parameters: p_wdate like bsed-wdate." obligatory.      "振出日
select-options: s_zfbdt for bsik-zfbdt  no intervals.  "決済日
selection-screen end of block bl2.

*仕入先マスタ選択
selection-screen begin of block bl3 with frame title text-s03.
parameters: p_zwels like lfb1-zwels." obligatory.      "支払方法
selection-screen end of block bl3.

*入力関連
selection-screen begin of block bl4 with frame title text-s04.
parameters: p_kgycd(10) type c." obligatory.           "企業コード
parameters: p_kgytx(40) type c." obligatory.           "企業名
parameters: p_sight(3) type n." obligatory.            "サイト
selection-screen end of block bl4.

*出力関連
selection-screen begin of block bl5 with frame title text-s05.
parameters: p_folder(50) type c." obligatory.          "出力フォルダ
selection-screen end of block bl5.

************************************************************************
*INITIALIZATION.
************************************************************************

************************************************************************
* AT SELECTION-SCREEN
************************************************************************
at selection-screen.
*変数の初期化
perform work_init.

*入力パラメータのチェック
perform check_input.

at selection-screen on value-request for p_folder.
*出力先フォルダF4検索ヘルプ
perform f4_help.  "call function 'TMP_GUI_BROWSE_FOR_FOLDER '

************************************************************************
*START-OF-SELECTION
************************************************************************
start-of-selection.
*データの取得
perform get_data.

*ファクタリング明細の編集
perform edit_fact.

*ファクタリング明細のチェック
perform check_fact.
************************************************************************
* 出力
************************************************************************
*ファイル編集・出力
perform output_file.

*ログ編集・出力
perform output_log.

************************************************************************
*TOP-OF-PAGE.
************************************************************************
top-of-page.
*一覧ヘッダの作成
perform write_head.


*<<<<<<<<<<<<<<<<<<<<<<<<<以下サブルーチン>>>>>>>>>>>>>>>>>>>>>>>>>>>>*
*---------------------------------------------------------------------*
*       FORM work_init                                                *
*---------------------------------------------------------------------*
*       変数の初期化                                                  *
*---------------------------------------------------------------------*
form work_init.
clear: gw_kinsoku, gw_zwels, gw_errflg, gw_loghead.

refresh:gt_shiire, gt_fact_raw, gt_fact_in, gt_fact_out,
gt_furiden, gt_errlog, gt_hrecord, gt_drecord,
gt_trecord, gt_erecord.

endform.
*---------------------------------------------------------------------*
*       FORM check_input                                              *
*---------------------------------------------------------------------*
*       入力パラメータのチェック                                      *
*---------------------------------------------------------------------*
form check_input.

*チェック用変数準備
concatenate cns_suji cns_eiji cns_kana cns_kigo into gw_kinsoku.
concatenate '%' p_zwels '%' into gw_zwels.

* 会社コードの必須チェック
if s_bukrs = space.
message e006 with '会社コード'.
else.
* 会社コードの存在チェック
select bukrs into t001-bukrs
from t001
where bukrs in s_bukrs.
endselect.

if sy-subrc <> 0.
message e614 with '会社コード'.
endif.
endif.

* 仕入先コードの存在チェック
if s_lifnr <> space.
select lifnr into lfb1-lifnr
from lfb1
where lifnr in s_lifnr
and bukrs in s_bukrs
and zwels like gw_zwels.
endselect.

if sy-subrc <> 0.
message e614 with '仕入先コード'.
endif.
endif.

* 特殊仕訳コードの必須チェック
if s_umskz = space.
message e006 with '特殊仕訳コード'.
endif.

* 振出日の必須チェック
if p_wdate = '00000000'.
message e006 with '振出日'.
endif.

* 支払方法の必須チェック
if p_zwels = space.
message e006 with '支払方法'.
endif.

* 企業コードの必須チェック
if p_kgycd = space.
message e006 with '企業コード'.
* 企業コードの禁則文字チェック
else.
if p_kgycd cn cns_suji.
message e400 with '企業コードは10桁の数字です'.
endif.
endif.

* 企業名の必須チェック
if p_kgytx = space.
message e006 with '企業名'.
*企業名の禁則文字チェック
else.
if p_kgytx cn gw_kinsoku.
message e400 with '企業名に禁則文字が含まれています'.
endif.
endif.

* サイトの必須チェック
if p_sight = space.
message e006 with 'サイト'.
endif.

* 出力フォルダの必須チェック
if p_folder = space.
message e006 with '出力フォルダ'.
*その他のチェック①
*末尾が\でなければエラー
else.
if p_folder cp '*\'.
else.
message e400 with '出力フォルダの末尾に\をつけてください'.
endif.
*その他のチェック②
*フォルダが存在しなければエラー
perform folder_exit.  "call function 'TMP_GUI_GET_FILE_EXIST'
endif.

endform.
*---------------------------------------------------------------------*
*       FORM folder_exit                                              *
*---------------------------------------------------------------------*
*       出力フォルダの存在チェック                                    *
*---------------------------------------------------------------------*
form folder_exit.
data: lw_exist(1) type c.

call function 'TMP_GUI_GET_FILE_EXIST'
EXPORTING
FNAME          = p_folder
IMPORTING
EXIST          = lw_exist
EXCEPTIONS
FILEINFO_ERROR = 1
others         = 2.

if lw_exist <> 'X' or p_folder = '\'.
message e400 with '出力フォルダが存在しません'.
endif.
endform.
*---------------------------------------------------------------------*
*       FORM f4_help                                                  *
*---------------------------------------------------------------------*
*       出力フォルダの取得                                            *
*---------------------------------------------------------------------*
form f4_help.

call function 'TMP_GUI_BROWSE_FOR_FOLDER '
EXPORTING
WINDOW_TITLE    = cns_wtitle
INITIAL_FOLDER  = cns_ifolder
IMPORTING
SELECTED_FOLDER = p_folder
EXCEPTIONS
CNTL_ERROR      = 1
others          = 2.

endform.
*---------------------------------------------------------------------*
*       FORM get_data                                                 *
*---------------------------------------------------------------------*
*       データの取得                                                  *
*---------------------------------------------------------------------*
form get_data.
data: lw_fact type t_fact_in,
lw_furiden type t_furiden.

*対象仕入先コードの取得
select bukrs lifnr
into table gt_shiire
from lfb1
where lifnr in s_lifnr
and bukrs in s_bukrs
and zwels like gw_zwels.

*対象仕入先明細（ファクタリング明細）候補の取得
select bukrs lifnr zfbdt wrbtr
gjahr belnr waers shkzg buzei
into corresponding fields of table gt_fact_raw
from bsik
for all entries in gt_shiire
where bukrs = gt_shiire-bukrs
and lifnr = gt_shiire-lifnr
and umskz in s_umskz
and zfbdt in s_zfbdt.

*ファクタリング明細が1件も存在しない場合エラー
if sy-subrc <> 0.
message s400
with '対象の仕入先明細(ファクタリング明細)が存在しません'.
stop.
else.

*該当振出日の伝票の取得
select bukrs belnr gjahr buzei wdate
into corresponding fields of table gt_furiden
from bsed
for all entries in gt_fact_raw
where bukrs = gt_fact_raw-bukrs
and gjahr = gt_fact_raw-gjahr
and belnr = gt_fact_raw-belnr
and buzei = gt_fact_raw-buzei
and wdate = p_wdate.

*ファクタリング明細が1件も存在しない場合エラー
if sy-subrc <> 0.
message s400
with '対象の仕入先明細(ファクタリング明細)が存在しません'.
stop.
endif.
endif.

sort gt_fact_raw by bukrs gjahr belnr buzei.

*対象仕入先明細候補から該当振出日のものを選択
loop at gt_fact_raw into lw_fact.
loop at gt_furiden into lw_furiden.
if lw_fact-bukrs =  lw_furiden-bukrs
and lw_fact-gjahr =  lw_furiden-gjahr
and lw_fact-belnr = lw_furiden-belnr
and lw_fact-buzei = lw_furiden-buzei.
lw_fact-wdate = lw_furiden-wdate.
append lw_fact to gt_fact_in.
endif.
endloop.
endloop.

endform.
*---------------------------------------------------------------------*
*       FORM edit_fact                                                *
*---------------------------------------------------------------------*
*       ファクタリング明細の編集                                      *
*---------------------------------------------------------------------*
form edit_fact.
data: lw_fact type t_fact_out,
lw_year(2) type n,
lw_month(2) type n,
lw_day(2) type n.

loop at gt_fact_in into lw_fact.

*企業コード・企業名の付与
lw_fact-kgycd = p_kgycd.
lw_fact-kgytx = p_kgytx.

*仕入先名称の付与
select single sortl
into lw_fact-sortl
from lfa1
where lifnr = lw_fact-lifnr.

*仕入先コード頭ゼロ埋め "call function 'CONVERSION_EXIT_ALPHA_INPUT'
perform zero_ume using lw_fact-lifnr
changing lw_fact-z_lifnr.

*和暦変換　①振出日      "call function 'CONVERSION_EXIT_JPDAT_OUTPUT'
perform change_wareki using lw_fact-wdate
changing lw_year
lw_month
lw_day.
concatenate lw_year lw_month lw_day into lw_fact-w_wdate.

*和暦変換　②決済日      "call function 'CONVERSION_EXIT_JPDAT_OUTPUT'
perform change_wareki using lw_fact-zfbdt
changing lw_year
lw_month
lw_day.
concatenate lw_year lw_month lw_day into lw_fact-w_zfbdt.

*金額表示の編集
lw_fact-wrbtr = lw_fact-wrbtr * 100.
perform edit_kingaku using lw_fact-wrbtr
changing lw_fact-w_wrbtr.

*ファクタリング明細の編集内容を反映
append lw_fact to gt_fact_out.
endloop.

endform.

*---------------------------------------------------------------------*
*       FORM zero_ume                                                 *
*---------------------------------------------------------------------*
*       頭ゼロ埋め                                                    *
*---------------------------------------------------------------------*
form zero_ume using i_data
changing o_data.

call function 'CONVERSION_EXIT_ALPHA_INPUT'
EXPORTING
input  = i_data
IMPORTING
output = o_data
EXCEPTIONS
others = 1.

endform.
*---------------------------------------------------------------------*
*       FORM change_wareki                                            *
*---------------------------------------------------------------------*
*       和暦変換                                                      *
*---------------------------------------------------------------------*
form change_wareki using i_date
changing o_year
o_month
o_day.
data:o_output(20) type c.
call function 'CONVERSION_EXIT_JPDAT_OUTPUT'
EXPORTING
input      = i_date
IMPORTING
OUTPUT     = o_output
yearn      = o_year
monthn     = o_month
dayn       = o_day
EXCEPTIONS
CNTL_ERROR = 1
others     = 2.

endform.

*---------------------------------------------------------------------*
*       FORM edit_kingaku                                             *
*---------------------------------------------------------------------*
*       金額表示の編集                                                *
*---------------------------------------------------------------------*
form edit_kingaku using i_wrbtr
changing o_wrbtr.

data: lw_wrbtr(10) type n.

*小数点以下を四捨五入
write i_wrbtr to lw_wrbtr decimals 0.
*カンマ削除
do.
replace ',' with ' ' into lw_wrbtr.
if sy-subrc <> 0.
exit.
endif.
enddo.
condense lw_wrbtr no-gaps.
*金額の頭ゼロ埋め     "call function 'CONVERSION_EXIT_ALPHA_INPUT'
perform zero_ume using lw_wrbtr
changing o_wrbtr.

endform.
*---------------------------------------------------------------------*
*       FORM check_fact                                               *
*---------------------------------------------------------------------*
*       ファクタリング明細のチェック                                  *
*---------------------------------------------------------------------*
form check_fact.
data:lw_fact type t_fact_out,
lw_errlog type t_errlog,
lw_stcheck type i,
lw_kagen(4) type n.

clear: gw_errflg, lw_fact, lw_errlog, lw_stcheck.
loop at gt_fact_out into lw_fact.
*サイトチェック
lw_stcheck = ( lw_fact-zfbdt - lw_fact-wdate ) .
if  lw_stcheck > p_sight.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
concatenate text-m15 p_sight text-m16 into lw_errlog-errtx.
"サイトが(入力日数)を超えています
append lw_errlog to gt_errlog.
endif.

*仕入先コードの禁則文字チェック
if lw_fact-lifnr cn gw_kinsoku.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
lw_errlog-errtx = text-m18.
"仕入先コードに禁則文字が使用されています
append lw_errlog to gt_errlog.
endif.

*仕入先名の禁則文字チェック
if lw_fact-sortl cn gw_kinsoku.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
lw_errlog-errtx = text-m17.
"仕入先名に禁則文字が使用されています
append lw_errlog to gt_errlog.
endif.

*金額の下限チェック
if  lw_fact-wrbtr < cns_kagen.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
lw_kagen = cns_kagen. "数値⇒文字
concatenate text-m21 lw_kagen text-m22 into lw_errlog-errtx.
"金額が(設定金額)円未満です
append lw_errlog to gt_errlog.
endif.

*金額の通貨コードチェック
if  lw_fact-waers <> cns_tsuka.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
concatenate text-m19 cns_tsuka text-m20 into lw_errlog-errtx.
"通貨コードが(設定通貨)以外です
append lw_errlog to gt_errlog.
endif.

*金額のマイナスチェック
if  lw_fact-shkzg = cns_dc.
gw_errflg = 'X'.

lw_errlog-bukrs = lw_fact-bukrs.
lw_errlog-lifnr = lw_fact-lifnr.
lw_errlog-gjahr = lw_fact-gjahr.
lw_errlog-belnr = lw_fact-belnr.
lw_errlog-errtx = text-m23.
"金額がマイナスです
append lw_errlog to gt_errlog.
endif.
endloop.

endform.

*---------------------------------------------------------------------*
*       FORM output_file                                              *
*---------------------------------------------------------------------*
*       ファイル出力                                                  *
*---------------------------------------------------------------------*
form output_file.

*エラーログの件数がゼロ件のときのみ処理を行う
if gw_errflg <> 'X'.

*ファイル編集
perform edit_file.

*ファイルダウンロード
perform file_download.

endif.
endform.

*---------------------------------------------------------------------*
*       FORM edit_file                                                *
*---------------------------------------------------------------------*
*       ファイル編集                                                  *
*---------------------------------------------------------------------*
form edit_file.
data:lw_fact type t_fact_out,
lw_fact_new type t_fact_out,
lw_hrecord type t_hrecord,
lw_drecord type t_drecord,
lw_trecord type t_trecord,
lw_erecord type t_erecord,
lw_file type t_file.

clear:lw_fact, lw_hrecord, lw_drecord,
lw_trecord, lw_erecord, lw_file.

*決済日>仕入先コード>伝票番号にて内部テーブルをソート
sort gt_fact_out by zfbdt z_lifnr belnr.

loop at gt_fact_out into lw_fact.
lw_fact_new = lw_fact.
*①決済日ごとにヘッダレコードを作成
at new zfbdt.
clear: lw_file.
lw_file-field+0 = cns_head01.
lw_file-field+1 = cns_head02.
lw_file-field+3 = cns_head03.
lw_file-field+4 = p_kgycd.
lw_file-field+14 = p_kgytx.
lw_file-field+103 = lw_fact_new-w_wdate.
lw_file-field+109 = lw_fact_new-w_zfbdt.

lw_file-field+120(2) = cns_flg.
replace cns_flg with cns_crlf into lw_file+120(2).
append lw_file to gt_file.
endat.

*②同決済日のデータレコードを作成
clear: lw_file.
lw_file-field+0 = cns_data01.
lw_file-field+50 = lw_fact-sortl.
lw_file-field+80 = lw_fact-w_wrbtr.
lw_file-field+91 = lw_fact-lifnr.
lw_file-field+120(2) = cns_flg.
replace cns_flg with cns_crlf into lw_file+120(2).
append lw_file to gt_file.

*合計件数取得
lw_trecord-trail02 = lw_trecord-trail02 + 1.

*合計金額取得
lw_trecord-trail03 = lw_trecord-trail03 + lw_fact-w_wrbtr.

*③決済日ごとにトレイルレコードを作成
at end of zfbdt.
clear:lw_file.
lw_file-field+0 = cns_trail01.
lw_file-field+1  = lw_trecord-trail02.
lw_file-field+7 = lw_trecord-trail03.

lw_file-field+120(2) = cns_flg.
replace cns_flg with cns_crlf into lw_file+120(2).
append lw_file to gt_file.
clear: lw_trecord.
endat.

*④ファイルの最後にエンドレコードを追加
at last.
clear:lw_file.
lw_file-field+0 = cns_foot01.

lw_file-field+120(2) = cns_flg.
replace cns_flg with cns_crlf into lw_file+120(2).
append lw_file to gt_file.
endat.
endloop.

endform.
*---------------------------------------------------------------------*
*       FORM file_download                                            *
*---------------------------------------------------------------------*
*       ファイルダウンロード                                          *
*---------------------------------------------------------------------*
form file_download.
data: lw_odate(8) type c,
lw_otime(6) type c,
lw_pname type String.

lw_odate = sy-datum.
lw_otime = sy-uzeit.

concatenate p_folder cns_fname lw_odate lw_otime cns_ftype
into lw_pname.

call function 'GUI_DOWNLOAD'
EXPORTING
filename            = lw_pname
filetype            = 'ASC'  "テキスト形式
WRITE_LF            = ''  "汎用モジュールでの改行なし
TABLES
data_tab            = gt_file
EXCEPTIONS
file_open_error     = 1
file_write_error    = 2
invalid_table_width = 3
invalid_type        = 5
no_batch            = 6
unknown_error       = 7
others              = 8.

if sy-subrc <> 0.
message s400 with 'ダウンロードに失敗しました。'.
stop.
endif.

endform.
*---------------------------------------------------------------------*
*       FORM output_log                                               *
*---------------------------------------------------------------------*
*       ログ出力                                                      *
*---------------------------------------------------------------------*
form output_log.

*ログヘッダ編集
perform edit_loghead.

*スプール登録
perform set_spoole.

*ログ一覧出力
perform write_log.

endform.

*---------------------------------------------------------------------*
*       FORM edit_loghead                                             *
*---------------------------------------------------------------------*
*       ログヘッダ編集                                                *
*---------------------------------------------------------------------*
form edit_loghead.

*エラーログの件数がゼロ件（正常終了）
if gw_errflg <> 'X'.
*ログヘッダ作成
gw_loghead-mngn01 = text-m01.
gw_loghead-mngn02 = text-m02.
gw_loghead-kmkm01 = text-m03.
gw_loghead-kmkm02 = text-m04.
gw_loghead-kmkm03 = text-m05.
gw_loghead-kmkm04 = text-m06.
gw_loghead-kmkm05 = text-m07.

*エラーログの件数が1件以上（以上終了）
else.
*ログヘッダ部分
gw_loghead-mngn01 = text-m08.
gw_loghead-mngn02 = text-m09.
gw_loghead-kmkm01 = text-m10.
gw_loghead-kmkm02 = text-m11.
gw_loghead-kmkm03 = text-m12.
gw_loghead-kmkm04 = text-m13.
gw_loghead-kmkm05 = text-m14.
endif.

endform.
*---------------------------------------------------------------------*
*       FORM set_spoole                                               *
*---------------------------------------------------------------------*
*       スプール登録                                                  *
*---------------------------------------------------------------------*
form set_spoole.

data:lf_pri_params like pri_params.

*ユーザ設定取得
select single * from usr01 where bname = sy-uname.

* プリンタ設定
lf_pri_params-pdest = usr01-spld.
* 書式設定
lf_pri_params-paart = 'X_90_120'.
call function 'SET_PRINT_PARAMETERS'
EXPORTING
DESTINATION = lf_pri_params-pdest
IMMEDIATELY = ''
NEW_LIST_ID = 'X'
RELEASE     = usr01-spda.

new-page print on layout lf_pri_params-paart no dialog.

*スプールへのログ出力
perform write_log.

*画面印刷への切替
new-page print off.

endform.

*---------------------------------------------------------------------*
*       FORM write_head                                               *
*---------------------------------------------------------------------*
*       ログヘッダ出力                                                *
*---------------------------------------------------------------------*
form write_head.
*ログヘッダ出力
write: /003 gw_loghead-mngn01.
write: /003 gw_loghead-mngn02.
write: /.
if gw_errflg = ''.
write: /003 gw_loghead-kmkm01,
015 gw_loghead-kmkm02,
027 gw_loghead-kmkm03,
039 gw_loghead-kmkm04,
061 gw_loghead-kmkm05.
else.
write: /003 gw_loghead-kmkm01,
009 gw_loghead-kmkm02,
019 gw_loghead-kmkm03,
031 gw_loghead-kmkm04,
043 gw_loghead-kmkm05.
endif.
write: / sy-uline no-gap.

endform.
*---------------------------------------------------------------------*
*       FORM write_log                                                *
*---------------------------------------------------------------------*
*       ログ一覧出力                                                  *
*---------------------------------------------------------------------*
form write_log.
data: lw_oklog type t_fact_out,
lw_errlog type t_errlog,
lw_sum_wrbtr like bsik-wrbtr.

*ログデータ部分出力
*正常終了時
if gw_errflg <> 'X'.
loop at gt_fact_out into lw_oklog.
write: /003 lw_oklog-wdate,
015 lw_oklog-zfbdt,
027 lw_oklog-lifnr,
039 lw_oklog-sortl,
061 lw_oklog-wrbtr decimals 0.
*>>>mod_start 2009/08/26 m.maekawa
*合計金額の表示
lw_sum_wrbtr = lw_sum_wrbtr + lw_oklog-wrbtr.
at end of lifnr.
format color 3 on intensified.
write: /056 text-m24,
061 lw_sum_wrbtr decimals 0,
sy-uline no-gap.
format color off.
clear: lw_sum_wrbtr.
endat.
*<<<mod_end   2009/08/26 m.maekawa
endloop.
else.
*異常終了時
loop at gt_errlog into lw_errlog.
write: /003 lw_errlog-bukrs,
011 lw_errlog-gjahr,
019 lw_errlog-belnr,
031 lw_errlog-lifnr,
043 lw_errlog-errtx.
endloop.
endif.

endform.
