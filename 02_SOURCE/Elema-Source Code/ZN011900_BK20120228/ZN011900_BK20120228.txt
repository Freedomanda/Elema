*&---------------------------------------------------------------------*
*&  REPORT ZN011900                                                    *
*&         売掛金年齢表                                                *
*&---------------------------------------------------------------------*
*&  機能     : 指定された期間の売掛金年齢表をALV表示する
*&
*&  作成日   : 2012/02/10
*&  作成者   : H.KOMIYAMA(SOLFIS)
*&  変更履歴 :
*&  変更内容 :
*&---------------------------------------------------------------------*
REPORT ZN011900
NO STANDARD PAGE HEADING
LINE-SIZE   170
LINE-COUNT  58
MESSAGE-ID  YN01.

*----------------------------------------------------------------------*
*   参照先
*----------------------------------------------------------------------*
TYPE-POOLS: SSCR,
SLIS.
TABLES: BSID,
YN120.

*----------------------------------------------------------------------*
*   型定義
*----------------------------------------------------------------------*
*-- 営業所名称取得用
TYPES: BEGIN OF T_TVKBT,
VKBUR TYPE TVKBT-VKBUR,            " 営業所
BEZEI TYPE TVKBT-BEZEI,            " 内容説明
END   OF T_TVKBT,
*-- 営業グループ名称取得用
BEGIN OF T_TVGRT,
VKGRP TYPE TVGRT-VKGRP,            " 営業グループ
BEZEI TYPE TVGRT-BEZEI,            " 内容説明
END   OF T_TVGRT,
*-- 得意先名称取得用
BEGIN  OF T_KNxx,
KUNNR TYPE KNA1-KUNNR,             " 得意先コード(請求先)
NAME1 TYPE KNA1-NAME1,             " 名称１
END    OF T_KNxx,
*-- 未消込伝票取得用
BEGIN OF T_BSxx,
KUNNR   TYPE BSID-KUNNR,           " 得意先コード
GJAHR   TYPE BSID-GJAHR,           " 会計年度
BELNR   TYPE BSID-BELNR,           " 会計伝票番号
BUZEI   TYPE BSID-BUZEI,           " 会計伝票内の明細番号
BUDAT   TYPE BSID-BUDAT,           " 伝票の転記日付
WAERS   TYPE BSID-WAERS,           " 通貨コード
BLART   TYPE BSID-BLART,           " 伝票タイプ
SHKZG   TYPE BSID-SHKZG,           " 貸借フラグ
WRBTR   TYPE BSID-WRBTR,           " 伝票通貨額
ZUONR   TYPE BSID-ZUONR,           " ソートキー
SGTXT   TYPE BSID-SGTXT,           " 明細テキスト
ZFBDT   TYPE BSID-ZFBDT,           " 期日計算の支払基準日
ZTERM   TYPE BSID-ZTERM,           " 支払条件キー
MANDT   TYPE BSAD-MANDT,           " クライアント(BSADフラグ)
GJAHR2  TYPE YN120-YNGJAHR,        " 自社データ取得用会計年度
BELNR2  TYPE YN120-BELNR,          " 自社データ取得用伝票番号
BUZEI2  TYPE YN120-BUZEI,          " 自社データ取得用伝票明細
END   OF T_BSxx,
*-- 自社データ取得用
BEGIN OF T_YN120,
YNGJAHR TYPE YN120-YNGJAHR,        " 伝票会計年度
BELNR   TYPE YN120-BELNR,          " 伝票番号
BUZEI   TYPE YN120-BUZEI,          " 伝票明細番号
VRFCTON TYPE YN120-VRFCTON,        " 得意先コード(支払人)
BUKRS   TYPE YN120-BUKRS,          " 会社コード
ZFBDT   TYPE YN120-ZFBDT,          " 計上締日
DVSON   TYPE YN120-DVSON,          " 営業所
CSTS    TYPE YN120-CSTS,           " 照合ステータス
COMMT   TYPE YN120-COMMT,          " コメント
LIST2   TYPE YN120-LIST2,          " 得意先発注番号
LIST3   TYPE YN120-LIST3,          " 受注伝票
LIST4   TYPE YN120-LIST4,          " 営業グループ
LDATE1  TYPE YN120-LDATE1,         " 請求日
GJAHR   TYPE YN120-GJAHR,          " 伝票会計年度
SLPDOC  TYPE YN120-SLPDOC,         " 伝票番号
DTLDOC  TYPE YN120-DTLDOC,         " 伝票明細番号
END OF   T_YN120,
*-- データ出力用
BEGIN OF T_OUTPUT,
DVSON   TYPE YN120-DVSON,          " 営業所
KUNNR   TYPE BSID-KUNNR,           " 得意先（支払人）
NAME1   TYPE KNA1-NAME1,           " 名称
LIST2   TYPE YN120-LIST2,          " 得意先発注番号
WAERS   TYPE BSID-WAERS,           " 通貨
BUDAT   TYPE BSID-BUDAT,           " 転記日付
ZFBDT   TYPE BSID-ZFBDT,           " 締日
PAYDT   TYPE DATUM,                " 条件入金日
TERM00  TYPE BSID-WRBTR,           " 未滞留
TERM30  TYPE BSID-WRBTR,           " 1 〜 30 日
TERM60  TYPE BSID-WRBTR,           " 31 〜 60 日
TERM90  TYPE BSID-WRBTR,           " 61 〜 90 日
TERM99  TYPE BSID-WRBTR,           " 91日超
TOTAL   TYPE BSID-WRBTR,           " 合計
BEZEI1  TYPE TVKBT-BEZEI,          " 営業所名
LIST4   TYPE YN120-LIST4,          " 営業G
LIST3   TYPE YN120-LIST3,          " 受注番号
RETENT1 TYPE I,                    " 滞留（日）
RETENT2 TYPE I,                    " 転記日滞留（日）
STATUS  TYPE CHAR20,               " ステータス
COMMT   TYPE YN120-COMMT,          " コメント
BLART   TYPE BSID-BLART,           " 伝票タイプ
BELNR   TYPE BSID-BELNR,           " 会計伝票
BUZEI   TYPE BSID-BUZEI,           " 明細
GJAHR   TYPE BSID-GJAHR,           " 会計年度
BEZEI2  TYPE TVGRT-BEZEI,          " 営業グループ名
END   OF T_OUTPUT,
*-- ドメインテキスト取得用
T_DDTEXT TYPE DD07V.

*----------------------------------------------------------------------*
*   構造定義
*----------------------------------------------------------------------*
DATA: W_TVKBT  TYPE T_TVKBT,
W_TVGRT  TYPE T_TVGRT,
W_KNxx   TYPE T_KNxx,
W_BSxx   TYPE T_BSxx,
W_YN120  TYPE T_YN120,
W_DDTEXT TYPE T_DDTEXT,
W_OUTPUT TYPE T_OUTPUT.

*----------------------------------------------------------------------*
*   内部テーブル定義
*----------------------------------------------------------------------*
DATA: I_TVKBT  TYPE HASHED   TABLE OF T_TVKBT
WITH UNIQUE KEY VKBUR,
I_TVGRT  TYPE HASHED   TABLE OF T_TVGRT
WITH UNIQUE KEY VKGRP,
I_KNxx   TYPE HASHED   TABLE OF T_KNxx
WITH UNIQUE KEY KUNNR,
I_BSxx   TYPE STANDARD TABLE OF T_BSxx,
I_YN120  TYPE SORTED TABLE OF T_YN120
WITH NON-UNIQUE KEY YNGJAHR BELNR BUZEI VRFCTON,
I_OUTPUT TYPE STANDARD TABLE OF T_OUTPUT,
I_DDTEXT TYPE STANDARD TABLE OF T_DDTEXT.

*----------------------------------------------------------------------*
*   ALV出力用定義
*----------------------------------------------------------------------*
*-- ALV出力用
DATA: W_LAYOUT      TYPE SLIS_LAYOUT_ALV,     " ALV出力レイアウト
I_FIELDCAT    TYPE SLIS_T_FIELDCAT_ALV, " ALV出力項目設定
W_REPID       TYPE SY-REPID,            " ALV出力用：プログラムID
W_EDISVARIANT TYPE DISVARIANT,          " ALV出力バリアント
I_EVENT       TYPE SLIS_T_EVENT,        " イベント
W_EVENT       LIKE LINE OF I_EVENT,
I_HEAD        TYPE SLIS_T_LISTHEADER,   " ヘッダ
W_HEAD        LIKE LINE OF I_HEAD,
W_PRINT       TYPE SLIS_PRINT_ALV.

*----------------------------------------------------------------------*
*   定数定義
*----------------------------------------------------------------------*
CONSTANTS:
C_ON         TYPE CHAR1 VALUE 'X',        " フラグON
C_SAVE       TYPE CHAR1 VALUE 'A',        " SAVE機能ON
C_TYP1       TYPE CHAR1 VALUE 'H',        " ヘッダ書式(H)
C_TYP2       TYPE CHAR1 VALUE 'S',        " ヘッダ書式(S)
C_STATUS     TYPE CHAR6 VALUE '入金済'.   " 照合ステータステキスト

*&---------------------------------------------------------------------*
*&   画面項目定義
*&---------------------------------------------------------------------*
*-- データ選択
SELECTION-SCREEN: BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-S01.
*-- 会社コード
PARAMETERS:     P_BUKRS TYPE T001-BUKRS OBLIGATORY MEMORY ID BUK,
*-- 年齢基準日
P_BUDAT TYPE BSID-BUDAT OBLIGATORY.
*-- 得意先コード（支払人）
SELECT-OPTIONS: S_KUNNR FOR  BSID-KUNNR,
*-- 通貨コード
S_WAERS FOR  BSID-WAERS NO INTERVALS,
*-- 営業所
S_DVSON FOR  YN120-DVSON.
SELECTION-SCREEN: END   OF BLOCK BL01.

*-- 処理オプション
SELECTION-SCREEN: BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-S02.
*-- ALVレイアウト
PARAMETERS: P_LAYOUT TYPE SLIS_VARI.
SELECTION-SCREEN:END   OF BLOCK BL02.

*-- システム項目
SELECTION-SCREEN: BEGIN OF BLOCK BL03 WITH FRAME TITLE TEXT-S03.
*-- 売掛金勘定コード
SELECT-OPTIONS: S_SAKNR  FOR  BSID-SAKNR OBLIGATORY.
*-- 売上売掛金伝票タイプ
PARAMETERS:     P_BLART1 TYPE BSID-BLART OBLIGATORY,
*-- 部分照合伝票タイプ
P_BLART2 TYPE BSID-BLART OBLIGATORY,
*-- 消込伝票タイプ
P_BLART3 TYPE BSID-BLART OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK BL03.

************************************************************************
INITIALIZATION.
************************************************************************
*---選択画面の複数入力ボタン制御
PERFORM F_BUTTON_CONTROL.

************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_LAYOUT.
************************************************************************
*-- ALVバリアント取得
PERFORM F_CALL_ALV_VARIANT CHANGING P_LAYOUT.

************************************************************************
AT SELECTION-SCREEN.
************************************************************************
*-- チェック処理
PERFORM F_CHECK_PARAMETER.

************************************************************************
START-OF-SELECTION.
************************************************************************
*-- 初期処理
PERFORM INT_SEC.
*-- メイン処理
PERFORM MAIN_SEC.
*-- 終了処理
PERFORM END_SEC.

************************************************************************
END-OF-SELECTION.
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  F_BUTTON_CONTROL
*&---------------------------------------------------------------------*
*       選択画面の複数ボタン制御
*----------------------------------------------------------------------*
FORM F_BUTTON_CONTROL.

DATA:LW_RES        TYPE SSCR_RESTRICT,
LW_OPT_LIST   TYPE SSCR_OPT_LIST,
LW_ASS        TYPE SSCR_ASS.

*-- 単一指定のみ可能
LW_OPT_LIST-NAME       = 'JUST_EQ'.
LW_OPT_LIST-OPTIONS-EQ = 'X'.
APPEND LW_OPT_LIST TO LW_RES-OPT_LIST_TAB.

LW_ASS-KIND    = 'S'.
LW_ASS-SG_MAIN = 'I'.
LW_ASS-OP_MAIN = 'JUST_EQ'.

*-- 通貨コード
LW_ASS-NAME    = 'S_WAERS'.
APPEND LW_ASS TO LW_RES-ASS_TAB.

*-- 実行
CALL FUNCTION 'SELECT_OPTIONS_RESTRICT'
EXPORTING
RESTRICTION = LW_RES.

ENDFORM.                    " F_BUTTON_CONTROL
*&-------------------------------------------------------------------*
*&      Form  F_CALL_ALV_VARIANT
*&-------------------------------------------------------------------*
*       ALVレイアウト用F4ヘルプ
*--------------------------------------------------------------------*
*      <--AO_VARI    ALVレイアウト
*--------------------------------------------------------------------*
FORM F_CALL_ALV_VARIANT CHANGING VALUE(AO_VARI) TYPE ANY.

DATA: LW_DISVARIANT TYPE DISVARIANT.

CLEAR: LW_DISVARIANT,
AO_VARI.

LW_DISVARIANT-REPORT   = SY-REPID.
LW_DISVARIANT-USERNAME = SY-UNAME.

CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
EXPORTING
IS_VARIANT         = LW_DISVARIANT
I_SAVE             = C_SAVE
I_DISPLAY_VIA_GRID = C_ON
IMPORTING
ES_VARIANT         = LW_DISVARIANT
EXCEPTIONS
NOT_FOUND          = 1
PROGRAM_ERROR      = 2
OTHERS             = 3.

IF SY-SUBRC = 0.
AO_VARI = LW_DISVARIANT-VARIANT.
ENDIF.

ENDFORM.                    " F_CALL_ALV_VARIANT
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       チェック処理
*----------------------------------------------------------------------*
FORM F_CHECK_PARAMETER.

*-- 会社コードの存在チェック
PERFORM F_CHECK_P_BUKRS.

ENDFORM.                    " F_CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  F_CHECK_P_BUKRS
*&---------------------------------------------------------------------*
*       会社コードの存在チェック
*----------------------------------------------------------------------*
FORM F_CHECK_P_BUKRS.

DATA: L_BUKRS TYPE T001-BUKRS.

SELECT SINGLE BUKRS             " 会社コード
FROM T001
INTO L_BUKRS
WHERE BUKRS = P_BUKRS.         " 会社コード

IF SY-SUBRC <> 0.
*-- 対象データがありません
MESSAGE E762.
ENDIF.

ENDFORM.                    " F_CHECK_P_BUKRS
*&---------------------------------------------------------------------*
*&      Form  INT_SEC
*&---------------------------------------------------------------------*
*       初期処理
*----------------------------------------------------------------------*
FORM INT_SEC.

CLEAR: W_TVKBT,
W_TVGRT,
W_KNxx,
W_BSxx,
W_YN120,
W_DDTEXT,
W_OUTPUT,
W_LAYOUT,
W_REPID,
W_EDISVARIANT,
W_EVENT,
W_HEAD,
W_PRINT.

REFRESH: I_TVKBT,
I_TVGRT,
I_KNxx,
I_BSxx,
I_YN120,
I_OUTPUT,
I_DDTEXT,
I_FIELDCAT,
I_EVENT,
I_HEAD.

ENDFORM.                    " INT_SEC
*&---------------------------------------------------------------------*
*&      Form  MAIN_SEC
*&---------------------------------------------------------------------*
*      メイン処理
*----------------------------------------------------------------------*
FORM MAIN_SEC.

*-- 付属データ取得
PERFORM F_GET_ADJUNCT_DATA.

*-- 未消込伝票取得
PERFORM F_GET_NOT_SCRUB_DATA.

*-- 自社データ取得
PERFORM F_GET_YN120_DATA.

*-- 出力データ編集
PERFORM F_EDIT_OUTPUT_DATA.

ENDFORM.                    " MAIN_SEC
*&---------------------------------------------------------------------*
*&      Form  F_GET_ADJUNCT_DATA
*&---------------------------------------------------------------------*
*       付属データ取得
*----------------------------------------------------------------------*
FORM F_GET_ADJUNCT_DATA.

*-- 営業所名称の一括取得
PERFORM F_SELECT_TVKBT.

*-- 営業グループ名称の一括取得
PERFORM F_SELECT_TVGRT.

*-- 得意先名称の一括取得
PERFORM F_SELECT_KNA1.

*-- 照合ステータステキストの一括取得
PERFORM F_CALL_DDUT_DOMVALUES_GET USING 'YNCHAR1'.

ENDFORM.                    " F_GET_ADJUNCT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_TVKBT
*&---------------------------------------------------------------------*
*       営業所名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_TVKBT.

REFRESH: I_TVKBT[].

SELECT VKBUR                              " 営業所
BEZEI                              " 内容説明
FROM TVKBT
INTO CORRESPONDING FIELDS OF TABLE I_TVKBT
WHERE SPRAS = SY-LANGU.                  " 言語

ENDFORM.                    " F_SELECT_TVKBT
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_TVGRT
*&---------------------------------------------------------------------*
*       営業グループ名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_TVGRT.

REFRESH: I_TVGRT[].

SELECT VKGRP                              " 営業グループ
BEZEI                              " 名称 1
FROM TVGRT
INTO CORRESPONDING FIELDS OF TABLE I_TVGRT
WHERE SPRAS = SY-LANGU.                  " 言語

ENDFORM.                    " F_SELECT_TVGRT
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_KNA1
*&---------------------------------------------------------------------*
*       得意先名称の一括取得
*----------------------------------------------------------------------*
FORM F_SELECT_KNA1.

REFRESH: I_KNxx[].

SELECT KUNNR                             " 得意先コード
NAME1                             " 名称１
FROM KNA1
INTO CORRESPONDING FIELDS OF TABLE I_KNxx
WHERE KUNNR IN S_KUNNR.

ENDFORM.                    " F_SELECT_KNA1
*&---------------------------------------------------------------------*
*&      Form  F_CALL_DDUT_DOMVALUES_GET
*&---------------------------------------------------------------------*
*       ドメインテキストの取得
*----------------------------------------------------------------------*
*      -->AI_NAME   ドメイン名
*      <--AO_TABLE  テキストテーブル
*----------------------------------------------------------------------*
FORM F_CALL_DDUT_DOMVALUES_GET USING VALUE(AI_NAME) TYPE DDOBJNAME.

REFRESH: I_DDTEXT[].

CALL FUNCTION 'DDUT_DOMVALUES_GET'
EXPORTING
NAME                = AI_NAME
LANGU               = SY-LANGU
TABLES
DD07V_TAB           = I_DDTEXT
EXCEPTIONS
OTHERS              = 0.

ENDFORM.                    " F_CALL_DDUT_DOMVALUES_GET
*&---------------------------------------------------------------------*
*&      Form  F_GET_NOT_SCRUB_DATA
*&---------------------------------------------------------------------*
*       未消込伝票データ取得
*----------------------------------------------------------------------*
FORM F_GET_NOT_SCRUB_DATA.

*-- BSIDより未消込伝票取得
PERFORM F_SELECT_BSID.

*-- BSADより未消込伝票取得
PERFORM F_SELECT_BSAD.

IF I_BSxx[] IS INITIAL.
*-- 対象データがありません
MESSAGE S762.
LEAVE LIST-PROCESSING.
ENDIF.

*-- 自社データ取得用項目の作成
PERFORM F_EDIT_I_BSxx.

ENDFORM.                    " F_GET_NOT_SCRUB_DATA
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_BSID
*&---------------------------------------------------------------------*
*       BSIDより未消込伝票取得
*----------------------------------------------------------------------*
FORM F_SELECT_BSID.

REFRESH: I_BSxx[].

SELECT KUNNR                              " 得意先コード
GJAHR                              " 会計年度
BELNR                              " 会計伝票番号
BUZEI                              " 会計伝票内の明細番号
BUDAT                              " 伝票の転記日付
WAERS                              " 通貨コード
BLART                              " 伝票タイプ
SHKZG                              " 貸借フラグ
WRBTR                              " 伝票通貨額
ZUONR                              " ソートキー
SGTXT                              " 明細テキスト
ZFBDT                              " 期日計算の支払基準日
ZTERM                              " 支払条件キー
FROM BSID
INTO CORRESPONDING FIELDS OF TABLE I_BSxx
WHERE BUKRS =  P_BUKRS                   " 会社コード
AND KUNNR IN S_KUNNR                   " 得意先コード
AND UMSKZ =  SPACE                     " 特殊仕訳コード
AND BUDAT <= P_BUDAT                   " 転記日付
AND WAERS IN S_WAERS                   " 通貨コード
AND SAKNR IN S_SAKNR.                  " G/L 勘定コード

ENDFORM.                    " F_SELECT_BSID
*&---------------------------------------------------------------------*
*&      Form  F_SELECT_BSAD
*&---------------------------------------------------------------------*
*       BSADより未消込伝票取得
*----------------------------------------------------------------------*
FORM F_SELECT_BSAD.

SELECT KUNNR                              " 得意先コード
GJAHR                              " 会計年度
BELNR                              " 会計伝票番号
BUZEI                              " 会計伝票内の明細番号
BUDAT                              " 伝票の転記日付
WAERS                              " 通貨コード
BLART                              " 伝票タイプ
SHKZG                              " 貸借フラグ
WRBTR                              " 伝票通貨額
ZUONR                              " ソートキー
SGTXT                              " 明細テキスト
ZFBDT                              " 期日計算の支払基準日
ZTERM                              " 支払条件キー
MANDT                              " クライアント(BSADフラグ)
FROM BSAD
APPENDING CORRESPONDING FIELDS OF TABLE I_BSxx
WHERE BUKRS =  P_BUKRS                   " 会社コード
AND KUNNR IN S_KUNNR                   " 得意先コード
AND UMSKZ =  SPACE                     " 特殊仕訳コード
AND AUGDT >  P_BUDAT                   " 決済日付
AND BUDAT <= P_BUDAT                   " 転記日付
AND WAERS IN S_WAERS                   " 通貨コード
AND SAKNR IN S_SAKNR.                  " G/L 勘定コード

ENDFORM.                    " F_SELECT_BSAD
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_I_BSxx
*&---------------------------------------------------------------------*
*       自社データ取得用項目の作成
*----------------------------------------------------------------------*
FORM F_EDIT_I_BSxx.

LOOP AT I_BSxx INTO W_BSxx.
CASE W_BSxx-BLART.
WHEN P_BLART1                         " 売上売掛金伝票タイプ
OR P_BLART2.                        " 部分照合伝票タイプ
*-- 伝票情報をそのまま設定
W_BSxx-GJAHR2 = W_BSxx-GJAHR.       " 会計年度
W_BSxx-BELNR2 = W_BSxx-BELNR.       " 会計伝票番号
W_BSxx-BUZEI2 = W_BSxx-BUZEI.       " 会計伝票内の明細番号
WHEN P_BLART3.
*-- 消込時に発生した伝票を元伝票の情報に置き換える
W_BSxx-GJAHR2 = W_BSxx-ZUONR+0(4).  " 会計年度
W_BSxx-BELNR2 = W_BSxx-ZUONR+4(10). " 会計伝票番号
W_BSxx-BUZEI2 = W_BSxx-ZUONR+14(3). " 会計伝票内の明細番号
WHEN OTHERS.
*-- それ以外は初期値（取得対象外）
CONTINUE.
ENDCASE.
MODIFY I_BSxx FROM W_BSxx.
ENDLOOP.

ENDFORM.                    " F_EDIT_I_BSxx
*&---------------------------------------------------------------------*
*&      Form  F_GET_YN120_DATA
*&---------------------------------------------------------------------*
*       自社データ取得
*----------------------------------------------------------------------*
FORM F_GET_YN120_DATA.

REFRESH: I_YN120[].
CHECK NOT I_BSxx[] IS INITIAL.

SELECT YNGJAHR                            " 伝票会計年度
BELNR                              " 伝票番号
BUZEI                              " 伝票明細番号
VRFCTON                            " 得意先コード(支払人)
BUKRS                              " 会社コード
ZFBDT                              " 計上締日
DVSON                              " 営業所
CSTS                               " 照合ステータス
COMMT                              " コメント
LIST2                              " 得意先発注番号
LIST3                              " 受注伝票
LIST4                              " 営業グループ
LDATE1                             " 請求日
GJAHR                              " 伝票会計年度
SLPDOC                             " 伝票番号
DTLDOC                             " 伝票明細番号
FROM YN120
INTO CORRESPONDING FIELDS OF TABLE I_YN120
FOR ALL ENTRIES IN I_BSxx
WHERE VRFCTON =  I_BSxx-KUNNR            " 得意先コード(支払人)
AND BUKRS   =  P_BUKRS                 " 会社コード
AND DVSON   IN S_DVSON                 " 営業所
AND YNGJAHR =  I_BSxx-GJAHR2           " 会計年度
AND BELNR   =  I_BSxx-BELNR2           " 伝票番号
AND BUZEI   =  I_BSxx-BUZEI2.          " 明細番号

ENDFORM.                    " F_GET_YN120_DATA
*&---------------------------------------------------------------------*
*&      Form  F_EDIT_OUTPUT_DATA
*&---------------------------------------------------------------------*
*       出力データ編集
*----------------------------------------------------------------------*
FORM F_EDIT_OUTPUT_DATA.

SORT I_BSxx[].

LOOP AT I_BSxx INTO W_BSxx.
CLEAR: W_OUTPUT.
*-- 内部テーブルデータ読込み
PERFORM F_READ_TABLE_DATA USING W_BSxx       " 対象データ
CHANGING W_KNxx       " 得意先マスタデータ
W_YN120      " 自社データ
W_TVKBT      " 営業所名称
W_TVGRT      " 営業Ｇ名称
W_DDTEXT.    " 照合St.テキスト
*-- 画面：営業所によるデータ絞込み
IF NOT W_YN120-DVSON IN S_DVSON.
CONTINUE.
ENDIF.
*-- 金額変換(+/-)
IF W_BSxx-SHKZG = 'H'.                       " 貸方
W_BSxx-WRBTR = W_BSxx-WRBTR * -1.
ENDIF.

*-- 営業所
W_OUTPUT-DVSON = W_YN120-DVSON.
*-- 得意先(支払人)
W_OUTPUT-KUNNR = W_BSxx-KUNNR.
*-- 名称
W_OUTPUT-NAME1 = W_KNxx-NAME1.
*-- 得意先発注番号
W_OUTPUT-LIST2 = W_YN120-LIST2.
*-- 通貨
W_OUTPUT-WAERS = W_BSxx-WAERS.
*-- 転記日付
IF W_BSxx-BLART = P_BLART2                   " 部分照合伝票タイプ
OR W_BSxx-BLART = P_BLART3.                  " 消込伝票タイプ
W_OUTPUT-BUDAT = W_YN120-LDATE1.           " 請求日
ELSE.
W_OUTPUT-BUDAT = W_BSxx-BUDAT.             " 転記日
ENDIF.
*-- 締日
W_OUTPUT-ZFBDT = W_YN120-ZFBDT.

IF W_BSxx-BLART = P_BLART1                   " 売上売掛金伝票タイプ
OR W_BSxx-BLART = P_BLART2                   " 部分照合伝票タイプ
OR W_BSxx-BLART = P_BLART3.                  " 消込伝票タイプ
*-- 条件入金日
PERFORM F_CALL_YK_ZFBDT_GET_BY_ZTERM USING W_BSxx-ZTERM
W_OUTPUT-BUDAT
CHANGING W_OUTPUT-PAYDT.
*-- 滞留（日）
IF NOT W_OUTPUT-PAYDT IS INITIAL.
W_OUTPUT-RETENT1 = P_BUDAT - W_OUTPUT-PAYDT.
IF W_OUTPUT-RETENT1 <= 0.
W_OUTPUT-RETENT1 = 0.
ENDIF.
ENDIF.
*-- 転記日滞留（日）
IF NOT W_OUTPUT-BUDAT IS INITIAL.
W_OUTPUT-RETENT2 = P_BUDAT - W_OUTPUT-BUDAT.
IF W_OUTPUT-RETENT2 <= 0.
W_OUTPUT-RETENT2 = 0.
ENDIF.
ENDIF.
ENDIF.
IF W_OUTPUT-RETENT1 = 0.            " 滞留(日) = 0
*-- 未滞留
W_OUTPUT-TERM00 = W_BSxx-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >  0        " 滞留(日) = 1~30
AND W_OUTPUT-RETENT1 <= 30.
*-- 1 〜 30 日
W_OUTPUT-TERM30 = W_BSxx-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >  30       " 滞留(日) = 31~60
AND W_OUTPUT-RETENT1 <= 60.
*-- 31 〜 60 日
W_OUTPUT-TERM60 = W_BSxx-WRBTR.
ELSEIF W_OUTPUT-RETENT1 >  60       " 滞留(日) = 61~90
AND W_OUTPUT-RETENT1 <= 90.
*-- 61 〜 30 日
W_OUTPUT-TERM90 = W_BSxx-WRBTR.
ELSE.                              " 滞留(日) = 91~
*-- 91日超
W_OUTPUT-TERM99 = W_BSxx-WRBTR.
ENDIF.
*-- 合計
W_OUTPUT-TOTAL  = W_BSxx-WRBTR.
*-- 営業所名
W_OUTPUT-BEZEI1 = W_TVKBT-BEZEI.
*-- 営業G
W_OUTPUT-LIST4  = W_YN120-LIST4.
*-- 受注番号
W_OUTPUT-LIST3  = W_YN120-LIST3.
*-- ステータス
IF NOT W_BSxx-BLART = P_BLART1     " 伝票ﾀｲﾌﾟ <> 画面：売上伝票ﾀｲﾌﾟ
OR NOT W_BSxx-BLART = P_BLART2     " 伝票ﾀｲﾌﾟ <> 画面：部照伝票ﾀｲﾌﾟ
OR NOT W_BSxx-BLART = P_BLART3.    " 伝票ﾀｲﾌﾟ <> 画面：消込伝票ﾀｲﾌﾟ
IF NOT W_BSxx-MANDT IS INITIAL.  " データ取得元 = [BSAD]
W_OUTPUT-STATUS = C_STATUS.
ELSE.                            " データ取得元 = [BSID]
W_OUTPUT-STATUS = W_DDTEXT-DDTEXT.
ENDIF.
ENDIF.
*-- コメント
W_OUTPUT-COMMT  = W_YN120-COMMT.
*-- 伝票タイプ
W_OUTPUT-BLART  = W_BSxx-BLART.
*-- 会計伝票
W_OUTPUT-BELNR  = W_BSxx-BELNR.
*-- 明細
W_OUTPUT-BUZEI  = W_BSxx-BUZEI.
*-- 会計年度
W_OUTPUT-GJAHR  = W_BSxx-GJAHR.
*-- 営業グループ名
W_OUTPUT-BEZEI2 = W_TVGRT-BEZEI.

APPEND W_OUTPUT TO I_OUTPUT.
ENDLOOP.

IF I_OUTPUT[] IS INITIAL.
*-- 対象データがありません
MESSAGE S762.
LEAVE LIST-PROCESSING.
ENDIF.

SORT I_OUTPUT[].

FREE: I_BSxx,
I_KNxx,
I_YN120,
I_TVKBT,
I_TVGRT,
I_DDTEXT.

ENDFORM.                    " F_EDIT_OUTPUT_DATA
*&---------------------------------------------------------------------*
*&      Form  F_READ_TABLE_DATA
*&---------------------------------------------------------------------*
*       内部テーブルデータ読込み
*----------------------------------------------------------------------*
*      -->AI_BSxx   対象データ
*      <--AO_KNxx   得意先マスタデータ
*      <--AO_YN120  自社データ
*      <--AO_TVKBT  営業所名称
*      <--AO_TVGRT  営業Ｇ名称
*      <--AO_DDTEXT ドメイン(照合St.)テキスト
*----------------------------------------------------------------------*
FORM F_READ_TABLE_DATA USING VALUE(AI_BSxx)   TYPE T_BSxx
CHANGING VALUE(AO_KNxx)   TYPE T_KNxx
VALUE(AO_YN120)  TYPE T_YN120
VALUE(AO_TVKBT)  TYPE T_TVKBT
VALUE(AO_TVGRT)  TYPE T_TVGRT
VALUE(AO_DDTEXT) TYPE T_DDTEXT.

CLEAR: AO_KNxx,
AO_YN120,
AO_TVKBT,
AO_TVGRT,
AO_DDTEXT.

*-- 得意先名称読込み
READ TABLE I_KNxx INTO AO_KNxx
WITH TABLE KEY KUNNR = AI_BSxx-KUNNR.
*-- 自社データ読込み
READ TABLE I_YN120 INTO AO_YN120
WITH KEY YNGJAHR = AI_BSxx-GJAHR2 " 会計年度
BELNR   = AI_BSxx-BELNR2 " 伝票番号
BUZEI   = AI_BSxx-BUZEI2 " 明細番号
VRFCTON = AI_BSxx-KUNNR " 得意先(支払人)
BINARY SEARCH.
IF SY-SUBRC = 0.
*-- 営業所名称読込み
READ TABLE I_TVKBT INTO AO_TVKBT
WITH TABLE KEY VKBUR = AO_YN120-DVSON. " 営業所
*-- 営業Ｇ名称読込み
READ TABLE I_TVGRT INTO AO_TVGRT
WITH TABLE KEY VKGRP = AO_YN120-LIST4+0(3).
*-- 照合ステータステキスト読込み
READ TABLE I_DDTEXT INTO AO_DDTEXT
WITH KEY DOMVALUE_L = AO_YN120-CSTS.  " 照合St
ENDIF.

ENDFORM.                    " F_READ_TABLE_DATA
*&---------------------------------------------------------------------*
*&      Form  F_CALL_YK_ZFBDT_GET_BY_ZTERM
*&---------------------------------------------------------------------*
*       条件入金日の取得
*----------------------------------------------------------------------*
*      -->AI_ZTERM   支払条件キー
*      -->AI_ZFBDT   期日計算の支払基準日
*      <--AO_ZFBDT   条件入金日
*----------------------------------------------------------------------*
FORM F_CALL_YK_ZFBDT_GET_BY_ZTERM USING VALUE(AI_ZTERM) TYPE ANY
VALUE(AI_ZFBDT) TYPE ANY
CHANGING VALUE(AO_ZFBDT) TYPE ANY.

CALL FUNCTION 'YK_ZFBDT_GET_BY_ZTERM'
EXPORTING
I_ZTERM              = AI_ZTERM
I_DAY                = AI_ZFBDT
IMPORTING
E_SIHARAIBI          = AO_ZFBDT
EXCEPTIONS
OTHERS               = 0.

ENDFORM.                    " F_CALL_YK_ZFBDT_GET_BY_ZTERM
*&---------------------------------------------------------------------*
*&      Form  END_SEC
*&---------------------------------------------------------------------*
*       終了処理
*----------------------------------------------------------------------*
FORM END_SEC.

*-- ALVリスト出力
PERFORM F_OUTPUT_ALV.

ENDFORM.                    " END_SEC
*&---------------------------------------------------------------------*
*&      Form  F_OUTPUT_ALV
*&---------------------------------------------------------------------*
*       ALVリスト出力
*----------------------------------------------------------------------*
FORM F_OUTPUT_ALV.

*-- 項目属性編集
PERFORM F_SET_ALV_FIELDCAT.

*-- その他編集
PERFORM F_SET_ALV_LAYOUT.

*-- ALV一覧出力
PERFORM F_CALL_ALV.

ENDFORM.                    " F_OUTPUT_ALV
*&---------------------------------------------------------------------*
*&      Form  F_SET_ALV_FIELDCAT
*&---------------------------------------------------------------------*
*       項目属性編集
*----------------------------------------------------------------------*
FORM F_SET_ALV_FIELDCAT.

*-- 項目名を編集する
PERFORM SET_FIELDCAT USING 'DVSON'          "項目ID
TEXT-F01         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'KUNNR'          "項目ID
TEXT-F02         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'NAME1'          "項目ID
TEXT-F03         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'LIST2'          "項目ID
TEXT-F04         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'WAERS'          "項目ID
TEXT-F05         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'BUDAT'          "項目ID
TEXT-F06         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'ZFBDT'          "項目ID
TEXT-F07         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'PAYDT'          "項目ID
TEXT-F08         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'TERM00'         "項目ID
TEXT-F09         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'TERM30'         "項目ID
TEXT-F10         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'TERM60'         "項目ID
TEXT-F11         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'TERM90'         "項目ID
TEXT-F12         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'TERM99'         "項目ID
TEXT-F13         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'TOTAL'          "項目ID
TEXT-F14         "項目テキスト
C_ON             "金額項目フラグ
16               "データ長
18               "出力データ長(カンマ含む)
'WAERS'.         "通貨参照項目
PERFORM SET_FIELDCAT USING 'BEZEI1'         "項目ID
TEXT-F15         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'LIST4'          "項目ID
TEXT-F16         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'LIST3'          "項目ID
TEXT-F17         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'RETENT1'        "項目ID
TEXT-F18         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'RETENT2'        "項目ID
TEXT-F19         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'STATUS'         "項目ID
TEXT-F20         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'COMMT'          "項目ID
TEXT-F21         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'BLART'          "項目ID
TEXT-F22         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'BELNR'          "項目ID
TEXT-F23         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'BUZEI'          "項目ID
TEXT-F24         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'GJAHR'          "項目ID
TEXT-F25         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目
PERFORM SET_FIELDCAT USING 'BEZEI2'         "項目ID
TEXT-F26         "項目テキスト
SPACE            "金額項目フラグ
SPACE            "データ長
SPACE            "出力データ長(カンマ含む)
SPACE.           "通貨参照項目

ENDFORM.                    " F_SET_ALV_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       FIELD CATALOGの編集
*----------------------------------------------------------------------*
*      -->P_FIELDNAME     項目ID
*      -->P_FIELDTEXT     項目テキスト(見出し)
*      -->P_CURR_FLG      金額項目フラグ('X'->金額、SPACE->金額以外)
*      -->P_INTLEN        金額項目の桁数(数値のみ)
*      -->P_OUTPUTLEN     金額項目の出力桁数(カンマ、小数点、符号含む)
*      -->P_CFIELD        金額項目の場合参照する通貨項目名
*----------------------------------------------------------------------*
FORM SET_FIELDCAT USING    P_FIELDNAME
P_FIELDTEXT
P_CURR_FLG   "金額項目時に使用
P_INTLEN     "金額項目時に使用
P_OUTPUTLEN  "金額項目時に使用
P_CFIELD.    "金額項目時に使用
DATA:W_FIELDCAT TYPE SLIS_FIELDCAT_ALV.

*-- 見出し設定
IF NOT P_FIELDTEXT IS INITIAL.
W_FIELDCAT-FIELDNAME = P_FIELDNAME.       "項目ID
W_FIELDCAT-SELTEXT_L = P_FIELDTEXT.       "項目テキスト
W_FIELDCAT-DDICTXT   = 'L'.               "出力名長
W_FIELDCAT-OUTPUTLEN = P_OUTPUTLEN.    "出力データ長
ENDIF.

*-- 項目が金額項目
IF P_CURR_FLG = 'X'.
W_FIELDCAT-DATATYPE   = 'CURR'.         "データ型
W_FIELDCAT-INTTYPE    = 'P'.            "数値のデータ型
W_FIELDCAT-INTLEN     = P_INTLEN.       "データ長
W_FIELDCAT-CFIELDNAME = P_CFIELD.       "通貨参照項目
ENDIF.

APPEND W_FIELDCAT TO I_FIELDCAT.

ENDFORM.                    " SET_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  F_SET_ALV_LAYOUT
*&---------------------------------------------------------------------*
*       その他編集
*----------------------------------------------------------------------*
FORM F_SET_ALV_LAYOUT.

*-- プログラムID取得
W_REPID = SY-REPID.

*-- レイアウト設定
W_LAYOUT-COLWIDTH_OPTIMIZE    = C_ON.  "ALV コントロール: 列幅の最適化
W_LAYOUT-DETAIL_INITIAL_LINES = C_ON.
W_LAYOUT-ZEBRA                = C_ON.
W_LAYOUT-GROUP_CHANGE_EDIT    = C_ON.

*-- バリアント
IF NOT P_LAYOUT IS INITIAL.
W_EDISVARIANT-REPORT  = W_REPID.
W_EDISVARIANT-VARIANT = P_LAYOUT.
ENDIF.

*-- ALV印刷時用 設定項目
W_PRINT-NO_PRINT_LISTINFOS = C_ON.

*-- 処理イベントの取得
CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
EXPORTING
I_LIST_TYPE           = 0
IMPORTING
ET_EVENTS             = I_EVENT.

*-- イベント時にコールされるFORM名を設定
CLEAR: W_EVENT.
READ TABLE I_EVENT INTO W_EVENT WITH KEY NAME = 'TOP_OF_PAGE'.
IF SY-SUBRC = 0.
W_EVENT-FORM = 'HEADER_WRITE'.
MODIFY I_EVENT INDEX SY-TABIX FROM W_EVENT.
ENDIF.

ENDFORM.                    " F_SET_ALV_LAYOUT
*&---------------------------------------------------------------------*
*&      Form  F_CALL_ALV
*&---------------------------------------------------------------------*
*       ALV一覧出力
*----------------------------------------------------------------------*
FORM F_CALL_ALV.

CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM      = W_REPID     "プログラム名
IS_LAYOUT               = W_LAYOUT    "レイアウト定義
IS_PRINT                = W_PRINT
IT_FIELDCAT             = I_FIELDCAT  "構造定義
I_DEFAULT               = C_ON
I_SAVE                  = C_SAVE
IS_VARIANT              = W_EDISVARIANT
IT_EVENTS               = I_EVENT
TABLES
T_OUTTAB                = I_OUTPUT     "出力データ
EXCEPTIONS
PROGRAM_ERROR           = 1
OTHERS                  = 2.

IF SY-SUBRC <> 0.
*-- ALV出力エラー
MESSAGE I789 WITH TEXT-E01.
STOP.
ENDIF.

ENDFORM.                    " F_CALL_ALV
*&---------------------------------------------------------------------*
*&      Form  HEADER_WRITE
*&---------------------------------------------------------------------*
*       ALVヘッダ編集
*----------------------------------------------------------------------*
FORM HEADER_WRITE.

DATA:LW_WORK     TYPE CHAR100,
LW_PAGNO(4) TYPE C.

*-- タイトル
PERFORM SET_HEADER USING C_TYP1
TEXT-H01
SPACE.

*-- ページ
CLEAR: LW_WORK,
LW_PAGNO.
LW_PAGNO = SY-PAGNO.
CONDENSE LW_PAGNO.
CONCATENATE TEXT-H02
LW_PAGNO
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*-- 処理日付
CLEAR: LW_WORK.
WRITE: SY-DATUM TO LW_WORK.
CONCATENATE TEXT-H03
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*-- 処理時刻
CLEAR: LW_WORK.
WRITE: SY-UZEIT TO LW_WORK.
CONCATENATE TEXT-H04
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*--- ユーザID
CLEAR: LW_WORK.
CONCATENATE TEXT-H05
SY-UNAME
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
SPACE
LW_WORK.

*--- 年齢基準日
CLEAR: LW_WORK.
WRITE: P_BUDAT TO LW_WORK.
CONCATENATE TEXT-H06
LW_WORK
INTO LW_WORK.
PERFORM SET_HEADER USING C_TYP2
LW_WORK
TEXT-H07.

* ヘッダ出力
CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
EXPORTING
IT_LIST_COMMENTARY       = I_HEAD.

REFRESH: I_HEAD.

ENDFORM.                    " HEADER_WRITE
*&---------------------------------------------------------------------*
*&      Form  SET_HEADER
*&---------------------------------------------------------------------*
*       ヘッダ編集
*----------------------------------------------------------------------*
*      -->L_TYP    タイプ
*      -->L_INFO   編集内容2
*      -->L_KEY    編集内容1
*----------------------------------------------------------------------*
FORM SET_HEADER  USING     L_TYP
L_INFO
L_KEY.
CLEAR: W_HEAD.
W_HEAD-TYP  = L_TYP.
W_HEAD-KEY  = L_KEY.
W_HEAD-INFO = L_INFO.
APPEND W_HEAD TO I_HEAD.

ENDFORM.                    " SET_HEADER
