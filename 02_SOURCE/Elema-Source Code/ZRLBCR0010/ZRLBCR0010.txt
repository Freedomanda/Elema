*& ISID Co.,Ltd. 2008 All rights.reserved.
*&---------------------------------------------------------------------*
*& プログラム：ZRLBCR0010
*& 作成者    ：ISID T.Ofuji
*& 作成日付  ：2008/04/30
*& 機能ID    ：
*&---------------------------------------------------------------------*
*& 処理概要&
*&   バッチインプットツールの初期画面処理
*&   ・バッチインプットレコーダーの記録データ作成
*&   ・バッチインプットデータ編集用のサンプルデータダウンロード
*&   ・バッチインプット処理（ZRLBCR0020のメイン画面へ推移）
*&   ・記録データの削除
*&---------------------------------------------------------------------*
*& 更新履歴&
*& No.   更新日付    更新者    更新内容
*& 新規  2008/04/30  T.Ofuji
*& #001  2012/10/01  ISID144   レイアウト日本語化、他
*&---------------------------------------------------------------------*
REPORT ZRLBCR0010 NO STANDARD PAGE HEADING
LINE-SIZE 123
MESSAGE-ID ZRLBC00.
*----------------------------------------------------------------------*
*                            TABLES                                    *
*----------------------------------------------------------------------*
TABLES: ZTRLBCT004,
ZTRLBCT007,
TSTC,
TSTCT,
DD03L,
DD04T.
*----------------------------------------------------------------------*
*                             DATA                                     *
*----------------------------------------------------------------------*
DATA:
GDT_BIDEF      TYPE STANDARD TABLE OF ZTRLBCT004,
GDS_BIDEF      TYPE ZTRLBCT004,
GDT_BIDSUM     TYPE STANDARD TABLE OF ZTRLBCT006,
CBOX(1)        TYPE C,
WA_INTENS      TYPE I   VALUE 1,
WA_RECID       LIKE ZTRLBCT004-RECID,
WA_HIDE_RECID  LIKE ZTRLBCT004-RECID.

DATA:
BEGIN OF D9100,
TCODE   LIKE ZTRLBCT004-TCODE,
RECID   LIKE ZTRLBCT004-RECID,
RECTX   LIKE ZTRLBCT004-RECTX,
END OF D9100.

*----------------------------------------------------------------------*
*                             CONSTANTS                                *
*----------------------------------------------------------------------*
CONSTANTS:
GCF_ON(01)           TYPE C VALUE 'X',
GCF_BACK(04)         TYPE C VALUE 'BACK',
GCF_END(03)          TYPE C VALUE 'END',
GCF_REFRESH(07)      TYPE C VALUE 'REFRESH',
GCF_RECORD(06)       TYPE C VALUE 'RECORD',
GCF_DELETE(06)       TYPE C VALUE 'DELETE',
GCF_DOWNLOAD(08)     TYPE C VALUE 'DOWNLOAD',
GCF_BIMODE(06)       TYPE C VALUE 'BIMODE',
GCF_DETAIL(06)       TYPE C VALUE 'DETAIL',
GCF_RECSTART(08)     TYPE C VALUE 'RECSTART'.

*&---------------------------------------------------------------------*
*                         START-OF-SELECTION                           *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* バッチインプット記録ＩＤ定義取得
SELECT * FROM ZTRLBCT004 INTO TABLE GDT_BIDEF.
SORT GDT_BIDEF BY RECID.                         "記録ID

* 一覧表示
FORMAT COLOR COL_NORMAL.
LOOP AT GDT_BIDEF INTO GDS_BIDEF.
IF WA_INTENS > 0.
FORMAT INTENSIFIED ON.
ELSE.
FORMAT INTENSIFIED OFF.
ENDIF.
WA_INTENS = WA_INTENS * ( -1 ).
WRITE :  /001 SY-VLINE NO-GAP,
002 CBOX AS CHECKBOX NO-GAP,
004 SY-VLINE NO-GAP,
005 GDS_BIDEF-RECID NO-GAP,
017 SY-VLINE NO-GAP,
018 GDS_BIDEF-RECTX NO-GAP,
060 SY-VLINE NO-GAP,
061 GDS_BIDEF-TCODE NO-GAP,
077 SY-VLINE NO-GAP,
078 GDS_BIDEF-ZMANDT,                "#001 ADD 20121001
090 SY-VLINE NO-GAP,                 "#001 ADD 20121001
091 GDS_BIDEF-UNAME NO-GAP,
103 SY-VLINE NO-GAP,
104 GDS_BIDEF-DATUM NO-GAP,
114 SY-VLINE NO-GAP,
115 GDS_BIDEF-TIMES NO-GAP,
123 SY-VLINE NO-GAP.
WA_HIDE_RECID = GDS_BIDEF-RECID.
HIDE: WA_HIDE_RECID.
ENDLOOP.
*
SET PF-STATUS '1000'.
*
*&---------------------------------------------------------------------*
*                         END-OF-SELECTION                             *
*&---------------------------------------------------------------------*
END-OF-SELECTION.

ULINE.

*&---------------------------------------------------------------------*
*                            TOP-OF-PAGE                               *
*&---------------------------------------------------------------------*
TOP-OF-PAGE.

FORMAT COLOR COL_HEADING.
ULINE.
WRITE : /001 SY-VLINE NO-GAP,
002 '' NO-GAP,
004 SY-VLINE NO-GAP,
005 TEXT-001 NO-GAP,
017 SY-VLINE NO-GAP,
018 TEXT-002 NO-GAP,
060 SY-VLINE NO-GAP,
061 TEXT-003 NO-GAP,
077 SY-VLINE NO-GAP,
078 TEXT-007 NO-GAP,                    "#001 ADD 20121001
090 SY-VLINE NO-GAP,                    "#001 ADD 20121001
091 TEXT-004 NO-GAP,
103 SY-VLINE NO-GAP,
104 TEXT-005 NO-GAP,
114 SY-VLINE NO-GAP,
115 TEXT-006 NO-GAP,
123 SY-VLINE NO-GAP.
ULINE.

*&---------------------------------------------------------------------*
*                        AT LINE-SELECTION                             *
*&---------------------------------------------------------------------*
AT LINE-SELECTION.

*&---------------------------------------------------------------------*
*                         AT USER-COMMAND                              *
*&---------------------------------------------------------------------*
AT USER-COMMAND.

CASE SY-UCOMM.
WHEN GCF_BACK.
LEAVE PROGRAM.
WHEN GCF_END.
LEAVE PROGRAM.
WHEN GCF_REFRESH.
SUBMIT ZRLBCR0010.
WHEN GCF_RECORD.                               "記録開始
CALL SCREEN 9100 STARTING AT 5 5.
WHEN GCF_DELETE.                               "記録削除
PERFORM SUB_DELETE.
WHEN GCF_DOWNLOAD.                             "ダウンロード
PERFORM SUB_DOWNLOAD.
WHEN GCF_BIMODE                                "バッチインプット処理
OR GCF_DETAIL.
SUBMIT ZRLBCR0020 WITH S_RECID EQ WA_HIDE_RECID AND RETURN.
ENDCASE.

SET USER-COMMAND GCF_REFRESH.

*&---------------------------------------------------------------------*
*&      Module  STATUS_9100 OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE STATUS_9100 OUTPUT.

SET PF-STATUS '9100'.

ENDMODULE.                 " STATUS_9100  OUTPUT
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9100  INPUT
*&---------------------------------------------------------------------*
*       ボタン処理
*----------------------------------------------------------------------*
MODULE USER_COMMAND_9100 INPUT.

CASE SY-UCOMM.
WHEN GCF_RECSTART                              "記録開始
OR GCF_RECORD.
*     バッチインプット記録ＩＤ定義取得
SELECT SINGLE * FROM ZTRLBCT004
WHERE RECID = D9100-RECID.            "記録ID
IF SY-SUBRC = 0.
*       記録ＩＤ & は既に存在します。別の名前を指定してください。
MESSAGE E300 WITH D9100-RECID.
ENDIF.

*     SAP トランザクションコード取得
SELECT SINGLE * FROM TSTC
WHERE TCODE = D9100-TCODE.            "トランザクション
IF SY-SUBRC <> 0.
*       トランザクション & は登録されていません。
MESSAGE E301 WITH D9100-TCODE.
ENDIF.

*     編集処理
CLEAR ZTRLBCT004.
ZTRLBCT004-RECID    = D9100-RECID.           "記録ID
ZTRLBCT004-TCODE    = D9100-TCODE.           "トランザクション

*     記録IDテキストが未入力の場合
IF D9100-RECTX IS INITIAL.
*       トランザクションコ−ドテキスト取得
SELECT SINGLE * FROM TSTCT
WHERE SPRSL = SY-LANGU
AND TCODE = D9100-TCODE.
IF SY-SUBRC = 0.
ZTRLBCT004-RECTX    = TSTCT-TTEXT.       "トラン内容説明
ELSE.
*         記録内容テキストを入力して下さい。
MESSAGE E302.
ENDIF.
ELSE.
*       入力されている場合、入力値セット
ZTRLBCT004-RECTX    = D9100-RECTX.
ENDIF.

ZTRLBCT004-UNAME    = SY-UNAME.              "記録者
ZTRLBCT004-DATUM    = SY-DATUM.              "記録年月日
ZTRLBCT004-TIMES    = SY-UZEIT.              "記録時刻
ZTRLBCT004-ZMANDT   = SY-MANDT.              "#001 ADD 20121001
INSERT ZTRLBCT004.

*     バッチインプット記録処理
PERFORM SUB_TRANSACTION_RECORD.
LEAVE TO SCREEN 0.
WHEN OTHERS.
LEAVE TO SCREEN 0.
ENDCASE.

ENDMODULE.                 " USER_COMMAND_9100  INPUT

*&---------------------------------------------------------------------*
*&      バッチインプット記録処理
*&---------------------------------------------------------------------*
FORM SUB_TRANSACTION_RECORD.

DATA: LDS_OPTIONS LIKE CTU_PARAMS.
DATA: LDT_TRANS LIKE BDCDATA OCCURS 0 WITH HEADER LINE.

* バッチインプット記録実行
CLEAR LDS_OPTIONS.
LDS_OPTIONS-DEFSIZE = 'X'.
CALL FUNCTION 'BDC_RECORD_TRANSACTION'
EXPORTING
TCODE     = D9100-TCODE
OPTIONS   = LDS_OPTIONS
TABLES
DYNPROTAB = LDT_TRANS.

CLEAR: ZTRLBCT004, ZTRLBCT007.

* 記録データを編集
LOOP AT LDT_TRANS.
ZTRLBCT007-RECID    = D9100-RECID.             "記録ID
ZTRLBCT007-SEQNO    = ZTRLBCT007-SEQNO + 1.    "記録番号
ZTRLBCT007-PROG     = LDT_TRANS-PROGRAM.       "BDCモジュールプール
ZTRLBCT007-DYNPRO   = LDT_TRANS-DYNPRO.        "BDC Dynpro番号
ZTRLBCT007-DYNBEGIN = LDT_TRANS-DYNBEGIN.      "BDC Dynpro開始
ZTRLBCT007-FNAM     = LDT_TRANS-FNAM.          "項目名
ZTRLBCT007-FVAL     = LDT_TRANS-FVAL.          "BDC項目値
INSERT ZTRLBCT007.
ENDLOOP.

* 記録完了: トランザクションがコピーされました
MESSAGE S605(MS).

ENDFORM.                    " SUB_TRANSACTION_RECORD

*&---------------------------------------------------------------------*
*&      記録削除
*&---------------------------------------------------------------------*
FORM SUB_DELETE.

CONSTANTS:
LCF_U(01)        TYPE C VALUE 'U',
LCF_S(01)        TYPE C VALUE 'S'.

DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

IF CBOX = GCF_ON.                              "選択
*     バッチインプット記録ＩＤ定義取得
WA_RECID = SY-LISEL+4(12).                   "記録ID
SELECT SINGLE * FROM ZTRLBCT004
WHERE RECID = WA_RECID.
IF SY-SUBRC = 0.
*       保護区分の状態チェック ※2012/10/01現在、保護区分は何も設定されていない
CASE ZTRLBCT004-PROTECT.
WHEN LCF_U.
*           記録ＩＤ & は & によって保護されています
MESSAGE E303 WITH WA_RECID ZTRLBCT004-UNAME.
EXIT.
WHEN LCF_S.
*           記録ＩＤ & はシステムによって保護されています。
MESSAGE E304 WITH WA_RECID.
EXIT.
WHEN OTHERS.
*           バッチインプットデータ処理合計情報取得
SELECT * INTO TABLE GDT_BIDSUM
FROM ZTRLBCT006
WHERE RECID = WA_RECID.        "記録ID
IF SY-SUBRC = 0.
*             記録ＩＤ & でバッチインプットデータが存在します。
MESSAGE E308 WITH WA_RECID.
ELSE.
*             削除処理
DELETE FROM ZTRLBCT004 WHERE RECID = WA_RECID.
DELETE FROM ZTRLBCT007 WHERE RECID = WA_RECID.
COMMIT WORK.
ENDIF.
ENDCASE.
ENDIF.
ENDIF.
CLEAR CBOX.
ENDDO.

ENDFORM.                    " SUB_DELETE

*&---------------------------------------------------------------------*
*&      サンプルダウンロード
*&---------------------------------------------------------------------*
FORM SUB_DOWNLOAD.

DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

IF CBOX = GCF_ON.                              "選択
*     ダウンロード処理
WA_RECID = SY-LISEL+4(12).                   "記録ID
CALL FUNCTION 'ZRL_BC_BIREC_DOWNLOAD'
EXPORTING
RECORDID = WA_RECID.
ENDIF.
ENDDO.

ENDFORM.                    " SUB_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  SUB_PROTECT
*&---------------------------------------------------------------------*
FORM SUB_PROTECT.

DO.
READ LINE SY-INDEX FIELD VALUE CBOX.
IF SY-SUBRC <> 0.
EXIT.
ENDIF.
IF CBOX = 'X'.
WA_RECID = SY-LISEL+4(12).
SELECT SINGLE * FROM ZTRLBCT004
WHERE RECID = WA_RECID.
IF SY-SUBRC = 0.
IF ZTRLBCT004-PROTECT = 'U' AND
ZTRLBCT004-UNAME <> SY-UNAME.
MESSAGE E303 WITH WA_RECID ZTRLBCT004-UNAME.
EXIT.
ENDIF.
IF ZTRLBCT004-PROTECT = 'S'.
MESSAGE E304 WITH WA_RECID.
EXIT.
ENDIF.
UPDATE ZTRLBCT004 SET PROTECT = 'U'
WHERE RECID = WA_RECID.
MESSAGE S306 WITH WA_RECID.
ENDIF.
ENDIF.
CLEAR CBOX.
ENDDO.
ENDFORM.                    " SUB_PROTECT

*&---------------------------------------------------------------------*
*&      Form  SUB_UNPROTECT
*&---------------------------------------------------------------------*
FORM SUB_UNPROTECT.
DO.
READ LINE SY-INDEX FIELD VALUE CBOX.
IF SY-SUBRC <> 0.
EXIT.
ENDIF.
IF CBOX = 'X'.
WA_RECID = SY-LISEL+4(12).
SELECT SINGLE * FROM ZTRLBCT004
WHERE RECID = WA_RECID.
IF SY-SUBRC = 0.
IF ZTRLBCT004-PROTECT = 'U' AND
ZTRLBCT004-UNAME <> SY-UNAME.
MESSAGE E303 WITH WA_RECID ZTRLBCT004-UNAME.
EXIT.
ENDIF.
IF ZTRLBCT004-PROTECT = 'S'.
MESSAGE E304 WITH WA_RECID.
EXIT.
ENDIF.
UPDATE ZTRLBCT004 SET PROTECT = ' '
WHERE RECID = WA_RECID.
MESSAGE S307 WITH WA_RECID.
ENDIF.
ENDIF.
CLEAR CBOX.
ENDDO.

ENDFORM.                    " SUB_UNPROTECT
*Text symbol text・
*001:Rec ID
*002:Rec Det
*003:Transaction
*004:Crea.
*005:Create YMD
*006:Create T
*007:Client
