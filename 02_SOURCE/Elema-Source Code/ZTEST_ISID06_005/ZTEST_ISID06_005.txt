*&---------------------------------------------------------------------*
*& Report  ZTEST_ISID06_005
*&---------------------------------------------------------------------*
REPORT  ZTEST_ISID06_005.
*----------------------------------------------------------------------*
*  データ宣言
*----------------------------------------------------------------------*
TYPE-POOLS: ABAP, LIST.
*----------------------------------------------------------------------*
*  画面定義
*----------------------------------------------------------------------*
TABLES SSCRFIELDS.
SELECTION-SCREEN PUSHBUTTON 1(8) TEXT_001 USER-COMMAND OUT_CE.
* 選択画面下部
DATA: DOCKING TYPE REF TO CL_GUI_DOCKING_CONTAINER,
SPLITTER    TYPE REF TO CL_GUI_EASY_SPLITTER_CONTAINER,
* 選択画面左部
CONTAINER_L TYPE REF TO CL_GUI_CONTAINER,
TEXTEDIT TYPE REF TO CL_GUI_TEXTEDIT,
* 選択画面右部
CONTAINER_R TYPE REF TO CL_GUI_CONTAINER,
GRID  TYPE REF TO CL_GUI_ALV_GRID,
I_ALV TYPE RSTRAN_GUI_T_ROWICON.
*----------------------------------------------------------------------*
*  データ定義
*----------------------------------------------------------------------*
CONSTANTS CNS_SJIS TYPE STRING VALUE `shift_jis`.
*----------------------------------------------------------------------*
*  INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.
TEXT_001 = 'チェック'.
PERFORM INITIALIZATION.
*----------------------------------------------------------------------*
*  AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON EXIT-COMMAND.
* 各種コントロール解放
IF TEXTEDIT IS BOUND.
TEXTEDIT->FREE( ).
ENDIF.
IF DOCKING IS BOUND.
DOCKING->FREE( ).
ENDIF.
* ユーザコマンド処理
AT SELECTION-SCREEN.
CASE SSCRFIELDS-UCOMM.
WHEN 'OUT_CE'.
PERFORM CONV_OUT.
ENDCASE.
CLEAR SSCRFIELDS-UCOMM.
*&---------------------------------------------------------------------*
*&      選択画面作成
*&---------------------------------------------------------------------*
FORM INITIALIZATION.
DATA L_REPID TYPE SYREPID.
* 選択画面を上下に分割
L_REPID = CL_ABAP_SYST=>GET_CURRENT_PROGRAM( ).
CREATE OBJECT: DOCKING
EXPORTING
REPID = L_REPID
DYNNR = SY-DYNNR
SIDE  = CL_GUI_DOCKING_CONTAINER=>DOCK_AT_BOTTOM
RATIO = 90,
* 選択画面下部を左右に分割
SPLITTER
EXPORTING
PARENT        = DOCKING
ORIENTATION   = CL_GUI_EASY_SPLITTER_CONTAINER=>ORIENTATION_HORIZONTAL
SASH_POSITION = 67.
CONTAINER_L = SPLITTER->TOP_LEFT_CONTAINER.
CONTAINER_R = SPLITTER->BOTTOM_RIGHT_CONTAINER.
* 選択画面下部左にテキストボックスを作成
CREATE OBJECT: TEXTEDIT
EXPORTING
PARENT = CONTAINER_L
WORDWRAP_MODE = CL_GUI_TEXTEDIT=>WORDWRAP_AT_FIXED_POSITION
WORDWRAP_TO_LINEBREAK_MODE = CL_GUI_TEXTEDIT=>TRUE,
* 選択画面下部右にグリッドを作成
GRID
EXPORTING I_PARENT = CONTAINER_R.
GRID->SET_TABLE_FOR_FIRST_DISPLAY(
EXPORTING
I_STRUCTURE_NAME = 'RSTRAN_GUI_S_ROWICON'
CHANGING
IT_OUTTAB        = I_ALV ).
ENDFORM.                    "INITIALIZATION
*&---------------------------------------------------------------------*
*&      Form  UNICODE ---> SJIS
*&---------------------------------------------------------------------*
FORM CONV_OUT.
TYPES T_DATA TYPE C LENGTH 65535.
DATA: L_CODEPAGE TYPE ABAP_ENCODING,
CONV TYPE REF TO CL_ABAP_CONV_OUT_CE,
L_DATA  TYPE T_DATA.
DATA: I_DATA TYPE TABLE OF T_DATA,
L_ALV  TYPE REF TO RSTRAN_GUI_S_ROWICON.
L_CODEPAGE = CL_ABAP_CODEPAGE=>SAP_CODEPAGE( CNS_SJIS ).
CONV = CL_ABAP_CONV_OUT_CE=>CREATE( ENCODING = L_CODEPAGE ).
TEXTEDIT->GET_TEXT_AS_R3TABLE( IMPORTING TABLE = I_DATA ).
CLEAR I_ALV.
LOOP AT I_DATA INTO L_DATA.
*   グリッド編集
APPEND INITIAL LINE TO I_ALV REFERENCE INTO L_ALV.
L_ALV->ROWNR = SY-TABIX.
L_ALV->ICON = ICON_GREEN_LIGHT.
*   変換チェック
TRY.
CONV->CONVERT( DATA = L_DATA ).
CATCH CX_SY_CONVERSION_CODEPAGE .
L_ALV->ICON = ICON_RED_LIGHT.
ENDTRY.
ENDLOOP.
* グリッド表示
GRID->REFRESH_TABLE_DISPLAY( ).
ENDFORM.                    "conv_out
