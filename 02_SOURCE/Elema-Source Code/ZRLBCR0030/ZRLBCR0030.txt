*& ISID Co.,Ltd. 2008 All rights.reserved.
*&---------------------------------------------------------------------*
*& プログラム：ZRLBCR0030
*& 作成者    ：ISID T.Ofuji
*& 作成日付  ：2008/04/30
*& 機能ID    ：
*&---------------------------------------------------------------------*
*& 処理概要&
*&   バッチインプットデータ一覧を表示し選択した行のバッチインプット
*&   を行う
*&   ・バッチインプット実行
*&   ・バッチインプットデータ詳細表示（ZRLBCR0040のメイン画面へ推移）
*&---------------------------------------------------------------------*
*& 更新履歴&
*& No.   更新日付    更新者    更新内容
*& 新規  2008/04/30  T.Ofuji
*& #001  2012/10/01  ISID144   レイアウト日本語化、他
*&---------------------------------------------------------------------*
REPORT ZRLBCR0030 NO STANDARD PAGE HEADING
LINE-SIZE 190
MESSAGE-ID ZRLBC00.

*----------------------------------------------------------------------*
*                            TABLES                                    *
*----------------------------------------------------------------------*
TABLES: ZTRLBCT001,
ZTRLBCT002,
ZTRLBCT006,
TSTCT,
DD03L,
DD04T.

*----------------------------------------------------------------------*
*                             DATA                                     *
*----------------------------------------------------------------------*
DATA:
GDT_BIDAT1 TYPE STANDARD TABLE OF ZTRLBCT001,
GDS_BIDAT1 TYPE ZTRLBCT001,
*
WA_CBOX(1)      TYPE C,
WA_TTEXT        LIKE TSTCT-TTEXT,
WA_HIDE_DATASEQ LIKE ZTRLBCT001-DATASEQ,
WA_TABNAME      LIKE DNTAB-TABNAME,
WA_FIELDNAME    LIKE DNTAB-FIELDNAME,
WA_SCRTEXT_L    LIKE DD04T-SCRTEXT_L,
WA_RECID        TYPE ZTRLBCT006-RECID,           "#001 ADD 20121001
SW_INTENSIFIED  TYPE I VALUE 1.

*----------------------------------------------------------------------*
*                             CONSTANTS                                *----------------------------------------------------------------------*
CONSTANTS:
GCF_ON(01)         TYPE C VALUE 'X',
GCF_CLN(01)        TYPE C VALUE ':',
GCF_OK(02)         TYPE C VALUE 'OK',
GCF_NG(02)         TYPE C VALUE 'NG',
GCF_REFRESH(07)    TYPE C VALUE 'REFRESH',
GCF_BACK(04)       TYPE C VALUE 'BACK',
GCF_END(03)        TYPE C VALUE 'END',
GCF_DETAIL(06)     TYPE C VALUE 'DETAIL',
GCF_EXECUTE(07)    TYPE C VALUE 'EXECUTE',
GCF_EXECMODEA(09)  TYPE C VALUE 'EXECMODEA',
GCF_SELALL(06)     TYPE C VALUE 'SELALL',
GCF_DESELALL(08)   TYPE C VALUE 'DESELALL',
GCF_BATCHJOB(08)   TYPE C VALUE 'BATCHJOB'.

*----------------------------------------------------------------------*
*                            PARAMETERS                                *
*----------------------------------------------------------------------*
PARAMETERS: P_CTLNO    LIKE ZTRLBCT006-CTLNO.

*----------------------------------------------------------------------*
*                            INCLUDE                                   *
*----------------------------------------------------------------------*
INCLUDE <ICON>.

*&---------------------------------------------------------------------*
*                         START-OF-SELECTION                           *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

PERFORM SUB_REFRESH.

*&---------------------------------------------------------------------*
*                             TOP-OF-PAGE                              *
*&---------------------------------------------------------------------*
TOP-OF-PAGE.

PERFORM SUB_HEADER.

TOP-OF-PAGE DURING LINE-SELECTION .

PERFORM SUB_HEADER.

*&---------------------------------------------------------------------*
*                         AT USER-COMMAND                              *
*&---------------------------------------------------------------------*
AT USER-COMMAND.

CASE SY-UCOMM.
WHEN GCF_REFRESH.
SY-LSIND = SY-LSIND - 1.
PERFORM SUB_REFRESH.
WHEN GCF_BACK.
LEAVE TO SCREEN 0.
WHEN GCF_END.
LEAVE PROGRAM.
WHEN GCF_DETAIL.                               "詳細
SUBMIT ZRLBCR0040 WITH P_CTLNO  = P_CTLNO
WITH P_DATSEQ = WA_HIDE_DATASEQ
AND RETURN.
WHEN GCF_EXECUTE.                              "処理実行
PERFORM SUB_EXECUTE USING 'N'.
WHEN GCF_EXECMODEA.                            "??ボタン表示なし
PERFORM SUB_EXECUTE USING 'A'.
WHEN GCF_SELALL.
PERFORM MODIFY_WA_CBOX USING 'X'.            "全選択
WHEN GCF_DESELALL.
PERFORM MODIFY_WA_CBOX USING ' '.            "選択解除
************************************************ #001 ADD 20121001 STA *
WHEN GCF_BATCHJOB.
PERFORM SUB_SUBMIT_BIJOB.                    "バックグランド実行
************************************************ #001 ADD 20121001 END *
ENDCASE.

*&---------------------------------------------------------------------*
*&      Form  SUB_REFRESH
*&---------------------------------------------------------------------*
FORM SUB_REFRESH.

DATA: L_OLD_STARO LIKE SY-STARO.
L_OLD_STARO = SY-STARO.

SET PF-STATUS '1000'.

* バッチインプットデータ（概要）取得
SELECT * INTO  TABLE GDT_BIDAT1
FROM  ZTRLBCT001
WHERE CTLNO = P_CTLNO.
SORT GDT_BIDAT1 BY CTLNO DATASEQ.

* 一覧表示
FORMAT COLOR COL_NORMAL.
LOOP AT GDT_BIDAT1 INTO GDS_BIDAT1.
IF SW_INTENSIFIED > 0.
FORMAT INTENSIFIED ON.
ELSE.
FORMAT INTENSIFIED OFF.
ENDIF.
SW_INTENSIFIED = SW_INTENSIFIED * ( -1 ).

WRITE : /001 SY-VLINE NO-GAP,
002 WA_CBOX AS CHECKBOX,
004 SY-VLINE NO-GAP.

CASE GDS_BIDAT1-STATUS.
WHEN ' '.
WRITE : 005 ICON_YELLOW_LIGHT AS ICON.
WHEN GCF_NG.
WRITE : 005 ICON_RED_LIGHT AS ICON.
WHEN GCF_OK.
WRITE : 005 ICON_GREEN_LIGHT AS ICON.
ENDCASE.

WRITE :  015 SY-VLINE NO-GAP,
016 GDS_BIDAT1-DATASEQ NO-GAP,
024 SY-VLINE NO-GAP,
025 GDS_BIDAT1-LDATE NO-GAP,
035 SY-VLINE NO-GAP,
036 GDS_BIDAT1-LTIME,
044 SY-VLINE NO-GAP,
045 GDS_BIDAT1-LUSER NO-GAP,
057 SY-VLINE NO-GAP,
058 GDS_BIDAT1-FVAL NO-GAP,
190 SY-VLINE NO-GAP.
WA_HIDE_DATASEQ = GDS_BIDAT1-DATASEQ.
HIDE: WA_HIDE_DATASEQ.
ENDLOOP.

ULINE.
SCROLL LIST TO LAST PAGE INDEX 1 LINE L_OLD_STARO.
*
ENDFORM.                    "SUB_REFRESH
*&---------------------------------------------------------------------*
*&      処理実行
*&---------------------------------------------------------------------*
FORM SUB_EXECUTE USING P_MODE.

DO.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.

IF WA_CBOX = GCF_ON.
*     バッチインプットデータ（概要）取得
SELECT SINGLE * FROM  ZTRLBCT001
WHERE CTLNO = P_CTLNO           "コントロールＮＯ
AND   DATASEQ = WA_HIDE_DATASEQ."データ番号

IF ZTRLBCT001-STATUS = GCF_OK.
*       既に処理済です。処理済のデータを再処理することは出来ません。
MESSAGE E501.
ELSE.
*        バッチインプット処理実行
CALL FUNCTION 'ZRL_BC_BI_EXECUTE'
EXPORTING
CTLNO         = P_CTLNO               "コントロールＮＯ
DATASEQ       = WA_HIDE_DATASEQ       "データ番号
MODE          = P_MODE.               "実行モード

ENDIF.
ENDIF.
CLEAR WA_CBOX.
ENDDO.

ENDFORM.                    " SUB_EXECUTE
*&---------------------------------------------------------------------*
*&      Form  SUB_HEADER
*&---------------------------------------------------------------------*
FORM SUB_HEADER .

SELECT SINGLE * FROM  ZTRLBCT006
WHERE CTLNO = P_CTLNO.
FORMAT COLOR COL_BACKGROUND.
WRITE : /001 TEXT-001 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-CTLNO.
WRITE : /001 TEXT-002 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-RECID,
032 ZTRLBCT006-RECTX.
WA_RECID = ZTRLBCT006-RECID.                     "#001 ADD 20121001
WA_TTEXT = ''.
SELECT SINGLE * FROM  TSTCT
WHERE SPRSL = SY-LANGU
AND   TCODE = ZTRLBCT006-TCODE.
IF SY-SUBRC = 0.
WA_TTEXT = TSTCT-TTEXT.
ENDIF.
WRITE : /001 TEXT-003 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-TCODE,
032 WA_TTEXT.
WRITE : /001 TEXT-004 NO-GAP, 017 GCF_CLN, 019 ZTRLBCT006-DATUM,
030 ZTRLBCT006-TIMES.
FORMAT COLOR COL_HEADING.
ULINE.
WRITE : /001 SY-VLINE NO-GAP,
002 '',
004 SY-VLINE NO-GAP,
005 TEXT-010 NO-GAP,
015 SY-VLINE NO-GAP,
016 TEXT-011 NO-GAP,
024 SY-VLINE NO-GAP,
025 TEXT-005 NO-GAP,
035 SY-VLINE NO-GAP,
036 TEXT-006 NO-GAP,
044 SY-VLINE NO-GAP,
045 TEXT-007 NO-GAP,
057 SY-VLINE NO-GAP,
058 TEXT-008 NO-GAP,
190 SY-VLINE NO-GAP.
ULINE.

ENDFORM.                    " SUB_HEADER
*&---------------------------------------------------------------------*
*&      Form  MODIFY_WA_CBOX
*&---------------------------------------------------------------------*
FORM MODIFY_WA_CBOX  USING    VALUE(FLAG).

DO.
CLEAR: WA_CBOX.
*   表示中の一覧読込
READ LINE SY-INDEX FIELD VALUE WA_CBOX.

IF SY-SUBRC <> 0.
EXIT.
ENDIF.
*   チェックボックス更新
IF SY-LISEL+1(1) <> FLAG.
MODIFY CURRENT LINE FIELD VALUE WA_CBOX FROM FLAG.
ENDIF.
ENDDO.

ENDFORM.                    " MODIFY_WA_CBOX
*&---------------------------------------------------------------------*
*&      Form  SUB_SUBMIT_BIJOB
*&---------------------------------------------------------------------*
*&      バックグランド実行
*&---------------------------------------------------------------------*
FORM SUB_SUBMIT_BIJOB.

DATA: L_JOBNAME   LIKE TBTCJOB-JOBNAME.
DATA: L_JOBCOUNT  LIKE TBTCJOB-JOBCOUNT.

CONSTANTS:
LCF_JOB(06)     TYPE C VALUE 'BIJOB-',
LCF_HFN(01)     TYPE C VALUE '-'.

* ジョブ名編集
CONCATENATE LCF_JOB WA_RECID LCF_HFN P_CTLNO INTO L_JOBNAME.
CONDENSE L_JOBNAME NO-GAPS.

* ジョブ開始
CALL FUNCTION 'JOB_OPEN'
EXPORTING
JOBNAME  = L_JOBNAME
IMPORTING
JOBCOUNT = L_JOBCOUNT.

* バックグランドで処理実行
SUBMIT ZRLBCR0021 WITH P_CTLNO  = P_CTLNO        "コントロールＮＯ
WITH P_RECID  = WA_RECID       "記録ID
WITH P_DATSEQ = WA_HIDE_DATASEQ"データ番号
USER SY-UNAME VIA JOB L_JOBNAME NUMBER L_JOBCOUNT
AND RETURN.

* ジョブ終了
CALL FUNCTION 'JOB_CLOSE'
EXPORTING
JOBCOUNT  = L_JOBCOUNT
JOBNAME   = L_JOBNAME
STRTIMMED = GCF_ON.

* バッチインプットジョブ & を起動しました。
MESSAGE S410 WITH L_JOBNAME.

ENDFORM.                    " SUB_SUBMIT_BIJOB
*Text symbol text・
*001:Control No.
*002:Rec ID
*003:Transaction
*004:Cre. DT
*005:Cre. YMD
*006:Cre. T
*007:Cre. P
*008:Batch input data
*009:Client
*010:Status
*011:Data No.
*Selection text・
*P_CTLNO:D       .
