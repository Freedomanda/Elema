*& 検収照合システム                                                    *
*&     プログラム名称：検収チェックリスト                              *
*&     プログラムＩＤ：YK011300                                        *
*&     作成者　　　　：K.ISHIKAWA(NSP)   作成日　　　　：2001/11/26    *
*&     更新者　　　　：K.KAJISA(NDSC)    更新日　　　　：2002/02/28    *
*&     更新者　　　　：K.KAJISA(NDSC)    更新日　　　　：2002/03/04    *
*&                     選択画面調整                                    *
*&     更新者　　　　：K.KAJISA(NDSC)    更新日　　　　：2002/03/18    *
*&                     選択画面調整の２                                *
*&     更新者　　　　：K.KAJISA(NDSC)    更新日　　　　：2002/04/01    *
*&                     再出力時はチェック済みのみに変更                *
*&     更新者        :M.SHIRAISHI(NDSC)  更新日　　　　:2002/04/12     *
*&                     印刷ヘッダ部の締日修正                          *
*&     更新者　　　　：M.SHIRAISHI(NDSC) 更新日        : 2002/04/12    *
*&                     総合計ページ消去　　　　　　　　　　　　　　　　*
*&     更新者        :M.SHIRAISHI(NDSC)  更新日　　　 : 2002/05/23     *
*&                     レポート定義追加                                *
*&     更新者        :K.KAJISA(NDSC)     更新日　　   : 2002/12/12     *
*&                     選択画面初期値提案                              *
*&     更新者　　    ：K.kajisa(NDSC)    更新日       : 2003/05/13     *
*&                     営業所、営業グループの初期値提案                *
*&---------------------------------------------------------------------*
REPORT      YK011300
LINE-SIZE 170
LINE-COUNT 58
NO STANDARD PAGE HEADING
MESSAGE-ID Y1.

*----------------------------------------------------------------------*
*   宣言部
*----------------------------------------------------------------------*
TYPE-POOLS: SLIS.
TABLES:     USR01,          " ユーザマスターレコード
YK110,          " 検収データ
KNA1,           " 得意先マスター： 一般データ
TVGRT,          " 組織単位： 販売グループ： テキスト
PRI_PARAMS,     " 印刷パラメータ受渡し用構造
YK150.          " 売上照合マスタ 2003/05/13 ADD
*   ALV関係の定義
*   （改頁、ソート）
DATA: W_SORT     TYPE SLIS_SORTINFO_ALV,    " 改頁のソートキー情報
I_SORT     TYPE SLIS_T_SORTINFO_ALV,  " (内部テーブル）
W_LAYOUT   TYPE SLIS_LAYOUT_ALV,      " 改頁処理の宣言
*   （ヘッダー出力）
W_EVENT    TYPE SLIS_ALV_EVENT,       " 実行可能なイベント群
I_EVENT    TYPE SLIS_T_EVENT,         " （内部テーブル）
*   （フィールドカタログ）
W_SNAME    LIKE DD02L-TABNAME VALUE 'YK01130010',
I_FIELDCAT TYPE SLIS_T_FIELDCAT_ALV,  " （内部テーブル）
*   （帳票印刷用）
W_PRINT    TYPE SLIS_PRINT_ALV.

*   （出力レイアウトテーブル）
TYPES BEGIN OF ST_OUT.
INCLUDE TYPE YK01130010.
TYPES END OF ST_OUT.
*DATA: I_OUT      TYPE STANDARD TABLE OF ST_OUT.

*   検収リスト
DATA: I_KENSHU TYPE ST_OUT OCCURS 1
WITH HEADER LINE.

*   PRINTパラメータ
DATA: PRINT_PARAMS LIKE PRI_PARAMS.

*   印刷パラメータ
DATA: BEGIN OF SP_PARAMS,
SPLD LIKE USR01-SPLD,
SPDA LIKE USR01-SPDA,
END OF SP_PARAMS.

*   FUNCTION 'GET_PRINT_PARAMETERS'からの戻り値
DATA: RET TYPE C VALUE ' '.

*   ワークエリア
DATA: W_REPID LIKE SY-REPID,
W_SUBRC LIKE SY-SUBRC.      " SY-SUBRCの一次保管
DATA W_OLD_KUNNR LIKE YK110-KUNNR. "2002/12/12 ADD
DATA: W_SPART LIKE vbrk-SPART.     "2003/05/23 ADD
*----------------------------------------------------------------------*
*   選択画面定義
*----------------------------------------------------------------------*
*SELECTION-SCREEN SKIP 3. "2002/03/04 DEL
*2002/03/04 ADD START
PARAMETERS:P_BUKRS LIKE VBRK-BUKRS MEMORY ID BUK OBLIGATORY, "会社コード
P_VKORG LIKE VBRK-VKORG MEMORY ID VKO OBLIGATORY, "販売組織
P_VTWEG LIKE VBRK-VTWEG MEMORY ID VTW OBLIGATORY, "流通チャネ
P_SPART LIKE VBRK-SPART MEMORY ID SPA OBLIGATORY. "製品部門
SELECT-OPTIONS: S_GSBER FOR YK110-GSBER MEMORY ID GSB,   "事業領域
S_VKBUR FOR YK110-VKBUR                  "営業所
*                                        MEMORY ID VKB   "DEL 2003/05/13
NO INTERVALS,
S_VKGRP FOR YK110-VKGRP                  "営業グループ
*                                        MEMORY ID VKG   "DEL 2003/05/13
NO INTERVALS.
*2002/03/04 ADD END
SELECT-OPTIONS S_KUNNR FOR YK110-KUNNR                  " 得意先コード
*2002/12/12 MOD START
*                  memory id kun no intervals.           "2002/03/18 ADD
NO INTERVALS.
*2002/12/12 MOD END
SELECT-OPTIONS S_ZFBDT FOR YK110-ZFBDT  NO INTERVALS.   " 締日
SELECT-OPTIONS S_INSDAT FOR YK110-INSDAT                " 登録日
DEFAULT SY-DATUM  NO INTERVALS.        "2002/03/18 ADD
SELECT-OPTIONS S_INSUSR FOR YK110-INSUSR                " 登録ユーザ
DEFAULT SY-UNAME  NO INTERVALS.        "2002/03/18 ADD
SELECT-OPTIONS S_KEKKBN FOR YK110-KEKKBN.               " 区分
PARAMETERS P_PRINT LIKE USR01-SPLD.                     " プリンタ
PARAMETERS P_LINE LIKE PRI_PARAMS-LINCT DEFAULT 58.     " 行数/頁
PARAMETERS P_SIZE LIKE PRI_PARAMS-LINSZ DEFAULT 170.    " 桁数/行
PARAMETERS P_REOUT AS CHECKBOX DEFAULT ' '.             " 再出力
SELECT-OPTIONS S_OUTFLG FOR YK110-OUTFLG NO-DISPLAY.    "2002/02/28 ADD
*----------------------------------------------------------------------*
*   実行
*----------------------------------------------------------------------*
INITIALIZATION.
*   SPLD（スプール： 出力デバイス）, SPDA（印刷パラメータ3）の取得
SELECT SINGLE SPLD SPDA
FROM USR01 INTO CORRESPONDING FIELDS OF SP_PARAMS
WHERE BNAME = SY-UNAME.
P_PRINT = SP_PARAMS-SPLD.   " プリンタ名
CLEAR W_OLD_KUNNR. "2002/12/12 ADD
AT SELECTION-SCREEN.
*2002/03/18 ADD START
DATA CT_REC TYPE P.
*  check sy-ucomm is initial.          "ENTER時のみ処理 "DEL 2002/12/12
DESCRIBE TABLE S_KUNNR LINES CT_REC.
IF CT_REC = 1.
READ TABLE S_KUNNR INDEX 1.
IF W_OLD_KUNNR <> S_KUNNR-LOW. "2002/12/12 ADD
IF S_KUNNR-OPTION = 'EQ' OR  S_KUNNR-OPTION = SPACE.
REFRESH: S_ZFBDT, S_VKBUR, S_VKGRP.
PERFORM SET_ZFBDT.              "締日初期値セット
PERFORM SET_EIGYO.              "営業所&営業グループ初期値セット
MESSAGE ID 'Y1' TYPE 'S' NUMBER '400' WITH '初期値設定しました'.
ENDIF.
W_OLD_KUNNR = S_KUNNR-LOW. "2002/12/12 ADD
ENDIF.                       "2002/12/12 ADD
ELSE.                          "2003/05/13 ADD
CLEAR W_OLD_KUNNR.           "2003/05/13 ADD
ENDIF.
*2002/03/18 ADD END
START-OF-SELECTION.
* 2002/02/28 ADD START
IF P_REOUT IS INITIAL.
S_OUTFLG-SIGN   = 'I'.
S_OUTFLG-OPTION = 'NE'.
S_OUTFLG-LOW    = 'X'.
APPEND S_OUTFLG.
*2002/04/01 ADD START
ELSE.
S_OUTFLG-SIGN   = 'I'.
S_OUTFLG-OPTION = 'EQ'.
S_OUTFLG-LOW    = 'X'.
APPEND S_OUTFLG.
*2002/04/01 ADD END
ENDIF.
*2002/02/28 ADD END
*** ０． ALVの初期設定
*

*   呼出し元を設定
W_REPID = SY-REPID.

*   項目カタログの作成
CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
EXPORTING
I_STRUCTURE_NAME       = W_SNAME
CHANGING
CT_FIELDCAT            = I_FIELDCAT
EXCEPTIONS
INCONSISTENT_INTERFACE = 1
PROGRAM_ERROR          = 2
OTHERS                 = 3.

IF SY-SUBRC > 0.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.

*   実行可能なイベント群の作成
W_EVENT-FORM = 'TOP_OF_PAGE'.
W_EVENT-NAME = 'TOP_OF_PAGE'.
APPEND W_EVENT TO I_EVENT.
W_EVENT-FORM = 'CALLER_EXIT'.
W_EVENT-NAME = 'CALLER_EXIT'.
APPEND W_EVENT TO I_EVENT.

*   "TOP-OF-PAGE"を定義
READ TABLE I_EVENT INTO W_EVENT WITH KEY NAME = 'TOP_OF_PAGE'.
W_EVENT-FORM = 'TOP_OF_PAGE'.
MODIFY I_EVENT FROM W_EVENT TRANSPORTING
FORM WHERE NAME = 'TOP_OF_PAGE'.

*   ソート & ページイジェクト
W_LAYOUT-GROUP_CHANGE_EDIT = 'X'.
W_PRINT-NO_PRINT_LISTINFOS = 'X'.   " Extra printing pageを抑制
W_LAYOUT-NO_TOTALLINE = 'X'.        " 2002/04/12 ADD

W_SORT-SPOS      = '01'.
W_SORT-FIELDNAME = 'ZFBDT'.    " 支払基準日（締め日）
W_SORT-UP        = 'X'.
W_SORT-SUBTOT    = 'X'.
W_SORT-GROUP     = '*'.
APPEND W_SORT TO I_SORT.

W_SORT-SPOS      = '02'.
W_SORT-FIELDNAME = 'KUNNR'.    " 得意先コードの項目名
W_SORT-UP        = 'X'.
W_SORT-SUBTOT    = 'X'.
W_SORT-GROUP     = '*'.
APPEND W_SORT TO I_SORT.

W_SORT-SPOS      = '03'.
W_SORT-FIELDNAME = 'VKGRP'.    " 営業グループの項目名
W_SORT-UP        = 'X'.
W_SORT-SUBTOT    = 'X'.
W_SORT-GROUP     = '*'.
APPEND W_SORT TO I_SORT.

*** １． データベース読込処理
*
*       - 前提条件（ただし，絶対的な条件ではない）：
*             検収データ（YK110）のソート順
*                 (1) BURKS（会社コード）
*                 (2) YKYEAR（暦年）
*                 (3) AUDDOC（検収番号）
*
*       - 事後条件：
*             検収チェックリスト（I_KENSHU）
*                 (1) ZFBDT（支払基準日 - 締め日）
*                 (2) KUNNR（得意先コード）
*                 (3) VKGRP（営業グループ）
*                 (4) AUDDAT（検収日）
*                 (5) BSTKD（得意先発注番号）
*                 (6) AUDDOC（検収番号）
*

*   選択条件を満たす検収データ（YK110）レコードの読込み
REFRESH I_KENSHU.
SELECT *
FROM YK110
INTO CORRESPONDING FIELDS OF TABLE I_KENSHU
WHERE DELFLG = SPACE        " 削除フラグチェック
AND OUTFLG IN S_OUTFLG    "2002/02/28 ADD
*2002/03/04 ADD START
AND BUKRS  = P_BUKRS
AND VKORG  = P_VKORG
AND VTWEG  = P_VTWEG
AND SPART  = P_SPART
AND GSBER IN S_GSBER
AND VKBUR IN S_VKBUR
AND VKGRP IN S_VKGRP
*2002/03/04 ADD END
AND KUNNR  IN S_KUNNR     " 得意先コードチェック
AND ZFBDT  IN S_ZFBDT     " 支払基準日（締め日）チェック
AND INSDAT IN S_INSDAT    " 登録日チェック
AND INSUSR IN S_INSUSR    " 登録ユーザチェック
AND KEKKBN IN S_KEKKBN.   " 区分チェック

*   検収データに選択条件を満たすレコードが存在するかどうかのチェック
*   １件も存在しなければ，強制終了させる
IF SY-SUBRC > 0.
*---   （本ランではコメントアウトすること！！！）
MESSAGE S677 .
STOP.
ENDIF.

*   内部テーブル（I_KENSHU）のソーティング
SORT I_KENSHU ASCENDING
BY ZFBDT      " (1) 支払基準日(締め日）
KUNNR      " (2) 得意先コード
VKGRP      " (3) 営業グループ
AUDDOC     " (4) 検収番号
AUDDAT     " (5) 検収日
BSTKD.     " (6) 得意先発注番号

*** ２． 強制出力処理(印刷），および出力フラグの更新
*                                     （再出力フラグがOFFの場合のみ）
*

*   再出力がチェックされていないか？
IF P_REOUT = ' '.
*       データベース（YK110）をアイテム単位でロックする
*       （内部テーブルの全アイテムについて行う）
PERFORM ENQUEUE_YK110 USING W_SUBRC.

IF W_SUBRC > 0.
PERFORM DEQUEUE_YK110.
STOP.
ENDIF.
*
*       バックグラウンド出力用パラメータの設定
CALL FUNCTION 'GET_PRINT_PARAMETERS'
EXPORTING
DESTINATION            = P_PRINT
IMMEDIATELY            = 'X'        " 即時印刷
LINE_COUNT             = P_LINE
LINE_SIZE              = P_SIZE
NEW_LIST_ID            = 'X'        " 新規依頼
RELEASE                = SP_PARAMS-SPDA
" 出力後削除
NO_DIALOG              = 'X'        " BG印刷Paramの抑制
IMPORTING
OUT_PARAMETERS         = PRINT_PARAMS
VALID                  = RET        " 返り値格納
EXCEPTIONS
ARCHIVE_INFO_NOT_FOUND = 1
INVALID_PRINT_PARAMS   = 2
INVALID_ARCHIVE_PARAMS = 3
OTHERS                 = 4.

*       出力先を直接印刷に切替える
NEW-PAGE PRINT ON PARAMETERS PRINT_PARAMS NO DIALOG.

*       レポート表示
CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM = W_REPID
IT_FIELDCAT        = I_FIELDCAT
I_SAVE             = 'A'
IT_EVENTS          = I_EVENT[]
IS_LAYOUT          = W_LAYOUT
IT_SORT            = I_SORT
IS_PRINT           = W_PRINT
TABLES
T_OUTTAB           = I_KENSHU
EXCEPTIONS
PROGRAM_ERROR      = 1
OTHERS             = 2.

IF SY-SUBRC <> 0.
*            MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                    WITH SY-MASV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.

*       出力先を元の表示に戻す
NEW-PAGE PRINT OFF.

*       データベース（YK110）のアップデート
W_SUBRC = 0.
LOOP AT I_KENSHU.
*           （チェックリスト出力フラグOUTFLGをセット）
UPDATE YK110
SET OUTFLG = 'X'
UPDDAT = SY-DATUM
UPDTIM = SY-UZEIT
UPDUSR = SY-UNAME
WHERE BUKRS  = I_KENSHU-BUKRS
AND YKYEAR = I_KENSHU-YKYEAR
AND AUDDOC = I_KENSHU-AUDDOC.

W_SUBRC = SY-SUBRC.
IF W_SUBRC > 0.
EXIT.       " アップデート失敗
ENDIF.
ENDLOOP.

IF W_SUBRC = 0.
COMMIT WORK.
ELSE.
MESSAGE S678 .
ROLLBACK WORK.
ENDIF.

*       ロック解除
PERFORM DEQUEUE_YK110.

IF W_SUBRC > 0.
STOP.
ENDIF.

ENDIF.

*** ３． ALV帳票の出力
*
***2002/02/06 ADD:バッチ実行時に帳票が２部出力されるので修正***
IF SY-BATCH <> 'X'.
***************************************************************
*   レポート表示
CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
EXPORTING
I_CALLBACK_PROGRAM = W_REPID
IT_FIELDCAT        = I_FIELDCAT
I_SAVE             = 'A'
IT_EVENTS          = I_EVENT[]
IS_LAYOUT          = W_LAYOUT
IT_SORT            = I_SORT
IS_PRINT           = W_PRINT
TABLES
T_OUTTAB           = I_KENSHU
EXCEPTIONS
PROGRAM_ERROR      = 1
OTHERS             = 2.

IF SY-SUBRC <> 0.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MASV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.
*********************************************************
ENDIF.
***2002/02/06 ADD END************************************
*----------------------------------------------------------------------*
*   FORM CALLER_EXIT
*----------------------------------------------------------------------*
*       CALLER EXIT
*----------------------------------------------------------------------*
*   -> RS_DATA
*----------------------------------------------------------------------*

FORM CALLER_EXIT USING RS_DATA TYPE SLIS_DATA_CALLER_EXIT.
RS_DATA-CALLBACK_HEADER_TRANSPORT = 'FILITEXTS_TEXT_TRANSPORT'.
ENDFORM.

*----------------------------------------------------------------------*
*   FORM TOP_OF_PAGE
*----------------------------------------------------------------------*
*       改ページ処理
*----------------------------------------------------------------------*

FORM TOP_OF_PAGE.
DATA: W_NAME1 LIKE KNA1-NAME1
VALUE '                                   ',
W_BEZEI LIKE TVGRT-BEZEI
VALUE '                    '.

*   ページヘッダー
WRITE: /1 '検収チェックリスト'.
WRITE: /1 'Page:', SY-PAGNO.
WRITE: /1 '作成日時： ', SY-DATUM USING EDIT MASK '____/__/__',
SY-UZEIT USING EDIT MASK '__:__'.
IF NOT ( I_KENSHU-ZFBDT IS INITIAL AND
I_KENSHU-KUNNR IS INITIAL ).
WRITE: /1 '支払基準日： ',     "2002/06/14
I_KENSHU-ZFBDT.
*   write: /1 '締日： ',           "2002/04/12 ADD
*             i_kenshu-zfbdt.
WRITE: /.
SELECT SINGLE NAME1
FROM KNA1 INTO W_NAME1
WHERE KUNNR = I_KENSHU-KUNNR.
WRITE: /1 '得意先　　　',
I_KENSHU-KUNNR,
W_NAME1.
SELECT SINGLE BEZEI
FROM TVGRT INTO W_BEZEI
WHERE VKGRP = I_KENSHU-VKGRP.
WRITE: /1 '営業グループ',
I_KENSHU-VKGRP,
W_BEZEI.
ENDIF.
ENDFORM.

*----------------------------------------------------------------------*
*   FORM ENQUEUE/DEQUEUE_YK110
*----------------------------------------------------------------------*
*       ロック処理/解除
*----------------------------------------------------------------------*
*   <- W_SUBRC: SY-SUBRC (ENQUEUE only)
*----------------------------------------------------------------------*

FORM ENQUEUE_YK110 USING W_SUBRC.
CLEAR W_SUBRC.
LOOP AT I_KENSHU.
CALL FUNCTION 'ENQUEUE_EY_YK110'
EXPORTING
MODE_YK110     = 'E'
MANDT          = SY-MANDT
*              DELFLG         = SPACE  " 削除フラグチェック
BUKRS          = I_KENSHU-BUKRS
YKYEAR         = I_KENSHU-YKYEAR
AUDDOC         = I_KENSHU-AUDDOC
EXCEPTIONS
FOREIGN_LOCK   = 1
SYSTEM_FAILURE = 2
OTHERS         = 3.

W_SUBRC = SY-SUBRC.
IF W_SUBRC > 0.
MESSAGE S679 .
EXIT.
ENDIF.
ENDLOOP.
ENDFORM.

*---------------------------------------------------------------------*
*       FORM DEQUEUE_YK110                                            *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM DEQUEUE_YK110.
LOOP AT I_KENSHU.
CALL FUNCTION 'DEQUEUE_EY_YK110'
EXPORTING
MODE_YK110 = 'E'
MANDT      = SY-MANDT
*              DELFLG     = SPACE  " 削除フラグチェック
BUKRS      = I_KENSHU-BUKRS
YKYEAR     = I_KENSHU-YKYEAR
AUDDOC     = I_KENSHU-AUDDOC.
*         EXCEPTIONS
*              OTHERS     = 1.
*
*    IF SY-SUBRC > 0.
*      MESSAGE S680 .
*      STOP.
*    ENDIF.
ENDLOOP.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  SET_ZFBDT
*&---------------------------------------------------------------------*
*       締日の初期値セット 2002/03/11 add
*----------------------------------------------------------------------*
FORM SET_ZFBDT.
CALL FUNCTION 'YK_ZFBDT_GET_BY_KNVV'
EXPORTING
I_CORD               = S_KUNNR-LOW
I_VKORG              = P_VKORG
I_VTWEG              = P_VTWEG
I_SPART              = P_SPART
I_DAY                = SY-DATUM
IMPORTING
E_ZFBDT_OLD          = S_ZFBDT-LOW
EXCEPTIONS
NO_GET_T052          = 1
NO_GOOD_GETDAY       = 2
NO_GET_ZTERM         = 3
OTHERS               = 4.
IF SY-SUBRC = 0.
S_ZFBDT-SIGN   = 'I'.
S_ZFBDT-OPTION = 'EQ'.
APPEND S_ZFBDT.
ENDIF.

ENDFORM.                    " SET_ZFBDT
*&---------------------------------------------------------------------*
*&      Form  SET_EIGYO     " 2002/03/11 add
*&---------------------------------------------------------------------*
*       営業所&営業グループ初期値セット
*----------------------------------------------------------------------*
FORM SET_EIGYO.
TABLES KNVV.
PERFORM GET_INIT_CHK."2003/05/13 ADD
CLEAR KNVV.
SELECT SINGLE VKBUR VKGRP INTO (S_VKBUR-LOW, S_VKGRP-LOW) FROM KNVV
WHERE KUNNR = S_KUNNR-LOW
AND VKORG = P_VKORG
AND VTWEG = P_VTWEG
*     AND SPART = P_SPART.       "2003/05/13 MOD
and spart = W_spart.        "2003/05/13 MOD
IF SY-SUBRC = 0.
IF YK150-B_VKBUR_INIT = 'X'. "2003/05/13 ADD
S_VKBUR-SIGN   = 'I'.
S_VKBUR-OPTION = 'EQ'.
APPEND S_VKBUR.
ENDIF.                       "2003/05/13 ADD
IF YK150-B_VKGRP_INIT = 'X'. "2003/05/13 ADD
S_VKGRP-SIGN   = 'I'.
S_VKGRP-OPTION = 'EQ'.
APPEND S_VKGRP.
ENDIF.                       "2003/05/13 ADD
ENDIF.
IF YK150-B_VKBUR_INIT <> 'X'.  "2003/05/13 ADD
CLEAR S_VKBUR.               "2003/05/13 ADD
REFRESH S_VKBUR.             "2003/05/13 ADD
ENDIF.                         "2003/05/13 ADD
IF YK150-B_VKGRP_INIT <> 'X'.  "2003/05/13 ADD
CLEAR S_VKGRP.               "2003/05/13 ADD
REFRESH S_VKGRP.             "2003/05/13 ADD
ENDIF.                         "2003/05/13 ADD

ENDFORM.                    " SET_EIGYO
*&---------------------------------------------------------------------*
*&      Form  GET_INIT_CHK
*&---------------------------------------------------------------------*
*       製品部門変換 2003/05/013 ADD
*----------------------------------------------------------------------*
FORM GET_INIT_CHK.
CLEAR W_SPART.
SELECT SINGLE SPAKU FROM TVTA INTO  W_SPART
WHERE vkorg = P_vkorg
AND VTWEG = P_VTWEG
AND SPART = P_SPART.
IF SY-SUBRC <> 0.
W_SPART = P_SPART.
ENDIF.
CLEAR YK150.
SELECT SINGLE B_VKBUR_INIT B_VKGRP_INIT
INTO (YK150-B_VKBUR_INIT,YK150-B_VKGRP_INIT) FROM YK150
WHERE BUKRS = p_BUKRS
AND KUNNR = S_KUNNR-LOW
AND VKORG = p_VKORG
AND VTWEG = p_VTWEG
AND SPART = p_SPART.
IF SY-SUBRC <> 0.
SELECT  B_VKBUR_INIT B_VKGRP_INIT
INTO (YK150-B_VKBUR_INIT,YK150-B_VKGRP_INIT) FROM YK150
UP TO 1 ROWS
WHERE KUNNR = 'DEFAULT'.
ENDSELECT.
ENDIF.

ENDFORM.                    " GET_INIT_CHK
